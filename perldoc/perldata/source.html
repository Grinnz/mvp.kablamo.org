<!DOCTYPE html>
<html>
    <head>
        <meta charset="utf-8">
        <meta content="width=device-width, initial-scale=1" name="viewport">
        <link href="/theme/css/normalize.css" rel="stylesheet">
        <!--<link rel="stylesheet" href="/theme/css/skeleton.css" />-->
        <link href="/theme/css/statocles-default.css" rel="stylesheet">
        <link href="//maxcdn.bootstrapcdn.com/font-awesome/4.3.0/css/font-awesome.min.css" rel="stylesheet">
        <script crossorigin="anonymous" integrity="sha256-hwg4gsxgFZhOsEEamdOYGBf13FyQuiTwlAQgxVSNgt4=" src="http://code.jquery.com/jquery-3.2.1.min.js"></script>
        <title>perldata (source) - Minimum Viable Perl</title>
        <meta content="Statocles 0.086" name="generator">
        
        <script>
            $( document ).ready(function() {
                $('.sub-menu ul').hide();
                $('.sub-menu.perldoc ul').show();
                $('.sub-menu.perldoc div').toggleClass("fa-caret-up fa-caret-down");
                $(".sub-menu a").click(function () {
                    $(this).parent(".sub-menu").siblings().children('ul').hide();
                    $(this).parent(".sub-menu").children("ul").slideToggle("100");
                    $(this).find(".right").toggleClass("fa-caret-up fa-caret-down");
                });
            });
        </script>
        <!-- Global site tag (gtag.js) - Google Analytics -->
        <script async src="https://www.googletagmanager.com/gtag/js?id=UA-110455056-1"></script>
        <script>
            window.dataLayer = window.dataLayer || [];
            function gtag(){dataLayer.push(arguments);}
            gtag('js', new Date());
            gtag('config', 'UA-110455056-1');
        </script>
    </head>
    <body>
        <header>
        </header>
        <div class="container">

                <div class="left column full-height">
                    <div class="sidebar">
                    
<a class="home" href="/">Minimum<br>Viable<br>Perl</a><br><br><br>

<div class="animated bounceInDown">
  <ul>
    <li class="basics sub-menu">
      <a href="/perldoc/perldata/#basics">The basics<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/basics/hello">Hello world</a></li>
        <li><a href="/basics/variables">Variables</a></li>
        <li><a href="/basics/references">References</a></li>
        <li><a href="/basics/functions">Functions</a></li>
        <li><a href="/basics/flow">Conditionals</a></li>
        <li><a href="/basics/loops">Loops</a></li>
        <li><a href="/basics/exceptions">Exceptions</a></li>
        <li><a href="/basics/regexps">Regexps</a></li>
        <li><a href="/basics/files">Files</a></li>
      </ul>
    </li>
    <li class="oo sub-menu">
      <a href="/perldoc/perldata/#oo">Object oriented programming<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/oo/story">The (weird) story of Perl OO</a></li>
        <li><a href="/oo/classes">Classes</a></li>
        <li><a href="/oo/attributes">Attributes</a></li>
        <li><a href="/oo/modifiers">Method modifiers</a></li>
        <li><a href="/oo/roles">Roles</a></li>
        <li><a href="/oo/inheritance">Inheritance</a></li>
      </ul>
    </li>
    <li class="debugging sub-menu">
      <a href="/perldoc/perldata/#basics">Debugging<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/debugging/data-printer">Examining data structures</a></li>
        <li><a href="/debugging/stack-trace">Forcing stack traces</a></li>
      </ul>
    </li>
    <li class="dependencies sub-menu">
      <a href="/perldoc/perldata/#dependencies">Perl libraries<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/dependencies/perl5lib">Loading libraries from custom locations</a></li>
        <li><a href="/dependencies/cpanm">Installing libraries with cpanm</a></li>
        <li><a href="/dependencies/metacpan">Choosing libraries from MetaCPAN</a></li>
        <li><a href="/dependencies/carton">Managing project dependencies with Carton</a></li>
        <li><a href="/dependencies/pmtools">What version is installed and where?</a></li>
      </ul>
    </li>
    <li class="testing sub-menu">
      <a href="/perldoc/perldata/#testing">Testing<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/testing/intro">Introduction to testing</a></li>
        <li><a href="/testing/warnings">Testing warnings and exceptions</a></li>
        <li><a href="/testing/data">Testing data structures</a></li>
        <li><a href="/testing/mock">Testing with mock objects</a></li>
        <li><a href="/testing/libs">Build your own test libraries</a></li>
      </ul>
    </li>
    <li><a href="/perl">Perl design goals</a></li>
    <li><a href="/see-also">Book recommendations</a></li>
    <li><a href="/about">About this book</a></li>
  </ul>
</div>

                    
                    
                    </div>
                </div>

                <div class="right column">
                    <main>
                    <a class="button" href="/perldoc/perldata/">
    Back to documentation
</a>
<pre>=head1 NAME

perldata - Perl data types

=head1 DESCRIPTION

=head2 Variable names
X&lt;variable, name&gt; X&lt;variable name&gt; X&lt;data type&gt; X&lt;type&gt;

Perl has three built-in data types: scalars, arrays of scalars, and
associative arrays of scalars, known as &quot;hashes&quot;.  A scalar is a 
single string (of any size, limited only by the available memory),
number, or a reference to something (which will be discussed
in L&lt;perlref&gt;).  Normal arrays are ordered lists of scalars indexed
by number, starting with 0.  Hashes are unordered collections of scalar 
values indexed by their associated string key.

Values are usually referred to by name, or through a named reference.
The first character of the name tells you to what sort of data
structure it refers.  The rest of the name tells you the particular
value to which it refers.  Usually this name is a single I&lt;identifier&gt;,
that is, a string beginning with a letter or underscore, and
containing letters, underscores, and digits.  In some cases, it may
be a chain of identifiers, separated by C&lt;::&gt; (or by the slightly
archaic C&lt;&#39;&gt;); all but the last are interpreted as names of packages,
to locate the namespace in which to look up the final identifier
(see L&lt;perlmod/Packages&gt; for details).  For a more in-depth discussion
on identifiers, see L&lt;/Identifier parsing&gt;.  It&#39;s possible to
substitute for a simple identifier, an expression that produces a reference
to the value at runtime.   This is described in more detail below
and in L&lt;perlref&gt;.
X&lt;identifier&gt;

Perl also has its own built-in variables whose names don&#39;t follow
these rules.  They have strange names so they don&#39;t accidentally
collide with one of your normal variables.  Strings that match
parenthesized parts of a regular expression are saved under names
containing only digits after the C&lt;$&gt; (see L&lt;perlop&gt; and L&lt;perlre&gt;).
In addition, several special variables that provide windows into
the inner working of Perl have names containing punctuation characters.
These are documented in L&lt;perlvar&gt;.
X&lt;variable, built-in&gt;

Scalar values are always named with &#39;$&#39;, even when referring to a
scalar that is part of an array or a hash.  The &#39;$&#39; symbol works
semantically like the English word &quot;the&quot; in that it indicates a
single value is expected.
X&lt;scalar&gt;

    $days		# the simple scalar value &quot;days&quot;
    $days[28]		# the 29th element of array @days
    $days{&#39;Feb&#39;}	# the &#39;Feb&#39; value from hash %days
    $#days		# the last index of array @days

Entire arrays (and slices of arrays and hashes) are denoted by &#39;@&#39;,
which works much as the word &quot;these&quot; or &quot;those&quot; does in English,
in that it indicates multiple values are expected.
X&lt;array&gt;

    @days		# ($days[0], $days[1],... $days[n])
    @days[3,4,5]	# same as ($days[3],$days[4],$days[5])
    @days{&#39;a&#39;,&#39;c&#39;}	# same as ($days{&#39;a&#39;},$days{&#39;c&#39;})

Entire hashes are denoted by &#39;%&#39;:
X&lt;hash&gt;

    %days		# (key1, val1, key2, val2 ...)

In addition, subroutines are named with an initial &#39;&amp;&#39;, though this
is optional when unambiguous, just as the word &quot;do&quot; is often redundant
in English.  Symbol table entries can be named with an initial &#39;*&#39;,
but you don&#39;t really care about that yet (if ever :-).

Every variable type has its own namespace, as do several
non-variable identifiers.  This means that you can, without fear
of conflict, use the same name for a scalar variable, an array, or
a hash--or, for that matter, for a filehandle, a directory handle, a
subroutine name, a format name, or a label.  This means that $foo
and @foo are two different variables.  It also means that C&lt;$foo[1]&gt;
is a part of @foo, not a part of $foo.  This may seem a bit weird,
but that&#39;s okay, because it is weird.
X&lt;namespace&gt;

Because variable references always start with &#39;$&#39;, &#39;@&#39;, or &#39;%&#39;, the
&quot;reserved&quot; words aren&#39;t in fact reserved with respect to variable
names.  They I&lt;are&gt; reserved with respect to labels and filehandles,
however, which don&#39;t have an initial special character.  You can&#39;t
have a filehandle named &quot;log&quot;, for instance.  Hint: you could say
C&lt;open(LOG,&#39;logfile&#39;)&gt; rather than C&lt;open(log,&#39;logfile&#39;)&gt;.  Using
uppercase filehandles also improves readability and protects you
from conflict with future reserved words.  Case I&lt;is&gt; significant--&quot;FOO&quot;,
&quot;Foo&quot;, and &quot;foo&quot; are all different names.  Names that start with a
letter or underscore may also contain digits and underscores.
X&lt;identifier, case sensitivity&gt;
X&lt;case&gt;

It is possible to replace such an alphanumeric name with an expression
that returns a reference to the appropriate type.  For a description
of this, see L&lt;perlref&gt;.

Names that start with a digit may contain only more digits.  Names
that do not start with a letter, underscore, digit or a caret are
limited to one character, e.g.,  C&lt;$%&gt; or
C&lt;$$&gt;.  (Most of these one character names have a predefined
significance to Perl.  For instance, C&lt;$$&gt; is the current process
id.  And all such names are reserved for Perl&#39;s possible use.)

=head2 Identifier parsing
X&lt;identifiers&gt;

Up until Perl 5.18, the actual rules of what a valid identifier
was were a bit fuzzy.  However, in general, anything defined here should
work on previous versions of Perl, while the opposite -- edge cases
that work in previous versions, but aren&#39;t defined here -- probably
won&#39;t work on newer versions.
As an important side note, please note that the following only applies
to bareword identifiers as found in Perl source code, not identifiers
introduced through symbolic references, which have much fewer
restrictions.
If working under the effect of the C&lt;use utf8;&gt; pragma, the following
rules apply:

    / (?[ ( \p{Word} &amp; \p{XID_Start} ) + [_] ])
      (?[ ( \p{Word} &amp; \p{XID_Continue} ) ]) *    /x

That is, a &quot;start&quot; character followed by any number of &quot;continue&quot;
characters.  Perl requires every character in an identifier to also
match C&lt;\w&gt; (this prevents some problematic cases); and Perl
additionally accepts identfier names beginning with an underscore.

If not under C&lt;use utf8&gt;, the source is treated as ASCII + 128 extra
generic characters, and identifiers should match

    / (?aa) (?!\d) \w+ /x

That is, any word character in the ASCII range, as long as the first
character is not a digit.

There are two package separators in Perl: A double colon (C&lt;::&gt;) and a single
quote (C&lt;&#39;&gt;).  Normal identifiers can start or end with a double colon, and
can contain several parts delimited by double colons.
Single quotes have similar rules, but with the exception that they are not
legal at the end of an identifier: That is, C&lt;$&#39;foo&gt; and C&lt;$foo&#39;bar&gt; are
legal, but C&lt;$foo&#39;bar&#39;&gt; is not.

Additionally, if the identifier is preceded by a sigil --
that is, if the identifier is part of a variable name -- it
may optionally be enclosed in braces.

While you can mix double colons with singles quotes, the quotes must come
after the colons: C&lt;$::::&#39;foo&gt; and C&lt;$foo::&#39;bar&gt; are legal, but C&lt;$::&#39;::foo&gt;
and C&lt;$foo&#39;::bar&gt; are not.

Put together, a grammar to match a basic identifier becomes

 /
  (?(DEFINE)
      (?&lt;variable&gt;
          (?&amp;sigil)
          (?:
                  (?&amp;normal_identifier)
              |   \{ \s* (?&amp;normal_identifier) \s* \}
          )
      )
      (?&lt;normal_identifier&gt;
          (?: :: )* &#39;?
           (?&amp;basic_identifier)
           (?: (?= (?: :: )+ &#39;? | (?: :: )* &#39; ) (?&amp;normal_identifier) )?
          (?: :: )*
      )
      (?&lt;basic_identifier&gt;
        # is use utf8 on?
          (?(?{ (caller(0))[8] &amp; $utf8::hint_bits })
              (?&amp;Perl_XIDS) (?&amp;Perl_XIDC)*
            | (?aa) (?!\d) \w+
          )
      )
      (?&lt;sigil&gt; [&amp;*\$\@\%])
      (?&lt;Perl_XIDS&gt; (?[ ( \p{Word} &amp; \p{XID_Start} ) + [_] ]) )
      (?&lt;Perl_XIDC&gt; (?[ \p{Word} &amp; \p{XID_Continue} ]) )
  )
 /x

Meanwhile, special identifiers don&#39;t follow the above rules; For the most
part, all of the identifiers in this category have a special meaning given
by Perl.  Because they have special parsing rules, these generally can&#39;t be
fully-qualified.  They come in six forms (but don&#39;t use forms 5 and 6):

=over

=item 1.

A sigil, followed solely by digits matching C&lt;\p{POSIX_Digit}&gt;, like
C&lt;$0&gt;, C&lt;$1&gt;, or C&lt;$10000&gt;.

=item 2.

A sigil followed by a single character matching the C&lt;\p{POSIX_Punct}&gt;
property, like C&lt;$!&gt; or C&lt;%+&gt;, except the character C&lt;&quot;{&quot;&gt; doesn&#39;t work.

=item 3.

A sigil, followed by a caret and any one of the characters
C&lt;[][A-Z^_?\]&gt;, like C&lt;$^V&gt; or C&lt;$^]&gt;.

=item 4.

Similar to the above, a sigil, followed by bareword text in braces,
where the first character is a caret.  The next character is any one of
the characters C&lt;[][A-Z^_?\]&gt;, followed by ASCII word characters.  An
example is C&lt;${^GLOBAL_PHASE}&gt;.

=item 5.

A sigil, followed by any single character in the range C&lt;[\xA1-\xAC\xAE-\xFF]&gt;
when not under C&lt;S&lt;&quot;use utf8&quot;&gt;&gt;.  (Under C&lt;S&lt;&quot;use utf8&quot;&gt;&gt;, the normal
identifier rules given earlier in this section apply.)  Use of
non-graphic characters (the C1 controls, the NO-BREAK SPACE, and the
SOFT HYPHEN) has been disallowed since v5.26.0.
The use of the other characters is unwise, as these are all
reserved to have special meaning to Perl, and none of them currently
do have special meaning, though this could change without notice.

Note that an implication of this form is that there are identifiers only
legal under C&lt;S&lt;&quot;use utf8&quot;&gt;&gt;, and vice-versa, for example the identifier
C&lt;$E&lt;233&gt;tat&gt; is legal under C&lt;S&lt;&quot;use utf8&quot;&gt;&gt;, but is otherwise
considered to be the single character variable C&lt;$E&lt;233&gt;&gt; followed by
the bareword C&lt;&quot;tat&quot;&gt;, the combination of which is a syntax error.

=item 6.

This is a combination of the previous two forms.  It is valid only when
not under S&lt;C&lt;&quot;use utf8&quot;&gt;&gt; (normal identifier rules apply when under
S&lt;C&lt;&quot;use utf8&quot;&gt;&gt;).  The form is a sigil, followed by text in braces,
where the first character is any one of the characters in the range
C&lt;[\x80-\xFF]&gt; followed by ASCII word characters up to the trailing
brace.

The same caveats as the previous form apply:  The non-graphic
characters are no longer allowed with S&lt;&quot;use utf8&quot;&gt;, it is unwise
to use this form at all, and utf8ness makes a big difference.

=back

Prior to Perl v5.24, non-graphical ASCII control characters were also
allowed in some situations; this had been deprecated since v5.20.

=head2 Context
X&lt;context&gt; X&lt;scalar context&gt; X&lt;list context&gt;

The interpretation of operations and values in Perl sometimes depends
on the requirements of the context around the operation or value.
There are two major contexts: list and scalar.  Certain operations
return list values in contexts wanting a list, and scalar values
otherwise.  If this is true of an operation it will be mentioned in
the documentation for that operation.  In other words, Perl overloads
certain operations based on whether the expected return value is
singular or plural.  Some words in English work this way, like &quot;fish&quot;
and &quot;sheep&quot;.

In a reciprocal fashion, an operation provides either a scalar or a
list context to each of its arguments.  For example, if you say

    int( &lt;STDIN&gt; )

the integer operation provides scalar context for the &lt;&gt;
operator, which responds by reading one line from STDIN and passing it
back to the integer operation, which will then find the integer value
of that line and return that.  If, on the other hand, you say

    sort( &lt;STDIN&gt; )

then the sort operation provides list context for &lt;&gt;, which
will proceed to read every line available up to the end of file, and
pass that list of lines back to the sort routine, which will then
sort those lines and return them as a list to whatever the context
of the sort was.

Assignment is a little bit special in that it uses its left argument
to determine the context for the right argument.  Assignment to a
scalar evaluates the right-hand side in scalar context, while
assignment to an array or hash evaluates the righthand side in list
context.  Assignment to a list (or slice, which is just a list
anyway) also evaluates the right-hand side in list context.

When you use the C&lt;use warnings&gt; pragma or Perl&#39;s B&lt;-w&gt; command-line 
option, you may see warnings
about useless uses of constants or functions in &quot;void context&quot;.
Void context just means the value has been discarded, such as a
statement containing only C&lt;&quot;fred&quot;;&gt; or C&lt;getpwuid(0);&gt;.  It still
counts as scalar context for functions that care whether or not
they&#39;re being called in list context.

User-defined subroutines may choose to care whether they are being
called in a void, scalar, or list context.  Most subroutines do not
need to bother, though.  That&#39;s because both scalars and lists are
automatically interpolated into lists.  See L&lt;perlfunc/wantarray&gt;
for how you would dynamically discern your function&#39;s calling
context.

=head2 Scalar values
X&lt;scalar&gt; X&lt;number&gt; X&lt;string&gt; X&lt;reference&gt;

All data in Perl is a scalar, an array of scalars, or a hash of
scalars.  A scalar may contain one single value in any of three
different flavors: a number, a string, or a reference.  In general,
conversion from one form to another is transparent.  Although a
scalar may not directly hold multiple values, it may contain a
reference to an array or hash which in turn contains multiple values.

Scalars aren&#39;t necessarily one thing or another.  There&#39;s no place
to declare a scalar variable to be of type &quot;string&quot;, type &quot;number&quot;,
type &quot;reference&quot;, or anything else.  Because of the automatic
conversion of scalars, operations that return scalars don&#39;t need
to care (and in fact, cannot care) whether their caller is looking
for a string, a number, or a reference.  Perl is a contextually
polymorphic language whose scalars can be strings, numbers, or
references (which includes objects).  Although strings and numbers
are considered pretty much the same thing for nearly all purposes,
references are strongly-typed, uncastable pointers with builtin
reference-counting and destructor invocation.

A scalar value is interpreted as FALSE in the Boolean sense
if it is undefined, the null string or the number 0 (or its
string equivalent, &quot;0&quot;), and TRUE if it is anything else.  The
Boolean context is just a special kind of scalar context where no 
conversion to a string or a number is ever performed.
X&lt;boolean&gt; X&lt;bool&gt; X&lt;true&gt; X&lt;false&gt; X&lt;truth&gt;

There are actually two varieties of null strings (sometimes referred
to as &quot;empty&quot; strings), a defined one and an undefined one.  The
defined version is just a string of length zero, such as C&lt;&quot;&quot;&gt;.
The undefined version is the value that indicates that there is
no real value for something, such as when there was an error, or
at end of file, or when you refer to an uninitialized variable or
element of an array or hash.  Although in early versions of Perl,
an undefined scalar could become defined when first used in a
place expecting a defined value, this no longer happens except for
rare cases of autovivification as explained in L&lt;perlref&gt;.  You can
use the defined() operator to determine whether a scalar value is
defined (this has no meaning on arrays or hashes), and the undef()
operator to produce an undefined value.
X&lt;defined&gt; X&lt;undefined&gt; X&lt;undef&gt; X&lt;null&gt; X&lt;string, null&gt;

To find out whether a given string is a valid non-zero number, it&#39;s
sometimes enough to test it against both numeric 0 and also lexical
&quot;0&quot; (although this will cause noises if warnings are on).  That&#39;s 
because strings that aren&#39;t numbers count as 0, just as they do in B&lt;awk&gt;:

    if ($str == 0 &amp;&amp; $str ne &quot;0&quot;)  {
	warn &quot;That doesn&#39;t look like a number&quot;;
    }

That method may be best because otherwise you won&#39;t treat IEEE
notations like C&lt;NaN&gt; or C&lt;Infinity&gt; properly.  At other times, you
might prefer to determine whether string data can be used numerically
by calling the POSIX::strtod() function or by inspecting your string
with a regular expression (as documented in L&lt;perlre&gt;).

    warn &quot;has nondigits&quot;	if     /\D/;
    warn &quot;not a natural number&quot; unless /^\d+$/;             # rejects -3
    warn &quot;not an integer&quot;       unless /^-?\d+$/;           # rejects +3
    warn &quot;not an integer&quot;       unless /^[+-]?\d+$/;
    warn &quot;not a decimal number&quot; unless /^-?\d+\.?\d*$/;     # rejects .2
    warn &quot;not a decimal number&quot; unless /^-?(?:\d+(?:\.\d*)?|\.\d+)$/;
    warn &quot;not a C float&quot;
	unless /^([+-]?)(?=\d|\.\d)\d*(\.\d*)?([Ee]([+-]?\d+))?$/;

The length of an array is a scalar value.  You may find the length
of array @days by evaluating C&lt;$#days&gt;, as in B&lt;csh&gt;.  However, this
isn&#39;t the length of the array; it&#39;s the subscript of the last element,
which is a different value since there is ordinarily a 0th element.
Assigning to C&lt;$#days&gt; actually changes the length of the array.
Shortening an array this way destroys intervening values.  Lengthening
an array that was previously shortened does not recover values
that were in those elements.
X&lt;$#&gt; X&lt;array, length&gt;

You can also gain some minuscule measure of efficiency by pre-extending
an array that is going to get big.  You can also extend an array
by assigning to an element that is off the end of the array.  You
can truncate an array down to nothing by assigning the null list
() to it.  The following are equivalent:

    @whatever = ();
    $#whatever = -1;

If you evaluate an array in scalar context, it returns the length
of the array.  (Note that this is not true of lists, which return
the last value, like the C comma operator, nor of built-in functions,
which return whatever they feel like returning.)  The following is
always true:
X&lt;array, length&gt;

    scalar(@whatever) == $#whatever + 1;

Some programmers choose to use an explicit conversion so as to 
leave nothing to doubt:

    $element_count = scalar(@whatever);

If you evaluate a hash in scalar context, it returns false if the
hash is empty.  If there are any key/value pairs, it returns true.
A more precise definition is version dependent.

Prior to Perl 5.25 the value returned was a string consisting of the
number of used buckets and the number of allocated buckets, separated
by a slash.  This is pretty much useful only to find out whether
Perl&#39;s internal hashing algorithm is performing poorly on your data
set.  For example, you stick 10,000 things in a hash, but evaluating
%HASH in scalar context reveals C&lt;&quot;1/16&quot;&gt;, which means only one out
of sixteen buckets has been touched, and presumably contains all
10,000 of your items.  This isn&#39;t supposed to happen.

As of Perl 5.25 the return was changed to be the count of keys in the
hash. If you need access to the old behavior you can use
C&lt;Hash::Util::bucket_ratio()&gt; instead.

If a tied hash is evaluated in scalar context, the C&lt;SCALAR&gt; method is
called (with a fallback to C&lt;FIRSTKEY&gt;).
X&lt;hash, scalar context&gt; X&lt;hash, bucket&gt; X&lt;bucket&gt;

You can preallocate space for a hash by assigning to the keys() function.
This rounds up the allocated buckets to the next power of two:

    keys(%users) = 1000;		# allocate 1024 buckets

=head2 Scalar value constructors
X&lt;scalar, literal&gt; X&lt;scalar, constant&gt;

Numeric literals are specified in any of the following floating point or
integer formats:

 12345
 12345.67
 .23E-10             # a very small number
 3.14_15_92          # a very important number
 4_294_967_296       # underscore for legibility
 0xff                # hex
 0xdead_beef         # more hex
 0377                # octal (only numbers, begins with 0)
 0b011011            # binary
 0x1.999ap-4         # hexadecimal floating point (the &#39;p&#39; is required)

You are allowed to use underscores (underbars) in numeric literals
between digits for legibility (but not multiple underscores in a row:
C&lt;23__500&gt; is not legal; C&lt;23_500&gt; is).
You could, for example, group binary
digits by threes (as for a Unix-style mode argument such as 0b110_100_100)
or by fours (to represent nibbles, as in 0b1010_0110) or in other groups.
X&lt;number, literal&gt;

String literals are usually delimited by either single or double
quotes.  They work much like quotes in the standard Unix shells:
double-quoted string literals are subject to backslash and variable
substitution; single-quoted strings are not (except for C&lt;\&#39;&gt; and
C&lt;\\&gt;).  The usual C-style backslash rules apply for making
characters such as newline, tab, etc., as well as some more exotic
forms.  See L&lt;perlop/&quot;Quote and Quote-like Operators&quot;&gt; for a list.
X&lt;string, literal&gt;

Hexadecimal, octal, or binary, representations in string literals
(e.g. &#39;0xff&#39;) are not automatically converted to their integer
representation.  The hex() and oct() functions make these conversions
for you.  See L&lt;perlfunc/hex&gt; and L&lt;perlfunc/oct&gt; for more details.

Hexadecimal floating point can start just like a hexadecimal literal,
and it can be followed by an optional fractional hexadecimal part,
but it must be followed by C&lt;p&gt;, an optional sign, and a power of two.
The format is useful for accurately presenting floating point values,
avoiding conversions to or from decimal floating point, and therefore
avoiding possible loss in precision.  Notice that while most current
platforms use the 64-bit IEEE 754 floating point, not all do.  Another
potential source of (low-order) differences are the floating point
rounding modes, which can differ between CPUs, operating systems,
and compilers, and which Perl doesn&#39;t control.

You can also embed newlines directly in your strings, i.e., they can end
on a different line than they begin.  This is nice, but if you forget
your trailing quote, the error will not be reported until Perl finds
another line containing the quote character, which may be much further
on in the script.  Variable substitution inside strings is limited to
scalar variables, arrays, and array or hash slices.  (In other words,
names beginning with $ or @, followed by an optional bracketed
expression as a subscript.)  The following code segment prints out &quot;The
price is $Z&lt;&gt;100.&quot;
X&lt;interpolation&gt;

    $Price = &#39;$100&#39;;	# not interpolated
    print &quot;The price is $Price.\n&quot;;	# interpolated

There is no double interpolation in Perl, so the C&lt;$100&gt; is left as is.

By default floating point numbers substituted inside strings use the
dot (&quot;.&quot;)  as the decimal separator.  If C&lt;use locale&gt; is in effect,
and POSIX::setlocale() has been called, the character used for the
decimal separator is affected by the LC_NUMERIC locale.
See L&lt;perllocale&gt; and L&lt;POSIX&gt;.

As in some shells, you can enclose the variable name in braces to
disambiguate it from following alphanumerics (and underscores).
You must also do
this when interpolating a variable into a string to separate the
variable name from a following double-colon or an apostrophe, since
these would be otherwise treated as a package separator:
X&lt;interpolation&gt;

    $who = &quot;Larry&quot;;
    print PASSWD &quot;${who}::0:0:Superuser:/:/bin/perl\n&quot;;
    print &quot;We use ${who}speak when ${who}&#39;s here.\n&quot;;

Without the braces, Perl would have looked for a $whospeak, a
C&lt;$who::0&gt;, and a C&lt;$who&#39;s&gt; variable.  The last two would be the
$0 and the $s variables in the (presumably) non-existent package
C&lt;who&gt;.

In fact, a simple identifier within such curlies is forced to be
a string, and likewise within a hash subscript.  Neither need
quoting.  Our earlier example, C&lt;$days{&#39;Feb&#39;}&gt; can be written as
C&lt;$days{Feb}&gt; and the quotes will be assumed automatically.  But
anything more complicated in the subscript will be interpreted as an
expression.  This means for example that C&lt;$version{2.0}++&gt; is
equivalent to C&lt;$version{2}++&gt;, not to C&lt;$version{&#39;2.0&#39;}++&gt;.

=head3 Special floating point: infinity (Inf) and not-a-number (NaN)

Floating point values include the special values C&lt;Inf&gt; and C&lt;NaN&gt;,
for infinity and not-a-number.  The infinity can be also negative.

The infinity is the result of certain math operations that overflow
the floating point range, like 9**9**9.  The not-a-number is the
result when the result is undefined or unrepresentable.  Though note
that you cannot get C&lt;NaN&gt; from some common &quot;undefined&quot; or
&quot;out-of-range&quot; operations like dividing by zero, or square root of
a negative number, since Perl generates fatal errors for those.

The infinity and not-a-number have their own special arithmetic rules.
The general rule is that they are &quot;contagious&quot;: C&lt;Inf&gt; plus one is
C&lt;Inf&gt;, and C&lt;NaN&gt; plus one is C&lt;NaN&gt;.  Where things get interesting
is when you combine infinities and not-a-numbers: C&lt;Inf&gt; minus C&lt;Inf&gt;
and C&lt;Inf&gt; divided by C&lt;Inf&gt; are C&lt;NaN&gt; (while C&lt;Inf&gt; plus C&lt;Inf&gt; is
C&lt;Inf&gt; and C&lt;Inf&gt; times C&lt;Inf&gt; is C&lt;Inf&gt;).  C&lt;NaN&gt; is also curious
in that it does not equal any number, I&lt;including&gt; itself:
C&lt;NaN&gt; != C&lt;NaN&gt;.

Perl doesn&#39;t understand C&lt;Inf&gt; and C&lt;NaN&gt; as numeric literals, but
you can have them as strings, and Perl will convert them as needed:
&quot;Inf&quot; + 1.  (You can, however, import them from the POSIX extension;
C&lt;use POSIX qw(Inf NaN);&gt; and then use them as literals.)

Note that on input (string to number) Perl accepts C&lt;Inf&gt; and C&lt;NaN&gt;
in many forms.   Case is ignored, and the Win32-specific forms like
C&lt;1.#INF&gt; are understood, but on output the values are normalized to
C&lt;Inf&gt; and C&lt;NaN&gt;.

=head3 Version Strings
X&lt;version string&gt; X&lt;vstring&gt; X&lt;v-string&gt;

A literal of the form C&lt;v1.20.300.4000&gt; is parsed as a string composed
of characters with the specified ordinals.  This form, known as
v-strings, provides an alternative, more readable way to construct
strings, rather than use the somewhat less readable interpolation form
C&lt;&quot;\x{1}\x{14}\x{12c}\x{fa0}&quot;&gt;.  This is useful for representing
Unicode strings, and for comparing version &quot;numbers&quot; using the string
comparison operators, C&lt;cmp&gt;, C&lt;gt&gt;, C&lt;lt&gt; etc.  If there are two or
more dots in the literal, the leading C&lt;v&gt; may be omitted.

    print v9786;              # prints SMILEY, &quot;\x{263a}&quot;
    print v102.111.111;       # prints &quot;foo&quot;
    print 102.111.111;        # same

Such literals are accepted by both C&lt;require&gt; and C&lt;use&gt; for
doing a version check.  Note that using the v-strings for IPv4
addresses is not portable unless you also use the
inet_aton()/inet_ntoa() routines of the Socket package.

Note that since Perl 5.8.1 the single-number v-strings (like C&lt;v65&gt;)
are not v-strings before the C&lt;&lt; =&gt; &gt;&gt; operator (which is usually used
to separate a hash key from a hash value); instead they are interpreted
as literal strings (&#39;v65&#39;).  They were v-strings from Perl 5.6.0 to
Perl 5.8.0, but that caused more confusion and breakage than good.
Multi-number v-strings like C&lt;v65.66&gt; and C&lt;65.66.67&gt; continue to
be v-strings always.

=head3 Special Literals
X&lt;special literal&gt; X&lt;__END__&gt; X&lt;__DATA__&gt; X&lt;END&gt; X&lt;DATA&gt;
X&lt;end&gt; X&lt;data&gt; X&lt;^D&gt; X&lt;^Z&gt;

The special literals __FILE__, __LINE__, and __PACKAGE__
represent the current filename, line number, and package name at that
point in your program.  __SUB__ gives a reference to the current
subroutine.  They may be used only as separate tokens; they
will not be interpolated into strings.  If there is no current package
(due to an empty C&lt;package;&gt; directive), __PACKAGE__ is the undefined
value.  (But the empty C&lt;package;&gt; is no longer supported, as of version
5.10.)  Outside of a subroutine, __SUB__ is the undefined value.  __SUB__
is only available in 5.16 or higher, and only with a C&lt;use v5.16&gt; or
C&lt;use feature &quot;current_sub&quot;&gt; declaration.
X&lt;__FILE__&gt; X&lt;__LINE__&gt; X&lt;__PACKAGE__&gt; X&lt;__SUB__&gt;
X&lt;line&gt; X&lt;file&gt; X&lt;package&gt;

The two control characters ^D and ^Z, and the tokens __END__ and __DATA__
may be used to indicate the logical end of the script before the actual
end of file.  Any following text is ignored.

Text after __DATA__ may be read via the filehandle C&lt;PACKNAME::DATA&gt;,
where C&lt;PACKNAME&gt; is the package that was current when the __DATA__
token was encountered.  The filehandle is left open pointing to the
line after __DATA__.  The program should C&lt;close DATA&gt; when it is done
reading from it.  (Leaving it open leaks filehandles if the module is
reloaded for any reason, so it&#39;s a safer practice to close it.)  For
compatibility with older scripts written before __DATA__ was
introduced, __END__ behaves like __DATA__ in the top level script (but
not in files loaded with C&lt;require&gt; or C&lt;do&gt;) and leaves the remaining
contents of the file accessible via C&lt;main::DATA&gt;.

See L&lt;SelfLoader&gt; for more description of __DATA__, and
an example of its use.  Note that you cannot read from the DATA
filehandle in a BEGIN block: the BEGIN block is executed as soon
as it is seen (during compilation), at which point the corresponding
__DATA__ (or __END__) token has not yet been seen.

=head3 Barewords
X&lt;bareword&gt;

A word that has no other interpretation in the grammar will
be treated as if it were a quoted string.  These are known as
&quot;barewords&quot;.  As with filehandles and labels, a bareword that consists
entirely of lowercase letters risks conflict with future reserved
words, and if you use the C&lt;use warnings&gt; pragma or the B&lt;-w&gt; switch, 
Perl will warn you about any such words.  Perl limits barewords (like
identifiers) to about 250 characters.  Future versions of Perl are likely
to eliminate these arbitrary limitations.

Some people may wish to outlaw barewords entirely.  If you
say

    use strict &#39;subs&#39;;

then any bareword that would NOT be interpreted as a subroutine call
produces a compile-time error instead.  The restriction lasts to the
end of the enclosing block.  An inner block may countermand this
by saying C&lt;no strict &#39;subs&#39;&gt;.

=head3 Array Interpolation
X&lt;array, interpolation&gt; X&lt;interpolation, array&gt; X&lt;$&quot;&gt;

Arrays and slices are interpolated into double-quoted strings
by joining the elements with the delimiter specified in the C&lt;$&quot;&gt;
variable (C&lt;$LIST_SEPARATOR&gt; if &quot;use English;&quot; is specified), 
space by default.  The following are equivalent:

    $temp = join($&quot;, @ARGV);
    system &quot;echo $temp&quot;;

    system &quot;echo @ARGV&quot;;

Within search patterns (which also undergo double-quotish substitution)
there is an unfortunate ambiguity:  Is C&lt;/$foo[bar]/&gt; to be interpreted as
C&lt;/${foo}[bar]/&gt; (where C&lt;[bar]&gt; is a character class for the regular
expression) or as C&lt;/${foo[bar]}/&gt; (where C&lt;[bar]&gt; is the subscript to array
@foo)?  If @foo doesn&#39;t otherwise exist, then it&#39;s obviously a
character class.  If @foo exists, Perl takes a good guess about C&lt;[bar]&gt;,
and is almost always right.  If it does guess wrong, or if you&#39;re just
plain paranoid, you can force the correct interpretation with curly
braces as above.

If you&#39;re looking for the information on how to use here-documents,
which used to be here, that&#39;s been moved to
L&lt;perlop/Quote and Quote-like Operators&gt;.

=head2 List value constructors
X&lt;list&gt;

List values are denoted by separating individual values by commas
(and enclosing the list in parentheses where precedence requires it):

    (LIST)

In a context not requiring a list value, the value of what appears
to be a list literal is simply the value of the final element, as
with the C comma operator.  For example,

    @foo = (&#39;cc&#39;, &#39;-E&#39;, $bar);

assigns the entire list value to array @foo, but

    $foo = (&#39;cc&#39;, &#39;-E&#39;, $bar);

assigns the value of variable $bar to the scalar variable $foo.
Note that the value of an actual array in scalar context is the
length of the array; the following assigns the value 3 to $foo:

    @foo = (&#39;cc&#39;, &#39;-E&#39;, $bar);
    $foo = @foo;                # $foo gets 3

You may have an optional comma before the closing parenthesis of a
list literal, so that you can say:

    @foo = (
        1,
        2,
        3,
    );

To use a here-document to assign an array, one line per element,
you might use an approach like this:

    @sauces = &lt;&lt;End_Lines =~ m/(\S.*\S)/g;
        normal tomato
        spicy tomato
        green chile
        pesto
        white wine
    End_Lines

LISTs do automatic interpolation of sublists.  That is, when a LIST is
evaluated, each element of the list is evaluated in list context, and
the resulting list value is interpolated into LIST just as if each
individual element were a member of LIST.  Thus arrays and hashes lose their
identity in a LIST--the list

    (@foo,@bar,&amp;SomeSub,%glarch)

contains all the elements of @foo followed by all the elements of @bar,
followed by all the elements returned by the subroutine named SomeSub 
called in list context, followed by the key/value pairs of %glarch.
To make a list reference that does I&lt;NOT&gt; interpolate, see L&lt;perlref&gt;.

The null list is represented by ().  Interpolating it in a list
has no effect.  Thus ((),(),()) is equivalent to ().  Similarly,
interpolating an array with no elements is the same as if no
array had been interpolated at that point.

This interpolation combines with the facts that the opening
and closing parentheses are optional (except when necessary for
precedence) and lists may end with an optional comma to mean that
multiple commas within lists are legal syntax.  The list C&lt;1,,3&gt; is a
concatenation of two lists, C&lt;1,&gt; and C&lt;3&gt;, the first of which ends
with that optional comma.  C&lt;1,,3&gt; is C&lt;(1,),(3)&gt; is C&lt;1,3&gt; (And
similarly for C&lt;1,,,3&gt; is C&lt;(1,),(,),3&gt; is C&lt;1,3&gt; and so on.)  Not that
we&#39;d advise you to use this obfuscation.

A list value may also be subscripted like a normal array.  You must
put the list in parentheses to avoid ambiguity.  For example:

    # Stat returns list value.
    $time = (stat($file))[8];

    # SYNTAX ERROR HERE.
    $time = stat($file)[8];  # OOPS, FORGOT PARENTHESES

    # Find a hex digit.
    $hexdigit = (&#39;a&#39;,&#39;b&#39;,&#39;c&#39;,&#39;d&#39;,&#39;e&#39;,&#39;f&#39;)[$digit-10];

    # A &quot;reverse comma operator&quot;.
    return (pop(@foo),pop(@foo))[0];

Lists may be assigned to only when each element of the list
is itself legal to assign to:

    ($a, $b, $c) = (1, 2, 3);

    ($map{&#39;red&#39;}, $map{&#39;blue&#39;}, $map{&#39;green&#39;}) = (0x00f, 0x0f0, 0xf00);

An exception to this is that you may assign to C&lt;undef&gt; in a list.
This is useful for throwing away some of the return values of a
function:

    ($dev, $ino, undef, undef, $uid, $gid) = stat($file);

As of Perl 5.22, you can also use C&lt;(undef)x2&gt; instead of C&lt;undef, undef&gt;.
(You can also do C&lt;($x) x 2&gt;, which is less useful, because it assigns to
the same variable twice, clobbering the first value assigned.)

List assignment in scalar context returns the number of elements
produced by the expression on the right side of the assignment:

    $x = (($foo,$bar) = (3,2,1));       # set $x to 3, not 2
    $x = (($foo,$bar) = f());           # set $x to f()&#39;s return count

This is handy when you want to do a list assignment in a Boolean
context, because most list functions return a null list when finished,
which when assigned produces a 0, which is interpreted as FALSE.

It&#39;s also the source of a useful idiom for executing a function or
performing an operation in list context and then counting the number of
return values, by assigning to an empty list and then using that
assignment in scalar context.  For example, this code:

    $count = () = $string =~ /\d+/g;

will place into $count the number of digit groups found in $string.
This happens because the pattern match is in list context (since it
is being assigned to the empty list), and will therefore return a list
of all matching parts of the string.  The list assignment in scalar
context will translate that into the number of elements (here, the
number of times the pattern matched) and assign that to $count.  Note
that simply using

    $count = $string =~ /\d+/g;

would not have worked, since a pattern match in scalar context will
only return true or false, rather than a count of matches.

The final element of a list assignment may be an array or a hash:

    ($a, $b, @rest) = split;
    my($a, $b, %rest) = @_;

You can actually put an array or hash anywhere in the list, but the first one
in the list will soak up all the values, and anything after it will become
undefined.  This may be useful in a my() or local().

A hash can be initialized using a literal list holding pairs of
items to be interpreted as a key and a value:

    # same as map assignment above
    %map = (&#39;red&#39;,0x00f,&#39;blue&#39;,0x0f0,&#39;green&#39;,0xf00);

While literal lists and named arrays are often interchangeable, that&#39;s
not the case for hashes.  Just because you can subscript a list value like
a normal array does not mean that you can subscript a list value as a
hash.  Likewise, hashes included as parts of other lists (including
parameters lists and return lists from functions) always flatten out into
key/value pairs.  That&#39;s why it&#39;s good to use references sometimes.

It is often more readable to use the C&lt;&lt; =&gt; &gt;&gt; operator between key/value
pairs.  The C&lt;&lt; =&gt; &gt;&gt; operator is mostly just a more visually distinctive
synonym for a comma, but it also arranges for its left-hand operand to be
interpreted as a string if it&#39;s a bareword that would be a legal simple
identifier.  C&lt;&lt; =&gt; &gt;&gt; doesn&#39;t quote compound identifiers, that contain
double colons.  This makes it nice for initializing hashes:

    %map = (
                 red   =&gt; 0x00f,
                 blue  =&gt; 0x0f0,
                 green =&gt; 0xf00,
   );

or for initializing hash references to be used as records:

    $rec = {
                witch =&gt; &#39;Mable the Merciless&#39;,
                cat   =&gt; &#39;Fluffy the Ferocious&#39;,
                date  =&gt; &#39;10/31/1776&#39;,
    };

or for using call-by-named-parameter to complicated functions:

   $field = $query-&gt;radio_group(
               name      =&gt; &#39;group_name&#39;,
               values    =&gt; [&#39;eenie&#39;,&#39;meenie&#39;,&#39;minie&#39;],
               default   =&gt; &#39;meenie&#39;,
               linebreak =&gt; &#39;true&#39;,
               labels    =&gt; \%labels
   );

Note that just because a hash is initialized in that order doesn&#39;t
mean that it comes out in that order.  See L&lt;perlfunc/sort&gt; for examples
of how to arrange for an output ordering.

If a key appears more than once in the initializer list of a hash, the last
occurrence wins:

    %circle = (
                  center =&gt; [5, 10],
                  center =&gt; [27, 9],
                  radius =&gt; 100,
                  color =&gt; [0xDF, 0xFF, 0x00],
                  radius =&gt; 54,
    );

    # same as
    %circle = (
                  center =&gt; [27, 9],
                  color =&gt; [0xDF, 0xFF, 0x00],
                  radius =&gt; 54,
    );

This can be used to provide overridable configuration defaults:

    # values in %args take priority over %config_defaults
    %config = (%config_defaults, %args);

=head2 Subscripts

An array can be accessed one scalar at a
time by specifying a dollar sign (C&lt;$&gt;), then the
name of the array (without the leading C&lt;@&gt;), then the subscript inside
square brackets.  For example:

    @myarray = (5, 50, 500, 5000);
    print &quot;The Third Element is&quot;, $myarray[2], &quot;\n&quot;;

The array indices start with 0.  A negative subscript retrieves its 
value from the end.  In our example, C&lt;$myarray[-1]&gt; would have been 
5000, and C&lt;$myarray[-2]&gt; would have been 500.

Hash subscripts are similar, only instead of square brackets curly brackets
are used.  For example:

    %scientists = 
    (
        &quot;Newton&quot; =&gt; &quot;Isaac&quot;,
        &quot;Einstein&quot; =&gt; &quot;Albert&quot;,
        &quot;Darwin&quot; =&gt; &quot;Charles&quot;,
        &quot;Feynman&quot; =&gt; &quot;Richard&quot;,
    );

    print &quot;Darwin&#39;s First Name is &quot;, $scientists{&quot;Darwin&quot;}, &quot;\n&quot;;

You can also subscript a list to get a single element from it:

    $dir = (getpwnam(&quot;daemon&quot;))[7];

=head2 Multi-dimensional array emulation

Multidimensional arrays may be emulated by subscripting a hash with a
list.  The elements of the list are joined with the subscript separator
(see L&lt;perlvar/$;&gt;).

    $foo{$a,$b,$c}

is equivalent to

    $foo{join($;, $a, $b, $c)}

The default subscript separator is &quot;\034&quot;, the same as SUBSEP in B&lt;awk&gt;.

=head2 Slices
X&lt;slice&gt; X&lt;array, slice&gt; X&lt;hash, slice&gt;

A slice accesses several elements of a list, an array, or a hash
simultaneously using a list of subscripts.  It&#39;s more convenient
than writing out the individual elements as a list of separate
scalar values.

    ($him, $her)   = @folks[0,-1];              # array slice
    @them          = @folks[0 .. 3];            # array slice
    ($who, $home)  = @ENV{&quot;USER&quot;, &quot;HOME&quot;};      # hash slice
    ($uid, $dir)   = (getpwnam(&quot;daemon&quot;))[2,7]; # list slice

Since you can assign to a list of variables, you can also assign to
an array or hash slice.

    @days[3..5]    = qw/Wed Thu Fri/;
    @colors{&#39;red&#39;,&#39;blue&#39;,&#39;green&#39;} 
                   = (0xff0000, 0x0000ff, 0x00ff00);
    @folks[0, -1]  = @folks[-1, 0];

The previous assignments are exactly equivalent to

    ($days[3], $days[4], $days[5]) = qw/Wed Thu Fri/;
    ($colors{&#39;red&#39;}, $colors{&#39;blue&#39;}, $colors{&#39;green&#39;})
                   = (0xff0000, 0x0000ff, 0x00ff00);
    ($folks[0], $folks[-1]) = ($folks[-1], $folks[0]);

Since changing a slice changes the original array or hash that it&#39;s
slicing, a C&lt;foreach&gt; construct will alter some--or even all--of the
values of the array or hash.

    foreach (@array[ 4 .. 10 ]) { s/peter/paul/ } 

    foreach (@hash{qw[key1 key2]}) {
        s/^\s+//;           # trim leading whitespace
        s/\s+$//;           # trim trailing whitespace
        s/(\w+)/\u\L$1/g;   # &quot;titlecase&quot; words
    }

As a special exception, when you slice a list (but not an array or a hash),
if the list evaluates to empty, then taking a slice of that empty list will
always yield the empty list in turn.  Thus:

    @a = ()[0,1];          # @a has no elements
    @b = (@a)[0,1];        # @b has no elements
    @c = (sub{}-&gt;())[0,1]; # @c has no elements
    @d = (&#39;a&#39;,&#39;b&#39;)[0,1];   # @d has two elements
    @e = (@d)[0,1,8,9];    # @e has four elements
    @f = (@d)[8,9];        # @f has two elements

This makes it easy to write loops that terminate when a null list
is returned:

    while ( ($home, $user) = (getpwent)[7,0] ) {
        printf &quot;%-8s %s\n&quot;, $user, $home;
    }

As noted earlier in this document, the scalar sense of list assignment
is the number of elements on the right-hand side of the assignment.
The null list contains no elements, so when the password file is
exhausted, the result is 0, not 2.

Slices in scalar context return the last item of the slice.

    @a = qw/first second third/;
    %h = (first =&gt; &#39;A&#39;, second =&gt; &#39;B&#39;);
    $t = @a[0, 1];                  # $t is now &#39;second&#39;
    $u = @h{&#39;first&#39;, &#39;second&#39;};     # $u is now &#39;B&#39;

If you&#39;re confused about why you use an &#39;@&#39; there on a hash slice
instead of a &#39;%&#39;, think of it like this.  The type of bracket (square
or curly) governs whether it&#39;s an array or a hash being looked at.
On the other hand, the leading symbol (&#39;$&#39; or &#39;@&#39;) on the array or
hash indicates whether you are getting back a singular value (a
scalar) or a plural one (a list).

=head3 Key/Value Hash Slices

Starting in Perl 5.20, a hash slice operation
with the % symbol is a variant of slice operation
returning a list of key/value pairs rather than just values:

    %h = (blonk =&gt; 2, foo =&gt; 3, squink =&gt; 5, bar =&gt; 8);
    %subset = %h{&#39;foo&#39;, &#39;bar&#39;}; # key/value hash slice
    # %subset is now (foo =&gt; 3, bar =&gt; 8)

However, the result of such a slice cannot be localized, deleted or used
in assignment.  These are otherwise very much consistent with hash slices
using the @ symbol.

=head3 Index/Value Array Slices

Similar to key/value hash slices (and also introduced
in Perl 5.20), the % array slice syntax returns a list
of index/value pairs:

    @a = &quot;a&quot;..&quot;z&quot;;
    @list = %a[3,4,6];
    # @list is now (3, &quot;d&quot;, 4, &quot;e&quot;, 6, &quot;g&quot;)

=head2 Typeglobs and Filehandles
X&lt;typeglob&gt; X&lt;filehandle&gt; X&lt;*&gt;

Perl uses an internal type called a I&lt;typeglob&gt; to hold an entire
symbol table entry.  The type prefix of a typeglob is a C&lt;*&gt;, because
it represents all types.  This used to be the preferred way to
pass arrays and hashes by reference into a function, but now that
we have real references, this is seldom needed.  

The main use of typeglobs in modern Perl is create symbol table aliases.
This assignment:

    *this = *that;

makes $this an alias for $that, @this an alias for @that, %this an alias
for %that, &amp;this an alias for &amp;that, etc.  Much safer is to use a reference.
This:

    local *Here::blue = \$There::green;

temporarily makes $Here::blue an alias for $There::green, but doesn&#39;t
make @Here::blue an alias for @There::green, or %Here::blue an alias for
%There::green, etc.  See L&lt;perlmod/&quot;Symbol Tables&quot;&gt; for more examples
of this.  Strange though this may seem, this is the basis for the whole
module import/export system.

Another use for typeglobs is to pass filehandles into a function or
to create new filehandles.  If you need to use a typeglob to save away
a filehandle, do it this way:

    $fh = *STDOUT;

or perhaps as a real reference, like this:

    $fh = \*STDOUT;

See L&lt;perlsub&gt; for examples of using these as indirect filehandles
in functions.

Typeglobs are also a way to create a local filehandle using the local()
operator.  These last until their block is exited, but may be passed back.
For example:

    sub newopen {
        my $path = shift;
        local  *FH;  # not my!
        open   (FH, $path)          or  return undef;
        return *FH;
    }
    $fh = newopen(&#39;/etc/passwd&#39;);

Now that we have the C&lt;*foo{THING}&gt; notation, typeglobs aren&#39;t used as much
for filehandle manipulations, although they&#39;re still needed to pass brand
new file and directory handles into or out of functions.  That&#39;s because
C&lt;*HANDLE{IO}&gt; only works if HANDLE has already been used as a handle.
In other words, C&lt;*FH&gt; must be used to create new symbol table entries;
C&lt;*foo{THING}&gt; cannot.  When in doubt, use C&lt;*FH&gt;.

All functions that are capable of creating filehandles (open(),
opendir(), pipe(), socketpair(), sysopen(), socket(), and accept())
automatically create an anonymous filehandle if the handle passed to
them is an uninitialized scalar variable.  This allows the constructs
such as C&lt;open(my $fh, ...)&gt; and C&lt;open(local $fh,...)&gt; to be used to
create filehandles that will conveniently be closed automatically when
the scope ends, provided there are no other references to them.  This
largely eliminates the need for typeglobs when opening filehandles
that must be passed around, as in the following example:

    sub myopen {
        open my $fh, &quot;@_&quot;
             or die &quot;Can&#39;t open &#39;@_&#39;: $!&quot;;
        return $fh;
    }

    {
        my $f = myopen(&quot;&lt;/etc/motd&quot;);
        print &lt;$f&gt;;
        # $f implicitly closed here
    }

Note that if an initialized scalar variable is used instead the
result is different: C&lt;my $fh=&#39;zzz&#39;; open($fh, ...)&gt; is equivalent
to C&lt;open( *{&#39;zzz&#39;}, ...)&gt;.
C&lt;use strict &#39;refs&#39;&gt; forbids such practice.

Another way to create anonymous filehandles is with the Symbol
module or with the IO::Handle module and its ilk.  These modules
have the advantage of not hiding different types of the same name
during the local().  See the bottom of L&lt;perlfunc/open&gt; for an
example.

=head1 SEE ALSO

See L&lt;perlvar&gt; for a description of Perl&#39;s built-in variables and
a discussion of legal variable names.  See L&lt;perlref&gt;, L&lt;perlsub&gt;,
and L&lt;perlmod/&quot;Symbol Tables&quot;&gt; for more discussion on typeglobs and
the C&lt;*foo{THING}&gt; syntax.
</pre>

                    </main>
                </div>

        </div>
        <footer>
        
        </footer>


    </body>
</html>

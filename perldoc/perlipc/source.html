<!DOCTYPE html>
<html>
    <head>
        <meta charset="utf-8">
        <meta content="width=device-width, initial-scale=1" name="viewport">
        <link href="/theme/css/normalize.css" rel="stylesheet">
        <!--<link rel="stylesheet" href="/theme/css/skeleton.css" />-->
        <link href="/theme/css/statocles-default.css" rel="stylesheet">
        <link href="//maxcdn.bootstrapcdn.com/font-awesome/4.3.0/css/font-awesome.min.css" rel="stylesheet">
        <script crossorigin="anonymous" integrity="sha256-hwg4gsxgFZhOsEEamdOYGBf13FyQuiTwlAQgxVSNgt4=" src="http://code.jquery.com/jquery-3.2.1.min.js"></script>
        <title>perlipc (source) - Minimum Viable Perl</title>
        <meta content="Statocles 0.086" name="generator">
        
        <script>
            $( document ).ready(function() {
                $('.sub-menu ul').hide();
                $('.sub-menu.perldoc ul').show();
                $('.sub-menu.perldoc div').toggleClass("fa-caret-up fa-caret-down");
                $(".sub-menu a").click(function () {
                    $(this).parent(".sub-menu").siblings().children('ul').hide();
                    $(this).parent(".sub-menu").children("ul").slideToggle("100");
                    $(this).find(".right").toggleClass("fa-caret-up fa-caret-down");
                });
            });
        </script>
        <!-- Global site tag (gtag.js) - Google Analytics -->
        <script async src="https://www.googletagmanager.com/gtag/js?id=UA-110455056-1"></script>
        <script>
            window.dataLayer = window.dataLayer || [];
            function gtag(){dataLayer.push(arguments);}
            gtag('js', new Date());
            gtag('config', 'UA-110455056-1');
        </script>
    </head>
    <body>
        <header>
        </header>
        <div class="container">

                <div class="left column full-height">
                    <div class="sidebar">
                    
<a class="home" href="/">Minimum<br>Viable<br>Perl</a><br><br><br>

<div class="animated bounceInDown">
  <ul>
    <li class="basics sub-menu">
      <a href="/perldoc/perlipc/#basics">The basics<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/basics/hello">Hello world</a></li>
        <li><a href="/basics/variables">Variables</a></li>
        <li><a href="/basics/references">References</a></li>
        <li><a href="/basics/functions">Functions</a></li>
        <li><a href="/basics/flow">Conditionals</a></li>
        <li><a href="/basics/loops">Loops</a></li>
        <li><a href="/basics/exceptions">Exceptions</a></li>
        <li><a href="/basics/regexps">Regexps</a></li>
        <li><a href="/basics/files">Files</a></li>
      </ul>
    </li>
    <li class="oo sub-menu">
      <a href="/perldoc/perlipc/#oo">Object oriented programming<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/oo/story">The (weird) story of Perl OO</a></li>
        <li><a href="/oo/classes">Classes</a></li>
        <li><a href="/oo/attributes">Attributes</a></li>
        <li><a href="/oo/modifiers">Method modifiers</a></li>
        <li><a href="/oo/roles">Roles</a></li>
        <li><a href="/oo/inheritance">Inheritance</a></li>
      </ul>
    </li>
    <li class="debugging sub-menu">
      <a href="/perldoc/perlipc/#basics">Debugging<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/debugging/data-printer">Examining data structures</a></li>
        <li><a href="/debugging/stack-trace">Forcing stack traces</a></li>
      </ul>
    </li>
    <li class="dependencies sub-menu">
      <a href="/perldoc/perlipc/#dependencies">Perl libraries<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/dependencies/perl5lib">Loading libraries from custom locations</a></li>
        <li><a href="/dependencies/cpanm">Installing libraries with cpanm</a></li>
        <li><a href="/dependencies/metacpan">Choosing libraries from MetaCPAN</a></li>
        <li><a href="/dependencies/carton">Managing project dependencies with Carton</a></li>
        <li><a href="/dependencies/pmtools">What version is installed and where?</a></li>
      </ul>
    </li>
    <li class="testing sub-menu">
      <a href="/perldoc/perlipc/#testing">Testing<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/testing/intro">Introduction to testing</a></li>
        <li><a href="/testing/warnings">Testing warnings and exceptions</a></li>
        <li><a href="/testing/data">Testing data structures</a></li>
        <li><a href="/testing/mock">Testing with mock objects</a></li>
        <li><a href="/testing/libs">Build your own test libraries</a></li>
      </ul>
    </li>
    <li><a href="/perl">Perl design goals</a></li>
    <li><a href="/see-also">Book recommendations</a></li>
    <li><a href="/about">About this book</a></li>
  </ul>
</div>

                    
                    
                    </div>
                </div>

                <div class="right column">
                    <main>
                    <a class="button" href="/perldoc/perlipc/">
    Back to documentation
</a>
<pre>=head1 NAME

perlipc - Perl interprocess communication (signals, fifos, pipes, safe subprocesses, sockets, and semaphores)

=head1 DESCRIPTION

The basic IPC facilities of Perl are built out of the good old Unix
signals, named pipes, pipe opens, the Berkeley socket routines, and SysV
IPC calls.  Each is used in slightly different situations.

=head1 Signals

Perl uses a simple signal handling model: the %SIG hash contains names
or references of user-installed signal handlers.  These handlers will
be called with an argument which is the name of the signal that
triggered it.  A signal may be generated intentionally from a
particular keyboard sequence like control-C or control-Z, sent to you
from another process, or triggered automatically by the kernel when
special events transpire, like a child process exiting, your own process
running out of stack space, or hitting a process file-size limit.

For example, to trap an interrupt signal, set up a handler like this:

    our $shucks;

    sub catch_zap {
        my $signame = shift;
        $shucks++;
        die &quot;Somebody sent me a SIG$signame&quot;;
    }
    $SIG{INT} = __PACKAGE__ . &quot;::catch_zap&quot;;
    $SIG{INT} = \&amp;catch_zap;  # best strategy

Prior to Perl 5.8.0 it was necessary to do as little as you possibly
could in your handler; notice how all we do is set a global variable
and then raise an exception.  That&#39;s because on most systems,
libraries are not re-entrant; particularly, memory allocation and I/O
routines are not.  That meant that doing nearly I&lt;anything&gt; in your
handler could in theory trigger a memory fault and subsequent core
dump - see L&lt;/Deferred Signals (Safe Signals)&gt; below.

The names of the signals are the ones listed out by C&lt;kill -l&gt; on your
system, or you can retrieve them using the CPAN module L&lt;IPC::Signal&gt;.

You may also choose to assign the strings C&lt;&quot;IGNORE&quot;&gt; or C&lt;&quot;DEFAULT&quot;&gt; as
the handler, in which case Perl will try to discard the signal or do the
default thing.

On most Unix platforms, the C&lt;CHLD&gt; (sometimes also known as C&lt;CLD&gt;) signal
has special behavior with respect to a value of C&lt;&quot;IGNORE&quot;&gt;.
Setting C&lt;$SIG{CHLD}&gt; to C&lt;&quot;IGNORE&quot;&gt; on such a platform has the effect of
not creating zombie processes when the parent process fails to C&lt;wait()&gt;
on its child processes (i.e., child processes are automatically reaped).
Calling C&lt;wait()&gt; with C&lt;$SIG{CHLD}&gt; set to C&lt;&quot;IGNORE&quot;&gt; usually returns
C&lt;-1&gt; on such platforms.

Some signals can be neither trapped nor ignored, such as the KILL and STOP
(but not the TSTP) signals. Note that ignoring signals makes them disappear.
If you only want them blocked temporarily without them getting lost you&#39;ll
have to use POSIX&#39; sigprocmask.

Sending a signal to a negative process ID means that you send the signal
to the entire Unix process group.  This code sends a hang-up signal to all
processes in the current process group, and also sets $SIG{HUP} to C&lt;&quot;IGNORE&quot;&gt;
so it doesn&#39;t kill itself:

    # block scope for local
    {
        local $SIG{HUP} = &quot;IGNORE&quot;;
        kill HUP =&gt; -$$;
        # snazzy writing of: kill(&quot;HUP&quot;, -$$)
    }

Another interesting signal to send is signal number zero.  This doesn&#39;t
actually affect a child process, but instead checks whether it&#39;s alive
or has changed its UIDs.

    unless (kill 0 =&gt; $kid_pid) {
        warn &quot;something wicked happened to $kid_pid&quot;;
    }

Signal number zero may fail because you lack permission to send the
signal when directed at a process whose real or saved UID is not
identical to the real or effective UID of the sending process, even
though the process is alive.  You may be able to determine the cause of
failure using C&lt;$!&gt; or C&lt;%!&gt;.

    unless (kill(0 =&gt; $pid) || $!{EPERM}) {
        warn &quot;$pid looks dead&quot;;
    }

You might also want to employ anonymous functions for simple signal
handlers:

    $SIG{INT} = sub { die &quot;\nOutta here!\n&quot; };

SIGCHLD handlers require some special care.  If a second child dies
while in the signal handler caused by the first death, we won&#39;t get
another signal. So must loop here else we will leave the unreaped child
as a zombie. And the next time two children die we get another zombie.
And so on.

    use POSIX &quot;:sys_wait_h&quot;;
    $SIG{CHLD} = sub {
        while ((my $child = waitpid(-1, WNOHANG)) &gt; 0) {
            $Kid_Status{$child} = $?;
        }
    };
    # do something that forks...

Be careful: qx(), system(), and some modules for calling external commands
do a fork(), then wait() for the result. Thus, your signal handler
will be called. Because wait() was already called by system() or qx(),
the wait() in the signal handler will see no more zombies and will
therefore block.

The best way to prevent this issue is to use waitpid(), as in the following
example:

    use POSIX &quot;:sys_wait_h&quot;; # for nonblocking read

    my %children;

    $SIG{CHLD} = sub {
        # don&#39;t change $! and $? outside handler
        local ($!, $?);
        while ( (my $pid = waitpid(-1, WNOHANG)) &gt; 0 ) {
            delete $children{$pid};
            cleanup_child($pid, $?);
        }
    };

    while (1) {
        my $pid = fork();
        die &quot;cannot fork&quot; unless defined $pid;
        if ($pid == 0) {
            # ...
            exit 0;
        } else {
            $children{$pid}=1;
            # ...
            system($command);
            # ...
       }
    }

Signal handling is also used for timeouts in Unix.  While safely
protected within an C&lt;eval{}&gt; block, you set a signal handler to trap
alarm signals and then schedule to have one delivered to you in some
number of seconds.  Then try your blocking operation, clearing the alarm
when it&#39;s done but not before you&#39;ve exited your C&lt;eval{}&gt; block.  If it
goes off, you&#39;ll use die() to jump out of the block.

Here&#39;s an example:

    my $ALARM_EXCEPTION = &quot;alarm clock restart&quot;;
    eval {
        local $SIG{ALRM} = sub { die $ALARM_EXCEPTION };
        alarm 10;
        flock(FH, 2)    # blocking write lock
                        || die &quot;cannot flock: $!&quot;;
        alarm 0;
    };
    if ($@ &amp;&amp; $@ !~ quotemeta($ALARM_EXCEPTION)) { die }

If the operation being timed out is system() or qx(), this technique
is liable to generate zombies.    If this matters to you, you&#39;ll
need to do your own fork() and exec(), and kill the errant child process.

For more complex signal handling, you might see the standard POSIX
module.  Lamentably, this is almost entirely undocumented, but the
F&lt;ext/POSIX/t/sigaction.t&gt; file from the Perl source distribution has
some examples in it.

=head2 Handling the SIGHUP Signal in Daemons

A process that usually starts when the system boots and shuts down
when the system is shut down is called a daemon (Disk And Execution
MONitor). If a daemon process has a configuration file which is
modified after the process has been started, there should be a way to
tell that process to reread its configuration file without stopping
the process. Many daemons provide this mechanism using a C&lt;SIGHUP&gt;
signal handler. When you want to tell the daemon to reread the file,
simply send it the C&lt;SIGHUP&gt; signal.

The following example implements a simple daemon, which restarts
itself every time the C&lt;SIGHUP&gt; signal is received. The actual code is
located in the subroutine C&lt;code()&gt;, which just prints some debugging
info to show that it works; it should be replaced with the real code.

  #!/usr/bin/perl

  use strict;
  use warnings;

  use POSIX ();
  use FindBin ();
  use File::Basename ();
  use File::Spec::Functions qw(catfile);

  $| = 1;

  # make the daemon cross-platform, so exec always calls the script
  # itself with the right path, no matter how the script was invoked.
  my $script = File::Basename::basename($0);
  my $SELF  = catfile($FindBin::Bin, $script);

  # POSIX unmasks the sigprocmask properly
  $SIG{HUP} = sub {
      print &quot;got SIGHUP\n&quot;;
      exec($SELF, @ARGV)        || die &quot;$0: couldn&#39;t restart: $!&quot;;
  };

  code();

  sub code {
      print &quot;PID: $$\n&quot;;
      print &quot;ARGV: @ARGV\n&quot;;
      my $count = 0;
      while (1) {
          sleep 2;
          print ++$count, &quot;\n&quot;;
      }
  }


=head2 Deferred Signals (Safe Signals)

Before Perl 5.8.0, installing Perl code to deal with signals exposed you to
danger from two things.  First, few system library functions are
re-entrant.  If the signal interrupts while Perl is executing one function
(like malloc(3) or printf(3)), and your signal handler then calls the same
function again, you could get unpredictable behavior--often, a core dump.
Second, Perl isn&#39;t itself re-entrant at the lowest levels.  If the signal
interrupts Perl while Perl is changing its own internal data structures,
similarly unpredictable behavior may result.

There were two things you could do, knowing this: be paranoid or be
pragmatic.  The paranoid approach was to do as little as possible in your
signal handler.  Set an existing integer variable that already has a
value, and return.  This doesn&#39;t help you if you&#39;re in a slow system call,
which will just restart.  That means you have to C&lt;die&gt; to longjmp(3) out
of the handler.  Even this is a little cavalier for the true paranoiac,
who avoids C&lt;die&gt; in a handler because the system I&lt;is&gt; out to get you.
The pragmatic approach was to say &quot;I know the risks, but prefer the
convenience&quot;, and to do anything you wanted in your signal handler,
and be prepared to clean up core dumps now and again.

Perl 5.8.0 and later avoid these problems by &quot;deferring&quot; signals.  That is,
when the signal is delivered to the process by the system (to the C code
that implements Perl) a flag is set, and the handler returns immediately.
Then at strategic &quot;safe&quot; points in the Perl interpreter (e.g. when it is
about to execute a new opcode) the flags are checked and the Perl level
handler from %SIG is executed. The &quot;deferred&quot; scheme allows much more
flexibility in the coding of signal handlers as we know the Perl
interpreter is in a safe state, and that we are not in a system library
function when the handler is called.  However the implementation does
differ from previous Perls in the following ways:

=over 4

=item Long-running opcodes

As the Perl interpreter looks at signal flags only when it is about
to execute a new opcode, a signal that arrives during a long-running
opcode (e.g. a regular expression operation on a very large string) will
not be seen until the current opcode completes.

If a signal of any given type fires multiple times during an opcode
(such as from a fine-grained timer), the handler for that signal will
be called only once, after the opcode completes; all other
instances will be discarded.  Furthermore, if your system&#39;s signal queue
gets flooded to the point that there are signals that have been raised
but not yet caught (and thus not deferred) at the time an opcode
completes, those signals may well be caught and deferred during
subsequent opcodes, with sometimes surprising results.  For example, you
may see alarms delivered even after calling C&lt;alarm(0)&gt; as the latter
stops the raising of alarms but does not cancel the delivery of alarms
raised but not yet caught.  Do not depend on the behaviors described in
this paragraph as they are side effects of the current implementation and
may change in future versions of Perl.

=item Interrupting IO

When a signal is delivered (e.g., SIGINT from a control-C) the operating
system breaks into IO operations like I&lt;read&gt;(2), which is used to
implement Perl&#39;s readline() function, the C&lt;&lt; &lt;&gt; &gt;&gt; operator. On older
Perls the handler was called immediately (and as C&lt;read&gt; is not &quot;unsafe&quot;,
this worked well). With the &quot;deferred&quot; scheme the handler is I&lt;not&gt; called
immediately, and if Perl is using the system&#39;s C&lt;stdio&gt; library that
library may restart the C&lt;read&gt; without returning to Perl to give it a
chance to call the %SIG handler. If this happens on your system the
solution is to use the C&lt;:perlio&gt; layer to do IO--at least on those handles
that you want to be able to break into with signals. (The C&lt;:perlio&gt; layer
checks the signal flags and calls %SIG handlers before resuming IO
operation.)

The default in Perl 5.8.0 and later is to automatically use
the C&lt;:perlio&gt; layer.

Note that it is not advisable to access a file handle within a signal
handler where that signal has interrupted an I/O operation on that same
handle. While perl will at least try hard not to crash, there are no
guarantees of data integrity; for example, some data might get dropped or
written twice.

Some networking library functions like gethostbyname() are known to have
their own implementations of timeouts which may conflict with your
timeouts.  If you have problems with such functions, try using the POSIX
sigaction() function, which bypasses Perl safe signals.  Be warned that
this does subject you to possible memory corruption, as described above.

Instead of setting C&lt;$SIG{ALRM}&gt;:

   local $SIG{ALRM} = sub { die &quot;alarm&quot; };

try something like the following:

 use POSIX qw(SIGALRM);
 POSIX::sigaction(SIGALRM,
                  POSIX::SigAction-&gt;new(sub { die &quot;alarm&quot; }))
          || die &quot;Error setting SIGALRM handler: $!\n&quot;;

Another way to disable the safe signal behavior locally is to use
the C&lt;Perl::Unsafe::Signals&gt; module from CPAN, which affects
all signals.

=item Restartable system calls

On systems that supported it, older versions of Perl used the
SA_RESTART flag when installing %SIG handlers.  This meant that
restartable system calls would continue rather than returning when
a signal arrived.  In order to deliver deferred signals promptly,
Perl 5.8.0 and later do I&lt;not&gt; use SA_RESTART.  Consequently,
restartable system calls can fail (with $! set to C&lt;EINTR&gt;) in places
where they previously would have succeeded.

The default C&lt;:perlio&gt; layer retries C&lt;read&gt;, C&lt;write&gt;
and C&lt;close&gt; as described above; interrupted C&lt;wait&gt; and
C&lt;waitpid&gt; calls will always be retried.

=item Signals as &quot;faults&quot;

Certain signals like SEGV, ILL, and BUS are generated by virtual memory
addressing errors and similar &quot;faults&quot;. These are normally fatal: there is
little a Perl-level handler can do with them.  So Perl delivers them
immediately rather than attempting to defer them.

=item Signals triggered by operating system state

On some operating systems certain signal handlers are supposed to &quot;do
something&quot; before returning. One example can be CHLD or CLD, which
indicates a child process has completed. On some operating systems the
signal handler is expected to C&lt;wait&gt; for the completed child
process. On such systems the deferred signal scheme will not work for
those signals: it does not do the C&lt;wait&gt;. Again the failure will
look like a loop as the operating system will reissue the signal because
there are completed child processes that have not yet been C&lt;wait&gt;ed for.

=back

If you want the old signal behavior back despite possible
memory corruption, set the environment variable C&lt;PERL_SIGNALS&gt; to
C&lt;&quot;unsafe&quot;&gt;.  This feature first appeared in Perl 5.8.1.

=head1 Named Pipes

A named pipe (often referred to as a FIFO) is an old Unix IPC
mechanism for processes communicating on the same machine.  It works
just like regular anonymous pipes, except that the
processes rendezvous using a filename and need not be related.

To create a named pipe, use the C&lt;POSIX::mkfifo()&gt; function.

    use POSIX qw(mkfifo);
    mkfifo($path, 0700)     ||  die &quot;mkfifo $path failed: $!&quot;;

You can also use the Unix command mknod(1), or on some
systems, mkfifo(1).  These may not be in your normal path, though.

    # system return val is backwards, so &amp;&amp; not ||
    #
    $ENV{PATH} .= &quot;:/etc:/usr/etc&quot;;
    if  (      system(&quot;mknod&quot;,  $path, &quot;p&quot;)
            &amp;&amp; system(&quot;mkfifo&quot;, $path) )
    {
        die &quot;mk{nod,fifo} $path failed&quot;;
    }


A fifo is convenient when you want to connect a process to an unrelated
one.  When you open a fifo, the program will block until there&#39;s something
on the other end.

For example, let&#39;s say you&#39;d like to have your F&lt;.signature&gt; file be a
named pipe that has a Perl program on the other end.  Now every time any
program (like a mailer, news reader, finger program, etc.) tries to read
from that file, the reading program will read the new signature from your
program.  We&#39;ll use the pipe-checking file-test operator, B&lt;-p&gt;, to find
out whether anyone (or anything) has accidentally removed our fifo.

    chdir();    # go home
    my $FIFO = &quot;.signature&quot;;

    while (1) {
        unless (-p $FIFO) {
            unlink $FIFO;   # discard any failure, will catch later
            require POSIX;  # delayed loading of heavy module
            POSIX::mkfifo($FIFO, 0700)
                                || die &quot;can&#39;t mkfifo $FIFO: $!&quot;;
        }

        # next line blocks till there&#39;s a reader
        open (FIFO, &quot;&gt; $FIFO&quot;)  || die &quot;can&#39;t open $FIFO: $!&quot;;
        print FIFO &quot;John Smith (smith\@host.org)\n&quot;, `fortune -s`;
        close(FIFO)             || die &quot;can&#39;t close $FIFO: $!&quot;;
        sleep 2;                # to avoid dup signals
    }

=head1 Using open() for IPC

Perl&#39;s basic open() statement can also be used for unidirectional
interprocess communication by either appending or prepending a pipe
symbol to the second argument to open().  Here&#39;s how to start
something up in a child process you intend to write to:

    open(SPOOLER, &quot;| cat -v | lpr -h 2&gt;/dev/null&quot;)
                        || die &quot;can&#39;t fork: $!&quot;;
    local $SIG{PIPE} = sub { die &quot;spooler pipe broke&quot; };
    print SPOOLER &quot;stuff\n&quot;;
    close SPOOLER       || die &quot;bad spool: $! $?&quot;;

And here&#39;s how to start up a child process you intend to read from:

    open(STATUS, &quot;netstat -an 2&gt;&amp;1 |&quot;)
                        || die &quot;can&#39;t fork: $!&quot;;
    while (&lt;STATUS&gt;) {
        next if /^(tcp|udp)/;
        print;
    }
    close STATUS        || die &quot;bad netstat: $! $?&quot;;

If one can be sure that a particular program is a Perl script expecting
filenames in @ARGV, the clever programmer can write something like this:

    % program f1 &quot;cmd1|&quot; - f2 &quot;cmd2|&quot; f3 &lt; tmpfile

and no matter which sort of shell it&#39;s called from, the Perl program will
read from the file F&lt;f1&gt;, the process F&lt;cmd1&gt;, standard input (F&lt;tmpfile&gt;
in this case), the F&lt;f2&gt; file, the F&lt;cmd2&gt; command, and finally the F&lt;f3&gt;
file.  Pretty nifty, eh?

You might notice that you could use backticks for much the
same effect as opening a pipe for reading:

    print grep { !/^(tcp|udp)/ } `netstat -an 2&gt;&amp;1`;
    die &quot;bad netstatus ($?)&quot; if $?;

While this is true on the surface, it&#39;s much more efficient to process the
file one line or record at a time because then you don&#39;t have to read the
whole thing into memory at once.  It also gives you finer control of the
whole process, letting you kill off the child process early if you&#39;d like.

Be careful to check the return values from both open() and close().  If
you&#39;re I&lt;writing&gt; to a pipe, you should also trap SIGPIPE.  Otherwise,
think of what happens when you start up a pipe to a command that doesn&#39;t
exist: the open() will in all likelihood succeed (it only reflects the
fork()&#39;s success), but then your output will fail--spectacularly.  Perl
can&#39;t know whether the command worked, because your command is actually
running in a separate process whose exec() might have failed.  Therefore,
while readers of bogus commands return just a quick EOF, writers
to bogus commands will get hit with a signal, which they&#39;d best be prepared
to handle.  Consider:

    open(FH, &quot;|bogus&quot;)      || die &quot;can&#39;t fork: $!&quot;;
    print FH &quot;bang\n&quot;;      #  neither necessary nor sufficient
                            #  to check print retval!
    close(FH)               || die &quot;can&#39;t close: $!&quot;;

The reason for not checking the return value from print() is because of
pipe buffering; physical writes are delayed.  That won&#39;t blow up until the
close, and it will blow up with a SIGPIPE.  To catch it, you could use
this:

    $SIG{PIPE} = &quot;IGNORE&quot;;
    open(FH, &quot;|bogus&quot;)  || die &quot;can&#39;t fork: $!&quot;;
    print FH &quot;bang\n&quot;;
    close(FH)           || die &quot;can&#39;t close: status=$?&quot;;

=head2 Filehandles

Both the main process and any child processes it forks share the same
STDIN, STDOUT, and STDERR filehandles.  If both processes try to access
them at once, strange things can happen.  You may also want to close
or reopen the filehandles for the child.  You can get around this by
opening your pipe with open(), but on some systems this means that the
child process cannot outlive the parent.

=head2 Background Processes

You can run a command in the background with:

    system(&quot;cmd &amp;&quot;);

The command&#39;s STDOUT and STDERR (and possibly STDIN, depending on your
shell) will be the same as the parent&#39;s.  You won&#39;t need to catch
SIGCHLD because of the double-fork taking place; see below for details.

=head2 Complete Dissociation of Child from Parent

In some cases (starting server processes, for instance) you&#39;ll want to
completely dissociate the child process from the parent.  This is
often called daemonization.  A well-behaved daemon will also chdir()
to the root directory so it doesn&#39;t prevent unmounting the filesystem
containing the directory from which it was launched, and redirect its
standard file descriptors from and to F&lt;/dev/null&gt; so that random
output doesn&#39;t wind up on the user&#39;s terminal.

 use POSIX &quot;setsid&quot;;

 sub daemonize {
     chdir(&quot;/&quot;)                  || die &quot;can&#39;t chdir to /: $!&quot;;
     open(STDIN,  &quot;&lt; /dev/null&quot;) || die &quot;can&#39;t read /dev/null: $!&quot;;
     open(STDOUT, &quot;&gt; /dev/null&quot;) || die &quot;can&#39;t write to /dev/null: $!&quot;;
     defined(my $pid = fork())   || die &quot;can&#39;t fork: $!&quot;;
     exit if $pid;               # non-zero now means I am the parent
     (setsid() != -1)            || die &quot;Can&#39;t start a new session: $!&quot;;
     open(STDERR, &quot;&gt;&amp;STDOUT&quot;)    || die &quot;can&#39;t dup stdout: $!&quot;;
 }

The fork() has to come before the setsid() to ensure you aren&#39;t a
process group leader; the setsid() will fail if you are.  If your
system doesn&#39;t have the setsid() function, open F&lt;/dev/tty&gt; and use the
C&lt;TIOCNOTTY&gt; ioctl() on it instead.  See tty(4) for details.

Non-Unix users should check their C&lt;&lt; I&lt;Your_OS&gt;::Process &gt;&gt; module for
other possible solutions.

=head2 Safe Pipe Opens

Another interesting approach to IPC is making your single program go
multiprocess and communicate between--or even amongst--yourselves.  The
open() function will accept a file argument of either C&lt;&quot;-|&quot;&gt; or C&lt;&quot;|-&quot;&gt;
to do a very interesting thing: it forks a child connected to the
filehandle you&#39;ve opened.  The child is running the same program as the
parent.  This is useful for safely opening a file when running under an
assumed UID or GID, for example.  If you open a pipe I&lt;to&gt; minus, you can
write to the filehandle you opened and your kid will find it in I&lt;his&gt;
STDIN.  If you open a pipe I&lt;from&gt; minus, you can read from the filehandle
you opened whatever your kid writes to I&lt;his&gt; STDOUT.

    use English;
    my $PRECIOUS = &quot;/path/to/some/safe/file&quot;;
    my $sleep_count;
    my $pid;

    do {
        $pid = open(KID_TO_WRITE, &quot;|-&quot;);
        unless (defined $pid) {
            warn &quot;cannot fork: $!&quot;;
            die &quot;bailing out&quot; if $sleep_count++ &gt; 6;
            sleep 10;
        }
    } until defined $pid;

    if ($pid) {                 # I am the parent
        print KID_TO_WRITE @some_data;
        close(KID_TO_WRITE)     || warn &quot;kid exited $?&quot;;
    } else {                    # I am the child
        # drop permissions in setuid and/or setgid programs:
        ($EUID, $EGID) = ($UID, $GID);
        open (OUTFILE, &quot;&gt; $PRECIOUS&quot;)
                                || die &quot;can&#39;t open $PRECIOUS: $!&quot;;
        while (&lt;STDIN&gt;) {
            print OUTFILE;      # child&#39;s STDIN is parent&#39;s KID_TO_WRITE
        }
        close(OUTFILE)          || die &quot;can&#39;t close $PRECIOUS: $!&quot;;
        exit(0);                # don&#39;t forget this!!
    }

Another common use for this construct is when you need to execute
something without the shell&#39;s interference.  With system(), it&#39;s
straightforward, but you can&#39;t use a pipe open or backticks safely.
That&#39;s because there&#39;s no way to stop the shell from getting its hands on
your arguments.   Instead, use lower-level control to call exec() directly.

Here&#39;s a safe backtick or pipe open for read:

    my $pid = open(KID_TO_READ, &quot;-|&quot;);
    defined($pid)           || die &quot;can&#39;t fork: $!&quot;;

    if ($pid) {             # parent
        while (&lt;KID_TO_READ&gt;) {
                            # do something interesting
        }
        close(KID_TO_READ)  || warn &quot;kid exited $?&quot;;

    } else {                # child
        ($EUID, $EGID) = ($UID, $GID); # suid only
        exec($program, @options, @args)
                            || die &quot;can&#39;t exec program: $!&quot;;
        # NOTREACHED
    }

And here&#39;s a safe pipe open for writing:

    my $pid = open(KID_TO_WRITE, &quot;|-&quot;);
    defined($pid)           || die &quot;can&#39;t fork: $!&quot;;

    $SIG{PIPE} = sub { die &quot;whoops, $program pipe broke&quot; };

    if ($pid) {             # parent
        print KID_TO_WRITE @data;
        close(KID_TO_WRITE) || warn &quot;kid exited $?&quot;;

    } else {                # child
        ($EUID, $EGID) = ($UID, $GID);
        exec($program, @options, @args)
                            || die &quot;can&#39;t exec program: $!&quot;;
        # NOTREACHED
    }

It is very easy to dead-lock a process using this form of open(), or
indeed with any use of pipe() with multiple subprocesses.  The
example above is &quot;safe&quot; because it is simple and calls exec().  See
L&lt;/&quot;Avoiding Pipe Deadlocks&quot;&gt; for general safety principles, but there
are extra gotchas with Safe Pipe Opens.

In particular, if you opened the pipe using C&lt;open FH, &quot;|-&quot;&gt;, then you
cannot simply use close() in the parent process to close an unwanted
writer.  Consider this code:

    my $pid = open(WRITER, &quot;|-&quot;);        # fork open a kid
    defined($pid)               || die &quot;first fork failed: $!&quot;;
    if ($pid) {
        if (my $sub_pid = fork()) {
            defined($sub_pid)   || die &quot;second fork failed: $!&quot;;
            close(WRITER)       || die &quot;couldn&#39;t close WRITER: $!&quot;;
            # now do something else...
        }
        else {
            # first write to WRITER
            # ...
            # then when finished
            close(WRITER)       || die &quot;couldn&#39;t close WRITER: $!&quot;;
            exit(0);
        }
    }
    else {
        # first do something with STDIN, then
        exit(0);
    }

In the example above, the true parent does not want to write to the WRITER
filehandle, so it closes it.  However, because WRITER was opened using
C&lt;open FH, &quot;|-&quot;&gt;, it has a special behavior: closing it calls
waitpid() (see L&lt;perlfunc/waitpid&gt;), which waits for the subprocess
to exit.  If the child process ends up waiting for something happening
in the section marked &quot;do something else&quot;, you have deadlock.

This can also be a problem with intermediate subprocesses in more
complicated code, which will call waitpid() on all open filehandles
during global destruction--in no predictable order.

To solve this, you must manually use pipe(), fork(), and the form of
open() which sets one file descriptor to another, as shown below:

    pipe(READER, WRITER)        || die &quot;pipe failed: $!&quot;;
    $pid = fork();
    defined($pid)               || die &quot;first fork failed: $!&quot;;
    if ($pid) {
        close READER;
        if (my $sub_pid = fork()) {
            defined($sub_pid)   || die &quot;first fork failed: $!&quot;;
            close(WRITER)       || die &quot;can&#39;t close WRITER: $!&quot;;
        }
        else {
            # write to WRITER...
            # ...
            # then  when finished
            close(WRITER)       || die &quot;can&#39;t close WRITER: $!&quot;;
            exit(0);
        }
        # write to WRITER...
    }
    else {
        open(STDIN, &quot;&lt;&amp;READER&quot;) || die &quot;can&#39;t reopen STDIN: $!&quot;;
        close(WRITER)           || die &quot;can&#39;t close WRITER: $!&quot;;
        # do something...
        exit(0);
    }

Since Perl 5.8.0, you can also use the list form of C&lt;open&gt; for pipes.
This is preferred when you wish to avoid having the shell interpret
metacharacters that may be in your command string.

So for example, instead of using:

    open(PS_PIPE, &quot;ps aux|&quot;)    || die &quot;can&#39;t open ps pipe: $!&quot;;

One would use either of these:

    open(PS_PIPE, &quot;-|&quot;, &quot;ps&quot;, &quot;aux&quot;)
                                || die &quot;can&#39;t open ps pipe: $!&quot;;

    @ps_args = qw[ ps aux ];
    open(PS_PIPE, &quot;-|&quot;, @ps_args)
                                || die &quot;can&#39;t open @ps_args|: $!&quot;;

Because there are more than three arguments to open(), forks the ps(1)
command I&lt;without&gt; spawning a shell, and reads its standard output via the
C&lt;PS_PIPE&gt; filehandle.  The corresponding syntax to I&lt;write&gt; to command
pipes is to use C&lt;&quot;|-&quot;&gt; in place of C&lt;&quot;-|&quot;&gt;.

This was admittedly a rather silly example, because you&#39;re using string
literals whose content is perfectly safe.  There is therefore no cause to
resort to the harder-to-read, multi-argument form of pipe open().  However,
whenever you cannot be assured that the program arguments are free of shell
metacharacters, the fancier form of open() should be used.  For example:

    @grep_args = (&quot;egrep&quot;, &quot;-i&quot;, $some_pattern, @many_files);
    open(GREP_PIPE, &quot;-|&quot;, @grep_args)
                        || die &quot;can&#39;t open @grep_args|: $!&quot;;

Here the multi-argument form of pipe open() is preferred because the
pattern and indeed even the filenames themselves might hold metacharacters.

Be aware that these operations are full Unix forks, which means they may
not be correctly implemented on all alien systems.

=head2 Avoiding Pipe Deadlocks

Whenever you have more than one subprocess, you must be careful that each
closes whichever half of any pipes created for interprocess communication
it is not using.  This is because any child process reading from the pipe
and expecting an EOF will never receive it, and therefore never exit. A
single process closing a pipe is not enough to close it; the last process
with the pipe open must close it for it to read EOF.

Certain built-in Unix features help prevent this most of the time.  For
instance, filehandles have a &quot;close on exec&quot; flag, which is set I&lt;en masse&gt;
under control of the C&lt;$^F&gt; variable.  This is so any filehandles you
didn&#39;t explicitly route to the STDIN, STDOUT or STDERR of a child
I&lt;program&gt; will be automatically closed.

Always explicitly and immediately call close() on the writable end of any
pipe, unless that process is actually writing to it.  Even if you don&#39;t
explicitly call close(), Perl will still close() all filehandles during
global destruction.  As previously discussed, if those filehandles have
been opened with Safe Pipe Open, this will result in calling waitpid(),
which may again deadlock.

=head2 Bidirectional Communication with Another Process

While this works reasonably well for unidirectional communication, what
about bidirectional communication?  The most obvious approach doesn&#39;t work:

    # THIS DOES NOT WORK!!
    open(PROG_FOR_READING_AND_WRITING, &quot;| some program |&quot;)

If you forget to C&lt;use warnings&gt;, you&#39;ll miss out entirely on the
helpful diagnostic message:

    Can&#39;t do bidirectional pipe at -e line 1.

If you really want to, you can use the standard open2() from the
C&lt;IPC::Open2&gt; module to catch both ends.  There&#39;s also an open3() in
C&lt;IPC::Open3&gt; for tridirectional I/O so you can also catch your child&#39;s
STDERR, but doing so would then require an awkward select() loop and
wouldn&#39;t allow you to use normal Perl input operations.

If you look at its source, you&#39;ll see that open2() uses low-level
primitives like the pipe() and exec() syscalls to create all the
connections.  Although it might have been more efficient by using
socketpair(), this would have been even less portable than it already
is. The open2() and open3() functions are unlikely to work anywhere
except on a Unix system, or at least one purporting POSIX compliance.

=for TODO
Hold on, is this even true?  First it says that socketpair() is avoided
for portability, but then it says it probably won&#39;t work except on
Unixy systems anyway.  Which one of those is true?

Here&#39;s an example of using open2():

    use FileHandle;
    use IPC::Open2;
    $pid = open2(*Reader, *Writer, &quot;cat -un&quot;);
    print Writer &quot;stuff\n&quot;;
    $got = &lt;Reader&gt;;

The problem with this is that buffering is really going to ruin your
day.  Even though your C&lt;Writer&gt; filehandle is auto-flushed so the process
on the other end gets your data in a timely manner, you can&#39;t usually do
anything to force that process to give its data to you in a similarly quick
fashion.  In this special case, we could actually so, because we gave
I&lt;cat&gt; a B&lt;-u&gt; flag to make it unbuffered.  But very few commands are
designed to operate over pipes, so this seldom works unless you yourself
wrote the program on the other end of the double-ended pipe.

A solution to this is to use a library which uses pseudottys to make your
program behave more reasonably.  This way you don&#39;t have to have control
over the source code of the program you&#39;re using.  The C&lt;Expect&gt; module
from CPAN also addresses this kind of thing.  This module requires two
other modules from CPAN, C&lt;IO::Pty&gt; and C&lt;IO::Stty&gt;.  It sets up a pseudo
terminal to interact with programs that insist on talking to the terminal
device driver.  If your system is supported, this may be your best bet.

=head2 Bidirectional Communication with Yourself

If you want, you may make low-level pipe() and fork() syscalls to stitch
this together by hand.  This example only talks to itself, but you could
reopen the appropriate handles to STDIN and STDOUT and call other processes.
(The following example lacks proper error checking.)

 #!/usr/bin/perl -w
 # pipe1 - bidirectional communication using two pipe pairs
 #         designed for the socketpair-challenged
 use IO::Handle;             # thousands of lines just for autoflush :-(
 pipe(PARENT_RDR, CHILD_WTR);  # XXX: check failure?
 pipe(CHILD_RDR,  PARENT_WTR); # XXX: check failure?
 CHILD_WTR-&gt;autoflush(1);
 PARENT_WTR-&gt;autoflush(1);

 if ($pid = fork()) {
     close PARENT_RDR;
     close PARENT_WTR;
     print CHILD_WTR &quot;Parent Pid $$ is sending this\n&quot;;
     chomp($line = &lt;CHILD_RDR&gt;);
     print &quot;Parent Pid $$ just read this: &#39;$line&#39;\n&quot;;
     close CHILD_RDR; close CHILD_WTR;
     waitpid($pid, 0);
 } else {
     die &quot;cannot fork: $!&quot; unless defined $pid;
     close CHILD_RDR;
     close CHILD_WTR;
     chomp($line = &lt;PARENT_RDR&gt;);
     print &quot;Child Pid $$ just read this: &#39;$line&#39;\n&quot;;
     print PARENT_WTR &quot;Child Pid $$ is sending this\n&quot;;
     close PARENT_RDR;
     close PARENT_WTR;
     exit(0);
 }

But you don&#39;t actually have to make two pipe calls.  If you
have the socketpair() system call, it will do this all for you.

 #!/usr/bin/perl -w
 # pipe2 - bidirectional communication using socketpair
 #   &quot;the best ones always go both ways&quot;

 use Socket;
 use IO::Handle;  # thousands of lines just for autoflush :-(

 # We say AF_UNIX because although *_LOCAL is the
 # POSIX 1003.1g form of the constant, many machines
 # still don&#39;t have it.
 socketpair(CHILD, PARENT, AF_UNIX, SOCK_STREAM, PF_UNSPEC)
                             ||  die &quot;socketpair: $!&quot;;

 CHILD-&gt;autoflush(1);
 PARENT-&gt;autoflush(1);

 if ($pid = fork()) {
     close PARENT;
     print CHILD &quot;Parent Pid $$ is sending this\n&quot;;
     chomp($line = &lt;CHILD&gt;);
     print &quot;Parent Pid $$ just read this: &#39;$line&#39;\n&quot;;
     close CHILD;
     waitpid($pid, 0);
 } else {
     die &quot;cannot fork: $!&quot; unless defined $pid;
     close CHILD;
     chomp($line = &lt;PARENT&gt;);
     print &quot;Child Pid $$ just read this: &#39;$line&#39;\n&quot;;
     print PARENT &quot;Child Pid $$ is sending this\n&quot;;
     close PARENT;
     exit(0);
 }

=head1 Sockets: Client/Server Communication

While not entirely limited to Unix-derived operating systems (e.g., WinSock
on PCs provides socket support, as do some VMS libraries), you might not have
sockets on your system, in which case this section probably isn&#39;t going to
do you much good.  With sockets, you can do both virtual circuits like TCP
streams and datagrams like UDP packets.  You may be able to do even more
depending on your system.

The Perl functions for dealing with sockets have the same names as
the corresponding system calls in C, but their arguments tend to differ
for two reasons.  First, Perl filehandles work differently than C file
descriptors.  Second, Perl already knows the length of its strings, so you
don&#39;t need to pass that information.

One of the major problems with ancient, antemillennial socket code in Perl
was that it used hard-coded values for some of the constants, which
severely hurt portability.  If you ever see code that does anything like
explicitly setting C&lt;$AF_INET = 2&gt;, you know you&#39;re in for big trouble.
An immeasurably superior approach is to use the C&lt;Socket&gt; module, which more
reliably grants access to the various constants and functions you&#39;ll need.

If you&#39;re not writing a server/client for an existing protocol like
NNTP or SMTP, you should give some thought to how your server will
know when the client has finished talking, and vice-versa.  Most
protocols are based on one-line messages and responses (so one party
knows the other has finished when a &quot;\n&quot; is received) or multi-line
messages and responses that end with a period on an empty line
(&quot;\n.\n&quot; terminates a message/response).

=head2 Internet Line Terminators

The Internet line terminator is &quot;\015\012&quot;.  Under ASCII variants of
Unix, that could usually be written as &quot;\r\n&quot;, but under other systems,
&quot;\r\n&quot; might at times be &quot;\015\015\012&quot;, &quot;\012\012\015&quot;, or something
completely different.  The standards specify writing &quot;\015\012&quot; to be
conformant (be strict in what you provide), but they also recommend
accepting a lone &quot;\012&quot; on input (be lenient in what you require).
We haven&#39;t always been very good about that in the code in this manpage,
but unless you&#39;re on a Mac from way back in its pre-Unix dark ages, you&#39;ll
probably be ok.

=head2 Internet TCP Clients and Servers

Use Internet-domain sockets when you want to do client-server
communication that might extend to machines outside of your own system.

Here&#39;s a sample TCP client using Internet-domain sockets:

    #!/usr/bin/perl -w
    use strict;
    use Socket;
    my ($remote, $port, $iaddr, $paddr, $proto, $line);

    $remote  = shift || &quot;localhost&quot;;
    $port    = shift || 2345;  # random port
    if ($port =~ /\D/) { $port = getservbyname($port, &quot;tcp&quot;) }
    die &quot;No port&quot; unless $port;
    $iaddr   = inet_aton($remote)       || die &quot;no host: $remote&quot;;
    $paddr   = sockaddr_in($port, $iaddr);

    $proto   = getprotobyname(&quot;tcp&quot;);
    socket(SOCK, PF_INET, SOCK_STREAM, $proto)  || die &quot;socket: $!&quot;;
    connect(SOCK, $paddr)               || die &quot;connect: $!&quot;;
    while ($line = &lt;SOCK&gt;) {
        print $line;
    }

    close (SOCK)                        || die &quot;close: $!&quot;;
    exit(0);

And here&#39;s a corresponding server to go along with it.  We&#39;ll
leave the address as C&lt;INADDR_ANY&gt; so that the kernel can choose
the appropriate interface on multihomed hosts.  If you want sit
on a particular interface (like the external side of a gateway
or firewall machine), fill this in with your real address instead.

 #!/usr/bin/perl -Tw
 use strict;
 BEGIN { $ENV{PATH} = &quot;/usr/bin:/bin&quot; }
 use Socket;
 use Carp;
 my $EOL = &quot;\015\012&quot;;

 sub logmsg { print &quot;$0 $$: @_ at &quot;, scalar localtime(), &quot;\n&quot; }

 my $port  = shift || 2345;
 die &quot;invalid port&quot; unless $port =~ /^ \d+ $/x;

 my $proto = getprotobyname(&quot;tcp&quot;);

 socket(Server, PF_INET, SOCK_STREAM, $proto)   || die &quot;socket: $!&quot;;
 setsockopt(Server, SOL_SOCKET, SO_REUSEADDR, pack(&quot;l&quot;, 1))
                                                || die &quot;setsockopt: $!&quot;;
 bind(Server, sockaddr_in($port, INADDR_ANY))   || die &quot;bind: $!&quot;;
 listen(Server, SOMAXCONN)                      || die &quot;listen: $!&quot;;

 logmsg &quot;server started on port $port&quot;;

 my $paddr;

 for ( ; $paddr = accept(Client, Server); close Client) {
     my($port, $iaddr) = sockaddr_in($paddr);
     my $name = gethostbyaddr($iaddr, AF_INET);

     logmsg &quot;connection from $name [&quot;,
             inet_ntoa($iaddr), &quot;]
             at port $port&quot;;

     print Client &quot;Hello there, $name, it&#39;s now &quot;,
                     scalar localtime(), $EOL;
 }

And here&#39;s a multitasking version.  It&#39;s multitasked in that
like most typical servers, it spawns (fork()s) a slave server to
handle the client request so that the master server can quickly
go back to service a new client.

 #!/usr/bin/perl -Tw
 use strict;
 BEGIN { $ENV{PATH} = &quot;/usr/bin:/bin&quot; }
 use Socket;
 use Carp;
 my $EOL = &quot;\015\012&quot;;

 sub spawn;  # forward declaration
 sub logmsg { print &quot;$0 $$: @_ at &quot;, scalar localtime(), &quot;\n&quot; }

 my $port  = shift || 2345;
 die &quot;invalid port&quot; unless $port =~ /^ \d+ $/x;

 my $proto = getprotobyname(&quot;tcp&quot;);

 socket(Server, PF_INET, SOCK_STREAM, $proto)   || die &quot;socket: $!&quot;;
 setsockopt(Server, SOL_SOCKET, SO_REUSEADDR, pack(&quot;l&quot;, 1))
                                                || die &quot;setsockopt: $!&quot;;
 bind(Server, sockaddr_in($port, INADDR_ANY))   || die &quot;bind: $!&quot;;
 listen(Server, SOMAXCONN)                      || die &quot;listen: $!&quot;;

 logmsg &quot;server started on port $port&quot;;

 my $waitedpid = 0;
 my $paddr;

 use POSIX &quot;:sys_wait_h&quot;;
 use Errno;

 sub REAPER {
     local $!;   # don&#39;t let waitpid() overwrite current error
     while ((my $pid = waitpid(-1, WNOHANG)) &gt; 0 &amp;&amp; WIFEXITED($?)) {
         logmsg &quot;reaped $waitedpid&quot; . ($? ? &quot; with exit $?&quot; : &quot;&quot;);
     }
     $SIG{CHLD} = \&amp;REAPER;  # loathe SysV
 }

 $SIG{CHLD} = \&amp;REAPER;

 while (1) {
     $paddr = accept(Client, Server) || do {
         # try again if accept() returned because got a signal
         next if $!{EINTR};
         die &quot;accept: $!&quot;;
     };
     my ($port, $iaddr) = sockaddr_in($paddr);
     my $name = gethostbyaddr($iaddr, AF_INET);

     logmsg &quot;connection from $name [&quot;,
            inet_ntoa($iaddr),
            &quot;] at port $port&quot;;

     spawn sub {
         $| = 1;
         print &quot;Hello there, $name, it&#39;s now &quot;,
               scalar localtime(),
               $EOL;
         exec &quot;/usr/games/fortune&quot;       # XXX: &quot;wrong&quot; line terminators
             or confess &quot;can&#39;t exec fortune: $!&quot;;
     };
     close Client;
 }

 sub spawn {
     my $coderef = shift;

     unless (@_ == 0 &amp;&amp; $coderef &amp;&amp; ref($coderef) eq &quot;CODE&quot;) {
         confess &quot;usage: spawn CODEREF&quot;;
     }

     my $pid;
     unless (defined($pid = fork())) {
         logmsg &quot;cannot fork: $!&quot;;
         return;
     }
     elsif ($pid) {
         logmsg &quot;begat $pid&quot;;
         return; # I&#39;m the parent
     }
     # else I&#39;m the child -- go spawn

     open(STDIN,  &quot;&lt;&amp;Client&quot;)    || die &quot;can&#39;t dup client to stdin&quot;;
     open(STDOUT, &quot;&gt;&amp;Client&quot;)    || die &quot;can&#39;t dup client to stdout&quot;;
     ## open(STDERR, &quot;&gt;&amp;STDOUT&quot;) || die &quot;can&#39;t dup stdout to stderr&quot;;
     exit($coderef-&gt;());
 }

This server takes the trouble to clone off a child version via fork()
for each incoming request.  That way it can handle many requests at
once, which you might not always want.  Even if you don&#39;t fork(), the
listen() will allow that many pending connections.  Forking servers
have to be particularly careful about cleaning up their dead children
(called &quot;zombies&quot; in Unix parlance), because otherwise you&#39;ll quickly
fill up your process table.  The REAPER subroutine is used here to
call waitpid() for any child processes that have finished, thereby
ensuring that they terminate cleanly and don&#39;t join the ranks of the
living dead.

Within the while loop we call accept() and check to see if it returns
a false value.  This would normally indicate a system error needs
to be reported.  However, the introduction of safe signals (see
L&lt;/Deferred Signals (Safe Signals)&gt; above) in Perl 5.8.0 means that
accept() might also be interrupted when the process receives a signal.
This typically happens when one of the forked subprocesses exits and
notifies the parent process with a CHLD signal.

If accept() is interrupted by a signal, $! will be set to EINTR.
If this happens, we can safely continue to the next iteration of
the loop and another call to accept().  It is important that your
signal handling code not modify the value of $!, or else this test
will likely fail.  In the REAPER subroutine we create a local version
of $! before calling waitpid().  When waitpid() sets $! to ECHILD as
it inevitably does when it has no more children waiting, it
updates the local copy and leaves the original unchanged.

You should use the B&lt;-T&gt; flag to enable taint checking (see L&lt;perlsec&gt;)
even if we aren&#39;t running setuid or setgid.  This is always a good idea
for servers or any program run on behalf of someone else (like CGI
scripts), because it lessens the chances that people from the outside will
be able to compromise your system.

Let&#39;s look at another TCP client.  This one connects to the TCP &quot;time&quot;
service on a number of different machines and shows how far their clocks
differ from the system on which it&#39;s being run:

    #!/usr/bin/perl  -w
    use strict;
    use Socket;

    my $SECS_OF_70_YEARS = 2208988800;
    sub ctime { scalar localtime(shift() || time()) }

    my $iaddr = gethostbyname(&quot;localhost&quot;);
    my $proto = getprotobyname(&quot;tcp&quot;);
    my $port = getservbyname(&quot;time&quot;, &quot;tcp&quot;);
    my $paddr = sockaddr_in(0, $iaddr);
    my($host);

    $| = 1;
    printf &quot;%-24s %8s %s\n&quot;, &quot;localhost&quot;, 0, ctime();

    foreach $host (@ARGV) {
        printf &quot;%-24s &quot;, $host;
        my $hisiaddr = inet_aton($host)     || die &quot;unknown host&quot;;
        my $hispaddr = sockaddr_in($port, $hisiaddr);
        socket(SOCKET, PF_INET, SOCK_STREAM, $proto)
                                            || die &quot;socket: $!&quot;;
        connect(SOCKET, $hispaddr)          || die &quot;connect: $!&quot;;
        my $rtime = pack(&quot;C4&quot;, ());
        read(SOCKET, $rtime, 4);
        close(SOCKET);
        my $histime = unpack(&quot;N&quot;, $rtime) - $SECS_OF_70_YEARS;
        printf &quot;%8d %s\n&quot;, $histime - time(), ctime($histime);
    }

=head2 Unix-Domain TCP Clients and Servers

That&#39;s fine for Internet-domain clients and servers, but what about local
communications?  While you can use the same setup, sometimes you don&#39;t
want to.  Unix-domain sockets are local to the current host, and are often
used internally to implement pipes.  Unlike Internet domain sockets, Unix
domain sockets can show up in the file system with an ls(1) listing.

    % ls -l /dev/log
    srw-rw-rw-  1 root            0 Oct 31 07:23 /dev/log

You can test for these with Perl&#39;s B&lt;-S&gt; file test:

    unless (-S &quot;/dev/log&quot;) {
        die &quot;something&#39;s wicked with the log system&quot;;
    }

Here&#39;s a sample Unix-domain client:

    #!/usr/bin/perl -w
    use Socket;
    use strict;
    my ($rendezvous, $line);

    $rendezvous = shift || &quot;catsock&quot;;
    socket(SOCK, PF_UNIX, SOCK_STREAM, 0)     || die &quot;socket: $!&quot;;
    connect(SOCK, sockaddr_un($rendezvous))   || die &quot;connect: $!&quot;;
    while (defined($line = &lt;SOCK&gt;)) {
        print $line;
    }
    exit(0);

And here&#39;s a corresponding server.  You don&#39;t have to worry about silly
network terminators here because Unix domain sockets are guaranteed
to be on the localhost, and thus everything works right.

    #!/usr/bin/perl -Tw
    use strict;
    use Socket;
    use Carp;

    BEGIN { $ENV{PATH} = &quot;/usr/bin:/bin&quot; }
    sub spawn;  # forward declaration
    sub logmsg { print &quot;$0 $$: @_ at &quot;, scalar localtime(), &quot;\n&quot; }

    my $NAME = &quot;catsock&quot;;
    my $uaddr = sockaddr_un($NAME);
    my $proto = getprotobyname(&quot;tcp&quot;);

    socket(Server, PF_UNIX, SOCK_STREAM, 0) || die &quot;socket: $!&quot;;
    unlink($NAME);
    bind  (Server, $uaddr)                  || die &quot;bind: $!&quot;;
    listen(Server, SOMAXCONN)               || die &quot;listen: $!&quot;;

    logmsg &quot;server started on $NAME&quot;;

    my $waitedpid;

    use POSIX &quot;:sys_wait_h&quot;;
    sub REAPER {
        my $child;
        while (($waitedpid = waitpid(-1, WNOHANG)) &gt; 0) {
            logmsg &quot;reaped $waitedpid&quot; . ($? ? &quot; with exit $?&quot; : &quot;&quot;);
        }
        $SIG{CHLD} = \&amp;REAPER;  # loathe SysV
    }

    $SIG{CHLD} = \&amp;REAPER;


    for ( $waitedpid = 0;
          accept(Client, Server) || $waitedpid;
          $waitedpid = 0, close Client)
    {
        next if $waitedpid;
        logmsg &quot;connection on $NAME&quot;;
        spawn sub {
            print &quot;Hello there, it&#39;s now &quot;, scalar localtime(), &quot;\n&quot;;
            exec(&quot;/usr/games/fortune&quot;)  || die &quot;can&#39;t exec fortune: $!&quot;;
        };
    }

    sub spawn {
        my $coderef = shift();

        unless (@_ == 0 &amp;&amp; $coderef &amp;&amp; ref($coderef) eq &quot;CODE&quot;) {
            confess &quot;usage: spawn CODEREF&quot;;
        }

        my $pid;
        unless (defined($pid = fork())) {
            logmsg &quot;cannot fork: $!&quot;;
            return;
        }
        elsif ($pid) {
            logmsg &quot;begat $pid&quot;;
            return; # I&#39;m the parent
        }
        else {
            # I&#39;m the child -- go spawn
        }

        open(STDIN,  &quot;&lt;&amp;Client&quot;)    || die &quot;can&#39;t dup client to stdin&quot;;
        open(STDOUT, &quot;&gt;&amp;Client&quot;)    || die &quot;can&#39;t dup client to stdout&quot;;
        ## open(STDERR, &quot;&gt;&amp;STDOUT&quot;) || die &quot;can&#39;t dup stdout to stderr&quot;;
        exit($coderef-&gt;());
    }

As you see, it&#39;s remarkably similar to the Internet domain TCP server, so
much so, in fact, that we&#39;ve omitted several duplicate functions--spawn(),
logmsg(), ctime(), and REAPER()--which are the same as in the other server.

So why would you ever want to use a Unix domain socket instead of a
simpler named pipe?  Because a named pipe doesn&#39;t give you sessions.  You
can&#39;t tell one process&#39;s data from another&#39;s.  With socket programming,
you get a separate session for each client; that&#39;s why accept() takes two
arguments.

For example, let&#39;s say that you have a long-running database server daemon
that you want folks to be able to access from the Web, but only
if they go through a CGI interface.  You&#39;d have a small, simple CGI
program that does whatever checks and logging you feel like, and then acts
as a Unix-domain client and connects to your private server.

=head1 TCP Clients with IO::Socket

For those preferring a higher-level interface to socket programming, the
IO::Socket module provides an object-oriented approach.  If for some reason
you lack this module, you can just fetch IO::Socket from CPAN, where you&#39;ll also
find modules providing easy interfaces to the following systems: DNS, FTP,
Ident (RFC 931), NIS and NISPlus, NNTP, Ping, POP3, SMTP, SNMP, SSLeay,
Telnet, and Time--to name just a few.

=head2 A Simple Client

Here&#39;s a client that creates a TCP connection to the &quot;daytime&quot;
service at port 13 of the host name &quot;localhost&quot; and prints out everything
that the server there cares to provide.

    #!/usr/bin/perl -w
    use IO::Socket;
    $remote = IO::Socket::INET-&gt;new(
                        Proto    =&gt; &quot;tcp&quot;,
                        PeerAddr =&gt; &quot;localhost&quot;,
                        PeerPort =&gt; &quot;daytime(13)&quot;,
                    )
                 || die &quot;can&#39;t connect to daytime service on localhost&quot;;
    while (&lt;$remote&gt;) { print }

When you run this program, you should get something back that
looks like this:

    Wed May 14 08:40:46 MDT 1997

Here are what those parameters to the new() constructor mean:

=over 4

=item C&lt;Proto&gt;

This is which protocol to use.  In this case, the socket handle returned
will be connected to a TCP socket, because we want a stream-oriented
connection, that is, one that acts pretty much like a plain old file.
Not all sockets are this of this type.  For example, the UDP protocol
can be used to make a datagram socket, used for message-passing.

=item C&lt;PeerAddr&gt;

This is the name or Internet address of the remote host the server is
running on.  We could have specified a longer name like C&lt;&quot;www.perl.com&quot;&gt;,
or an address like C&lt;&quot;207.171.7.72&quot;&gt;.  For demonstration purposes, we&#39;ve
used the special hostname C&lt;&quot;localhost&quot;&gt;, which should always mean the
current machine you&#39;re running on.  The corresponding Internet address
for localhost is C&lt;&quot;127.0.0.1&quot;&gt;, if you&#39;d rather use that.

=item C&lt;PeerPort&gt;

This is the service name or port number we&#39;d like to connect to.
We could have gotten away with using just C&lt;&quot;daytime&quot;&gt; on systems with a
well-configured system services file,[FOOTNOTE: The system services file
is found in I&lt;/etc/services&gt; under Unixy systems.] but here we&#39;ve specified the
port number (13) in parentheses.  Using just the number would have also
worked, but numeric literals make careful programmers nervous.

=back

Notice how the return value from the C&lt;new&gt; constructor is used as
a filehandle in the C&lt;while&gt; loop?  That&#39;s what&#39;s called an I&lt;indirect
filehandle&gt;, a scalar variable containing a filehandle.  You can use
it the same way you would a normal filehandle.  For example, you
can read one line from it this way:

    $line = &lt;$handle&gt;;

all remaining lines from is this way:

    @lines = &lt;$handle&gt;;

and send a line of data to it this way:

    print $handle &quot;some data\n&quot;;

=head2 A Webget Client

Here&#39;s a simple client that takes a remote host to fetch a document
from, and then a list of files to get from that host.  This is a
more interesting client than the previous one because it first sends
something to the server before fetching the server&#39;s response.

    #!/usr/bin/perl -w
    use IO::Socket;
    unless (@ARGV &gt; 1) { die &quot;usage: $0 host url ...&quot; }
    $host = shift(@ARGV);
    $EOL = &quot;\015\012&quot;;
    $BLANK = $EOL x 2;
    for my $document (@ARGV) {
        $remote = IO::Socket::INET-&gt;new( Proto     =&gt; &quot;tcp&quot;,
                                         PeerAddr  =&gt; $host,
                                         PeerPort  =&gt; &quot;http(80)&quot;,
                  )     || die &quot;cannot connect to httpd on $host&quot;;
        $remote-&gt;autoflush(1);
        print $remote &quot;GET $document HTTP/1.0&quot; . $BLANK;
        while ( &lt;$remote&gt; ) { print }
        close $remote;
    }

The web server handling the HTTP service is assumed to be at
its standard port, number 80.  If the server you&#39;re trying to
connect to is at a different port, like 1080 or 8080, you should specify it
as the named-parameter pair, C&lt;&lt; PeerPort =&gt; 8080 &gt;&gt;.  The C&lt;autoflush&gt;
method is used on the socket because otherwise the system would buffer
up the output we sent it.  (If you&#39;re on a prehistoric Mac, you&#39;ll also
need to change every C&lt;&quot;\n&quot;&gt; in your code that sends data over the network
to be a C&lt;&quot;\015\012&quot;&gt; instead.)

Connecting to the server is only the first part of the process: once you
have the connection, you have to use the server&#39;s language.  Each server
on the network has its own little command language that it expects as
input.  The string that we send to the server starting with &quot;GET&quot; is in
HTTP syntax.  In this case, we simply request each specified document.
Yes, we really are making a new connection for each document, even though
it&#39;s the same host.  That&#39;s the way you always used to have to speak HTTP.
Recent versions of web browsers may request that the remote server leave
the connection open a little while, but the server doesn&#39;t have to honor
such a request.

Here&#39;s an example of running that program, which we&#39;ll call I&lt;webget&gt;:

    % webget www.perl.com /guanaco.html
    HTTP/1.1 404 File Not Found
    Date: Thu, 08 May 1997 18:02:32 GMT
    Server: Apache/1.2b6
    Connection: close
    Content-type: text/html

    &lt;HEAD&gt;&lt;TITLE&gt;404 File Not Found&lt;/TITLE&gt;&lt;/HEAD&gt;
    &lt;BODY&gt;&lt;H1&gt;File Not Found&lt;/H1&gt;
    The requested URL /guanaco.html was not found on this server.&lt;P&gt;
    &lt;/BODY&gt;

Ok, so that&#39;s not very interesting, because it didn&#39;t find that
particular document.  But a long response wouldn&#39;t have fit on this page.

For a more featureful version of this program, you should look to
the I&lt;lwp-request&gt; program included with the LWP modules from CPAN.

=head2 Interactive Client with IO::Socket

Well, that&#39;s all fine if you want to send one command and get one answer,
but what about setting up something fully interactive, somewhat like
the way I&lt;telnet&gt; works?  That way you can type a line, get the answer,
type a line, get the answer, etc.

This client is more complicated than the two we&#39;ve done so far, but if
you&#39;re on a system that supports the powerful C&lt;fork&gt; call, the solution
isn&#39;t that rough.  Once you&#39;ve made the connection to whatever service
you&#39;d like to chat with, call C&lt;fork&gt; to clone your process.  Each of
these two identical process has a very simple job to do: the parent
copies everything from the socket to standard output, while the child
simultaneously copies everything from standard input to the socket.
To accomplish the same thing using just one process would be I&lt;much&gt;
harder, because it&#39;s easier to code two processes to do one thing than it
is to code one process to do two things.  (This keep-it-simple principle
a cornerstones of the Unix philosophy, and good software engineering as
well, which is probably why it&#39;s spread to other systems.)

Here&#39;s the code:

    #!/usr/bin/perl -w
    use strict;
    use IO::Socket;
    my ($host, $port, $kidpid, $handle, $line);

    unless (@ARGV == 2) { die &quot;usage: $0 host port&quot; }
    ($host, $port) = @ARGV;

    # create a tcp connection to the specified host and port
    $handle = IO::Socket::INET-&gt;new(Proto     =&gt; &quot;tcp&quot;,
                                    PeerAddr  =&gt; $host,
                                    PeerPort  =&gt; $port)
               || die &quot;can&#39;t connect to port $port on $host: $!&quot;;

    $handle-&gt;autoflush(1);       # so output gets there right away
    print STDERR &quot;[Connected to $host:$port]\n&quot;;

    # split the program into two processes, identical twins
    die &quot;can&#39;t fork: $!&quot; unless defined($kidpid = fork());

    # the if{} block runs only in the parent process
    if ($kidpid) {
        # copy the socket to standard output
        while (defined ($line = &lt;$handle&gt;)) {
            print STDOUT $line;
        }
        kill(&quot;TERM&quot;, $kidpid);   # send SIGTERM to child
    }
    # the else{} block runs only in the child process
    else {
        # copy standard input to the socket
        while (defined ($line = &lt;STDIN&gt;)) {
            print $handle $line;
        }
        exit(0);                # just in case
    }

The C&lt;kill&gt; function in the parent&#39;s C&lt;if&gt; block is there to send a
signal to our child process, currently running in the C&lt;else&gt; block,
as soon as the remote server has closed its end of the connection.

If the remote server sends data a byte at time, and you need that
data immediately without waiting for a newline (which might not happen),
you may wish to replace the C&lt;while&gt; loop in the parent with the
following:

    my $byte;
    while (sysread($handle, $byte, 1) == 1) {
        print STDOUT $byte;
    }

Making a system call for each byte you want to read is not very efficient
(to put it mildly) but is the simplest to explain and works reasonably
well.

=head1 TCP Servers with IO::Socket

As always, setting up a server is little bit more involved than running a client.
The model is that the server creates a special kind of socket that
does nothing but listen on a particular port for incoming connections.
It does this by calling the C&lt;&lt; IO::Socket::INET-&gt;new() &gt;&gt; method with
slightly different arguments than the client did.

=over 4

=item Proto

This is which protocol to use.  Like our clients, we&#39;ll
still specify C&lt;&quot;tcp&quot;&gt; here.

=item LocalPort

We specify a local
port in the C&lt;LocalPort&gt; argument, which we didn&#39;t do for the client.
This is service name or port number for which you want to be the
server. (Under Unix, ports under 1024 are restricted to the
superuser.)  In our sample, we&#39;ll use port 9000, but you can use
any port that&#39;s not currently in use on your system.  If you try
to use one already in used, you&#39;ll get an &quot;Address already in use&quot;
message.  Under Unix, the C&lt;netstat -a&gt; command will show
which services current have servers.

=item Listen

The C&lt;Listen&gt; parameter is set to the maximum number of
pending connections we can accept until we turn away incoming clients.
Think of it as a call-waiting queue for your telephone.
The low-level Socket module has a special symbol for the system maximum, which
is SOMAXCONN.

=item Reuse

The C&lt;Reuse&gt; parameter is needed so that we restart our server
manually without waiting a few minutes to allow system buffers to
clear out.

=back

Once the generic server socket has been created using the parameters
listed above, the server then waits for a new client to connect
to it.  The server blocks in the C&lt;accept&gt; method, which eventually accepts a
bidirectional connection from the remote client.  (Make sure to autoflush
this handle to circumvent buffering.)

To add to user-friendliness, our server prompts the user for commands.
Most servers don&#39;t do this.  Because of the prompt without a newline,
you&#39;ll have to use the C&lt;sysread&gt; variant of the interactive client above.

This server accepts one of five different commands, sending output back to
the client.  Unlike most network servers, this one handles only one
incoming client at a time.  Multitasking servers are covered in
Chapter 16 of the Camel.

Here&#39;s the code.  We&#39;ll

 #!/usr/bin/perl -w
 use IO::Socket;
 use Net::hostent;      # for OOish version of gethostbyaddr

 $PORT = 9000;          # pick something not in use

 $server = IO::Socket::INET-&gt;new( Proto     =&gt; &quot;tcp&quot;,
                                  LocalPort =&gt; $PORT,
                                  Listen    =&gt; SOMAXCONN,
                                  Reuse     =&gt; 1);

 die &quot;can&#39;t setup server&quot; unless $server;
 print &quot;[Server $0 accepting clients]\n&quot;;

 while ($client = $server-&gt;accept()) {
   $client-&gt;autoflush(1);
   print $client &quot;Welcome to $0; type help for command list.\n&quot;;
   $hostinfo = gethostbyaddr($client-&gt;peeraddr);
   printf &quot;[Connect from %s]\n&quot;,
          $hostinfo ? $hostinfo-&gt;name : $client-&gt;peerhost;
   print $client &quot;Command? &quot;;
   while ( &lt;$client&gt;) {
     next unless /\S/;     # blank line
     if    (/quit|exit/i)  { last                                      }
     elsif (/date|time/i)  { printf $client &quot;%s\n&quot;, scalar localtime() }
     elsif (/who/i )       { print  $client `who 2&gt;&amp;1`                 }
     elsif (/cookie/i )    { print  $client `/usr/games/fortune 2&gt;&amp;1`  }
     elsif (/motd/i )      { print  $client `cat /etc/motd 2&gt;&amp;1`       }
     else {
       print $client &quot;Commands: quit date who cookie motd\n&quot;;
     }
   } continue {
      print $client &quot;Command? &quot;;
   }
   close $client;
 }

=head1 UDP: Message Passing

Another kind of client-server setup is one that uses not connections, but
messages.  UDP communications involve much lower overhead but also provide
less reliability, as there are no promises that messages will arrive at
all, let alone in order and unmangled.  Still, UDP offers some advantages
over TCP, including being able to &quot;broadcast&quot; or &quot;multicast&quot; to a whole
bunch of destination hosts at once (usually on your local subnet).  If you
find yourself overly concerned about reliability and start building checks
into your message system, then you probably should use just TCP to start
with.

UDP datagrams are I&lt;not&gt; a bytestream and should not be treated as such.
This makes using I/O mechanisms with internal buffering like stdio (i.e.
print() and friends) especially cumbersome. Use syswrite(), or better
send(), like in the example below.

Here&#39;s a UDP program similar to the sample Internet TCP client given
earlier.  However, instead of checking one host at a time, the UDP version
will check many of them asynchronously by simulating a multicast and then
using select() to do a timed-out wait for I/O.  To do something similar
with TCP, you&#39;d have to use a different socket handle for each host.

 #!/usr/bin/perl -w
 use strict;
 use Socket;
 use Sys::Hostname;

 my ( $count, $hisiaddr, $hispaddr, $histime,
      $host, $iaddr, $paddr, $port, $proto,
      $rin, $rout, $rtime, $SECS_OF_70_YEARS);

 $SECS_OF_70_YEARS = 2_208_988_800;

 $iaddr = gethostbyname(hostname());
 $proto = getprotobyname(&quot;udp&quot;);
 $port = getservbyname(&quot;time&quot;, &quot;udp&quot;);
 $paddr = sockaddr_in(0, $iaddr); # 0 means let kernel pick

 socket(SOCKET, PF_INET, SOCK_DGRAM, $proto)   || die &quot;socket: $!&quot;;
 bind(SOCKET, $paddr)                          || die &quot;bind: $!&quot;;

 $| = 1;
 printf &quot;%-12s %8s %s\n&quot;,  &quot;localhost&quot;, 0, scalar localtime();
 $count = 0;
 for $host (@ARGV) {
     $count++;
     $hisiaddr = inet_aton($host)              || die &quot;unknown host&quot;;
     $hispaddr = sockaddr_in($port, $hisiaddr);
     defined(send(SOCKET, 0, 0, $hispaddr))    || die &quot;send $host: $!&quot;;
 }

 $rin = &quot;&quot;;
 vec($rin, fileno(SOCKET), 1) = 1;

 # timeout after 10.0 seconds
 while ($count &amp;&amp; select($rout = $rin, undef, undef, 10.0)) {
     $rtime = &quot;&quot;;
     $hispaddr = recv(SOCKET, $rtime, 4, 0)    || die &quot;recv: $!&quot;;
     ($port, $hisiaddr) = sockaddr_in($hispaddr);
     $host = gethostbyaddr($hisiaddr, AF_INET);
     $histime = unpack(&quot;N&quot;, $rtime) - $SECS_OF_70_YEARS;
     printf &quot;%-12s &quot;, $host;
     printf &quot;%8d %s\n&quot;, $histime - time(), scalar localtime($histime);
     $count--;
 }

This example does not include any retries and may consequently fail to
contact a reachable host. The most prominent reason for this is congestion
of the queues on the sending host if the number of hosts to contact is
sufficiently large.

=head1 SysV IPC

While System V IPC isn&#39;t so widely used as sockets, it still has some
interesting uses.  However, you cannot use SysV IPC or Berkeley mmap() to
have a variable shared amongst several processes.  That&#39;s because Perl
would reallocate your string when you weren&#39;t wanting it to.  You might
look into the C&lt;IPC::Shareable&gt; or C&lt;threads::shared&gt; modules for that.

Here&#39;s a small example showing shared memory usage.

    use IPC::SysV qw(IPC_PRIVATE IPC_RMID S_IRUSR S_IWUSR);

    $size = 2000;
    $id = shmget(IPC_PRIVATE, $size, S_IRUSR | S_IWUSR);
    defined($id)                    || die &quot;shmget: $!&quot;;
    print &quot;shm key $id\n&quot;;

    $message = &quot;Message #1&quot;;
    shmwrite($id, $message, 0, 60)  || die &quot;shmwrite: $!&quot;;
    print &quot;wrote: &#39;$message&#39;\n&quot;;
    shmread($id, $buff, 0, 60)      || die &quot;shmread: $!&quot;;
    print &quot;read : &#39;$buff&#39;\n&quot;;

    # the buffer of shmread is zero-character end-padded.
    substr($buff, index($buff, &quot;\0&quot;)) = &quot;&quot;;
    print &quot;un&quot; unless $buff eq $message;
    print &quot;swell\n&quot;;

    print &quot;deleting shm $id\n&quot;;
    shmctl($id, IPC_RMID, 0)        || die &quot;shmctl: $!&quot;;

Here&#39;s an example of a semaphore:

    use IPC::SysV qw(IPC_CREAT);

    $IPC_KEY = 1234;
    $id = semget($IPC_KEY, 10, 0666 | IPC_CREAT);
    defined($id)                    || die &quot;semget: $!&quot;;
    print &quot;sem id $id\n&quot;;

Put this code in a separate file to be run in more than one process.
Call the file F&lt;take&gt;:

    # create a semaphore

    $IPC_KEY = 1234;
    $id = semget($IPC_KEY, 0, 0);
    defined($id)                    || die &quot;semget: $!&quot;;

    $semnum  = 0;
    $semflag = 0;

    # &quot;take&quot; semaphore
    # wait for semaphore to be zero
    $semop = 0;
    $opstring1 = pack(&quot;s!s!s!&quot;, $semnum, $semop, $semflag);

    # Increment the semaphore count
    $semop = 1;
    $opstring2 = pack(&quot;s!s!s!&quot;, $semnum, $semop,  $semflag);
    $opstring  = $opstring1 . $opstring2;

    semop($id, $opstring)   || die &quot;semop: $!&quot;;

Put this code in a separate file to be run in more than one process.
Call this file F&lt;give&gt;:

    # &quot;give&quot; the semaphore
    # run this in the original process and you will see
    # that the second process continues

    $IPC_KEY = 1234;
    $id = semget($IPC_KEY, 0, 0);
    die unless defined($id);

    $semnum  = 0;
    $semflag = 0;

    # Decrement the semaphore count
    $semop = -1;
    $opstring = pack(&quot;s!s!s!&quot;, $semnum, $semop, $semflag);

    semop($id, $opstring)   || die &quot;semop: $!&quot;;

The SysV IPC code above was written long ago, and it&#39;s definitely
clunky looking.  For a more modern look, see the IPC::SysV module.

A small example demonstrating SysV message queues:

    use IPC::SysV qw(IPC_PRIVATE IPC_RMID IPC_CREAT S_IRUSR S_IWUSR);

    my $id = msgget(IPC_PRIVATE, IPC_CREAT | S_IRUSR | S_IWUSR);
    defined($id)                || die &quot;msgget failed: $!&quot;;

    my $sent      = &quot;message&quot;;
    my $type_sent = 1234;

    msgsnd($id, pack(&quot;l! a*&quot;, $type_sent, $sent), 0)
                                || die &quot;msgsnd failed: $!&quot;;

    msgrcv($id, my $rcvd_buf, 60, 0, 0)
                                || die &quot;msgrcv failed: $!&quot;;

    my($type_rcvd, $rcvd) = unpack(&quot;l! a*&quot;, $rcvd_buf);

    if ($rcvd eq $sent) {
        print &quot;okay\n&quot;;
    } else {
        print &quot;not okay\n&quot;;
    }

    msgctl($id, IPC_RMID, 0)    || die &quot;msgctl failed: $!\n&quot;;

=head1 NOTES

Most of these routines quietly but politely return C&lt;undef&gt; when they
fail instead of causing your program to die right then and there due to
an uncaught exception.  (Actually, some of the new I&lt;Socket&gt; conversion
functions do croak() on bad arguments.)  It is therefore essential to
check return values from these functions.  Always begin your socket
programs this way for optimal success, and don&#39;t forget to add the B&lt;-T&gt;
taint-checking flag to the C&lt;#!&gt; line for servers:

    #!/usr/bin/perl -Tw
    use strict;
    use sigtrap;
    use Socket;

=head1 BUGS

These routines all create system-specific portability problems.  As noted
elsewhere, Perl is at the mercy of your C libraries for much of its system
behavior.  It&#39;s probably safest to assume broken SysV semantics for
signals and to stick with simple TCP and UDP socket operations; e.g., don&#39;t
try to pass open file descriptors over a local UDP datagram socket if you
want your code to stand a chance of being portable.

=head1 AUTHOR

Tom Christiansen, with occasional vestiges of Larry Wall&#39;s original
version and suggestions from the Perl Porters.

=head1 SEE ALSO

There&#39;s a lot more to networking than this, but this should get you
started.

For intrepid programmers, the indispensable textbook is I&lt;Unix Network
Programming, 2nd Edition, Volume 1&gt; by W. Richard Stevens (published by
Prentice-Hall).  Most books on networking address the subject from the
perspective of a C programmer; translation to Perl is left as an exercise
for the reader.

The IO::Socket(3) manpage describes the object library, and the Socket(3)
manpage describes the low-level interface to sockets.  Besides the obvious
functions in L&lt;perlfunc&gt;, you should also check out the F&lt;modules&gt; file at
your nearest CPAN site, especially
L&lt;http://www.cpan.org/modules/00modlist.long.html#ID5_Networking_&gt;.
See L&lt;perlmodlib&gt; or best yet, the F&lt;Perl FAQ&gt; for a description
of what CPAN is and where to get it if the previous link doesn&#39;t work
for you.

Section 5 of CPAN&#39;s F&lt;modules&gt; file is devoted to &quot;Networking, Device
Control (modems), and Interprocess Communication&quot;, and contains numerous
unbundled modules numerous networking modules, Chat and Expect operations,
CGI programming, DCE, FTP, IPC, NNTP, Proxy, Ptty, RPC, SNMP, SMTP, Telnet,
Threads, and ToolTalk--to name just a few.
</pre>

                    </main>
                </div>

        </div>
        <footer>
        
        </footer>


    </body>
</html>

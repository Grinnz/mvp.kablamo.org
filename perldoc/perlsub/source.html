<!DOCTYPE html>
<html>
    <head>
        <meta charset="utf-8">
        <meta content="width=device-width, initial-scale=1" name="viewport">
        <link href="/theme/css/normalize.css" rel="stylesheet">
        <!--<link rel="stylesheet" href="/theme/css/skeleton.css" />-->
        <link href="/theme/css/statocles-default.css" rel="stylesheet">
        <link href="//maxcdn.bootstrapcdn.com/font-awesome/4.3.0/css/font-awesome.min.css" rel="stylesheet">
        <script crossorigin="anonymous" integrity="sha256-hwg4gsxgFZhOsEEamdOYGBf13FyQuiTwlAQgxVSNgt4=" src="http://code.jquery.com/jquery-3.2.1.min.js"></script>
        <title>perlsub (source) - Minimum Viable Perl</title>
        <meta content="Statocles 0.086" name="generator">
        
        <script>
            $( document ).ready(function() {
                $('.sub-menu ul').hide();
                $('.sub-menu.perldoc ul').show();
                $('.sub-menu.perldoc div').toggleClass("fa-caret-up fa-caret-down");
                $(".sub-menu a").click(function () {
                    $(this).parent(".sub-menu").siblings().children('ul').hide();
                    $(this).parent(".sub-menu").children("ul").slideToggle("100");
                    $(this).find(".right").toggleClass("fa-caret-up fa-caret-down");
                });
            });
        </script>
    </head>
    <body>
        <header>
        </header>
        <div class="container">

                <div class="left column full-height">
                    <div class="sidebar">
                    
<a class="home" href="/">Minimum<br>Viable<br>Perl</a><br><br><br>

<div class="animated bounceInDown">
  <ul>
    <li class="basics sub-menu">
      <a href="/perldoc/perlsub/#basics">The basics<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/basics/hello">Hello world</a></li>
        <li><a href="/basics/variables">Variables</a></li>
        <li><a href="/basics/references">References</a></li>
        <li><a href="/basics/functions">Functions</a></li>
        <li><a href="/basics/flow">Conditionals</a></li>
        <li><a href="/basics/loops">Loops</a></li>
        <li><a href="/basics/exceptions">Exceptions</a></li>
        <li><a href="/basics/regexps">Regexps</a></li>
        <li><a href="/basics/files">Files</a></li>
      </ul>
    </li>
    <li class="oo sub-menu">
      <a href="/perldoc/perlsub/#oo">Object oriented programming<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/oo/story">The (weird) story of Perl OO</a></li>
        <li><a href="/oo/classes">Classes</a></li>
        <li><a href="/oo/attributes">Attributes</a></li>
        <li><a href="/oo/modifiers">Method modifiers</a></li>
        <li><a href="/oo/roles">Roles</a></li>
        <li><a href="/oo/inheritance">Inheritance</a></li>
      </ul>
    </li>
    <li class="debugging sub-menu">
      <a href="/perldoc/perlsub/#basics">Debugging<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/debugging/data-printer">Examining data structures</a></li>
        <li><a href="/debugging/stack-trace">Forcing stack traces</a></li>
      </ul>
    </li>
    <li class="dependencies sub-menu">
      <a href="/perldoc/perlsub/#dependencies">Perl libraries<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/dependencies/perl5lib">Loading libraries from custom locations</a></li>
        <li><a href="/dependencies/cpanm">Installing libraries with cpanm</a></li>
        <li><a href="/dependencies/metacpan">Choosing libraries from MetaCPAN</a></li>
        <li><a href="/dependencies/carton">Managing project dependencies with Carton</a></li>
        <li><a href="/dependencies/pmtools">What version is installed and where?</a></li>
      </ul>
    </li>
    <li class="testing sub-menu">
      <a href="/perldoc/perlsub/#testing">Testing<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/testing/intro">Introduction to testing</a></li>
        <li><a href="/testing/warnings">Testing warnings and exceptions</a></li>
        <li><a href="/testing/data">Testing data structures</a></li>
        <li><a href="/testing/mock">Testing with mock objects</a></li>
        <li><a href="/testing/libs">Build your own test libraries</a></li>
      </ul>
    </li>
    <li><a href="/perl">Perl design goals</a></li>
    <li><a href="/see-also">Book recommendations</a></li>
    <li><a href="/about">About this book</a></li>
  </ul>
</div>

                    
                    
                    </div>
                </div>

                <div class="right column">
                    <main>
                    <a class="button" href="/perldoc/perlsub/">
    Back to documentation
</a>
<pre>=head1 NAME
X&lt;subroutine&gt; X&lt;function&gt;

perlsub - Perl subroutines

=head1 SYNOPSIS

To declare subroutines:
X&lt;subroutine, declaration&gt; X&lt;sub&gt;

    sub NAME;			  # A &quot;forward&quot; declaration.
    sub NAME(PROTO);		  #  ditto, but with prototypes
    sub NAME : ATTRS;		  #  with attributes
    sub NAME(PROTO) : ATTRS;	  #  with attributes and prototypes

    sub NAME BLOCK		  # A declaration and a definition.
    sub NAME(PROTO) BLOCK	  #  ditto, but with prototypes
    sub NAME(SIG) BLOCK           #  with a signature instead
    sub NAME : ATTRS BLOCK	  #  with attributes
    sub NAME(PROTO) : ATTRS BLOCK #  with prototypes and attributes
    sub NAME(SIG) : ATTRS BLOCK   #  with a signature and attributes

To define an anonymous subroutine at runtime:
X&lt;subroutine, anonymous&gt;

    $subref = sub BLOCK;		 # no proto
    $subref = sub (PROTO) BLOCK;	 # with proto
    $subref = sub (SIG) BLOCK;           # with signature
    $subref = sub : ATTRS BLOCK;	 # with attributes
    $subref = sub (PROTO) : ATTRS BLOCK; # with proto and attributes
    $subref = sub (SIG) : ATTRS BLOCK;   # with signature and attributes

To import subroutines:
X&lt;import&gt;

    use MODULE qw(NAME1 NAME2 NAME3);

To call subroutines:
X&lt;subroutine, call&gt; X&lt;call&gt;

    NAME(LIST);	   # &amp; is optional with parentheses.
    NAME LIST;	   # Parentheses optional if predeclared/imported.
    &amp;NAME(LIST);   # Circumvent prototypes.
    &amp;NAME;	   # Makes current @_ visible to called subroutine.

=head1 DESCRIPTION

Like many languages, Perl provides for user-defined subroutines.
These may be located anywhere in the main program, loaded in from
other files via the C&lt;do&gt;, C&lt;require&gt;, or C&lt;use&gt; keywords, or
generated on the fly using C&lt;eval&gt; or anonymous subroutines.
You can even call a function indirectly using a variable containing
its name or a CODE reference.

The Perl model for function call and return values is simple: all
functions are passed as parameters one single flat list of scalars, and
all functions likewise return to their caller one single flat list of
scalars.  Any arrays or hashes in these call and return lists will
collapse, losing their identities--but you may always use
pass-by-reference instead to avoid this.  Both call and return lists may
contain as many or as few scalar elements as you&#39;d like.  (Often a
function without an explicit return statement is called a subroutine, but
there&#39;s really no difference from Perl&#39;s perspective.)
X&lt;subroutine, parameter&gt; X&lt;parameter&gt;

Any arguments passed in show up in the array C&lt;@_&gt;.
(They may also show up in lexical variables introduced by a signature;
see L&lt;/Signatures&gt; below.)  Therefore, if
you called a function with two arguments, those would be stored in
C&lt;$_[0]&gt; and C&lt;$_[1]&gt;.  The array C&lt;@_&gt; is a local array, but its
elements are aliases for the actual scalar parameters.  In particular,
if an element C&lt;$_[0]&gt; is updated, the corresponding argument is
updated (or an error occurs if it is not updatable).  If an argument
is an array or hash element which did not exist when the function
was called, that element is created only when (and if) it is modified
or a reference to it is taken.  (Some earlier versions of Perl
created the element whether or not the element was assigned to.)
Assigning to the whole array C&lt;@_&gt; removes that aliasing, and does
not update any arguments.
X&lt;subroutine, argument&gt; X&lt;argument&gt; X&lt;@_&gt;

A C&lt;return&gt; statement may be used to exit a subroutine, optionally
specifying the returned value, which will be evaluated in the
appropriate context (list, scalar, or void) depending on the context of
the subroutine call.  If you specify no return value, the subroutine
returns an empty list in list context, the undefined value in scalar
context, or nothing in void context.  If you return one or more
aggregates (arrays and hashes), these will be flattened together into
one large indistinguishable list.

If no C&lt;return&gt; is found and if the last statement is an expression, its
value is returned.  If the last statement is a loop control structure
like a C&lt;foreach&gt; or a C&lt;while&gt;, the returned value is unspecified.  The
empty sub returns the empty list.
X&lt;subroutine, return value&gt; X&lt;return value&gt; X&lt;return&gt;

Aside from an experimental facility (see L&lt;/Signatures&gt; below),
Perl does not have named formal parameters.  In practice all you
do is assign to a C&lt;my()&gt; list of these.  Variables that aren&#39;t
declared to be private are global variables.  For gory details
on creating private variables, see L&lt;/&quot;Private Variables via my()&quot;&gt;
and L&lt;/&quot;Temporary Values via local()&quot;&gt;.  To create protected
environments for a set of functions in a separate package (and
probably a separate file), see L&lt;perlmod/&quot;Packages&quot;&gt;.
X&lt;formal parameter&gt; X&lt;parameter, formal&gt;

Example:

    sub max {
	my $max = shift(@_);
	foreach $foo (@_) {
	    $max = $foo if $max &lt; $foo;
	}
	return $max;
    }
    $bestday = max($mon,$tue,$wed,$thu,$fri);

Example:

    # get a line, combining continuation lines
    #  that start with whitespace

    sub get_line {
	$thisline = $lookahead;  # global variables!
	LINE: while (defined($lookahead = &lt;STDIN&gt;)) {
	    if ($lookahead =~ /^[ \t]/) {
		$thisline .= $lookahead;
	    }
	    else {
		last LINE;
	    }
	}
	return $thisline;
    }

    $lookahead = &lt;STDIN&gt;;	# get first line
    while (defined($line = get_line())) {
	...
    }

Assigning to a list of private variables to name your arguments:

    sub maybeset {
	my($key, $value) = @_;
	$Foo{$key} = $value unless $Foo{$key};
    }

Because the assignment copies the values, this also has the effect
of turning call-by-reference into call-by-value.  Otherwise a
function is free to do in-place modifications of C&lt;@_&gt; and change
its caller&#39;s values.
X&lt;call-by-reference&gt; X&lt;call-by-value&gt;

    upcase_in($v1, $v2);  # this changes $v1 and $v2
    sub upcase_in {
	for (@_) { tr/a-z/A-Z/ }
    }

You aren&#39;t allowed to modify constants in this way, of course.  If an
argument were actually literal and you tried to change it, you&#39;d take a
(presumably fatal) exception.   For example, this won&#39;t work:
X&lt;call-by-reference&gt; X&lt;call-by-value&gt;

    upcase_in(&quot;frederick&quot;);

It would be much safer if the C&lt;upcase_in()&gt; function
were written to return a copy of its parameters instead
of changing them in place:

    ($v3, $v4) = upcase($v1, $v2);  # this doesn&#39;t change $v1 and $v2
    sub upcase {
	return unless defined wantarray;  # void context, do nothing
	my @parms = @_;
	for (@parms) { tr/a-z/A-Z/ }
  	return wantarray ? @parms : $parms[0];
    }

Notice how this (unprototyped) function doesn&#39;t care whether it was
passed real scalars or arrays.  Perl sees all arguments as one big,
long, flat parameter list in C&lt;@_&gt;.  This is one area where
Perl&#39;s simple argument-passing style shines.  The C&lt;upcase()&gt;
function would work perfectly well without changing the C&lt;upcase()&gt;
definition even if we fed it things like this:

    @newlist   = upcase(@list1, @list2);
    @newlist   = upcase( split /:/, $var );

Do not, however, be tempted to do this:

    (@a, @b)   = upcase(@list1, @list2);

Like the flattened incoming parameter list, the return list is also
flattened on return.  So all you have managed to do here is stored
everything in C&lt;@a&gt; and made C&lt;@b&gt; empty.  See 
L&lt;/Pass by Reference&gt; for alternatives.

A subroutine may be called using an explicit C&lt;&amp;&gt; prefix.  The
C&lt;&amp;&gt; is optional in modern Perl, as are parentheses if the
subroutine has been predeclared.  The C&lt;&amp;&gt; is I&lt;not&gt; optional
when just naming the subroutine, such as when it&#39;s used as
an argument to defined() or undef().  Nor is it optional when you
want to do an indirect subroutine call with a subroutine name or
reference using the C&lt;&amp;$subref()&gt; or C&lt;&amp;{$subref}()&gt; constructs,
although the C&lt;&lt; $subref-&gt;() &gt;&gt; notation solves that problem.
See L&lt;perlref&gt; for more about all that.
X&lt;&amp;&gt;

Subroutines may be called recursively.  If a subroutine is called
using the C&lt;&amp;&gt; form, the argument list is optional, and if omitted,
no C&lt;@_&gt; array is set up for the subroutine: the C&lt;@_&gt; array at the
time of the call is visible to subroutine instead.  This is an
efficiency mechanism that new users may wish to avoid.
X&lt;recursion&gt;

    &amp;foo(1,2,3);	# pass three arguments
    foo(1,2,3);		# the same

    foo();		# pass a null list
    &amp;foo();		# the same

    &amp;foo;		# foo() get current args, like foo(@_) !!
    foo;		# like foo() IFF sub foo predeclared, else &quot;foo&quot;

Not only does the C&lt;&amp;&gt; form make the argument list optional, it also
disables any prototype checking on arguments you do provide.  This
is partly for historical reasons, and partly for having a convenient way
to cheat if you know what you&#39;re doing.  See L&lt;/Prototypes&gt; below.
X&lt;&amp;&gt;

Since Perl 5.16.0, the C&lt;__SUB__&gt; token is available under C&lt;use feature
&#39;current_sub&#39;&gt; and C&lt;use 5.16.0&gt;.  It will evaluate to a reference to the
currently-running sub, which allows for recursive calls without knowing
your subroutine&#39;s name.

    use 5.16.0;
    my $factorial = sub {
      my ($x) = @_;
      return 1 if $x == 1;
      return($x * __SUB__-&gt;( $x - 1 ) );
    };

The behavior of C&lt;__SUB__&gt; within a regex code block (such as C&lt;/(?{...})/&gt;)
is subject to change.

Subroutines whose names are in all upper case are reserved to the Perl
core, as are modules whose names are in all lower case.  A subroutine in
all capitals is a loosely-held convention meaning it will be called
indirectly by the run-time system itself, usually due to a triggered event.
Subroutines whose name start with a left parenthesis are also reserved the 
same way.  The following is a list of some subroutines that currently do 
special, pre-defined things.

=over

=item documented later in this document

C&lt;AUTOLOAD&gt;

=item documented in L&lt;perlmod&gt;

C&lt;CLONE&gt;, C&lt;CLONE_SKIP&gt;

=item documented in L&lt;perlobj&gt;

C&lt;DESTROY&gt;, C&lt;DOES&gt;

=item documented in L&lt;perltie&gt;

C&lt;BINMODE&gt;, C&lt;CLEAR&gt;, C&lt;CLOSE&gt;, C&lt;DELETE&gt;, C&lt;DESTROY&gt;, C&lt;EOF&gt;, C&lt;EXISTS&gt;, 
C&lt;EXTEND&gt;, C&lt;FETCH&gt;, C&lt;FETCHSIZE&gt;, C&lt;FILENO&gt;, C&lt;FIRSTKEY&gt;, C&lt;GETC&gt;, 
C&lt;NEXTKEY&gt;, C&lt;OPEN&gt;, C&lt;POP&gt;, C&lt;PRINT&gt;, C&lt;PRINTF&gt;, C&lt;PUSH&gt;, C&lt;READ&gt;, 
C&lt;READLINE&gt;, C&lt;SCALAR&gt;, C&lt;SEEK&gt;, C&lt;SHIFT&gt;, C&lt;SPLICE&gt;, C&lt;STORE&gt;, 
C&lt;STORESIZE&gt;, C&lt;TELL&gt;, C&lt;TIEARRAY&gt;, C&lt;TIEHANDLE&gt;, C&lt;TIEHASH&gt;, 
C&lt;TIESCALAR&gt;, C&lt;UNSHIFT&gt;, C&lt;UNTIE&gt;, C&lt;WRITE&gt;

=item documented in L&lt;PerlIO::via&gt;

C&lt;BINMODE&gt;, C&lt;CLEARERR&gt;, C&lt;CLOSE&gt;, C&lt;EOF&gt;, C&lt;ERROR&gt;, C&lt;FDOPEN&gt;, C&lt;FILENO&gt;, 
C&lt;FILL&gt;, C&lt;FLUSH&gt;, C&lt;OPEN&gt;, C&lt;POPPED&gt;, C&lt;PUSHED&gt;, C&lt;READ&gt;, C&lt;SEEK&gt;, 
C&lt;SETLINEBUF&gt;, C&lt;SYSOPEN&gt;, C&lt;TELL&gt;, C&lt;UNREAD&gt;, C&lt;UTF8&gt;, C&lt;WRITE&gt;

=item documented in L&lt;perlfunc&gt;

L&lt;&lt; C&lt;import&gt; | perlfunc/use &gt;&gt;, L&lt;&lt; C&lt;unimport&gt; | perlfunc/use &gt;&gt;,
L&lt;&lt; C&lt;INC&gt; | perlfunc/require &gt;&gt;

=item documented in L&lt;UNIVERSAL&gt;

C&lt;VERSION&gt;

=item documented in L&lt;perldebguts&gt;

C&lt;DB::DB&gt;, C&lt;DB::sub&gt;, C&lt;DB::lsub&gt;, C&lt;DB::goto&gt;, C&lt;DB::postponed&gt;

=item undocumented, used internally by the L&lt;overload&gt; feature

any starting with C&lt;(&gt;

=back

The C&lt;BEGIN&gt;, C&lt;UNITCHECK&gt;, C&lt;CHECK&gt;, C&lt;INIT&gt; and C&lt;END&gt; subroutines
are not so much subroutines as named special code blocks, of which you
can have more than one in a package, and which you can B&lt;not&gt; call
explicitly.  See L&lt;perlmod/&quot;BEGIN, UNITCHECK, CHECK, INIT and END&quot;&gt;

=head2 Signatures

B&lt;WARNING&gt;: Subroutine signatures are experimental.  The feature may be
modified or removed in future versions of Perl.

Perl has an experimental facility to allow a subroutine&#39;s formal
parameters to be introduced by special syntax, separate from the
procedural code of the subroutine body.  The formal parameter list
is known as a I&lt;signature&gt;.  The facility must be enabled first by a
pragmatic declaration, C&lt;use feature &#39;signatures&#39;&gt;, and it will produce
a warning unless the &quot;experimental::signatures&quot; warnings category is
disabled.

The signature is part of a subroutine&#39;s body.  Normally the body of a
subroutine is simply a braced block of code.  When using a signature,
the signature is a parenthesised list that goes immediately after
the subroutine name (or, for anonymous subroutines, immediately after
the C&lt;sub&gt; keyword).  The signature declares lexical variables that are
in scope for the block.  When the subroutine is called, the signature
takes control first.  It populates the signature variables from the
list of arguments that were passed.  If the argument list doesn&#39;t meet
the requirements of the signature, then it will throw an exception.
When the signature processing is complete, control passes to the block.

Positional parameters are handled by simply naming scalar variables in
the signature.  For example,

    sub foo ($left, $right) {
	return $left + $right;
    }

takes two positional parameters, which must be filled at runtime by
two arguments.  By default the parameters are mandatory, and it is
not permitted to pass more arguments than expected.  So the above is
equivalent to

    sub foo {
	die &quot;Too many arguments for subroutine&quot; unless @_ &lt;= 2;
	die &quot;Too few arguments for subroutine&quot; unless @_ &gt;= 2;
	my $left = $_[0];
	my $right = $_[1];
	return $left + $right;
    }

An argument can be ignored by omitting the main part of the name from
a parameter declaration, leaving just a bare C&lt;$&gt; sigil.  For example,

    sub foo ($first, $, $third) {
	return &quot;first=$first, third=$third&quot;;
    }

Although the ignored argument doesn&#39;t go into a variable, it is still
mandatory for the caller to pass it.

A positional parameter is made optional by giving a default value,
separated from the parameter name by C&lt;=&gt;:

    sub foo ($left, $right = 0) {
	return $left + $right;
    }

The above subroutine may be called with either one or two arguments.
The default value expression is evaluated when the subroutine is called,
so it may provide different default values for different calls.  It is
only evaluated if the argument was actually omitted from the call.
For example,

    my $auto_id = 0;
    sub foo ($thing, $id = $auto_id++) {
	print &quot;$thing has ID $id&quot;;
    }

automatically assigns distinct sequential IDs to things for which no
ID was supplied by the caller.  A default value expression may also
refer to parameters earlier in the signature, making the default for
one parameter vary according to the earlier parameters.  For example,

    sub foo ($first_name, $surname, $nickname = $first_name) {
	print &quot;$first_name $surname is known as \&quot;$nickname\&quot;&quot;;
    }

An optional parameter can be nameless just like a mandatory parameter.
For example,

    sub foo ($thing, $ = 1) {
	print $thing;
    }

The parameter&#39;s default value will still be evaluated if the corresponding
argument isn&#39;t supplied, even though the value won&#39;t be stored anywhere.
This is in case evaluating it has important side effects.  However, it
will be evaluated in void context, so if it doesn&#39;t have side effects
and is not trivial it will generate a warning if the &quot;void&quot; warning
category is enabled.  If a nameless optional parameter&#39;s default value
is not important, it may be omitted just as the parameter&#39;s name was:

    sub foo ($thing, $=) {
	print $thing;
    }

Optional positional parameters must come after all mandatory positional
parameters.  (If there are no mandatory positional parameters then an
optional positional parameters can be the first thing in the signature.)
If there are multiple optional positional parameters and not enough
arguments are supplied to fill them all, they will be filled from left
to right.

After positional parameters, additional arguments may be captured in a
slurpy parameter.  The simplest form of this is just an array variable:

    sub foo ($filter, @inputs) {
	print $filter-&gt;($_) foreach @inputs;
    }

With a slurpy parameter in the signature, there is no upper limit on how
many arguments may be passed.  A slurpy array parameter may be nameless
just like a positional parameter, in which case its only effect is to
turn off the argument limit that would otherwise apply:

    sub foo ($thing, @) {
	print $thing;
    }

A slurpy parameter may instead be a hash, in which case the arguments
available to it are interpreted as alternating keys and values.
There must be as many keys as values: if there is an odd argument then
an exception will be thrown.  Keys will be stringified, and if there are
duplicates then the later instance takes precedence over the earlier,
as with standard hash construction.

    sub foo ($filter, %inputs) {
	print $filter-&gt;($_, $inputs{$_}) foreach sort keys %inputs;
    }

A slurpy hash parameter may be nameless just like other kinds of
parameter.  It still insists that the number of arguments available to
it be even, even though they&#39;re not being put into a variable.

    sub foo ($thing, %) {
	print $thing;
    }

A slurpy parameter, either array or hash, must be the last thing in the
signature.  It may follow mandatory and optional positional parameters;
it may also be the only thing in the signature.  Slurpy parameters cannot
have default values: if no arguments are supplied for them then you get
an empty array or empty hash.

A signature may be entirely empty, in which case all it does is check
that the caller passed no arguments:

    sub foo () {
	return 123;
    }

When using a signature, the arguments are still available in the special
array variable C&lt;@_&gt;, in addition to the lexical variables of the
signature.  There is a difference between the two ways of accessing the
arguments: C&lt;@_&gt; I&lt;aliases&gt; the arguments, but the signature variables
get I&lt;copies&gt; of the arguments.  So writing to a signature variable
only changes that variable, and has no effect on the caller&#39;s variables,
but writing to an element of C&lt;@_&gt; modifies whatever the caller used to
supply that argument.

There is a potential syntactic ambiguity between signatures and prototypes
(see L&lt;/Prototypes&gt;), because both start with an opening parenthesis and
both can appear in some of the same places, such as just after the name
in a subroutine declaration.  For historical reasons, when signatures
are not enabled, any opening parenthesis in such a context will trigger
very forgiving prototype parsing.  Most signatures will be interpreted
as prototypes in those circumstances, but won&#39;t be valid prototypes.
(A valid prototype cannot contain any alphabetic character.)  This will
lead to somewhat confusing error messages.

To avoid ambiguity, when signatures are enabled the special syntax
for prototypes is disabled.  There is no attempt to guess whether a
parenthesised group was intended to be a prototype or a signature.
To give a subroutine a prototype under these circumstances, use a
L&lt;prototype attribute|attributes/Built-in Attributes&gt;.  For example,

    sub foo :prototype($) { $_[0] }

It is entirely possible for a subroutine to have both a prototype and
a signature.  They do different jobs: the prototype affects compilation
of calls to the subroutine, and the signature puts argument values into
lexical variables at runtime.  You can therefore write

    sub foo ($left, $right) : prototype($$) {
	return $left + $right;
    }

The prototype attribute, and any other attributes, come after 
the signature.

=head2 Private Variables via my()
X&lt;my&gt; X&lt;variable, lexical&gt; X&lt;lexical&gt; X&lt;lexical variable&gt; X&lt;scope, lexical&gt;
X&lt;lexical scope&gt; X&lt;attributes, my&gt;

Synopsis:

    my $foo;	    	# declare $foo lexically local
    my (@wid, %get); 	# declare list of variables local
    my $foo = &quot;flurp&quot;;	# declare $foo lexical, and init it
    my @oof = @bar;	# declare @oof lexical, and init it
    my $x : Foo = $y;	# similar, with an attribute applied

B&lt;WARNING&gt;: The use of attribute lists on C&lt;my&gt; declarations is still
evolving.  The current semantics and interface are subject to change.
See L&lt;attributes&gt; and L&lt;Attribute::Handlers&gt;.

The C&lt;my&gt; operator declares the listed variables to be lexically
confined to the enclosing block, conditional
(C&lt;if&gt;/C&lt;unless&gt;/C&lt;elsif&gt;/C&lt;else&gt;), loop
(C&lt;for&gt;/C&lt;foreach&gt;/C&lt;while&gt;/C&lt;until&gt;/C&lt;continue&gt;), subroutine, C&lt;eval&gt;,
or C&lt;do&gt;/C&lt;require&gt;/C&lt;use&gt;&#39;d file.  If more than one value is listed, the
list must be placed in parentheses.  All listed elements must be
legal lvalues.  Only alphanumeric identifiers may be lexically
scoped--magical built-ins like C&lt;$/&gt; must currently be C&lt;local&gt;ized
with C&lt;local&gt; instead.

Unlike dynamic variables created by the C&lt;local&gt; operator, lexical
variables declared with C&lt;my&gt; are totally hidden from the outside
world, including any called subroutines.  This is true if it&#39;s the
same subroutine called from itself or elsewhere--every call gets
its own copy.
X&lt;local&gt;

This doesn&#39;t mean that a C&lt;my&gt; variable declared in a statically
enclosing lexical scope would be invisible.  Only dynamic scopes
are cut off.   For example, the C&lt;bumpx()&gt; function below has access
to the lexical $x variable because both the C&lt;my&gt; and the C&lt;sub&gt;
occurred at the same scope, presumably file scope.

    my $x = 10;
    sub bumpx { $x++ } 

An C&lt;eval()&gt;, however, can see lexical variables of the scope it is
being evaluated in, so long as the names aren&#39;t hidden by declarations within
the C&lt;eval()&gt; itself.  See L&lt;perlref&gt;.
X&lt;eval, scope of&gt;

The parameter list to my() may be assigned to if desired, which allows you
to initialize your variables.  (If no initializer is given for a
particular variable, it is created with the undefined value.)  Commonly
this is used to name input parameters to a subroutine.  Examples:

    $arg = &quot;fred&quot;;	  # &quot;global&quot; variable
    $n = cube_root(27);
    print &quot;$arg thinks the root is $n\n&quot;;
 fred thinks the root is 3

    sub cube_root {
	my $arg = shift;  # name doesn&#39;t matter
	$arg **= 1/3;
	return $arg;
    }

The C&lt;my&gt; is simply a modifier on something you might assign to.  So when
you do assign to variables in its argument list, C&lt;my&gt; doesn&#39;t
change whether those variables are viewed as a scalar or an array.  So

    my ($foo) = &lt;STDIN&gt;;		# WRONG?
    my @FOO = &lt;STDIN&gt;;

both supply a list context to the right-hand side, while

    my $foo = &lt;STDIN&gt;;

supplies a scalar context.  But the following declares only one variable:

    my $foo, $bar = 1;			# WRONG

That has the same effect as

    my $foo;
    $bar = 1;

The declared variable is not introduced (is not visible) until after
the current statement.  Thus,

    my $x = $x;

can be used to initialize a new $x with the value of the old $x, and
the expression

    my $x = 123 and $x == 123

is false unless the old $x happened to have the value C&lt;123&gt;.

Lexical scopes of control structures are not bounded precisely by the
braces that delimit their controlled blocks; control expressions are
part of that scope, too.  Thus in the loop

    while (my $line = &lt;&gt;) {
        $line = lc $line;
    } continue {
        print $line;
    }

the scope of $line extends from its declaration throughout the rest of
the loop construct (including the C&lt;continue&gt; clause), but not beyond
it.  Similarly, in the conditional

    if ((my $answer = &lt;STDIN&gt;) =~ /^yes$/i) {
        user_agrees();
    } elsif ($answer =~ /^no$/i) {
        user_disagrees();
    } else {
	chomp $answer;
        die &quot;&#39;$answer&#39; is neither &#39;yes&#39; nor &#39;no&#39;&quot;;
    }

the scope of $answer extends from its declaration through the rest
of that conditional, including any C&lt;elsif&gt; and C&lt;else&gt; clauses, 
but not beyond it.  See L&lt;perlsyn/&quot;Simple Statements&quot;&gt; for information
on the scope of variables in statements with modifiers.

The C&lt;foreach&gt; loop defaults to scoping its index variable dynamically
in the manner of C&lt;local&gt;.  However, if the index variable is
prefixed with the keyword C&lt;my&gt;, or if there is already a lexical
by that name in scope, then a new lexical is created instead.  Thus
in the loop
X&lt;foreach&gt; X&lt;for&gt;

    for my $i (1, 2, 3) {
        some_function();
    }

the scope of $i extends to the end of the loop, but not beyond it,
rendering the value of $i inaccessible within C&lt;some_function()&gt;.
X&lt;foreach&gt; X&lt;for&gt;

Some users may wish to encourage the use of lexically scoped variables.
As an aid to catching implicit uses to package variables,
which are always global, if you say

    use strict &#39;vars&#39;;

then any variable mentioned from there to the end of the enclosing
block must either refer to a lexical variable, be predeclared via
C&lt;our&gt; or C&lt;use vars&gt;, or else must be fully qualified with the package name.
A compilation error results otherwise.  An inner block may countermand
this with C&lt;no strict &#39;vars&#39;&gt;.

A C&lt;my&gt; has both a compile-time and a run-time effect.  At compile
time, the compiler takes notice of it.  The principal usefulness
of this is to quiet C&lt;use strict &#39;vars&#39;&gt;, but it is also essential
for generation of closures as detailed in L&lt;perlref&gt;.  Actual
initialization is delayed until run time, though, so it gets executed
at the appropriate time, such as each time through a loop, for
example.

Variables declared with C&lt;my&gt; are not part of any package and are therefore
never fully qualified with the package name.  In particular, you&#39;re not
allowed to try to make a package variable (or other global) lexical:

    my $pack::var;	# ERROR!  Illegal syntax

In fact, a dynamic variable (also known as package or global variables)
are still accessible using the fully qualified C&lt;::&gt; notation even while a
lexical of the same name is also visible:

    package main;
    local $x = 10;
    my    $x = 20;
    print &quot;$x and $::x\n&quot;;

That will print out C&lt;20&gt; and C&lt;10&gt;.

You may declare C&lt;my&gt; variables at the outermost scope of a file
to hide any such identifiers from the world outside that file.  This
is similar in spirit to C&#39;s static variables when they are used at
the file level.  To do this with a subroutine requires the use of
a closure (an anonymous function that accesses enclosing lexicals).
If you want to create a private subroutine that cannot be called
from outside that block, it can declare a lexical variable containing
an anonymous sub reference:

    my $secret_version = &#39;1.001-beta&#39;;
    my $secret_sub = sub { print $secret_version };
    &amp;$secret_sub();

As long as the reference is never returned by any function within the
module, no outside module can see the subroutine, because its name is not in
any package&#39;s symbol table.  Remember that it&#39;s not I&lt;REALLY&gt; called
C&lt;$some_pack::secret_version&gt; or anything; it&#39;s just $secret_version,
unqualified and unqualifiable.

This does not work with object methods, however; all object methods
have to be in the symbol table of some package to be found.  See
L&lt;perlref/&quot;Function Templates&quot;&gt; for something of a work-around to
this.

=head2 Persistent Private Variables
X&lt;state&gt; X&lt;state variable&gt; X&lt;static&gt; X&lt;variable, persistent&gt; X&lt;variable, static&gt; X&lt;closure&gt;

There are two ways to build persistent private variables in Perl 5.10.
First, you can simply use the C&lt;state&gt; feature.  Or, you can use closures,
if you want to stay compatible with releases older than 5.10.

=head3 Persistent variables via state()

Beginning with Perl 5.10.0, you can declare variables with the C&lt;state&gt;
keyword in place of C&lt;my&gt;.  For that to work, though, you must have
enabled that feature beforehand, either by using the C&lt;feature&gt; pragma, or
by using C&lt;-E&gt; on one-liners (see L&lt;feature&gt;).  Beginning with Perl 5.16,
the C&lt;CORE::state&gt; form does not require the
C&lt;feature&gt; pragma.

The C&lt;state&gt; keyword creates a lexical variable (following the same scoping
rules as C&lt;my&gt;) that persists from one subroutine call to the next.  If a
state variable resides inside an anonymous subroutine, then each copy of
the subroutine has its own copy of the state variable.  However, the value
of the state variable will still persist between calls to the same copy of
the anonymous subroutine.  (Don&#39;t forget that C&lt;sub { ... }&gt; creates a new
subroutine each time it is executed.)

For example, the following code maintains a private counter, incremented
each time the gimme_another() function is called:

    use feature &#39;state&#39;;
    sub gimme_another { state $x; return ++$x }

And this example uses anonymous subroutines to create separate counters:

    use feature &#39;state&#39;;
    sub create_counter {
	return sub { state $x; return ++$x }
    }

Also, since C&lt;$x&gt; is lexical, it can&#39;t be reached or modified by any Perl
code outside.

When combined with variable declaration, simple scalar assignment to C&lt;state&gt;
variables (as in C&lt;state $x = 42&gt;) is executed only the first time.  When such
statements are evaluated subsequent times, the assignment is ignored.  The
behavior of this sort of assignment to non-scalar variables is undefined.

=head3 Persistent variables with closures

Just because a lexical variable is lexically (also called statically)
scoped to its enclosing block, C&lt;eval&gt;, or C&lt;do&gt; FILE, this doesn&#39;t mean that
within a function it works like a C static.  It normally works more
like a C auto, but with implicit garbage collection.  

Unlike local variables in C or C++, Perl&#39;s lexical variables don&#39;t
necessarily get recycled just because their scope has exited.
If something more permanent is still aware of the lexical, it will
stick around.  So long as something else references a lexical, that
lexical won&#39;t be freed--which is as it should be.  You wouldn&#39;t want
memory being free until you were done using it, or kept around once you
were done.  Automatic garbage collection takes care of this for you.

This means that you can pass back or save away references to lexical
variables, whereas to return a pointer to a C auto is a grave error.
It also gives us a way to simulate C&#39;s function statics.  Here&#39;s a
mechanism for giving a function private variables with both lexical
scoping and a static lifetime.  If you do want to create something like
C&#39;s static variables, just enclose the whole function in an extra block,
and put the static variable outside the function but in the block.

    {
	my $secret_val = 0;
	sub gimme_another {
	    return ++$secret_val;
	}
    }
    # $secret_val now becomes unreachable by the outside
    # world, but retains its value between calls to gimme_another

If this function is being sourced in from a separate file
via C&lt;require&gt; or C&lt;use&gt;, then this is probably just fine.  If it&#39;s
all in the main program, you&#39;ll need to arrange for the C&lt;my&gt;
to be executed early, either by putting the whole block above
your main program, or more likely, placing merely a C&lt;BEGIN&gt;
code block around it to make sure it gets executed before your program
starts to run:

    BEGIN {
	my $secret_val = 0;
	sub gimme_another {
	    return ++$secret_val;
	}
    }

See L&lt;perlmod/&quot;BEGIN, UNITCHECK, CHECK, INIT and END&quot;&gt; about the
special triggered code blocks, C&lt;BEGIN&gt;, C&lt;UNITCHECK&gt;, C&lt;CHECK&gt;,
C&lt;INIT&gt; and C&lt;END&gt;.

If declared at the outermost scope (the file scope), then lexicals
work somewhat like C&#39;s file statics.  They are available to all
functions in that same file declared below them, but are inaccessible
from outside that file.  This strategy is sometimes used in modules
to create private variables that the whole module can see.

=head2 Temporary Values via local()
X&lt;local&gt; X&lt;scope, dynamic&gt; X&lt;dynamic scope&gt; X&lt;variable, local&gt;
X&lt;variable, temporary&gt;

B&lt;WARNING&gt;: In general, you should be using C&lt;my&gt; instead of C&lt;local&gt;, because
it&#39;s faster and safer.  Exceptions to this include the global punctuation
variables, global filehandles and formats, and direct manipulation of the
Perl symbol table itself.  C&lt;local&gt; is mostly used when the current value
of a variable must be visible to called subroutines.

Synopsis:

    # localization of values

    local $foo;		       # make $foo dynamically local
    local (@wid, %get);	       # make list of variables local
    local $foo = &quot;flurp&quot;;      # make $foo dynamic, and init it
    local @oof = @bar;	       # make @oof dynamic, and init it

    local $hash{key} = &quot;val&quot;;  # sets a local value for this hash entry
    delete local $hash{key};   # delete this entry for the current block
    local ($cond ? $v1 : $v2); # several types of lvalues support
			       # localization

    # localization of symbols

    local *FH;		       # localize $FH, @FH, %FH, &amp;FH  ...
    local *merlyn = *randal;   # now $merlyn is really $randal, plus
                               #     @merlyn is really @randal, etc
    local *merlyn = &#39;randal&#39;;  # SAME THING: promote &#39;randal&#39; to *randal
    local *merlyn = \$randal;  # just alias $merlyn, not @merlyn etc

A C&lt;local&gt; modifies its listed variables to be &quot;local&quot; to the
enclosing block, C&lt;eval&gt;, or C&lt;do FILE&gt;--and to I&lt;any subroutine
called from within that block&gt;.  A C&lt;local&gt; just gives temporary
values to global (meaning package) variables.  It does I&lt;not&gt; create
a local variable.  This is known as dynamic scoping.  Lexical scoping
is done with C&lt;my&gt;, which works more like C&#39;s auto declarations.

Some types of lvalues can be localized as well: hash and array elements
and slices, conditionals (provided that their result is always
localizable), and symbolic references.  As for simple variables, this
creates new, dynamically scoped values.

If more than one variable or expression is given to C&lt;local&gt;, they must be
placed in parentheses.  This operator works
by saving the current values of those variables in its argument list on a
hidden stack and restoring them upon exiting the block, subroutine, or
eval.  This means that called subroutines can also reference the local
variable, but not the global one.  The argument list may be assigned to if
desired, which allows you to initialize your local variables.  (If no
initializer is given for a particular variable, it is created with an
undefined value.)

Because C&lt;local&gt; is a run-time operator, it gets executed each time
through a loop.  Consequently, it&#39;s more efficient to localize your
variables outside the loop.

=head3 Grammatical note on local()
X&lt;local, context&gt;

A C&lt;local&gt; is simply a modifier on an lvalue expression.  When you assign to
a C&lt;local&gt;ized variable, the C&lt;local&gt; doesn&#39;t change whether its list is viewed
as a scalar or an array.  So

    local($foo) = &lt;STDIN&gt;;
    local @FOO = &lt;STDIN&gt;;

both supply a list context to the right-hand side, while

    local $foo = &lt;STDIN&gt;;

supplies a scalar context.

=head3 Localization of special variables
X&lt;local, special variable&gt;

If you localize a special variable, you&#39;ll be giving a new value to it,
but its magic won&#39;t go away.  That means that all side-effects related
to this magic still work with the localized value.

This feature allows code like this to work :

    # Read the whole contents of FILE in $slurp
    { local $/ = undef; $slurp = &lt;FILE&gt;; }

Note, however, that this restricts localization of some values ; for
example, the following statement dies, as of perl 5.10.0, with an error
I&lt;Modification of a read-only value attempted&gt;, because the $1 variable is
magical and read-only :

    local $1 = 2;

One exception is the default scalar variable: starting with perl 5.14
C&lt;local($_)&gt; will always strip all magic from $_, to make it possible
to safely reuse $_ in a subroutine.

B&lt;WARNING&gt;: Localization of tied arrays and hashes does not currently
work as described.
This will be fixed in a future release of Perl; in the meantime, avoid
code that relies on any particular behavior of localising tied arrays
or hashes (localising individual elements is still okay).
See L&lt;perl58delta/&quot;Localising Tied Arrays and Hashes Is Broken&quot;&gt; for more
details.
X&lt;local, tie&gt;

=head3 Localization of globs
X&lt;local, glob&gt; X&lt;glob&gt;

The construct

    local *name;

creates a whole new symbol table entry for the glob C&lt;name&gt; in the
current package.  That means that all variables in its glob slot ($name,
@name, %name, &amp;name, and the C&lt;name&gt; filehandle) are dynamically reset.

This implies, among other things, that any magic eventually carried by
those variables is locally lost.  In other words, saying C&lt;local */&gt;
will not have any effect on the internal value of the input record
separator.

=head3 Localization of elements of composite types
X&lt;local, composite type element&gt; X&lt;local, array element&gt; X&lt;local, hash element&gt;

It&#39;s also worth taking a moment to explain what happens when you
C&lt;local&gt;ize a member of a composite type (i.e. an array or hash element).
In this case, the element is C&lt;local&gt;ized I&lt;by name&gt;.  This means that
when the scope of the C&lt;local()&gt; ends, the saved value will be
restored to the hash element whose key was named in the C&lt;local()&gt;, or
the array element whose index was named in the C&lt;local()&gt;.  If that
element was deleted while the C&lt;local()&gt; was in effect (e.g. by a
C&lt;delete()&gt; from a hash or a C&lt;shift()&gt; of an array), it will spring
back into existence, possibly extending an array and filling in the
skipped elements with C&lt;undef&gt;.  For instance, if you say

    %hash = ( &#39;This&#39; =&gt; &#39;is&#39;, &#39;a&#39; =&gt; &#39;test&#39; );
    @ary  = ( 0..5 );
    {
         local($ary[5]) = 6;
         local($hash{&#39;a&#39;}) = &#39;drill&#39;;
         while (my $e = pop(@ary)) {
             print &quot;$e . . .\n&quot;;
             last unless $e &gt; 3;
         }
         if (@ary) {
             $hash{&#39;only a&#39;} = &#39;test&#39;;
             delete $hash{&#39;a&#39;};
         }
    }
    print join(&#39; &#39;, map { &quot;$_ $hash{$_}&quot; } sort keys %hash),&quot;.\n&quot;;
    print &quot;The array has &quot;,scalar(@ary),&quot; elements: &quot;,
          join(&#39;, &#39;, map { defined $_ ? $_ : &#39;undef&#39; } @ary),&quot;\n&quot;;

Perl will print

    6 . . .
    4 . . .
    3 . . .
    This is a test only a test.
    The array has 6 elements: 0, 1, 2, undef, undef, 5

The behavior of local() on non-existent members of composite
types is subject to change in future.

=head3 Localized deletion of elements of composite types
X&lt;delete&gt; X&lt;local, composite type element&gt; X&lt;local, array element&gt; X&lt;local, hash element&gt;

You can use the C&lt;delete local $array[$idx]&gt; and C&lt;delete local $hash{key}&gt;
constructs to delete a composite type entry for the current block and restore
it when it ends.  They return the array/hash value before the localization,
which means that they are respectively equivalent to

    do {
        my $val = $array[$idx];
        local  $array[$idx];
        delete $array[$idx];
        $val
    }

and

    do {
        my $val = $hash{key};
        local  $hash{key};
        delete $hash{key};
        $val
    }

except that for those the C&lt;local&gt; is
scoped to the C&lt;do&gt; block.  Slices are
also accepted.

    my %hash = (
     a =&gt; [ 7, 8, 9 ],
     b =&gt; 1,
    )

    {
     my $a = delete local $hash{a};
     # $a is [ 7, 8, 9 ]
     # %hash is (b =&gt; 1)

     {
      my @nums = delete local @$a[0, 2]
      # @nums is (7, 9)
      # $a is [ undef, 8 ]

      $a[0] = 999; # will be erased when the scope ends
     }
     # $a is back to [ 7, 8, 9 ]

    }
    # %hash is back to its original state

=head2 Lvalue subroutines
X&lt;lvalue&gt; X&lt;subroutine, lvalue&gt;

It is possible to return a modifiable value from a subroutine.
To do this, you have to declare the subroutine to return an lvalue.

    my $val;
    sub canmod : lvalue {
	$val;  # or:  return $val;
    }
    sub nomod {
	$val;
    }

    canmod() = 5;   # assigns to $val
    nomod()  = 5;   # ERROR

The scalar/list context for the subroutine and for the right-hand
side of assignment is determined as if the subroutine call is replaced
by a scalar.  For example, consider:

    data(2,3) = get_data(3,4);

Both subroutines here are called in a scalar context, while in:

    (data(2,3)) = get_data(3,4);

and in:

    (data(2),data(3)) = get_data(3,4);

all the subroutines are called in a list context.

Lvalue subroutines are convenient, but you have to keep in mind that,
when used with objects, they may violate encapsulation.  A normal
mutator can check the supplied argument before setting the attribute
it is protecting, an lvalue subroutine cannot.  If you require any
special processing when storing and retrieving the values, consider
using the CPAN module Sentinel or something similar.

=head2 Lexical Subroutines
X&lt;my sub&gt; X&lt;state sub&gt; X&lt;our sub&gt; X&lt;subroutine, lexical&gt;

Beginning with Perl 5.18, you can declare a private subroutine with C&lt;my&gt;
or C&lt;state&gt;.  As with state variables, the C&lt;state&gt; keyword is only
available under C&lt;use feature &#39;state&#39;&gt; or C&lt;use 5.010&gt; or higher.

Prior to Perl 5.26, lexical subroutines were deemed experimental and were
available only under the C&lt;use feature &#39;lexical_subs&#39;&gt; pragma.  They also
produced a warning unless the &quot;experimental::lexical_subs&quot; warnings
category was disabled.

These subroutines are only visible within the block in which they are
declared, and only after that declaration:

    # Include these two lines if your code is intended to run under Perl
    # versions earlier than 5.26.
    no warnings &quot;experimental::lexical_subs&quot;;
    use feature &#39;lexical_subs&#39;;

    foo();		# calls the package/global subroutine
    state sub foo {
	foo();		# also calls the package subroutine
    }
    foo();		# calls &quot;state&quot; sub
    my $ref = \&amp;foo;	# take a reference to &quot;state&quot; sub

    my sub bar { ... }
    bar();		# calls &quot;my&quot; sub

To use a lexical subroutine from inside the subroutine itself, you must
predeclare it.  The C&lt;sub foo {...}&gt; subroutine definition syntax respects
any previous C&lt;my sub;&gt; or C&lt;state sub;&gt; declaration.

    my sub baz;		# predeclaration
    sub baz {		# define the &quot;my&quot; sub
	baz();		# recursive call
    }

=head3 C&lt;state sub&gt; vs C&lt;my sub&gt;

What is the difference between &quot;state&quot; subs and &quot;my&quot; subs?  Each time that
execution enters a block when &quot;my&quot; subs are declared, a new copy of each
sub is created.  &quot;State&quot; subroutines persist from one execution of the
containing block to the next.

So, in general, &quot;state&quot; subroutines are faster.  But &quot;my&quot; subs are
necessary if you want to create closures:

    sub whatever {
	my $x = shift;
	my sub inner {
	    ... do something with $x ...
	}
	inner();
    }

In this example, a new C&lt;$x&gt; is created when C&lt;whatever&gt; is called, and
also a new C&lt;inner&gt;, which can see the new C&lt;$x&gt;.  A &quot;state&quot; sub will only
see the C&lt;$x&gt; from the first call to C&lt;whatever&gt;.

=head3 C&lt;our&gt; subroutines

Like C&lt;our $variable&gt;, C&lt;our sub&gt; creates a lexical alias to the package
subroutine of the same name.

The two main uses for this are to switch back to using the package sub
inside an inner scope:

    sub foo { ... }

    sub bar {
	my sub foo { ... }
	{
	    # need to use the outer foo here
	    our sub foo;
	    foo();
	}
    }

and to make a subroutine visible to other packages in the same scope:

    package MySneakyModule;

    our sub do_something { ... }

    sub do_something_with_caller {
	package DB;
	() = caller 1;		# sets @DB::args
	do_something(@args);	# uses MySneakyModule::do_something
    }

=head2 Passing Symbol Table Entries (typeglobs)
X&lt;typeglob&gt; X&lt;*&gt;

B&lt;WARNING&gt;: The mechanism described in this section was originally
the only way to simulate pass-by-reference in older versions of
Perl.  While it still works fine in modern versions, the new reference
mechanism is generally easier to work with.  See below.

Sometimes you don&#39;t want to pass the value of an array to a subroutine
but rather the name of it, so that the subroutine can modify the global
copy of it rather than working with a local copy.  In perl you can
refer to all objects of a particular name by prefixing the name
with a star: C&lt;*foo&gt;.  This is often known as a &quot;typeglob&quot;, because the
star on the front can be thought of as a wildcard match for all the
funny prefix characters on variables and subroutines and such.

When evaluated, the typeglob produces a scalar value that represents
all the objects of that name, including any filehandle, format, or
subroutine.  When assigned to, it causes the name mentioned to refer to
whatever C&lt;*&gt; value was assigned to it.  Example:

    sub doubleary {
	local(*someary) = @_;
	foreach $elem (@someary) {
	    $elem *= 2;
	}
    }
    doubleary(*foo);
    doubleary(*bar);

Scalars are already passed by reference, so you can modify
scalar arguments without using this mechanism by referring explicitly
to C&lt;$_[0]&gt; etc.  You can modify all the elements of an array by passing
all the elements as scalars, but you have to use the C&lt;*&gt; mechanism (or
the equivalent reference mechanism) to C&lt;push&gt;, C&lt;pop&gt;, or change the size of
an array.  It will certainly be faster to pass the typeglob (or reference).

Even if you don&#39;t want to modify an array, this mechanism is useful for
passing multiple arrays in a single LIST, because normally the LIST
mechanism will merge all the array values so that you can&#39;t extract out
the individual arrays.  For more on typeglobs, see
L&lt;perldata/&quot;Typeglobs and Filehandles&quot;&gt;.

=head2 When to Still Use local()
X&lt;local&gt; X&lt;variable, local&gt;

Despite the existence of C&lt;my&gt;, there are still three places where the
C&lt;local&gt; operator still shines.  In fact, in these three places, you
I&lt;must&gt; use C&lt;local&gt; instead of C&lt;my&gt;.

=over 4

=item 1.

You need to give a global variable a temporary value, especially $_.

The global variables, like C&lt;@ARGV&gt; or the punctuation variables, must be 
C&lt;local&gt;ized with C&lt;local()&gt;.  This block reads in F&lt;/etc/motd&gt;, and splits
it up into chunks separated by lines of equal signs, which are placed
in C&lt;@Fields&gt;.

    {
	local @ARGV = (&quot;/etc/motd&quot;);
        local $/ = undef;
        local $_ = &lt;&gt;;	
	@Fields = split /^\s*=+\s*$/;
    } 

It particular, it&#39;s important to C&lt;local&gt;ize $_ in any routine that assigns
to it.  Look out for implicit assignments in C&lt;while&gt; conditionals.

=item 2.

You need to create a local file or directory handle or a local function.

A function that needs a filehandle of its own must use
C&lt;local()&gt; on a complete typeglob.   This can be used to create new symbol
table entries:

    sub ioqueue {
        local  (*READER, *WRITER);    # not my!
        pipe    (READER,  WRITER)     or die &quot;pipe: $!&quot;;
        return (*READER, *WRITER);
    }
    ($head, $tail) = ioqueue();

See the Symbol module for a way to create anonymous symbol table
entries.

Because assignment of a reference to a typeglob creates an alias, this
can be used to create what is effectively a local function, or at least,
a local alias.

    {
        local *grow = \&amp;shrink; # only until this block exits
        grow();                # really calls shrink()
	move();		       # if move() grow()s, it shrink()s too
    }
    grow();		       # get the real grow() again

See L&lt;perlref/&quot;Function Templates&quot;&gt; for more about manipulating
functions by name in this way.

=item 3.

You want to temporarily change just one element of an array or hash.

You can C&lt;local&gt;ize just one element of an aggregate.  Usually this
is done on dynamics:

    {
	local $SIG{INT} = &#39;IGNORE&#39;;
	funct();			    # uninterruptible
    } 
    # interruptibility automatically restored here

But it also works on lexically declared aggregates.

=back

=head2 Pass by Reference
X&lt;pass by reference&gt; X&lt;pass-by-reference&gt; X&lt;reference&gt;

If you want to pass more than one array or hash into a function--or
return them from it--and have them maintain their integrity, then
you&#39;re going to have to use an explicit pass-by-reference.  Before you
do that, you need to understand references as detailed in L&lt;perlref&gt;.
This section may not make much sense to you otherwise.

Here are a few simple examples.  First, let&#39;s pass in several arrays
to a function and have it C&lt;pop&gt; all of then, returning a new list
of all their former last elements:

    @tailings = popmany ( \@a, \@b, \@c, \@d );

    sub popmany {
	my $aref;
	my @retlist;
	foreach $aref ( @_ ) {
	    push @retlist, pop @$aref;
	}
	return @retlist;
    }

Here&#39;s how you might write a function that returns a
list of keys occurring in all the hashes passed to it:

    @common = inter( \%foo, \%bar, \%joe );
    sub inter {
	my ($k, $href, %seen); # locals
	foreach $href (@_) {
	    while ( $k = each %$href ) {
		$seen{$k}++;
	    }
	}
	return grep { $seen{$_} == @_ } keys %seen;
    }

So far, we&#39;re using just the normal list return mechanism.
What happens if you want to pass or return a hash?  Well,
if you&#39;re using only one of them, or you don&#39;t mind them
concatenating, then the normal calling convention is ok, although
a little expensive.

Where people get into trouble is here:

    (@a, @b) = func(@c, @d);
or
    (%a, %b) = func(%c, %d);

That syntax simply won&#39;t work.  It sets just C&lt;@a&gt; or C&lt;%a&gt; and
clears the C&lt;@b&gt; or C&lt;%b&gt;.  Plus the function didn&#39;t get passed
into two separate arrays or hashes: it got one long list in C&lt;@_&gt;,
as always.

If you can arrange for everyone to deal with this through references, it&#39;s
cleaner code, although not so nice to look at.  Here&#39;s a function that
takes two array references as arguments, returning the two array elements
in order of how many elements they have in them:

    ($aref, $bref) = func(\@c, \@d);
    print &quot;@$aref has more than @$bref\n&quot;;
    sub func {
	my ($cref, $dref) = @_;
	if (@$cref &gt; @$dref) {
	    return ($cref, $dref);
	} else {
	    return ($dref, $cref);
	}
    }

It turns out that you can actually do this also:

    (*a, *b) = func(\@c, \@d);
    print &quot;@a has more than @b\n&quot;;
    sub func {
	local (*c, *d) = @_;
	if (@c &gt; @d) {
	    return (\@c, \@d);
	} else {
	    return (\@d, \@c);
	}
    }

Here we&#39;re using the typeglobs to do symbol table aliasing.  It&#39;s
a tad subtle, though, and also won&#39;t work if you&#39;re using C&lt;my&gt;
variables, because only globals (even in disguise as C&lt;local&gt;s)
are in the symbol table.

If you&#39;re passing around filehandles, you could usually just use the bare
typeglob, like C&lt;*STDOUT&gt;, but typeglobs references work, too.
For example:

    splutter(\*STDOUT);
    sub splutter {
	my $fh = shift;
	print $fh &quot;her um well a hmmm\n&quot;;
    }

    $rec = get_rec(\*STDIN);
    sub get_rec {
	my $fh = shift;
	return scalar &lt;$fh&gt;;
    }

If you&#39;re planning on generating new filehandles, you could do this.
Notice to pass back just the bare *FH, not its reference.

    sub openit {
	my $path = shift;
	local *FH;
	return open (FH, $path) ? *FH : undef;
    }

=head2 Prototypes
X&lt;prototype&gt; X&lt;subroutine, prototype&gt;

Perl supports a very limited kind of compile-time argument checking
using function prototyping.  This can be declared in either the PROTO
section or with a L&lt;prototype attribute|attributes/Built-in Attributes&gt;.
If you declare either of

    sub mypush (\@@)
    sub mypush :prototype(\@@)

then C&lt;mypush()&gt; takes arguments exactly like C&lt;push()&gt; does.

If subroutine signatures are enabled (see L&lt;/Signatures&gt;), then
the shorter PROTO syntax is unavailable, because it would clash with
signatures.  In that case, a prototype can only be declared in the form
of an attribute.

The
function declaration must be visible at compile time.  The prototype
affects only interpretation of new-style calls to the function,
where new-style is defined as not using the C&lt;&amp;&gt; character.  In
other words, if you call it like a built-in function, then it behaves
like a built-in function.  If you call it like an old-fashioned
subroutine, then it behaves like an old-fashioned subroutine.  It
naturally falls out from this rule that prototypes have no influence
on subroutine references like C&lt;\&amp;foo&gt; or on indirect subroutine
calls like C&lt;&amp;{$subref}&gt; or C&lt;&lt; $subref-&gt;() &gt;&gt;.

Method calls are not influenced by prototypes either, because the
function to be called is indeterminate at compile time, since
the exact code called depends on inheritance.

Because the intent of this feature is primarily to let you define
subroutines that work like built-in functions, here are prototypes
for some other functions that parse almost exactly like the
corresponding built-in.

   Declared as		   Called as

   sub mylink ($$)	   mylink $old, $new
   sub myvec ($$$)	   myvec $var, $offset, 1
   sub myindex ($$;$)	   myindex &amp;getstring, &quot;substr&quot;
   sub mysyswrite ($$$;$)  mysyswrite $buf, 0, length($buf) - $off, $off
   sub myreverse (@)	   myreverse $a, $b, $c
   sub myjoin ($@)	   myjoin &quot;:&quot;, $a, $b, $c
   sub mypop (\@)	   mypop @array
   sub mysplice (\@$$@)	   mysplice @array, 0, 2, @pushme
   sub mykeys (\[%@])	   mykeys %{$hashref}
   sub myopen (*;$)	   myopen HANDLE, $name
   sub mypipe (**)	   mypipe READHANDLE, WRITEHANDLE
   sub mygrep (&amp;@)	   mygrep { /foo/ } $a, $b, $c
   sub myrand (;$)	   myrand 42
   sub mytime ()	   mytime

Any backslashed prototype character represents an actual argument
that must start with that character (optionally preceded by C&lt;my&gt;,
C&lt;our&gt; or C&lt;local&gt;), with the exception of C&lt;$&gt;, which will
accept any scalar lvalue expression, such as C&lt;$foo = 7&gt; or
C&lt;&lt; my_function()-&gt;[0] &gt;&gt;.  The value passed as part of C&lt;@_&gt; will be a
reference to the actual argument given in the subroutine call,
obtained by applying C&lt;\&gt; to that argument.

You can use the C&lt;\[]&gt; backslash group notation to specify more than one
allowed argument type.  For example:

    sub myref (\[$@%&amp;*])

will allow calling myref() as

    myref $var
    myref @array
    myref %hash
    myref &amp;sub
    myref *glob

and the first argument of myref() will be a reference to
a scalar, an array, a hash, a code, or a glob.

Unbackslashed prototype characters have special meanings.  Any
unbackslashed C&lt;@&gt; or C&lt;%&gt; eats all remaining arguments, and forces
list context.  An argument represented by C&lt;$&gt; forces scalar context.  An
C&lt;&amp;&gt; requires an anonymous subroutine, which, if passed as the first
argument, does not require the C&lt;sub&gt; keyword or a subsequent comma.

A C&lt;*&gt; allows the subroutine to accept a bareword, constant, scalar expression,
typeglob, or a reference to a typeglob in that slot.  The value will be
available to the subroutine either as a simple scalar, or (in the latter
two cases) as a reference to the typeglob.  If you wish to always convert
such arguments to a typeglob reference, use Symbol::qualify_to_ref() as
follows:

    use Symbol &#39;qualify_to_ref&#39;;

    sub foo (*) {
	my $fh = qualify_to_ref(shift, caller);
	...
    }

The C&lt;+&gt; prototype is a special alternative to C&lt;$&gt; that will act like
C&lt;\[@%]&gt; when given a literal array or hash variable, but will otherwise
force scalar context on the argument.  This is useful for functions which
should accept either a literal array or an array reference as the argument:

    sub mypush (+@) {
        my $aref = shift;
        die &quot;Not an array or arrayref&quot; unless ref $aref eq &#39;ARRAY&#39;;
        push @$aref, @_;
    }

When using the C&lt;+&gt; prototype, your function must check that the argument
is of an acceptable type.

A semicolon (C&lt;;&gt;) separates mandatory arguments from optional arguments.
It is redundant before C&lt;@&gt; or C&lt;%&gt;, which gobble up everything else.

As the last character of a prototype, or just before a semicolon, a C&lt;@&gt;
or a C&lt;%&gt;, you can use C&lt;_&gt; in place of C&lt;$&gt;: if this argument is not
provided, C&lt;$_&gt; will be used instead.

Note how the last three examples in the table above are treated
specially by the parser.  C&lt;mygrep()&gt; is parsed as a true list
operator, C&lt;myrand()&gt; is parsed as a true unary operator with unary
precedence the same as C&lt;rand()&gt;, and C&lt;mytime()&gt; is truly without
arguments, just like C&lt;time()&gt;.  That is, if you say

    mytime +2;

you&#39;ll get C&lt;mytime() + 2&gt;, not C&lt;mytime(2)&gt;, which is how it would be parsed
without a prototype.  If you want to force a unary function to have the
same precedence as a list operator, add C&lt;;&gt; to the end of the prototype:

    sub mygetprotobynumber($;);
    mygetprotobynumber $a &gt; $b; # parsed as mygetprotobynumber($a &gt; $b)

The interesting thing about C&lt;&amp;&gt; is that you can generate new syntax with it,
provided it&#39;s in the initial position:
X&lt;&amp;&gt;

    sub try (&amp;@) {
	my($try,$catch) = @_;
	eval { &amp;$try };
	if ($@) {
	    local $_ = $@;
	    &amp;$catch;
	}
    }
    sub catch (&amp;) { $_[0] }

    try {
	die &quot;phooey&quot;;
    } catch {
	/phooey/ and print &quot;unphooey\n&quot;;
    };

That prints C&lt;&quot;unphooey&quot;&gt;.  (Yes, there are still unresolved
issues having to do with visibility of C&lt;@_&gt;.  I&#39;m ignoring that
question for the moment.  (But note that if we make C&lt;@_&gt; lexically
scoped, those anonymous subroutines can act like closures... (Gee,
is this sounding a little Lispish?  (Never mind.))))

And here&#39;s a reimplementation of the Perl C&lt;grep&gt; operator:
X&lt;grep&gt;

    sub mygrep (&amp;@) {
	my $code = shift;
	my @result;
	foreach $_ (@_) {
	    push(@result, $_) if &amp;$code;
	}
	@result;
    }

Some folks would prefer full alphanumeric prototypes.  Alphanumerics have
been intentionally left out of prototypes for the express purpose of
someday in the future adding named, formal parameters.  The current
mechanism&#39;s main goal is to let module writers provide better diagnostics
for module users.  Larry feels the notation quite understandable to Perl
programmers, and that it will not intrude greatly upon the meat of the
module, nor make it harder to read.  The line noise is visually
encapsulated into a small pill that&#39;s easy to swallow.

If you try to use an alphanumeric sequence in a prototype you will
generate an optional warning - &quot;Illegal character in prototype...&quot;.
Unfortunately earlier versions of Perl allowed the prototype to be
used as long as its prefix was a valid prototype.  The warning may be
upgraded to a fatal error in a future version of Perl once the
majority of offending code is fixed.

It&#39;s probably best to prototype new functions, not retrofit prototyping
into older ones.  That&#39;s because you must be especially careful about
silent impositions of differing list versus scalar contexts.  For example,
if you decide that a function should take just one parameter, like this:

    sub func ($) {
	my $n = shift;
	print &quot;you gave me $n\n&quot;;
    }

and someone has been calling it with an array or expression
returning a list:

    func(@foo);
    func( $text =~ /\w+/g );

Then you&#39;ve just supplied an automatic C&lt;scalar&gt; in front of their
argument, which can be more than a bit surprising.  The old C&lt;@foo&gt;
which used to hold one thing doesn&#39;t get passed in.  Instead,
C&lt;func()&gt; now gets passed in a C&lt;1&gt;; that is, the number of elements
in C&lt;@foo&gt;.  And the C&lt;m//g&gt; gets called in scalar context so instead of a
list of words it returns a boolean result and advances C&lt;pos($text)&gt;.  Ouch!

If a sub has both a PROTO and a BLOCK, the prototype is not applied
until after the BLOCK is completely defined.  This means that a recursive
function with a prototype has to be predeclared for the prototype to take
effect, like so:

	sub foo($$);
	sub foo($$) {
		foo 1, 2;
	}

This is all very powerful, of course, and should be used only in moderation
to make the world a better place.

=head2 Constant Functions
X&lt;constant&gt;

Functions with a prototype of C&lt;()&gt; are potential candidates for
inlining.  If the result after optimization and constant folding
is either a constant or a lexically-scoped scalar which has no other
references, then it will be used in place of function calls made
without C&lt;&amp;&gt;.  Calls made using C&lt;&amp;&gt; are never inlined.  (See
F&lt;constant.pm&gt; for an easy way to declare most constants.)

The following functions would all be inlined:

    sub pi ()		{ 3.14159 }		# Not exact, but close.
    sub PI ()		{ 4 * atan2 1, 1 }	# As good as it gets,
						# and it&#39;s inlined, too!
    sub ST_DEV ()	{ 0 }
    sub ST_INO ()	{ 1 }

    sub FLAG_FOO ()	{ 1 &lt;&lt; 8 }
    sub FLAG_BAR ()	{ 1 &lt;&lt; 9 }
    sub FLAG_MASK ()	{ FLAG_FOO | FLAG_BAR }

    sub OPT_BAZ ()	{ not (0x1B58 &amp; FLAG_MASK) }

    sub N () { int(OPT_BAZ) / 3 }

    sub FOO_SET () { 1 if FLAG_MASK &amp; FLAG_FOO }
    sub FOO_SET2 () { if (FLAG_MASK &amp; FLAG_FOO) { 1 } }

(Be aware that the last example was not always inlined in Perl 5.20 and
earlier, which did not behave consistently with subroutines containing
inner scopes.)  You can countermand inlining by using an explicit
C&lt;return&gt;:

    sub baz_val () {
	if (OPT_BAZ) {
	    return 23;
	}
	else {
	    return 42;
	}
    }
    sub bonk_val () { return 12345 }

As alluded to earlier you can also declare inlined subs dynamically at
BEGIN time if their body consists of a lexically-scoped scalar which
has no other references.  Only the first example here will be inlined:

    BEGIN {
        my $var = 1;
        no strict &#39;refs&#39;;
        *INLINED = sub () { $var };
    }

    BEGIN {
        my $var = 1;
        my $ref = \$var;
        no strict &#39;refs&#39;;
        *NOT_INLINED = sub () { $var };
    }

A not so obvious caveat with this (see [RT #79908]) is that the
variable will be immediately inlined, and will stop behaving like a
normal lexical variable, e.g. this will print C&lt;79907&gt;, not C&lt;79908&gt;:

    BEGIN {
        my $x = 79907;
        *RT_79908 = sub () { $x };
        $x++;
    }
    print RT_79908(); # prints 79907

As of Perl 5.22, this buggy behavior, while preserved for backward
compatibility, is detected and emits a deprecation warning.  If you want
the subroutine to be inlined (with no warning), make sure the variable is
not used in a context where it could be modified aside from where it is
declared.

    # Fine, no warning
    BEGIN {
        my $x = 54321;
        *INLINED = sub () { $x };
    }
    # Warns.  Future Perl versions will stop inlining it.
    BEGIN {
        my $x;
        $x = 54321;
        *ALSO_INLINED = sub () { $x };
    }

Perl 5.22 also introduces the experimental &quot;const&quot; attribute as an
alternative.  (Disable the &quot;experimental::const_attr&quot; warnings if you want
to use it.)  When applied to an anonymous subroutine, it forces the sub to
be called when the C&lt;sub&gt; expression is evaluated.  The return value is
captured and turned into a constant subroutine:

    my $x = 54321;
    *INLINED = sub : const { $x };
    $x++;

The return value of C&lt;INLINED&gt; in this example will always be 54321,
regardless of later modifications to $x.  You can also put any arbitrary
code inside the sub, at it will be executed immediately and its return
value captured the same way.

If you really want a subroutine with a C&lt;()&gt; prototype that returns a
lexical variable you can easily force it to not be inlined by adding
an explicit C&lt;return&gt;:

    BEGIN {
        my $x = 79907;
        *RT_79908 = sub () { return $x };
        $x++;
    }
    print RT_79908(); # prints 79908

The easiest way to tell if a subroutine was inlined is by using
L&lt;B::Deparse&gt;.  Consider this example of two subroutines returning
C&lt;1&gt;, one with a C&lt;()&gt; prototype causing it to be inlined, and one
without (with deparse output truncated for clarity):

 $ perl -MO=Deparse -le &#39;sub ONE { 1 } if (ONE) { print ONE if ONE }&#39;
 sub ONE {
     1;
 }
 if (ONE ) {
     print ONE() if ONE ;
 }
 $ perl -MO=Deparse -le &#39;sub ONE () { 1 } if (ONE) { print ONE if ONE }&#39;
 sub ONE () { 1 }
 do {
     print 1
 };

If you redefine a subroutine that was eligible for inlining, you&#39;ll
get a warning by default.  You can use this warning to tell whether or
not a particular subroutine is considered inlinable, since it&#39;s
different than the warning for overriding non-inlined subroutines:

    $ perl -e &#39;sub one () {1} sub one () {2}&#39;
    Constant subroutine one redefined at -e line 1.
    $ perl -we &#39;sub one {1} sub one {2}&#39;
    Subroutine one redefined at -e line 1.

The warning is considered severe enough not to be affected by the
B&lt;-w&gt; switch (or its absence) because previously compiled invocations
of the function will still be using the old value of the function.  If
you need to be able to redefine the subroutine, you need to ensure
that it isn&#39;t inlined, either by dropping the C&lt;()&gt; prototype (which
changes calling semantics, so beware) or by thwarting the inlining
mechanism in some other way, e.g. by adding an explicit C&lt;return&gt;, as
mentioned above:

    sub not_inlined () { return 23 }

=head2 Overriding Built-in Functions
X&lt;built-in&gt; X&lt;override&gt; X&lt;CORE&gt; X&lt;CORE::GLOBAL&gt;

Many built-in functions may be overridden, though this should be tried
only occasionally and for good reason.  Typically this might be
done by a package attempting to emulate missing built-in functionality
on a non-Unix system.

Overriding may be done only by importing the name from a module at
compile time--ordinary predeclaration isn&#39;t good enough.  However, the
C&lt;use subs&gt; pragma lets you, in effect, predeclare subs
via the import syntax, and these names may then override built-in ones:

    use subs &#39;chdir&#39;, &#39;chroot&#39;, &#39;chmod&#39;, &#39;chown&#39;;
    chdir $somewhere;
    sub chdir { ... }

To unambiguously refer to the built-in form, precede the
built-in name with the special package qualifier C&lt;CORE::&gt;.  For example,
saying C&lt;CORE::open()&gt; always refers to the built-in C&lt;open()&gt;, even
if the current package has imported some other subroutine called
C&lt;&amp;open()&gt; from elsewhere.  Even though it looks like a regular
function call, it isn&#39;t: the CORE:: prefix in that case is part of Perl&#39;s
syntax, and works for any keyword, regardless of what is in the CORE
package.  Taking a reference to it, that is, C&lt;\&amp;CORE::open&gt;, only works
for some keywords.  See L&lt;CORE&gt;.

Library modules should not in general export built-in names like C&lt;open&gt;
or C&lt;chdir&gt; as part of their default C&lt;@EXPORT&gt; list, because these may
sneak into someone else&#39;s namespace and change the semantics unexpectedly.
Instead, if the module adds that name to C&lt;@EXPORT_OK&gt;, then it&#39;s
possible for a user to import the name explicitly, but not implicitly.
That is, they could say

    use Module &#39;open&#39;;

and it would import the C&lt;open&gt; override.  But if they said

    use Module;

they would get the default imports without overrides.

The foregoing mechanism for overriding built-in is restricted, quite
deliberately, to the package that requests the import.  There is a second
method that is sometimes applicable when you wish to override a built-in
everywhere, without regard to namespace boundaries.  This is achieved by
importing a sub into the special namespace C&lt;CORE::GLOBAL::&gt;.  Here is an
example that quite brazenly replaces the C&lt;glob&gt; operator with something
that understands regular expressions.

    package REGlob;
    require Exporter;
    @ISA = &#39;Exporter&#39;;
    @EXPORT_OK = &#39;glob&#39;;

    sub import {
	my $pkg = shift;
	return unless @_;
	my $sym = shift;
	my $where = ($sym =~ s/^GLOBAL_// ? &#39;CORE::GLOBAL&#39; : caller(0));
	$pkg-&gt;export($where, $sym, @_);
    }

    sub glob {
	my $pat = shift;
	my @got;
	if (opendir my $d, &#39;.&#39;) { 
	    @got = grep /$pat/, readdir $d; 
	    closedir $d;   
	}
	return @got;
    }
    1;

And here&#39;s how it could be (ab)used:

    #use REGlob &#39;GLOBAL_glob&#39;;	    # override glob() in ALL namespaces
    package Foo;
    use REGlob &#39;glob&#39;;		    # override glob() in Foo:: only
    print for &lt;^[a-z_]+\.pm\$&gt;;	    # show all pragmatic modules

The initial comment shows a contrived, even dangerous example.
By overriding C&lt;glob&gt; globally, you would be forcing the new (and
subversive) behavior for the C&lt;glob&gt; operator for I&lt;every&gt; namespace,
without the complete cognizance or cooperation of the modules that own
those namespaces.  Naturally, this should be done with extreme caution--if
it must be done at all.

The C&lt;REGlob&gt; example above does not implement all the support needed to
cleanly override perl&#39;s C&lt;glob&gt; operator.  The built-in C&lt;glob&gt; has
different behaviors depending on whether it appears in a scalar or list
context, but our C&lt;REGlob&gt; doesn&#39;t.  Indeed, many perl built-in have such
context sensitive behaviors, and these must be adequately supported by
a properly written override.  For a fully functional example of overriding
C&lt;glob&gt;, study the implementation of C&lt;File::DosGlob&gt; in the standard
library.

When you override a built-in, your replacement should be consistent (if
possible) with the built-in native syntax.  You can achieve this by using
a suitable prototype.  To get the prototype of an overridable built-in,
use the C&lt;prototype&gt; function with an argument of C&lt;&quot;CORE::builtin_name&quot;&gt;
(see L&lt;perlfunc/prototype&gt;).

Note however that some built-ins can&#39;t have their syntax expressed by a
prototype (such as C&lt;system&gt; or C&lt;chomp&gt;).  If you override them you won&#39;t
be able to fully mimic their original syntax.

The built-ins C&lt;do&gt;, C&lt;require&gt; and C&lt;glob&gt; can also be overridden, but due
to special magic, their original syntax is preserved, and you don&#39;t have
to define a prototype for their replacements.  (You can&#39;t override the
C&lt;do BLOCK&gt; syntax, though).

C&lt;require&gt; has special additional dark magic: if you invoke your
C&lt;require&gt; replacement as C&lt;require Foo::Bar&gt;, it will actually receive
the argument C&lt;&quot;Foo/Bar.pm&quot;&gt; in @_.  See L&lt;perlfunc/require&gt;.

And, as you&#39;ll have noticed from the previous example, if you override
C&lt;glob&gt;, the C&lt;&lt; &lt;*&gt; &gt;&gt; glob operator is overridden as well.

In a similar fashion, overriding the C&lt;readline&gt; function also overrides
the equivalent I/O operator C&lt;&lt; &lt;FILEHANDLE&gt; &gt;&gt;.  Also, overriding
C&lt;readpipe&gt; also overrides the operators C&lt;``&gt; and C&lt;qx//&gt;.

Finally, some built-ins (e.g. C&lt;exists&gt; or C&lt;grep&gt;) can&#39;t be overridden.

=head2 Autoloading
X&lt;autoloading&gt; X&lt;AUTOLOAD&gt;

If you call a subroutine that is undefined, you would ordinarily
get an immediate, fatal error complaining that the subroutine doesn&#39;t
exist.  (Likewise for subroutines being used as methods, when the
method doesn&#39;t exist in any base class of the class&#39;s package.)
However, if an C&lt;AUTOLOAD&gt; subroutine is defined in the package or
packages used to locate the original subroutine, then that
C&lt;AUTOLOAD&gt; subroutine is called with the arguments that would have
been passed to the original subroutine.  The fully qualified name
of the original subroutine magically appears in the global $AUTOLOAD
variable of the same package as the C&lt;AUTOLOAD&gt; routine.  The name
is not passed as an ordinary argument because, er, well, just
because, that&#39;s why.  (As an exception, a method call to a nonexistent
C&lt;import&gt; or C&lt;unimport&gt; method is just skipped instead.  Also, if
the AUTOLOAD subroutine is an XSUB, there are other ways to retrieve the
subroutine name.  See L&lt;perlguts/Autoloading with XSUBs&gt; for details.)


Many C&lt;AUTOLOAD&gt; routines load in a definition for the requested
subroutine using eval(), then execute that subroutine using a special
form of goto() that erases the stack frame of the C&lt;AUTOLOAD&gt; routine
without a trace.  (See the source to the standard module documented
in L&lt;AutoLoader&gt;, for example.)  But an C&lt;AUTOLOAD&gt; routine can
also just emulate the routine and never define it.   For example,
let&#39;s pretend that a function that wasn&#39;t defined should just invoke
C&lt;system&gt; with those arguments.  All you&#39;d do is:

    sub AUTOLOAD {
	my $program = $AUTOLOAD;
	$program =~ s/.*:://;
	system($program, @_);
    }
    date();
    who(&#39;am&#39;, &#39;i&#39;);
    ls(&#39;-l&#39;);

In fact, if you predeclare functions you want to call that way, you don&#39;t
even need parentheses:

    use subs qw(date who ls);
    date;
    who &quot;am&quot;, &quot;i&quot;;
    ls &#39;-l&#39;;

A more complete example of this is the Shell module on CPAN, which
can treat undefined subroutine calls as calls to external programs.

Mechanisms are available to help modules writers split their modules
into autoloadable files.  See the standard AutoLoader module
described in L&lt;AutoLoader&gt; and in L&lt;AutoSplit&gt;, the standard
SelfLoader modules in L&lt;SelfLoader&gt;, and the document on adding C
functions to Perl code in L&lt;perlxs&gt;.

=head2 Subroutine Attributes
X&lt;attribute&gt; X&lt;subroutine, attribute&gt; X&lt;attrs&gt;

A subroutine declaration or definition may have a list of attributes
associated with it.  If such an attribute list is present, it is
broken up at space or colon boundaries and treated as though a
C&lt;use attributes&gt; had been seen.  See L&lt;attributes&gt; for details
about what attributes are currently supported.
Unlike the limitation with the obsolescent C&lt;use attrs&gt;, the
C&lt;sub : ATTRLIST&gt; syntax works to associate the attributes with
a pre-declaration, and not just with a subroutine definition.

The attributes must be valid as simple identifier names (without any
punctuation other than the &#39;_&#39; character).  They may have a parameter
list appended, which is only checked for whether its parentheses (&#39;(&#39;,&#39;)&#39;)
nest properly.

Examples of valid syntax (even though the attributes are unknown):

    sub fnord (&amp;\%) : switch(10,foo(7,3))  :  expensive;
    sub plugh () : Ugly(&#39;\(&quot;) :Bad;
    sub xyzzy : _5x5 { ... }

Examples of invalid syntax:

    sub fnord : switch(10,foo(); # ()-string not balanced
    sub snoid : Ugly(&#39;(&#39;);	  # ()-string not balanced
    sub xyzzy : 5x5;		  # &quot;5x5&quot; not a valid identifier
    sub plugh : Y2::north;	  # &quot;Y2::north&quot; not a simple identifier
    sub snurt : foo + bar;	  # &quot;+&quot; not a colon or space

The attribute list is passed as a list of constant strings to the code
which associates them with the subroutine.  In particular, the second example
of valid syntax above currently looks like this in terms of how it&#39;s
parsed and invoked:

    use attributes __PACKAGE__, \&amp;plugh, q[Ugly(&#39;\(&quot;)], &#39;Bad&#39;;

For further details on attribute lists and their manipulation,
see L&lt;attributes&gt; and L&lt;Attribute::Handlers&gt;.

=head1 SEE ALSO

See L&lt;perlref/&quot;Function Templates&quot;&gt; for more about references and closures.
See L&lt;perlxs&gt; if you&#39;d like to learn about calling C subroutines from Perl.  
See L&lt;perlembed&gt; if you&#39;d like to learn about calling Perl subroutines from C.  
See L&lt;perlmod&gt; to learn about bundling up your functions in separate files.
See L&lt;perlmodlib&gt; to learn what library modules come standard on your system.
See L&lt;perlootut&gt; to learn how to make object method calls.
</pre>

                    </main>
                </div>

        </div>
        <footer>
        
        </footer>


    </body>
</html>

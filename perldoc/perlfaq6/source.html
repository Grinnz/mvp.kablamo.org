<!DOCTYPE html>
<html>
    <head>
        <meta charset="utf-8">
        <meta content="width=device-width, initial-scale=1" name="viewport">
        <link href="/theme/css/normalize.css" rel="stylesheet">
        <!--<link rel="stylesheet" href="/theme/css/skeleton.css" />-->
        <link href="/theme/css/statocles-default.css" rel="stylesheet">
        <link href="//maxcdn.bootstrapcdn.com/font-awesome/4.3.0/css/font-awesome.min.css" rel="stylesheet">
        <script crossorigin="anonymous" integrity="sha256-hwg4gsxgFZhOsEEamdOYGBf13FyQuiTwlAQgxVSNgt4=" src="http://code.jquery.com/jquery-3.2.1.min.js"></script>
        <title>perlfaq6 (source) - Minimum Viable Perl</title>
        <meta content="Statocles 0.086" name="generator">
        
        <script>
            $( document ).ready(function() {
                $('.sub-menu ul').hide();
                $('.sub-menu.perldoc ul').show();
                $('.sub-menu.perldoc div').toggleClass("fa-caret-up fa-caret-down");
                $(".sub-menu a").click(function () {
                    $(this).parent(".sub-menu").siblings().children('ul').hide();
                    $(this).parent(".sub-menu").children("ul").slideToggle("100");
                    $(this).find(".right").toggleClass("fa-caret-up fa-caret-down");
                });
            });
        </script>
    </head>
    <body>
        <header>
        </header>
        <div class="container">

                <div class="left column full-height">
                    <div class="sidebar">
                    
<a class="home" href="/">Minimum<br>Viable<br>Perl</a><br><br><br>

<div class="animated bounceInDown">
  <ul>
    <li class="basics sub-menu">
      <a href="/perldoc/perlfaq6/#basics">The basics<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/basics/hello">Hello world</a></li>
        <li><a href="/basics/variables">Variables</a></li>
        <li><a href="/basics/references">References</a></li>
        <li><a href="/basics/functions">Functions</a></li>
        <li><a href="/basics/flow">Conditionals</a></li>
        <li><a href="/basics/loops">Loops</a></li>
        <li><a href="/basics/exceptions">Exceptions</a></li>
        <li><a href="/basics/regexps">Regexps</a></li>
        <li><a href="/basics/files">Files</a></li>
      </ul>
    </li>
    <li class="oo sub-menu">
      <a href="/perldoc/perlfaq6/#oo">Object oriented programming<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/oo/story">The (weird) story of Perl OO</a></li>
        <li><a href="/oo/classes">Classes</a></li>
        <li><a href="/oo/attributes">Attributes</a></li>
        <li><a href="/oo/modifiers">Method modifiers</a></li>
        <li><a href="/oo/roles">Roles</a></li>
        <li><a href="/oo/inheritance">Inheritance</a></li>
      </ul>
    </li>
    <li class="debugging sub-menu">
      <a href="/perldoc/perlfaq6/#basics">Debugging<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/debugging/data-printer">Examining data structures</a></li>
        <li><a href="/debugging/stack-trace">Forcing stack traces</a></li>
      </ul>
    </li>
    <li class="dependencies sub-menu">
      <a href="/perldoc/perlfaq6/#dependencies">Perl libraries<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/dependencies/perl5lib">Loading libraries from custom locations</a></li>
        <li><a href="/dependencies/cpanm">Installing libraries with cpanm</a></li>
        <li><a href="/dependencies/metacpan">Choosing libraries from MetaCPAN</a></li>
        <li><a href="/dependencies/carton">Managing project dependencies with Carton</a></li>
        <li><a href="/dependencies/pmtools">What version is installed and where?</a></li>
      </ul>
    </li>
    <li class="testing sub-menu">
      <a href="/perldoc/perlfaq6/#testing">Testing<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/testing/intro">Introduction to testing</a></li>
        <li><a href="/testing/warnings">Testing warnings and exceptions</a></li>
        <li><a href="/testing/data">Testing data structures</a></li>
        <li><a href="/testing/mock">Testing with mock objects</a></li>
        <li><a href="/testing/libs">Build your own test libraries</a></li>
      </ul>
    </li>
    <li><a href="/perl">Perl design goals</a></li>
    <li><a href="/see-also">Book recommendations</a></li>
    <li><a href="/about">About this book</a></li>
  </ul>
</div>

                    
                    
                    </div>
                </div>

                <div class="right column">
                    <main>
                    <a class="button" href="/perldoc/perlfaq6/">
    Back to documentation
</a>
<pre>=head1 NAME

perlfaq6 - Regular Expressions

=head1 VERSION

version 5.021011

=head1 DESCRIPTION

This section is surprisingly small because the rest of the FAQ is
littered with answers involving regular expressions. For example,
decoding a URL and checking whether something is a number can be handled
with regular expressions, but those answers are found elsewhere in
this document (in L&lt;perlfaq9&gt;: &quot;How do I decode or create those %-encodings
on the web&quot; and L&lt;perlfaq4&gt;: &quot;How do I determine whether a scalar is
a number/whole/integer/float&quot;, to be precise).

=head2 How can I hope to use regular expressions without creating illegible and unmaintainable code?
X&lt;regex, legibility&gt; X&lt;regexp, legibility&gt;
X&lt;regular expression, legibility&gt; X&lt;/x&gt;

Three techniques can make regular expressions maintainable and
understandable.

=over 4

=item Comments Outside the Regex

Describe what you&#39;re doing and how you&#39;re doing it, using normal Perl
comments.

    # turn the line into the first word, a colon, and the
    # number of characters on the rest of the line
    s/^(\w+)(.*)/ lc($1) . &quot;:&quot; . length($2) /meg;

=item Comments Inside the Regex

The C&lt;/x&gt; modifier causes whitespace to be ignored in a regex pattern
(except in a character class and a few other places), and also allows you to
use normal comments there, too. As you can imagine, whitespace and comments
help a lot.

C&lt;/x&gt; lets you turn this:

    s{&lt;(?:[^&gt;&#39;&quot;]*|&quot;.*?&quot;|&#39;.*?&#39;)+&gt;}{}gs;

into this:

    s{ &lt;                    # opening angle bracket
        (?:                 # Non-backreffing grouping paren
            [^&gt;&#39;&quot;] *        # 0 or more things that are neither &gt; nor &#39; nor &quot;
                |           #    or else
            &quot;.*?&quot;           # a section between double quotes (stingy match)
                |           #    or else
            &#39;.*?&#39;           # a section between single quotes (stingy match)
        ) +                 #   all occurring one or more times
        &gt;                   # closing angle bracket
    }{}gsx;                 # replace with nothing, i.e. delete

It&#39;s still not quite so clear as prose, but it is very useful for
describing the meaning of each part of the pattern.

=item Different Delimiters

While we normally think of patterns as being delimited with C&lt;/&gt;
characters, they can be delimited by almost any character. L&lt;perlre&gt;
describes this. For example, the C&lt;s///&gt; above uses braces as
delimiters. Selecting another delimiter can avoid quoting the
delimiter within the pattern:

    s/\/usr\/local/\/usr\/share/g;    # bad delimiter choice
    s#/usr/local#/usr/share#g;        # better

Using logically paired delimiters can be even more readable:

    s{/usr/local/}{/usr/share}g;      # better still

=back

=head2 I&#39;m having trouble matching over more than one line. What&#39;s wrong?
X&lt;regex, multiline&gt; X&lt;regexp, multiline&gt; X&lt;regular expression, multiline&gt;

Either you don&#39;t have more than one line in the string you&#39;re looking
at (probably), or else you aren&#39;t using the correct modifier(s) on
your pattern (possibly).

There are many ways to get multiline data into a string. If you want
it to happen automatically while reading input, you&#39;ll want to set $/
(probably to &#39;&#39; for paragraphs or C&lt;undef&gt; for the whole file) to
allow you to read more than one line at a time.

Read L&lt;perlre&gt; to help you decide which of C&lt;/s&gt; and C&lt;/m&gt; (or both)
you might want to use: C&lt;/s&gt; allows dot to include newline, and C&lt;/m&gt;
allows caret and dollar to match next to a newline, not just at the
end of the string. You do need to make sure that you&#39;ve actually
got a multiline string in there.

For example, this program detects duplicate words, even when they span
line breaks (but not paragraph ones). For this example, we don&#39;t need
C&lt;/s&gt; because we aren&#39;t using dot in a regular expression that we want
to cross line boundaries. Neither do we need C&lt;/m&gt; because we don&#39;t
want caret or dollar to match at any point inside the record next
to newlines. But it&#39;s imperative that $/ be set to something other
than the default, or else we won&#39;t actually ever have a multiline
record read in.

    $/ = &#39;&#39;;          # read in whole paragraph, not just one line
    while ( &lt;&gt; ) {
        while ( /\b([\w&#39;-]+)(\s+\g1)+\b/gi ) {     # word starts alpha
            print &quot;Duplicate $1 at paragraph $.\n&quot;;
        }
    }

Here&#39;s some code that finds sentences that begin with &quot;From &quot; (which would
be mangled by many mailers):

    $/ = &#39;&#39;;          # read in whole paragraph, not just one line
    while ( &lt;&gt; ) {
        while ( /^From /gm ) { # /m makes ^ match next to \n
        print &quot;leading from in paragraph $.\n&quot;;
        }
    }

Here&#39;s code that finds everything between START and END in a paragraph:

    undef $/;          # read in whole file, not just one line or paragraph
    while ( &lt;&gt; ) {
        while ( /START(.*?)END/sgm ) { # /s makes . cross line boundaries
            print &quot;$1\n&quot;;
        }
    }

=head2 How can I pull out lines between two patterns that are themselves on different lines?
X&lt;..&gt;

You can use Perl&#39;s somewhat exotic C&lt;..&gt; operator (documented in
L&lt;perlop&gt;):

    perl -ne &#39;print if /START/ .. /END/&#39; file1 file2 ...

If you wanted text and not lines, you would use

    perl -0777 -ne &#39;print &quot;$1\n&quot; while /START(.*?)END/gs&#39; file1 file2 ...

But if you want nested occurrences of C&lt;START&gt; through C&lt;END&gt;, you&#39;ll
run up against the problem described in the question in this section
on matching balanced text.

Here&#39;s another example of using C&lt;..&gt;:

    while (&lt;&gt;) {
        my $in_header =   1  .. /^$/;
        my $in_body   = /^$/ .. eof;
    # now choose between them
    } continue {
        $. = 0 if eof;    # fix $.
    }

=head2 How do I match XML, HTML, or other nasty, ugly things with a regex?
X&lt;regex, XML&gt; X&lt;regex, HTML&gt; X&lt;XML&gt; X&lt;HTML&gt; X&lt;pain&gt; X&lt;frustration&gt;
X&lt;sucking out, will to live&gt;

Do not use regexes. Use a module and forget about the
regular expressions. The L&lt;XML::LibXML&gt;, L&lt;HTML::TokeParser&gt; and
L&lt;HTML::TreeBuilder&gt; modules are good starts, although each namespace
has other parsing modules specialized for certain tasks and different
ways of doing it. Start at CPAN Search ( L&lt;http://metacpan.org/&gt; )
and wonder at all the work people have done for you already! :)

=head2 I put a regular expression into $/ but it didn&#39;t work. What&#39;s wrong?
X&lt;$/, regexes in&gt; X&lt;$INPUT_RECORD_SEPARATOR, regexes in&gt;
X&lt;$RS, regexes in&gt;

$/ has to be a string. You can use these examples if you really need to
do this.

If you have L&lt;File::Stream&gt;, this is easy.

    use File::Stream;

    my $stream = File::Stream-&gt;new(
        $filehandle,
        separator =&gt; qr/\s*,\s*/,
        );

    print &quot;$_\n&quot; while &lt;$stream&gt;;

If you don&#39;t have File::Stream, you have to do a little more work.

You can use the four-argument form of sysread to continually add to
a buffer. After you add to the buffer, you check if you have a
complete line (using your regular expression).

    local $_ = &quot;&quot;;
    while( sysread FH, $_, 8192, length ) {
        while( s/^((?s).*?)your_pattern// ) {
            my $record = $1;
            # do stuff here.
        }
    }

You can do the same thing with foreach and a match using the
c flag and the \G anchor, if you do not mind your entire file
being in memory at the end.

    local $_ = &quot;&quot;;
    while( sysread FH, $_, 8192, length ) {
        foreach my $record ( m/\G((?s).*?)your_pattern/gc ) {
            # do stuff here.
        }
        substr( $_, 0, pos ) = &quot;&quot; if pos;
    }


=head2 How do I substitute case-insensitively on the LHS while preserving case on the RHS?
X&lt;replace, case preserving&gt; X&lt;substitute, case preserving&gt;
X&lt;substitution, case preserving&gt; X&lt;s, case preserving&gt;

Here&#39;s a lovely Perlish solution by Larry Rosler. It exploits
properties of bitwise xor on ASCII strings.

    $_= &quot;this is a TEsT case&quot;;

    $old = &#39;test&#39;;
    $new = &#39;success&#39;;

    s{(\Q$old\E)}
    { uc $new | (uc $1 ^ $1) .
        (uc(substr $1, -1) ^ substr $1, -1) x
        (length($new) - length $1)
    }egi;

    print;

And here it is as a subroutine, modeled after the above:

    sub preserve_case {
        my ($old, $new) = @_;
        my $mask = uc $old ^ $old;

        uc $new | $mask .
            substr($mask, -1) x (length($new) - length($old))
    }

    $string = &quot;this is a TEsT case&quot;;
    $string =~ s/(test)/preserve_case($1, &quot;success&quot;)/egi;
    print &quot;$string\n&quot;;

This prints:

    this is a SUcCESS case

As an alternative, to keep the case of the replacement word if it is
longer than the original, you can use this code, by Jeff Pinyan:

    sub preserve_case {
        my ($from, $to) = @_;
        my ($lf, $lt) = map length, @_;

        if ($lt &lt; $lf) { $from = substr $from, 0, $lt }
        else { $from .= substr $to, $lf }

        return uc $to | ($from ^ uc $from);
    }

This changes the sentence to &quot;this is a SUcCess case.&quot;

Just to show that C programmers can write C in any programming language,
if you prefer a more C-like solution, the following script makes the
substitution have the same case, letter by letter, as the original.
(It also happens to run about 240% slower than the Perlish solution runs.)
If the substitution has more characters than the string being substituted,
the case of the last character is used for the rest of the substitution.

    # Original by Nathan Torkington, massaged by Jeffrey Friedl
    #
    sub preserve_case
    {
        my ($old, $new) = @_;
        my $state = 0; # 0 = no change; 1 = lc; 2 = uc
        my ($i, $oldlen, $newlen, $c) = (0, length($old), length($new));
        my $len = $oldlen &lt; $newlen ? $oldlen : $newlen;

        for ($i = 0; $i &lt; $len; $i++) {
            if ($c = substr($old, $i, 1), $c =~ /[\W\d_]/) {
                $state = 0;
            } elsif (lc $c eq $c) {
                substr($new, $i, 1) = lc(substr($new, $i, 1));
                $state = 1;
            } else {
                substr($new, $i, 1) = uc(substr($new, $i, 1));
                $state = 2;
            }
        }
        # finish up with any remaining new (for when new is longer than old)
        if ($newlen &gt; $oldlen) {
            if ($state == 1) {
                substr($new, $oldlen) = lc(substr($new, $oldlen));
            } elsif ($state == 2) {
                substr($new, $oldlen) = uc(substr($new, $oldlen));
            }
        }
        return $new;
    }

=head2 How can I make C&lt;\w&gt; match national character sets?
X&lt;\w&gt;

Put C&lt;use locale;&gt; in your script. The \w character class is taken
from the current locale.

See L&lt;perllocale&gt; for details.

=head2 How can I match a locale-smart version of C&lt;/[a-zA-Z]/&gt;?
X&lt;alpha&gt;

You can use the POSIX character class syntax C&lt;/[[:alpha:]]/&gt;
documented in L&lt;perlre&gt;.

No matter which locale you are in, the alphabetic characters are
the characters in \w without the digits and the underscore.
As a regex, that looks like C&lt;/[^\W\d_]/&gt;. Its complement,
the non-alphabetics, is then everything in \W along with
the digits and the underscore, or C&lt;/[\W\d_]/&gt;.

=head2 How can I quote a variable to use in a regex?
X&lt;regex, escaping&gt; X&lt;regexp, escaping&gt; X&lt;regular expression, escaping&gt;

The Perl parser will expand $variable and @variable references in
regular expressions unless the delimiter is a single quote. Remember,
too, that the right-hand side of a C&lt;s///&gt; substitution is considered
a double-quoted string (see L&lt;perlop&gt; for more details). Remember
also that any regex special characters will be acted on unless you
precede the substitution with \Q. Here&#39;s an example:

    $string = &quot;Placido P. Octopus&quot;;
    $regex  = &quot;P.&quot;;

    $string =~ s/$regex/Polyp/;
    # $string is now &quot;Polypacido P. Octopus&quot;

Because C&lt;.&gt; is special in regular expressions, and can match any
single character, the regex C&lt;P.&gt; here has matched the &lt;Pl&gt; in the
original string.

To escape the special meaning of C&lt;.&gt;, we use C&lt;\Q&gt;:

    $string = &quot;Placido P. Octopus&quot;;
    $regex  = &quot;P.&quot;;

    $string =~ s/\Q$regex/Polyp/;
    # $string is now &quot;Placido Polyp Octopus&quot;

The use of C&lt;\Q&gt; causes the &lt;.&gt; in the regex to be treated as a
regular character, so that C&lt;P.&gt; matches a C&lt;P&gt; followed by a dot.

=head2 What is C&lt;/o&gt; really for?
X&lt;/o, regular expressions&gt; X&lt;compile, regular expressions&gt;

(contributed by brian d foy)

The C&lt;/o&gt; option for regular expressions (documented in L&lt;perlop&gt; and
L&lt;perlreref&gt;) tells Perl to compile the regular expression only once.
This is only useful when the pattern contains a variable. Perls 5.6
and later handle this automatically if the pattern does not change.

Since the match operator C&lt;m//&gt;, the substitution operator C&lt;s///&gt;,
and the regular expression quoting operator C&lt;qr//&gt; are double-quotish
constructs, you can interpolate variables into the pattern. See the
answer to &quot;How can I quote a variable to use in a regex?&quot; for more
details.

This example takes a regular expression from the argument list and
prints the lines of input that match it:

    my $pattern = shift @ARGV;

    while( &lt;&gt; ) {
        print if m/$pattern/;
    }

Versions of Perl prior to 5.6 would recompile the regular expression
for each iteration, even if C&lt;$pattern&gt; had not changed. The C&lt;/o&gt;
would prevent this by telling Perl to compile the pattern the first
time, then reuse that for subsequent iterations:

    my $pattern = shift @ARGV;

    while( &lt;&gt; ) {
        print if m/$pattern/o; # useful for Perl &lt; 5.6
    }

In versions 5.6 and later, Perl won&#39;t recompile the regular expression
if the variable hasn&#39;t changed, so you probably don&#39;t need the C&lt;/o&gt;
option. It doesn&#39;t hurt, but it doesn&#39;t help either. If you want any
version of Perl to compile the regular expression only once even if
the variable changes (thus, only using its initial value), you still
need the C&lt;/o&gt;.

You can watch Perl&#39;s regular expression engine at work to verify for
yourself if Perl is recompiling a regular expression. The C&lt;use re
&#39;debug&#39;&gt; pragma (comes with Perl 5.005 and later) shows the details.
With Perls before 5.6, you should see C&lt;re&gt; reporting that its
compiling the regular expression on each iteration. With Perl 5.6 or
later, you should only see C&lt;re&gt; report that for the first iteration.

    use re &#39;debug&#39;;

    my $regex = &#39;Perl&#39;;
    foreach ( qw(Perl Java Ruby Python) ) {
        print STDERR &quot;-&quot; x 73, &quot;\n&quot;;
        print STDERR &quot;Trying $_...\n&quot;;
        print STDERR &quot;\t$_ is good!\n&quot; if m/$regex/;
    }

=head2 How do I use a regular expression to strip C-style comments from a file?

While this actually can be done, it&#39;s much harder than you&#39;d think.
For example, this one-liner

    perl -0777 -pe &#39;s{/\*.*?\*/}{}gs&#39; foo.c

will work in many but not all cases. You see, it&#39;s too simple-minded for
certain kinds of C programs, in particular, those with what appear to be
comments in quoted strings. For that, you&#39;d need something like this,
created by Jeffrey Friedl and later modified by Fred Curtis.

    $/ = undef;
    $_ = &lt;&gt;;
    s#/\*[^*]*\*+([^/*][^*]*\*+)*/|(&quot;(\\.|[^&quot;\\])*&quot;|&#39;(\\.|[^&#39;\\])*&#39;|.[^/&quot;&#39;\\]*)#defined $2 ? $2 : &quot;&quot;#gse;
    print;

This could, of course, be more legibly written with the C&lt;/x&gt; modifier, adding
whitespace and comments. Here it is expanded, courtesy of Fred Curtis.

    s{
       /\*         ##  Start of /* ... */ comment
       [^*]*\*+    ##  Non-* followed by 1-or-more *&#39;s
       (
         [^/*][^*]*\*+
       )*          ##  0-or-more things which don&#39;t start with /
                   ##    but do end with &#39;*&#39;
       /           ##  End of /* ... */ comment

     |         ##     OR  various things which aren&#39;t comments:

       (
         &quot;           ##  Start of &quot; ... &quot; string
         (
           \\.           ##  Escaped char
         |               ##    OR
           [^&quot;\\]        ##  Non &quot;\
         )*
         &quot;           ##  End of &quot; ... &quot; string

       |         ##     OR

         &#39;           ##  Start of &#39; ... &#39; string
         (
           \\.           ##  Escaped char
         |               ##    OR
           [^&#39;\\]        ##  Non &#39;\
         )*
         &#39;           ##  End of &#39; ... &#39; string

       |         ##     OR

         .           ##  Anything other char
         [^/&quot;&#39;\\]*   ##  Chars which doesn&#39;t start a comment, string or escape
       )
     }{defined $2 ? $2 : &quot;&quot;}gxse;

A slight modification also removes C++ comments, possibly spanning multiple lines
using a continuation character:

 s#/\*[^*]*\*+([^/*][^*]*\*+)*/|//([^\\]|[^\n][\n]?)*?\n|(&quot;(\\.|[^&quot;\\])*&quot;|&#39;(\\.|[^&#39;\\])*&#39;|.[^/&quot;&#39;\\]*)#defined $3 ? $3 : &quot;&quot;#gse;

=head2 Can I use Perl regular expressions to match balanced text?
X&lt;regex, matching balanced test&gt; X&lt;regexp, matching balanced test&gt;
X&lt;regular expression, matching balanced test&gt; X&lt;possessive&gt; X&lt;PARNO&gt;
X&lt;Text::Balanced&gt; X&lt;Regexp::Common&gt; X&lt;backtracking&gt; X&lt;recursion&gt;

(contributed by brian d foy)

Your first try should probably be the L&lt;Text::Balanced&gt; module, which
is in the Perl standard library since Perl 5.8. It has a variety of
functions to deal with tricky text. The L&lt;Regexp::Common&gt; module can
also help by providing canned patterns you can use.

As of Perl 5.10, you can match balanced text with regular expressions
using recursive patterns. Before Perl 5.10, you had to resort to
various tricks such as using Perl code in C&lt;(??{})&gt; sequences.

Here&#39;s an example using a recursive regular expression. The goal is to
capture all of the text within angle brackets, including the text in
nested angle brackets. This sample text has two &quot;major&quot; groups: a
group with one level of nesting and a group with two levels of
nesting. There are five total groups in angle brackets:

    I have some &lt;brackets in &lt;nested brackets&gt; &gt; and
    &lt;another group &lt;nested once &lt;nested twice&gt; &gt; &gt;
    and that&#39;s it.

The regular expression to match the balanced text uses two new (to
Perl 5.10) regular expression features. These are covered in L&lt;perlre&gt;
and this example is a modified version of one in that documentation.

First, adding the new possessive C&lt;+&gt; to any quantifier finds the
longest match and does not backtrack. That&#39;s important since you want
to handle any angle brackets through the recursion, not backtracking.
The group C&lt;&lt; [^&lt;&gt;]++ &gt;&gt; finds one or more non-angle brackets without
backtracking.

Second, the new C&lt;(?PARNO)&gt; refers to the sub-pattern in the
particular capture group given by C&lt;PARNO&gt;. In the following regex,
the first capture group finds (and remembers) the balanced text, and
you need that same pattern within the first buffer to get past the
nested text. That&#39;s the recursive part. The C&lt;(?1)&gt; uses the pattern
in the outer capture group as an independent part of the regex.

Putting it all together, you have:

    #!/usr/local/bin/perl5.10.0

    my $string =&lt;&lt;&quot;HERE&quot;;
    I have some &lt;brackets in &lt;nested brackets&gt; &gt; and
    &lt;another group &lt;nested once &lt;nested twice&gt; &gt; &gt;
    and that&#39;s it.
    HERE

    my @groups = $string =~ m/
            (                   # start of capture group 1
            &lt;                   # match an opening angle bracket
                (?:
                    [^&lt;&gt;]++     # one or more non angle brackets, non backtracking
                      |
                    (?1)        # found &lt; or &gt;, so recurse to capture group 1
                )*
            &gt;                   # match a closing angle bracket
            )                   # end of capture group 1
            /xg;

    $&quot; = &quot;\n\t&quot;;
    print &quot;Found:\n\t@groups\n&quot;;

The output shows that Perl found the two major groups:

    Found:
        &lt;brackets in &lt;nested brackets&gt; &gt;
        &lt;another group &lt;nested once &lt;nested twice&gt; &gt; &gt;

With a little extra work, you can get all of the groups in angle
brackets even if they are in other angle brackets too. Each time you
get a balanced match, remove its outer delimiter (that&#39;s the one you
just matched so don&#39;t match it again) and add it to a queue of strings
to process. Keep doing that until you get no matches:

    #!/usr/local/bin/perl5.10.0

    my @queue =&lt;&lt;&quot;HERE&quot;;
    I have some &lt;brackets in &lt;nested brackets&gt; &gt; and
    &lt;another group &lt;nested once &lt;nested twice&gt; &gt; &gt;
    and that&#39;s it.
    HERE

    my $regex = qr/
            (                   # start of bracket 1
            &lt;                   # match an opening angle bracket
                (?:
                    [^&lt;&gt;]++     # one or more non angle brackets, non backtracking
                      |
                    (?1)        # recurse to bracket 1
                )*
            &gt;                   # match a closing angle bracket
            )                   # end of bracket 1
            /x;

    $&quot; = &quot;\n\t&quot;;

    while( @queue ) {
        my $string = shift @queue;

        my @groups = $string =~ m/$regex/g;
        print &quot;Found:\n\t@groups\n\n&quot; if @groups;

        unshift @queue, map { s/^&lt;//; s/&gt;$//; $_ } @groups;
    }

The output shows all of the groups. The outermost matches show up
first and the nested matches show up later:

    Found:
        &lt;brackets in &lt;nested brackets&gt; &gt;
        &lt;another group &lt;nested once &lt;nested twice&gt; &gt; &gt;

    Found:
        &lt;nested brackets&gt;

    Found:
        &lt;nested once &lt;nested twice&gt; &gt;

    Found:
        &lt;nested twice&gt;

=head2 What does it mean that regexes are greedy? How can I get around it?
X&lt;greedy&gt; X&lt;greediness&gt;

Most people mean that greedy regexes match as much as they can.
Technically speaking, it&#39;s actually the quantifiers (C&lt;?&gt;, C&lt;*&gt;, C&lt;+&gt;,
C&lt;{}&gt;) that are greedy rather than the whole pattern; Perl prefers local
greed and immediate gratification to overall greed. To get non-greedy
versions of the same quantifiers, use (C&lt;??&gt;, C&lt;*?&gt;, C&lt;+?&gt;, C&lt;{}?&gt;).

An example:

    my $s1 = my $s2 = &quot;I am very very cold&quot;;
    $s1 =~ s/ve.*y //;      # I am cold
    $s2 =~ s/ve.*?y //;     # I am very cold

Notice how the second substitution stopped matching as soon as it
encountered &quot;y &quot;. The C&lt;*?&gt; quantifier effectively tells the regular
expression engine to find a match as quickly as possible and pass
control on to whatever is next in line, as you would if you were
playing hot potato.

=head2 How do I process each word on each line?
X&lt;word&gt;

Use the split function:

    while (&lt;&gt;) {
        foreach my $word ( split ) {
            # do something with $word here
        }
    }

Note that this isn&#39;t really a word in the English sense; it&#39;s just
chunks of consecutive non-whitespace characters.

To work with only alphanumeric sequences (including underscores), you
might consider

    while (&lt;&gt;) {
        foreach $word (m/(\w+)/g) {
            # do something with $word here
        }
    }

=head2 How can I print out a word-frequency or line-frequency summary?

To do this, you have to parse out each word in the input stream. We&#39;ll
pretend that by word you mean chunk of alphabetics, hyphens, or
apostrophes, rather than the non-whitespace chunk idea of a word given
in the previous question:

    my (%seen);
    while (&lt;&gt;) {
        while ( /(\b[^\W_\d][\w&#39;-]+\b)/g ) {   # misses &quot;`sheep&#39;&quot;
            $seen{$1}++;
        }
    }

    while ( my ($word, $count) = each %seen ) {
        print &quot;$count $word\n&quot;;
    }

If you wanted to do the same thing for lines, you wouldn&#39;t need a
regular expression:

    my (%seen);

    while (&lt;&gt;) {
        $seen{$_}++;
    }

    while ( my ($line, $count) = each %seen ) {
        print &quot;$count $line&quot;;
    }

If you want these output in a sorted order, see L&lt;perlfaq4&gt;: &quot;How do I
sort a hash (optionally by value instead of key)?&quot;.

=head2 How can I do approximate matching?
X&lt;match, approximate&gt; X&lt;matching, approximate&gt;

See the module L&lt;String::Approx&gt; available from CPAN.

=head2 How do I efficiently match many regular expressions at once?
X&lt;regex, efficiency&gt; X&lt;regexp, efficiency&gt;
X&lt;regular expression, efficiency&gt;

(contributed by brian d foy)

If you have Perl 5.10 or later, this is almost trivial. You just smart
match against an array of regular expression objects:

    my @patterns = ( qr/Fr.d/, qr/B.rn.y/, qr/W.lm./ );

    if( $string ~~ @patterns ) {
        ...
    };

The smart match stops when it finds a match, so it doesn&#39;t have to try
every expression.

Earlier than Perl 5.10, you have a bit of work to do. You want to
avoid compiling a regular expression every time you want to match it.
In this example, perl must recompile the regular expression for every
iteration of the C&lt;foreach&gt; loop since it has no way to know what
C&lt;$pattern&gt; will be:

    my @patterns = qw( foo bar baz );

    LINE: while( &lt;DATA&gt; ) {
        foreach $pattern ( @patterns ) {
            if( /\b$pattern\b/i ) {
                print;
                next LINE;
            }
        }
    }

The C&lt;qr//&gt; operator showed up in perl 5.005. It compiles a regular
expression, but doesn&#39;t apply it. When you use the pre-compiled
version of the regex, perl does less work. In this example, I inserted
a C&lt;map&gt; to turn each pattern into its pre-compiled form. The rest of
the script is the same, but faster:

    my @patterns = map { qr/\b$_\b/i } qw( foo bar baz );

    LINE: while( &lt;&gt; ) {
        foreach $pattern ( @patterns ) {
            if( /$pattern/ ) {
                print;
                next LINE;
            }
        }
    }

In some cases, you may be able to make several patterns into a single
regular expression. Beware of situations that require backtracking
though.

    my $regex = join &#39;|&#39;, qw( foo bar baz );

    LINE: while( &lt;&gt; ) {
        print if /\b(?:$regex)\b/i;
    }

For more details on regular expression efficiency, see I&lt;Mastering
Regular Expressions&gt; by Jeffrey Friedl. He explains how the regular
expressions engine works and why some patterns are surprisingly
inefficient. Once you understand how perl applies regular expressions,
you can tune them for individual situations.

=head2 Why don&#39;t word-boundary searches with C&lt;\b&gt; work for me?
X&lt;\b&gt;

(contributed by brian d foy)

Ensure that you know what \b really does: it&#39;s the boundary between a
word character, \w, and something that isn&#39;t a word character. That
thing that isn&#39;t a word character might be \W, but it can also be the
start or end of the string.

It&#39;s not (not!) the boundary between whitespace and non-whitespace,
and it&#39;s not the stuff between words we use to create sentences.

In regex speak, a word boundary (\b) is a &quot;zero width assertion&quot;,
meaning that it doesn&#39;t represent a character in the string, but a
condition at a certain position.

For the regular expression, /\bPerl\b/, there has to be a word
boundary before the &quot;P&quot; and after the &quot;l&quot;. As long as something other
than a word character precedes the &quot;P&quot; and succeeds the &quot;l&quot;, the
pattern will match. These strings match /\bPerl\b/.

    &quot;Perl&quot;    # no word char before P or after l
    &quot;Perl &quot;   # same as previous (space is not a word char)
    &quot;&#39;Perl&#39;&quot;  # the &#39; char is not a word char
    &quot;Perl&#39;s&quot;  # no word char before P, non-word char after &quot;l&quot;

These strings do not match /\bPerl\b/.

    &quot;Perl_&quot;   # _ is a word char!
    &quot;Perler&quot;  # no word char before P, but one after l

You don&#39;t have to use \b to match words though. You can look for
non-word characters surrounded by word characters. These strings
match the pattern /\b&#39;\b/.

    &quot;don&#39;t&quot;   # the &#39; char is surrounded by &quot;n&quot; and &quot;t&quot;
    &quot;qep&#39;a&#39;&quot;  # the &#39; char is surrounded by &quot;p&quot; and &quot;a&quot;

These strings do not match /\b&#39;\b/.

    &quot;foo&#39;&quot;    # there is no word char after non-word &#39;

You can also use the complement of \b, \B, to specify that there
should not be a word boundary.

In the pattern /\Bam\B/, there must be a word character before the &quot;a&quot;
and after the &quot;m&quot;. These patterns match /\Bam\B/:

    &quot;llama&quot;   # &quot;am&quot; surrounded by word chars
    &quot;Samuel&quot;  # same

These strings do not match /\Bam\B/

    &quot;Sam&quot;      # no word boundary before &quot;a&quot;, but one after &quot;m&quot;
    &quot;I am Sam&quot; # &quot;am&quot; surrounded by non-word chars


=head2 Why does using $&amp;, $`, or $&#39; slow my program down?
X&lt;$MATCH&gt; X&lt;$&amp;&gt; X&lt;$POSTMATCH&gt; X&lt;$&#39;&gt; X&lt;$PREMATCH&gt; X&lt;$`&gt;

(contributed by Anno Siegel)

Once Perl sees that you need one of these variables anywhere in the
program, it provides them on each and every pattern match. That means
that on every pattern match the entire string will be copied, part of it
to $`, part to $&amp;, and part to $&#39;. Thus the penalty is most severe with
long strings and patterns that match often. Avoid $&amp;, $&#39;, and $` if you
can, but if you can&#39;t, once you&#39;ve used them at all, use them at will
because you&#39;ve already paid the price. Remember that some algorithms
really appreciate them. As of the 5.005 release, the $&amp; variable is no
longer &quot;expensive&quot; the way the other two are.

Since Perl 5.6.1 the special variables @- and @+ can functionally replace
$`, $&amp; and $&#39;. These arrays contain pointers to the beginning and end
of each match (see perlvar for the full story), so they give you
essentially the same information, but without the risk of excessive
string copying.

Perl 5.10 added three specials, C&lt;${^MATCH}&gt;, C&lt;${^PREMATCH}&gt;, and
C&lt;${^POSTMATCH}&gt; to do the same job but without the global performance
penalty. Perl 5.10 only sets these variables if you compile or execute the
regular expression with the C&lt;/p&gt; modifier.

=head2 What good is C&lt;\G&gt; in a regular expression?
X&lt;\G&gt;

You use the C&lt;\G&gt; anchor to start the next match on the same
string where the last match left off. The regular
expression engine cannot skip over any characters to find
the next match with this anchor, so C&lt;\G&gt; is similar to the
beginning of string anchor, C&lt;^&gt;. The C&lt;\G&gt; anchor is typically
used with the C&lt;g&gt; flag. It uses the value of C&lt;pos()&gt;
as the position to start the next match. As the match
operator makes successive matches, it updates C&lt;pos()&gt; with the
position of the next character past the last match (or the
first character of the next match, depending on how you like
to look at it). Each string has its own C&lt;pos()&gt; value.

Suppose you want to match all of consecutive pairs of digits
in a string like &quot;1122a44&quot; and stop matching when you
encounter non-digits. You want to match C&lt;11&gt; and C&lt;22&gt; but
the letter &lt;a&gt; shows up between C&lt;22&gt; and C&lt;44&gt; and you want
to stop at C&lt;a&gt;. Simply matching pairs of digits skips over
the C&lt;a&gt; and still matches C&lt;44&gt;.

    $_ = &quot;1122a44&quot;;
    my @pairs = m/(\d\d)/g;   # qw( 11 22 44 )

If you use the C&lt;\G&gt; anchor, you force the match after C&lt;22&gt; to
start with the C&lt;a&gt;. The regular expression cannot match
there since it does not find a digit, so the next match
fails and the match operator returns the pairs it already
found.

    $_ = &quot;1122a44&quot;;
    my @pairs = m/\G(\d\d)/g; # qw( 11 22 )

You can also use the C&lt;\G&gt; anchor in scalar context. You
still need the C&lt;g&gt; flag.

    $_ = &quot;1122a44&quot;;
    while( m/\G(\d\d)/g ) {
        print &quot;Found $1\n&quot;;
    }

After the match fails at the letter C&lt;a&gt;, perl resets C&lt;pos()&gt;
and the next match on the same string starts at the beginning.

    $_ = &quot;1122a44&quot;;
    while( m/\G(\d\d)/g ) {
        print &quot;Found $1\n&quot;;
    }

    print &quot;Found $1 after while&quot; if m/(\d\d)/g; # finds &quot;11&quot;

You can disable C&lt;pos()&gt; resets on fail with the C&lt;c&gt; flag, documented
in L&lt;perlop&gt; and L&lt;perlreref&gt;. Subsequent matches start where the last
successful match ended (the value of C&lt;pos()&gt;) even if a match on the
same string has failed in the meantime. In this case, the match after
the C&lt;while()&gt; loop starts at the C&lt;a&gt; (where the last match stopped),
and since it does not use any anchor it can skip over the C&lt;a&gt; to find
C&lt;44&gt;.

    $_ = &quot;1122a44&quot;;
    while( m/\G(\d\d)/gc ) {
        print &quot;Found $1\n&quot;;
    }

    print &quot;Found $1 after while&quot; if m/(\d\d)/g; # finds &quot;44&quot;

Typically you use the C&lt;\G&gt; anchor with the C&lt;c&gt; flag
when you want to try a different match if one fails,
such as in a tokenizer. Jeffrey Friedl offers this example
which works in 5.004 or later.

    while (&lt;&gt;) {
        chomp;
        PARSER: {
            m/ \G( \d+\b    )/gcx   &amp;&amp; do { print &quot;number: $1\n&quot;;  redo; };
            m/ \G( \w+      )/gcx   &amp;&amp; do { print &quot;word:   $1\n&quot;;  redo; };
            m/ \G( \s+      )/gcx   &amp;&amp; do { print &quot;space:  $1\n&quot;;  redo; };
            m/ \G( [^\w\d]+ )/gcx   &amp;&amp; do { print &quot;other:  $1\n&quot;;  redo; };
        }
    }

For each line, the C&lt;PARSER&gt; loop first tries to match a series
of digits followed by a word boundary. This match has to
start at the place the last match left off (or the beginning
of the string on the first match). Since C&lt;m/ \G( \d+\b
)/gcx&gt; uses the C&lt;c&gt; flag, if the string does not match that
regular expression, perl does not reset pos() and the next
match starts at the same position to try a different
pattern.

=head2 Are Perl regexes DFAs or NFAs? Are they POSIX compliant?
X&lt;DFA&gt; X&lt;NFA&gt; X&lt;POSIX&gt;

While it&#39;s true that Perl&#39;s regular expressions resemble the DFAs
(deterministic finite automata) of the egrep(1) program, they are in
fact implemented as NFAs (non-deterministic finite automata) to allow
backtracking and backreferencing. And they aren&#39;t POSIX-style either,
because those guarantee worst-case behavior for all cases. (It seems
that some people prefer guarantees of consistency, even when what&#39;s
guaranteed is slowness.) See the book &quot;Mastering Regular Expressions&quot;
(from O&#39;Reilly) by Jeffrey Friedl for all the details you could ever
hope to know on these matters (a full citation appears in
L&lt;perlfaq2&gt;).

=head2 What&#39;s wrong with using grep in a void context?
X&lt;grep&gt;

The problem is that grep builds a return list, regardless of the context.
This means you&#39;re making Perl go to the trouble of building a list that
you then just throw away. If the list is large, you waste both time and space.
If your intent is to iterate over the list, then use a for loop for this
purpose.

In perls older than 5.8.1, map suffers from this problem as well.
But since 5.8.1, this has been fixed, and map is context aware - in void
context, no lists are constructed.

=head2 How can I match strings with multibyte characters?
X&lt;regex, and multibyte characters&gt; X&lt;regexp, and multibyte characters&gt;
X&lt;regular expression, and multibyte characters&gt; X&lt;martian&gt; X&lt;encoding, Martian&gt;

Starting from Perl 5.6 Perl has had some level of multibyte character
support. Perl 5.8 or later is recommended. Supported multibyte
character repertoires include Unicode, and legacy encodings
through the Encode module. See L&lt;perluniintro&gt;, L&lt;perlunicode&gt;,
and L&lt;Encode&gt;.

If you are stuck with older Perls, you can do Unicode with the
L&lt;Unicode::String&gt; module, and character conversions using the
L&lt;Unicode::Map8&gt; and L&lt;Unicode::Map&gt; modules. If you are using
Japanese encodings, you might try using the jperl 5.005_03.

Finally, the following set of approaches was offered by Jeffrey
Friedl, whose article in issue #5 of The Perl Journal talks about
this very matter.

Let&#39;s suppose you have some weird Martian encoding where pairs of
ASCII uppercase letters encode single Martian letters (i.e. the two
bytes &quot;CV&quot; make a single Martian letter, as do the two bytes &quot;SG&quot;,
&quot;VS&quot;, &quot;XX&quot;, etc.). Other bytes represent single characters, just like
ASCII.

So, the string of Martian &quot;I am CVSGXX!&quot; uses 12 bytes to encode the
nine characters &#39;I&#39;, &#39; &#39;, &#39;a&#39;, &#39;m&#39;, &#39; &#39;, &#39;CV&#39;, &#39;SG&#39;, &#39;XX&#39;, &#39;!&#39;.

Now, say you want to search for the single character C&lt;/GX/&gt;. Perl
doesn&#39;t know about Martian, so it&#39;ll find the two bytes &quot;GX&quot; in the &quot;I
am CVSGXX!&quot; string, even though that character isn&#39;t there: it just
looks like it is because &quot;SG&quot; is next to &quot;XX&quot;, but there&#39;s no real
&quot;GX&quot;. This is a big problem.

Here are a few ways, all painful, to deal with it:

    # Make sure adjacent &quot;martian&quot; bytes are no longer adjacent.
    $martian =~ s/([A-Z][A-Z])/ $1 /g;

    print &quot;found GX!\n&quot; if $martian =~ /GX/;

Or like this:

    my @chars = $martian =~ m/([A-Z][A-Z]|[^A-Z])/g;
    # above is conceptually similar to:     my @chars = $text =~ m/(.)/g;
    #
    foreach my $char (@chars) {
        print &quot;found GX!\n&quot;, last if $char eq &#39;GX&#39;;
    }

Or like this:

    while ($martian =~ m/\G([A-Z][A-Z]|.)/gs) {  # \G probably unneeded
        if ($1 eq &#39;GX&#39;) {
            print &quot;found GX!\n&quot;;
            last;
        }
    }

Here&#39;s another, slightly less painful, way to do it from Benjamin
Goldberg, who uses a zero-width negative look-behind assertion.

    print &quot;found GX!\n&quot; if    $martian =~ m/
        (?&lt;![A-Z])
        (?:[A-Z][A-Z])*?
        GX
        /x;

This succeeds if the &quot;martian&quot; character GX is in the string, and fails
otherwise. If you don&#39;t like using (?&lt;!), a zero-width negative
look-behind assertion, you can replace (?&lt;![A-Z]) with (?:^|[^A-Z]).

It does have the drawback of putting the wrong thing in $-[0] and $+[0],
but this usually can be worked around.

=head2 How do I match a regular expression that&#39;s in a variable?
X&lt;regex, in variable&gt; X&lt;eval&gt; X&lt;regex&gt; X&lt;quotemeta&gt; X&lt;\Q, regex&gt;
X&lt;\E, regex&gt; X&lt;qr//&gt;

(contributed by brian d foy)

We don&#39;t have to hard-code patterns into the match operator (or
anything else that works with regular expressions). We can put the
pattern in a variable for later use.

The match operator is a double quote context, so you can interpolate
your variable just like a double quoted string. In this case, you
read the regular expression as user input and store it in C&lt;$regex&gt;.
Once you have the pattern in C&lt;$regex&gt;, you use that variable in the
match operator.

    chomp( my $regex = &lt;STDIN&gt; );

    if( $string =~ m/$regex/ ) { ... }

Any regular expression special characters in C&lt;$regex&gt; are still
special, and the pattern still has to be valid or Perl will complain.
For instance, in this pattern there is an unpaired parenthesis.

    my $regex = &quot;Unmatched ( paren&quot;;

    &quot;Two parens to bind them all&quot; =~ m/$regex/;

When Perl compiles the regular expression, it treats the parenthesis
as the start of a memory match. When it doesn&#39;t find the closing
parenthesis, it complains:

    Unmatched ( in regex; marked by &lt;-- HERE in m/Unmatched ( &lt;-- HERE  paren/ at script line 3.

You can get around this in several ways depending on our situation.
First, if you don&#39;t want any of the characters in the string to be
special, you can escape them with C&lt;quotemeta&gt; before you use the string.

    chomp( my $regex = &lt;STDIN&gt; );
    $regex = quotemeta( $regex );

    if( $string =~ m/$regex/ ) { ... }

You can also do this directly in the match operator using the C&lt;\Q&gt;
and C&lt;\E&gt; sequences. The C&lt;\Q&gt; tells Perl where to start escaping
special characters, and the C&lt;\E&gt; tells it where to stop (see L&lt;perlop&gt;
for more details).

    chomp( my $regex = &lt;STDIN&gt; );

    if( $string =~ m/\Q$regex\E/ ) { ... }

Alternately, you can use C&lt;qr//&gt;, the regular expression quote operator (see
L&lt;perlop&gt; for more details). It quotes and perhaps compiles the pattern,
and you can apply regular expression flags to the pattern.

    chomp( my $input = &lt;STDIN&gt; );

    my $regex = qr/$input/is;

    $string =~ m/$regex/  # same as m/$input/is;

You might also want to trap any errors by wrapping an C&lt;eval&gt; block
around the whole thing.

    chomp( my $input = &lt;STDIN&gt; );

    eval {
        if( $string =~ m/\Q$input\E/ ) { ... }
    };
    warn $@ if $@;

Or...

    my $regex = eval { qr/$input/is };
    if( defined $regex ) {
        $string =~ m/$regex/;
    }
    else {
        warn $@;
    }

=head1 AUTHOR AND COPYRIGHT

Copyright (c) 1997-2010 Tom Christiansen, Nathan Torkington, and
other authors as noted. All rights reserved.

This documentation is free; you can redistribute it and/or modify it
under the same terms as Perl itself.

Irrespective of its distribution, all code examples in this file
are hereby placed into the public domain. You are permitted and
encouraged to use this code in your own programs for fun
or for profit as you see fit. A simple comment in the code giving
credit would be courteous but is not required.
</pre>

                    </main>
                </div>

        </div>
        <footer>
        
        </footer>


    </body>
</html>

<!DOCTYPE html>
<html>
    <head>
        <meta charset="utf-8">
        <meta content="width=device-width, initial-scale=1" name="viewport">
        <link href="/theme/css/normalize.css" rel="stylesheet">
        <!--<link rel="stylesheet" href="/theme/css/skeleton.css" />-->
        <link href="/theme/css/statocles-default.css" rel="stylesheet">
        <link href="//maxcdn.bootstrapcdn.com/font-awesome/4.3.0/css/font-awesome.min.css" rel="stylesheet">
        <script crossorigin="anonymous" integrity="sha256-hwg4gsxgFZhOsEEamdOYGBf13FyQuiTwlAQgxVSNgt4=" src="http://code.jquery.com/jquery-3.2.1.min.js"></script>
        <title>perltie (source) - Minimum Viable Perl</title>
        <meta content="Statocles 0.086" name="generator">
        
        <script>
            $( document ).ready(function() {
                $('.sub-menu ul').hide();
                $('.sub-menu.perldoc ul').show();
                $('.sub-menu.perldoc div').toggleClass("fa-caret-up fa-caret-down");
                $(".sub-menu a").click(function () {
                    $(this).parent(".sub-menu").siblings().children('ul').hide();
                    $(this).parent(".sub-menu").children("ul").slideToggle("100");
                    $(this).find(".right").toggleClass("fa-caret-up fa-caret-down");
                });
            });
        </script>
        <!-- Global site tag (gtag.js) - Google Analytics -->
        <script async src="https://www.googletagmanager.com/gtag/js?id=UA-110455056-1"></script>
        <script>
            window.dataLayer = window.dataLayer || [];
            function gtag(){dataLayer.push(arguments);}
            gtag('js', new Date());
            gtag('config', 'UA-110455056-1');
        </script>
    </head>
    <body>
        <header>
        </header>
        <div class="container">

                <div class="left column full-height">
                    <div class="sidebar">
                    
<a class="home" href="/">Minimum<br>Viable<br>Perl</a><br><br><br>

<div class="animated bounceInDown">
  <ul>
    <li class="basics sub-menu">
      <a href="/perldoc/perltie/#basics">The basics<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/basics/hello">Hello world</a></li>
        <li><a href="/basics/variables">Variables</a></li>
        <li><a href="/basics/references">References</a></li>
        <li><a href="/basics/functions">Functions</a></li>
        <li><a href="/basics/flow">Conditionals</a></li>
        <li><a href="/basics/loops">Loops</a></li>
        <li><a href="/basics/exceptions">Exceptions</a></li>
        <li><a href="/basics/regexps">Regexps</a></li>
        <li><a href="/basics/files">Files</a></li>
      </ul>
    </li>
    <li class="oo sub-menu">
      <a href="/perldoc/perltie/#oo">Object oriented programming<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/oo/story">The (weird) story of Perl OO</a></li>
        <li><a href="/oo/classes">Classes</a></li>
        <li><a href="/oo/attributes">Attributes</a></li>
        <li><a href="/oo/modifiers">Method modifiers</a></li>
        <li><a href="/oo/roles">Roles</a></li>
        <li><a href="/oo/inheritance">Inheritance</a></li>
      </ul>
    </li>
    <li class="debugging sub-menu">
      <a href="/perldoc/perltie/#basics">Debugging<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/debugging/data-printer">Examining data structures</a></li>
        <li><a href="/debugging/stack-trace">Forcing stack traces</a></li>
      </ul>
    </li>
    <li class="dependencies sub-menu">
      <a href="/perldoc/perltie/#dependencies">Perl libraries<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/dependencies/perl5lib">Loading libraries from custom locations</a></li>
        <li><a href="/dependencies/cpanm">Installing libraries with cpanm</a></li>
        <li><a href="/dependencies/metacpan">Choosing libraries from MetaCPAN</a></li>
        <li><a href="/dependencies/carton">Managing project dependencies with Carton</a></li>
        <li><a href="/dependencies/pmtools">What version is installed and where?</a></li>
      </ul>
    </li>
    <li class="testing sub-menu">
      <a href="/perldoc/perltie/#testing">Testing<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/testing/intro">Introduction to testing</a></li>
        <li><a href="/testing/warnings">Testing warnings and exceptions</a></li>
        <li><a href="/testing/data">Testing data structures</a></li>
        <li><a href="/testing/mock">Testing with mock objects</a></li>
        <li><a href="/testing/libs">Build your own test libraries</a></li>
      </ul>
    </li>
    <li><a href="/perl">Perl design goals</a></li>
    <li><a href="/see-also">Book recommendations</a></li>
    <li><a href="/about">About this book</a></li>
  </ul>
</div>

                    
                    
                    </div>
                </div>

                <div class="right column">
                    <main>
                    <a class="button" href="/perldoc/perltie/">
    Back to documentation
</a>
<pre>=head1 NAME
X&lt;tie&gt;

perltie - how to hide an object class in a simple variable

=head1 SYNOPSIS

 tie VARIABLE, CLASSNAME, LIST

 $object = tied VARIABLE

 untie VARIABLE

=head1 DESCRIPTION

Prior to release 5.0 of Perl, a programmer could use dbmopen()
to connect an on-disk database in the standard Unix dbm(3x)
format magically to a %HASH in their program.  However, their Perl was either
built with one particular dbm library or another, but not both, and
you couldn&#39;t extend this mechanism to other packages or types of variables.

Now you can.

The tie() function binds a variable to a class (package) that will provide
the implementation for access methods for that variable.  Once this magic
has been performed, accessing a tied variable automatically triggers
method calls in the proper class.  The complexity of the class is
hidden behind magic methods calls.  The method names are in ALL CAPS,
which is a convention that Perl uses to indicate that they&#39;re called
implicitly rather than explicitly--just like the BEGIN() and END()
functions.

In the tie() call, C&lt;VARIABLE&gt; is the name of the variable to be
enchanted.  C&lt;CLASSNAME&gt; is the name of a class implementing objects of
the correct type.  Any additional arguments in the C&lt;LIST&gt; are passed to
the appropriate constructor method for that class--meaning TIESCALAR(),
TIEARRAY(), TIEHASH(), or TIEHANDLE().  (Typically these are arguments
such as might be passed to the dbminit() function of C.) The object
returned by the &quot;new&quot; method is also returned by the tie() function,
which would be useful if you wanted to access other methods in
C&lt;CLASSNAME&gt;. (You don&#39;t actually have to return a reference to a right
&quot;type&quot; (e.g., HASH or C&lt;CLASSNAME&gt;) so long as it&#39;s a properly blessed
object.)  You can also retrieve a reference to the underlying object
using the tied() function.

Unlike dbmopen(), the tie() function will not C&lt;use&gt; or C&lt;require&gt; a module
for you--you need to do that explicitly yourself.

=head2 Tying Scalars
X&lt;scalar, tying&gt;

A class implementing a tied scalar should define the following methods:
TIESCALAR, FETCH, STORE, and possibly UNTIE and/or DESTROY.

Let&#39;s look at each in turn, using as an example a tie class for
scalars that allows the user to do something like:

    tie $his_speed, &#39;Nice&#39;, getppid();
    tie $my_speed,  &#39;Nice&#39;, $$;

And now whenever either of those variables is accessed, its current
system priority is retrieved and returned.  If those variables are set,
then the process&#39;s priority is changed!

We&#39;ll use Jarkko Hietaniemi &lt;F&lt;jhi@iki.fi&gt;&gt;&#39;s BSD::Resource class (not
included) to access the PRIO_PROCESS, PRIO_MIN, and PRIO_MAX constants
from your system, as well as the getpriority() and setpriority() system
calls.  Here&#39;s the preamble of the class.

    package Nice;
    use Carp;
    use BSD::Resource;
    use strict;
    $Nice::DEBUG = 0 unless defined $Nice::DEBUG;

=over 4

=item TIESCALAR classname, LIST
X&lt;TIESCALAR&gt;

This is the constructor for the class.  That means it is
expected to return a blessed reference to a new scalar
(probably anonymous) that it&#39;s creating.  For example:

 sub TIESCALAR {
     my $class = shift;
     my $pid = shift || $$; # 0 means me

     if ($pid !~ /^\d+$/) {
         carp &quot;Nice::Tie::Scalar got non-numeric pid $pid&quot; if $^W;
         return undef;
     }

     unless (kill 0, $pid) { # EPERM or ERSCH, no doubt
         carp &quot;Nice::Tie::Scalar got bad pid $pid: $!&quot; if $^W;
         return undef;
     }

     return bless \$pid, $class;
 }

This tie class has chosen to return an error rather than raising an
exception if its constructor should fail.  While this is how dbmopen() works,
other classes may well not wish to be so forgiving.  It checks the global
variable C&lt;$^W&gt; to see whether to emit a bit of noise anyway.

=item FETCH this
X&lt;FETCH&gt;

This method will be triggered every time the tied variable is accessed
(read).  It takes no arguments beyond its self reference, which is the
object representing the scalar we&#39;re dealing with.  Because in this case
we&#39;re using just a SCALAR ref for the tied scalar object, a simple $$self
allows the method to get at the real value stored there.  In our example
below, that real value is the process ID to which we&#39;ve tied our variable.

    sub FETCH {
        my $self = shift;
        confess &quot;wrong type&quot; unless ref $self;
        croak &quot;usage error&quot; if @_;
        my $nicety;
        local($!) = 0;
        $nicety = getpriority(PRIO_PROCESS, $$self);
        if ($!) { croak &quot;getpriority failed: $!&quot; }
        return $nicety;
    }

This time we&#39;ve decided to blow up (raise an exception) if the renice
fails--there&#39;s no place for us to return an error otherwise, and it&#39;s
probably the right thing to do.

=item STORE this, value
X&lt;STORE&gt;

This method will be triggered every time the tied variable is set
(assigned).  Beyond its self reference, it also expects one (and only one)
argument: the new value the user is trying to assign. Don&#39;t worry about
returning a value from STORE; the semantic of assignment returning the
assigned value is implemented with FETCH.

 sub STORE {
     my $self = shift;
     confess &quot;wrong type&quot; unless ref $self;
     my $new_nicety = shift;
     croak &quot;usage error&quot; if @_;

     if ($new_nicety &lt; PRIO_MIN) {
         carp sprintf
           &quot;WARNING: priority %d less than minimum system priority %d&quot;,
               $new_nicety, PRIO_MIN if $^W;
         $new_nicety = PRIO_MIN;
     }

     if ($new_nicety &gt; PRIO_MAX) {
         carp sprintf
           &quot;WARNING: priority %d greater than maximum system priority %d&quot;,
               $new_nicety, PRIO_MAX if $^W;
         $new_nicety = PRIO_MAX;
     }

     unless (defined setpriority(PRIO_PROCESS,
                                 $$self,
                                 $new_nicety))
     {
         confess &quot;setpriority failed: $!&quot;;
     }
 }

=item UNTIE this
X&lt;UNTIE&gt;

This method will be triggered when the C&lt;untie&gt; occurs. This can be useful
if the class needs to know when no further calls will be made. (Except DESTROY
of course.) See L&lt;The C&lt;untie&gt; Gotcha&gt; below for more details.

=item DESTROY this
X&lt;DESTROY&gt;

This method will be triggered when the tied variable needs to be destructed.
As with other object classes, such a method is seldom necessary, because Perl
deallocates its moribund object&#39;s memory for you automatically--this isn&#39;t
C++, you know.  We&#39;ll use a DESTROY method here for debugging purposes only.

    sub DESTROY {
        my $self = shift;
        confess &quot;wrong type&quot; unless ref $self;
        carp &quot;[ Nice::DESTROY pid $$self ]&quot; if $Nice::DEBUG;
    }

=back

That&#39;s about all there is to it.  Actually, it&#39;s more than all there
is to it, because we&#39;ve done a few nice things here for the sake
of completeness, robustness, and general aesthetics.  Simpler
TIESCALAR classes are certainly possible.

=head2 Tying Arrays
X&lt;array, tying&gt;

A class implementing a tied ordinary array should define the following
methods: TIEARRAY, FETCH, STORE, FETCHSIZE, STORESIZE, CLEAR
and perhaps UNTIE and/or DESTROY.

FETCHSIZE and STORESIZE are used to provide C&lt;$#array&gt; and
equivalent C&lt;scalar(@array)&gt; access.

The methods POP, PUSH, SHIFT, UNSHIFT, SPLICE, DELETE, and EXISTS are
required if the perl operator with the corresponding (but lowercase) name
is to operate on the tied array. The B&lt;Tie::Array&gt; class can be used as a
base class to implement the first five of these in terms of the basic
methods above.  The default implementations of DELETE and EXISTS in
B&lt;Tie::Array&gt; simply C&lt;croak&gt;.

In addition EXTEND will be called when perl would have pre-extended
allocation in a real array.

For this discussion, we&#39;ll implement an array whose elements are a fixed
size at creation.  If you try to create an element larger than the fixed
size, you&#39;ll take an exception.  For example:

    use FixedElem_Array;
    tie @array, &#39;FixedElem_Array&#39;, 3;
    $array[0] = &#39;cat&#39;;  # ok.
    $array[1] = &#39;dogs&#39;; # exception, length(&#39;dogs&#39;) &gt; 3.

The preamble code for the class is as follows:

    package FixedElem_Array;
    use Carp;
    use strict;

=over 4

=item TIEARRAY classname, LIST
X&lt;TIEARRAY&gt;

This is the constructor for the class.  That means it is expected to
return a blessed reference through which the new array (probably an
anonymous ARRAY ref) will be accessed.

In our example, just to show you that you don&#39;t I&lt;really&gt; have to return an
ARRAY reference, we&#39;ll choose a HASH reference to represent our object.
A HASH works out well as a generic record type: the C&lt;{ELEMSIZE}&gt; field will
store the maximum element size allowed, and the C&lt;{ARRAY}&gt; field will hold the
true ARRAY ref.  If someone outside the class tries to dereference the
object returned (doubtless thinking it an ARRAY ref), they&#39;ll blow up.
This just goes to show you that you should respect an object&#39;s privacy.

    sub TIEARRAY {
      my $class    = shift;
      my $elemsize = shift;
      if ( @_ || $elemsize =~ /\D/ ) {
        croak &quot;usage: tie ARRAY, &#39;&quot; . __PACKAGE__ . &quot;&#39;, elem_size&quot;;
      }
      return bless {
        ELEMSIZE =&gt; $elemsize,
        ARRAY    =&gt; [],
      }, $class;
    }

=item FETCH this, index
X&lt;FETCH&gt;

This method will be triggered every time an individual element the tied array
is accessed (read).  It takes one argument beyond its self reference: the
index whose value we&#39;re trying to fetch.

    sub FETCH {
      my $self  = shift;
      my $index = shift;
      return $self-&gt;{ARRAY}-&gt;[$index];
    }

If a negative array index is used to read from an array, the index
will be translated to a positive one internally by calling FETCHSIZE
before being passed to FETCH.  You may disable this feature by
assigning a true value to the variable C&lt;$NEGATIVE_INDICES&gt; in the
tied array class.

As you may have noticed, the name of the FETCH method (et al.) is the same
for all accesses, even though the constructors differ in names (TIESCALAR
vs TIEARRAY).  While in theory you could have the same class servicing
several tied types, in practice this becomes cumbersome, and it&#39;s easiest
to keep them at simply one tie type per class.

=item STORE this, index, value
X&lt;STORE&gt;

This method will be triggered every time an element in the tied array is set
(written).  It takes two arguments beyond its self reference: the index at
which we&#39;re trying to store something and the value we&#39;re trying to put
there.

In our example, C&lt;undef&gt; is really C&lt;$self-E&lt;gt&gt;{ELEMSIZE}&gt; number of
spaces so we have a little more work to do here:

 sub STORE {
   my $self = shift;
   my( $index, $value ) = @_;
   if ( length $value &gt; $self-&gt;{ELEMSIZE} ) {
     croak &quot;length of $value is greater than $self-&gt;{ELEMSIZE}&quot;;
   }
   # fill in the blanks
   $self-&gt;EXTEND( $index ) if $index &gt; $self-&gt;FETCHSIZE();
   # right justify to keep element size for smaller elements
   $self-&gt;{ARRAY}-&gt;[$index] = sprintf &quot;%$self-&gt;{ELEMSIZE}s&quot;, $value;
 }

Negative indexes are treated the same as with FETCH.

=item FETCHSIZE this
X&lt;FETCHSIZE&gt;

Returns the total number of items in the tied array associated with
object I&lt;this&gt;. (Equivalent to C&lt;scalar(@array)&gt;).  For example:

    sub FETCHSIZE {
      my $self = shift;
      return scalar @{$self-&gt;{ARRAY}};
    }

=item STORESIZE this, count
X&lt;STORESIZE&gt;

Sets the total number of items in the tied array associated with
object I&lt;this&gt; to be I&lt;count&gt;. If this makes the array larger then
class&#39;s mapping of C&lt;undef&gt; should be returned for new positions.
If the array becomes smaller then entries beyond count should be
deleted. 

In our example, &#39;undef&#39; is really an element containing
C&lt;$self-E&lt;gt&gt;{ELEMSIZE}&gt; number of spaces.  Observe:

    sub STORESIZE {
      my $self  = shift;
      my $count = shift;
      if ( $count &gt; $self-&gt;FETCHSIZE() ) {
        foreach ( $count - $self-&gt;FETCHSIZE() .. $count ) {
          $self-&gt;STORE( $_, &#39;&#39; );
        }
      } elsif ( $count &lt; $self-&gt;FETCHSIZE() ) {
        foreach ( 0 .. $self-&gt;FETCHSIZE() - $count - 2 ) {
          $self-&gt;POP();
        }
      }
    }

=item EXTEND this, count
X&lt;EXTEND&gt;

Informative call that array is likely to grow to have I&lt;count&gt; entries.
Can be used to optimize allocation. This method need do nothing.

In our example, we want to make sure there are no blank (C&lt;undef&gt;)
entries, so C&lt;EXTEND&gt; will make use of C&lt;STORESIZE&gt; to fill elements
as needed:

    sub EXTEND {   
      my $self  = shift;
      my $count = shift;
      $self-&gt;STORESIZE( $count );
    }

=item EXISTS this, key
X&lt;EXISTS&gt;

Verify that the element at index I&lt;key&gt; exists in the tied array I&lt;this&gt;.

In our example, we will determine that if an element consists of
C&lt;$self-E&lt;gt&gt;{ELEMSIZE}&gt; spaces only, it does not exist:

 sub EXISTS {
   my $self  = shift;
   my $index = shift;
   return 0 if ! defined $self-&gt;{ARRAY}-&gt;[$index] ||
               $self-&gt;{ARRAY}-&gt;[$index] eq &#39; &#39; x $self-&gt;{ELEMSIZE};
   return 1;
 }

=item DELETE this, key
X&lt;DELETE&gt;

Delete the element at index I&lt;key&gt; from the tied array I&lt;this&gt;.

In our example, a deleted item is C&lt;$self-E&lt;gt&gt;{ELEMSIZE}&gt; spaces:

    sub DELETE {
      my $self  = shift;
      my $index = shift;
      return $self-&gt;STORE( $index, &#39;&#39; );
    }

=item CLEAR this
X&lt;CLEAR&gt;

Clear (remove, delete, ...) all values from the tied array associated with
object I&lt;this&gt;.  For example:

    sub CLEAR {
      my $self = shift;
      return $self-&gt;{ARRAY} = [];
    }

=item PUSH this, LIST 
X&lt;PUSH&gt;

Append elements of I&lt;LIST&gt; to the array.  For example:

    sub PUSH {  
      my $self = shift;
      my @list = @_;
      my $last = $self-&gt;FETCHSIZE();
      $self-&gt;STORE( $last + $_, $list[$_] ) foreach 0 .. $#list;
      return $self-&gt;FETCHSIZE();
    }   

=item POP this
X&lt;POP&gt;

Remove last element of the array and return it.  For example:

    sub POP {
      my $self = shift;
      return pop @{$self-&gt;{ARRAY}};
    }

=item SHIFT this
X&lt;SHIFT&gt;

Remove the first element of the array (shifting other elements down)
and return it.  For example:

    sub SHIFT {
      my $self = shift;
      return shift @{$self-&gt;{ARRAY}};
    }

=item UNSHIFT this, LIST 
X&lt;UNSHIFT&gt;

Insert LIST elements at the beginning of the array, moving existing elements
up to make room.  For example:

    sub UNSHIFT {
      my $self = shift;
      my @list = @_;
      my $size = scalar( @list );
      # make room for our list
      @{$self-&gt;{ARRAY}}[ $size .. $#{$self-&gt;{ARRAY}} + $size ]
       = @{$self-&gt;{ARRAY}};
      $self-&gt;STORE( $_, $list[$_] ) foreach 0 .. $#list;
    }

=item SPLICE this, offset, length, LIST
X&lt;SPLICE&gt;

Perform the equivalent of C&lt;splice&gt; on the array. 

I&lt;offset&gt; is optional and defaults to zero, negative values count back 
from the end of the array. 

I&lt;length&gt; is optional and defaults to rest of the array.

I&lt;LIST&gt; may be empty.

Returns a list of the original I&lt;length&gt; elements at I&lt;offset&gt;.

In our example, we&#39;ll use a little shortcut if there is a I&lt;LIST&gt;:

    sub SPLICE {
      my $self   = shift;
      my $offset = shift || 0;
      my $length = shift || $self-&gt;FETCHSIZE() - $offset;
      my @list   = (); 
      if ( @_ ) {
        tie @list, __PACKAGE__, $self-&gt;{ELEMSIZE};
        @list   = @_;
      }
      return splice @{$self-&gt;{ARRAY}}, $offset, $length, @list;
    }

=item UNTIE this
X&lt;UNTIE&gt;

Will be called when C&lt;untie&gt; happens. (See L&lt;The C&lt;untie&gt; Gotcha&gt; below.)

=item DESTROY this
X&lt;DESTROY&gt;

This method will be triggered when the tied variable needs to be destructed.
As with the scalar tie class, this is almost never needed in a
language that does its own garbage collection, so this time we&#39;ll
just leave it out.

=back

=head2 Tying Hashes
X&lt;hash, tying&gt;

Hashes were the first Perl data type to be tied (see dbmopen()).  A class
implementing a tied hash should define the following methods: TIEHASH is
the constructor.  FETCH and STORE access the key and value pairs.  EXISTS
reports whether a key is present in the hash, and DELETE deletes one.
CLEAR empties the hash by deleting all the key and value pairs.  FIRSTKEY
and NEXTKEY implement the keys() and each() functions to iterate over all
the keys. SCALAR is triggered when the tied hash is evaluated in scalar 
context. UNTIE is called when C&lt;untie&gt; happens, and DESTROY is called when
the tied variable is garbage collected.

If this seems like a lot, then feel free to inherit from merely the
standard Tie::StdHash module for most of your methods, redefining only the
interesting ones.  See L&lt;Tie::Hash&gt; for details.

Remember that Perl distinguishes between a key not existing in the hash,
and the key existing in the hash but having a corresponding value of
C&lt;undef&gt;.  The two possibilities can be tested with the C&lt;exists()&gt; and
C&lt;defined()&gt; functions.

Here&#39;s an example of a somewhat interesting tied hash class:  it gives you
a hash representing a particular user&#39;s dot files.  You index into the hash
with the name of the file (minus the dot) and you get back that dot file&#39;s
contents.  For example:

    use DotFiles;
    tie %dot, &#39;DotFiles&#39;;
    if ( $dot{profile} =~ /MANPATH/ ||
         $dot{login}   =~ /MANPATH/ ||
         $dot{cshrc}   =~ /MANPATH/    )
    {
	print &quot;you seem to set your MANPATH\n&quot;;
    }

Or here&#39;s another sample of using our tied class:

    tie %him, &#39;DotFiles&#39;, &#39;daemon&#39;;
    foreach $f ( keys %him ) {
	printf &quot;daemon dot file %s is size %d\n&quot;,
	    $f, length $him{$f};
    }

In our tied hash DotFiles example, we use a regular
hash for the object containing several important
fields, of which only the C&lt;{LIST}&gt; field will be what the
user thinks of as the real hash.

=over 5

=item USER

whose dot files this object represents

=item HOME

where those dot files live

=item CLOBBER

whether we should try to change or remove those dot files

=item LIST

the hash of dot file names and content mappings

=back

Here&#39;s the start of F&lt;Dotfiles.pm&gt;:

    package DotFiles;
    use Carp;
    sub whowasi { (caller(1))[3] . &#39;()&#39; }
    my $DEBUG = 0;
    sub debug { $DEBUG = @_ ? shift : 1 }

For our example, we want to be able to emit debugging info to help in tracing
during development.  We keep also one convenience function around
internally to help print out warnings; whowasi() returns the function name
that calls it.

Here are the methods for the DotFiles tied hash.

=over 4

=item TIEHASH classname, LIST
X&lt;TIEHASH&gt;

This is the constructor for the class.  That means it is expected to
return a blessed reference through which the new object (probably but not
necessarily an anonymous hash) will be accessed.

Here&#39;s the constructor:

    sub TIEHASH {
	my $self = shift;
	my $user = shift || $&gt;;
	my $dotdir = shift || &#39;&#39;;
	croak &quot;usage: @{[&amp;whowasi]} [USER [DOTDIR]]&quot; if @_;
	$user = getpwuid($user) if $user =~ /^\d+$/;
	my $dir = (getpwnam($user))[7]
		|| croak &quot;@{[&amp;whowasi]}: no user $user&quot;;
	$dir .= &quot;/$dotdir&quot; if $dotdir;

	my $node = {
	    USER    =&gt; $user,
	    HOME    =&gt; $dir,
	    LIST    =&gt; {},
	    CLOBBER =&gt; 0,
	};

	opendir(DIR, $dir)
		|| croak &quot;@{[&amp;whowasi]}: can&#39;t opendir $dir: $!&quot;;
	foreach $dot ( grep /^\./ &amp;&amp; -f &quot;$dir/$_&quot;, readdir(DIR)) {
	    $dot =~ s/^\.//;
	    $node-&gt;{LIST}{$dot} = undef;
	}
	closedir DIR;
	return bless $node, $self;
    }

It&#39;s probably worth mentioning that if you&#39;re going to filetest the
return values out of a readdir, you&#39;d better prepend the directory
in question.  Otherwise, because we didn&#39;t chdir() there, it would
have been testing the wrong file.

=item FETCH this, key
X&lt;FETCH&gt;

This method will be triggered every time an element in the tied hash is
accessed (read).  It takes one argument beyond its self reference: the key
whose value we&#39;re trying to fetch.

Here&#39;s the fetch for our DotFiles example.

    sub FETCH {
	carp &amp;whowasi if $DEBUG;
	my $self = shift;
	my $dot = shift;
	my $dir = $self-&gt;{HOME};
	my $file = &quot;$dir/.$dot&quot;;

	unless (exists $self-&gt;{LIST}-&gt;{$dot} || -f $file) {
	    carp &quot;@{[&amp;whowasi]}: no $dot file&quot; if $DEBUG;
	    return undef;
	}

	if (defined $self-&gt;{LIST}-&gt;{$dot}) {
	    return $self-&gt;{LIST}-&gt;{$dot};
	} else {
	    return $self-&gt;{LIST}-&gt;{$dot} = `cat $dir/.$dot`;
	}
    }

It was easy to write by having it call the Unix cat(1) command, but it
would probably be more portable to open the file manually (and somewhat
more efficient).  Of course, because dot files are a Unixy concept, we&#39;re
not that concerned.

=item STORE this, key, value
X&lt;STORE&gt;

This method will be triggered every time an element in the tied hash is set
(written).  It takes two arguments beyond its self reference: the index at
which we&#39;re trying to store something, and the value we&#39;re trying to put
there.

Here in our DotFiles example, we&#39;ll be careful not to let
them try to overwrite the file unless they&#39;ve called the clobber()
method on the original object reference returned by tie().

    sub STORE {
	carp &amp;whowasi if $DEBUG;
	my $self = shift;
	my $dot = shift;
	my $value = shift;
	my $file = $self-&gt;{HOME} . &quot;/.$dot&quot;;
	my $user = $self-&gt;{USER};

	croak &quot;@{[&amp;whowasi]}: $file not clobberable&quot;
	    unless $self-&gt;{CLOBBER};

	open(my $f, &#39;&gt;&#39;, $file) || croak &quot;can&#39;t open $file: $!&quot;;
	print $f $value;
	close($f);
    }

If they wanted to clobber something, they might say:

    $ob = tie %daemon_dots, &#39;daemon&#39;;
    $ob-&gt;clobber(1);
    $daemon_dots{signature} = &quot;A true daemon\n&quot;;

Another way to lay hands on a reference to the underlying object is to
use the tied() function, so they might alternately have set clobber
using:

    tie %daemon_dots, &#39;daemon&#39;;
    tied(%daemon_dots)-&gt;clobber(1);

The clobber method is simply:

    sub clobber {
	my $self = shift;
	$self-&gt;{CLOBBER} = @_ ? shift : 1;
    }

=item DELETE this, key
X&lt;DELETE&gt;

This method is triggered when we remove an element from the hash,
typically by using the delete() function.  Again, we&#39;ll
be careful to check whether they really want to clobber files.

 sub DELETE   {
     carp &amp;whowasi if $DEBUG;

     my $self = shift;
     my $dot = shift;
     my $file = $self-&gt;{HOME} . &quot;/.$dot&quot;;
     croak &quot;@{[&amp;whowasi]}: won&#39;t remove file $file&quot;
         unless $self-&gt;{CLOBBER};
     delete $self-&gt;{LIST}-&gt;{$dot};
     my $success = unlink($file);
     carp &quot;@{[&amp;whowasi]}: can&#39;t unlink $file: $!&quot; unless $success;
     $success;
 }

The value returned by DELETE becomes the return value of the call
to delete().  If you want to emulate the normal behavior of delete(),
you should return whatever FETCH would have returned for this key.
In this example, we have chosen instead to return a value which tells
the caller whether the file was successfully deleted.

=item CLEAR this
X&lt;CLEAR&gt;

This method is triggered when the whole hash is to be cleared, usually by
assigning the empty list to it.

In our example, that would remove all the user&#39;s dot files!  It&#39;s such a
dangerous thing that they&#39;ll have to set CLOBBER to something higher than
1 to make it happen.

 sub CLEAR    {
     carp &amp;whowasi if $DEBUG;
     my $self = shift;
     croak &quot;@{[&amp;whowasi]}: won&#39;t remove all dot files for $self-&gt;{USER}&quot;
         unless $self-&gt;{CLOBBER} &gt; 1;
     my $dot;
     foreach $dot ( keys %{$self-&gt;{LIST}}) {
         $self-&gt;DELETE($dot);
     }
 }

=item EXISTS this, key
X&lt;EXISTS&gt;

This method is triggered when the user uses the exists() function
on a particular hash.  In our example, we&#39;ll look at the C&lt;{LIST}&gt;
hash element for this:

    sub EXISTS   {
	carp &amp;whowasi if $DEBUG;
	my $self = shift;
	my $dot = shift;
	return exists $self-&gt;{LIST}-&gt;{$dot};
    }

=item FIRSTKEY this
X&lt;FIRSTKEY&gt;

This method will be triggered when the user is going
to iterate through the hash, such as via a keys(), values(), or each() call.

    sub FIRSTKEY {
	carp &amp;whowasi if $DEBUG;
	my $self = shift;
	my $a = keys %{$self-&gt;{LIST}};  # reset each() iterator
	each %{$self-&gt;{LIST}}
    }

FIRSTKEY is always called in scalar context and it should just
return the first key.  values(), and each() in list context,
will call FETCH for the returned keys.

=item NEXTKEY this, lastkey
X&lt;NEXTKEY&gt;

This method gets triggered during a keys(), values(), or each() iteration.  It has a
second argument which is the last key that had been accessed.  This is
useful if you&#39;re caring about ordering or calling the iterator from more
than one sequence, or not really storing things in a hash anywhere.

NEXTKEY is always called in scalar context and it should just
return the next key.  values(), and each() in list context,
will call FETCH for the returned keys.

For our example, we&#39;re using a real hash so we&#39;ll do just the simple
thing, but we&#39;ll have to go through the LIST field indirectly.

    sub NEXTKEY  {
	carp &amp;whowasi if $DEBUG;
	my $self = shift;
	return each %{ $self-&gt;{LIST} }
    }

=item SCALAR this
X&lt;SCALAR&gt;

This is called when the hash is evaluated in scalar context. In order
to mimic the behaviour of untied hashes, this method should return a
false value when the tied hash is considered empty. If this method does
not exist, perl will make some educated guesses and return true when
the hash is inside an iteration. If this isn&#39;t the case, FIRSTKEY is
called, and the result will be a false value if FIRSTKEY returns the empty
list, true otherwise.

However, you should B&lt;not&gt; blindly rely on perl always doing the right 
thing. Particularly, perl will mistakenly return true when you clear the 
hash by repeatedly calling DELETE until it is empty. You are therefore 
advised to supply your own SCALAR method when you want to be absolutely 
sure that your hash behaves nicely in scalar context.

In our example we can just call C&lt;scalar&gt; on the underlying hash
referenced by C&lt;$self-E&lt;gt&gt;{LIST}&gt;:

    sub SCALAR {
	carp &amp;whowasi if $DEBUG;
	my $self = shift;
	return scalar %{ $self-&gt;{LIST} }
    }

NOTE: In perl 5.25 the behavior of scalar %hash on an untied hash changed
to return the count of keys. Prior to this it returned a string containing
information about the bucket setup of the hash. See
L&lt;Hash::Util/bucket_ratio&gt; for a backwards compatibility path.

=item UNTIE this
X&lt;UNTIE&gt;

This is called when C&lt;untie&gt; occurs.  See L&lt;The C&lt;untie&gt; Gotcha&gt; below.

=item DESTROY this
X&lt;DESTROY&gt;

This method is triggered when a tied hash is about to go out of
scope.  You don&#39;t really need it unless you&#39;re trying to add debugging
or have auxiliary state to clean up.  Here&#39;s a very simple function:

    sub DESTROY  {
	carp &amp;whowasi if $DEBUG;
    }

=back

Note that functions such as keys() and values() may return huge lists
when used on large objects, like DBM files.  You may prefer to use the
each() function to iterate over such.  Example:

    # print out history file offsets
    use NDBM_File;
    tie(%HIST, &#39;NDBM_File&#39;, &#39;/usr/lib/news/history&#39;, 1, 0);
    while (($key,$val) = each %HIST) {
        print $key, &#39; = &#39;, unpack(&#39;L&#39;,$val), &quot;\n&quot;;
    }
    untie(%HIST);

=head2 Tying FileHandles
X&lt;filehandle, tying&gt;

This is partially implemented now.

A class implementing a tied filehandle should define the following
methods: TIEHANDLE, at least one of PRINT, PRINTF, WRITE, READLINE, GETC,
READ, and possibly CLOSE, UNTIE and DESTROY.  The class can also provide: BINMODE,
OPEN, EOF, FILENO, SEEK, TELL - if the corresponding perl operators are
used on the handle.

When STDERR is tied, its PRINT method will be called to issue warnings
and error messages.  This feature is temporarily disabled during the call, 
which means you can use C&lt;warn()&gt; inside PRINT without starting a recursive
loop.  And just like C&lt;__WARN__&gt; and C&lt;__DIE__&gt; handlers, STDERR&#39;s PRINT
method may be called to report parser errors, so the caveats mentioned under 
L&lt;perlvar/%SIG&gt; apply.

All of this is especially useful when perl is embedded in some other 
program, where output to STDOUT and STDERR may have to be redirected 
in some special way.  See nvi and the Apache module for examples.

When tying a handle, the first argument to C&lt;tie&gt; should begin with an
asterisk.  So, if you are tying STDOUT, use C&lt;*STDOUT&gt;.  If you have
assigned it to a scalar variable, say C&lt;$handle&gt;, use C&lt;*$handle&gt;.
C&lt;tie $handle&gt; ties the scalar variable C&lt;$handle&gt;, not the handle inside
it.

In our example we&#39;re going to create a shouting handle.

    package Shout;

=over 4

=item TIEHANDLE classname, LIST
X&lt;TIEHANDLE&gt;

This is the constructor for the class.  That means it is expected to
return a blessed reference of some sort. The reference can be used to
hold some internal information.

    sub TIEHANDLE { print &quot;&lt;shout&gt;\n&quot;; my $i; bless \$i, shift }

=item WRITE this, LIST
X&lt;WRITE&gt;

This method will be called when the handle is written to via the
C&lt;syswrite&gt; function.

 sub WRITE {
     $r = shift;
     my($buf,$len,$offset) = @_;
     print &quot;WRITE called, \$buf=$buf, \$len=$len, \$offset=$offset&quot;;
 }

=item PRINT this, LIST
X&lt;PRINT&gt;

This method will be triggered every time the tied handle is printed to
with the C&lt;print()&gt; or C&lt;say()&gt; functions.  Beyond its self reference
it also expects the list that was passed to the print function.

  sub PRINT { $r = shift; $$r++; print join($,,map(uc($_),@_)),$\ }

C&lt;say()&gt; acts just like C&lt;print()&gt; except $\ will be localized to C&lt;\n&gt; so
you need do nothing special to handle C&lt;say()&gt; in C&lt;PRINT()&gt;.

=item PRINTF this, LIST
X&lt;PRINTF&gt;

This method will be triggered every time the tied handle is printed to
with the C&lt;printf()&gt; function.
Beyond its self reference it also expects the format and list that was
passed to the printf function.

    sub PRINTF {
        shift;
        my $fmt = shift;
        print sprintf($fmt, @_);
    }

=item READ this, LIST
X&lt;READ&gt;

This method will be called when the handle is read from via the C&lt;read&gt;
or C&lt;sysread&gt; functions.

 sub READ {
   my $self = shift;
   my $bufref = \$_[0];
   my(undef,$len,$offset) = @_;
   print &quot;READ called, \$buf=$bufref, \$len=$len, \$offset=$offset&quot;;
   # add to $$bufref, set $len to number of characters read
   $len;
 }

=item READLINE this
X&lt;READLINE&gt;

This method is called when the handle is read via C&lt;E&lt;lt&gt;HANDLEE&lt;gt&gt;&gt;
or C&lt;readline HANDLE&gt;.

As per L&lt;C&lt;readline&gt;|perlfunc/readline&gt;, in scalar context it should return
the next line, or C&lt;undef&gt; for no more data.  In list context it should
return all remaining lines, or an empty list for no more data.  The strings
returned should include the input record separator C&lt;$/&gt; (see L&lt;perlvar&gt;),
unless it is C&lt;undef&gt; (which means &quot;slurp&quot; mode).

    sub READLINE {
      my $r = shift;
      if (wantarray) {
        return (&quot;all remaining\n&quot;,
                &quot;lines up\n&quot;,
                &quot;to eof\n&quot;);
      } else {
        return &quot;READLINE called &quot; . ++$$r . &quot; times\n&quot;;
      }
    }

=item GETC this
X&lt;GETC&gt;

This method will be called when the C&lt;getc&gt; function is called.

    sub GETC { print &quot;Don&#39;t GETC, Get Perl&quot;; return &quot;a&quot;; }

=item EOF this
X&lt;EOF&gt;

This method will be called when the C&lt;eof&gt; function is called.

Starting with Perl 5.12, an additional integer parameter will be passed.  It
will be zero if C&lt;eof&gt; is called without parameter; C&lt;1&gt; if C&lt;eof&gt; is given
a filehandle as a parameter, e.g. C&lt;eof(FH)&gt;; and C&lt;2&gt; in the very special
case that the tied filehandle is C&lt;ARGV&gt; and C&lt;eof&gt; is called with an empty
parameter list, e.g. C&lt;eof()&gt;.

    sub EOF { not length $stringbuf }

=item CLOSE this
X&lt;CLOSE&gt;

This method will be called when the handle is closed via the C&lt;close&gt;
function.

    sub CLOSE { print &quot;CLOSE called.\n&quot; }

=item UNTIE this
X&lt;UNTIE&gt;

As with the other types of ties, this method will be called when C&lt;untie&gt; happens.
It may be appropriate to &quot;auto CLOSE&quot; when this occurs.  See
L&lt;The C&lt;untie&gt; Gotcha&gt; below.

=item DESTROY this
X&lt;DESTROY&gt;

As with the other types of ties, this method will be called when the
tied handle is about to be destroyed. This is useful for debugging and
possibly cleaning up.

    sub DESTROY { print &quot;&lt;/shout&gt;\n&quot; }

=back

Here&#39;s how to use our little example:

    tie(*FOO,&#39;Shout&#39;);
    print FOO &quot;hello\n&quot;;
    $a = 4; $b = 6;
    print FOO $a, &quot; plus &quot;, $b, &quot; equals &quot;, $a + $b, &quot;\n&quot;;
    print &lt;FOO&gt;;

=head2 UNTIE this
X&lt;UNTIE&gt;

You can define for all tie types an UNTIE method that will be called
at untie().  See L&lt;The C&lt;untie&gt; Gotcha&gt; below.

=head2 The C&lt;untie&gt; Gotcha
X&lt;untie&gt;

If you intend making use of the object returned from either tie() or
tied(), and if the tie&#39;s target class defines a destructor, there is a
subtle gotcha you I&lt;must&gt; guard against.

As setup, consider this (admittedly rather contrived) example of a
tie; all it does is use a file to keep a log of the values assigned to
a scalar.

    package Remember;

    use strict;
    use warnings;
    use IO::File;

    sub TIESCALAR {
        my $class = shift;
        my $filename = shift;
        my $handle = IO::File-&gt;new( &quot;&gt; $filename&quot; )
                         or die &quot;Cannot open $filename: $!\n&quot;;

        print $handle &quot;The Start\n&quot;;
        bless {FH =&gt; $handle, Value =&gt; 0}, $class;
    }

    sub FETCH {
        my $self = shift;
        return $self-&gt;{Value};
    }

    sub STORE {
        my $self = shift;
        my $value = shift;
        my $handle = $self-&gt;{FH};
        print $handle &quot;$value\n&quot;;
        $self-&gt;{Value} = $value;
    }

    sub DESTROY {
        my $self = shift;
        my $handle = $self-&gt;{FH};
        print $handle &quot;The End\n&quot;;
        close $handle;
    }

    1;

Here is an example that makes use of this tie:

    use strict;
    use Remember;

    my $fred;
    tie $fred, &#39;Remember&#39;, &#39;myfile.txt&#39;;
    $fred = 1;
    $fred = 4;
    $fred = 5;
    untie $fred;
    system &quot;cat myfile.txt&quot;;

This is the output when it is executed:

    The Start
    1
    4
    5
    The End

So far so good.  Those of you who have been paying attention will have
spotted that the tied object hasn&#39;t been used so far.  So lets add an
extra method to the Remember class to allow comments to be included in
the file; say, something like this:

    sub comment {
        my $self = shift;
        my $text = shift;
        my $handle = $self-&gt;{FH};
        print $handle $text, &quot;\n&quot;;
    }

And here is the previous example modified to use the C&lt;comment&gt; method
(which requires the tied object):

    use strict;
    use Remember;

    my ($fred, $x);
    $x = tie $fred, &#39;Remember&#39;, &#39;myfile.txt&#39;;
    $fred = 1;
    $fred = 4;
    comment $x &quot;changing...&quot;;
    $fred = 5;
    untie $fred;
    system &quot;cat myfile.txt&quot;;

When this code is executed there is no output.  Here&#39;s why:

When a variable is tied, it is associated with the object which is the
return value of the TIESCALAR, TIEARRAY, or TIEHASH function.  This
object normally has only one reference, namely, the implicit reference
from the tied variable.  When untie() is called, that reference is
destroyed.  Then, as in the first example above, the object&#39;s
destructor (DESTROY) is called, which is normal for objects that have
no more valid references; and thus the file is closed.

In the second example, however, we have stored another reference to
the tied object in $x.  That means that when untie() gets called
there will still be a valid reference to the object in existence, so
the destructor is not called at that time, and thus the file is not
closed.  The reason there is no output is because the file buffers
have not been flushed to disk.

Now that you know what the problem is, what can you do to avoid it?
Prior to the introduction of the optional UNTIE method the only way
was the good old C&lt;-w&gt; flag. Which will spot any instances where you call
untie() and there are still valid references to the tied object.  If
the second script above this near the top C&lt;use warnings &#39;untie&#39;&gt;
or was run with the C&lt;-w&gt; flag, Perl prints this
warning message:

    untie attempted while 1 inner references still exist

To get the script to work properly and silence the warning make sure
there are no valid references to the tied object I&lt;before&gt; untie() is
called:

    undef $x;
    untie $fred;

Now that UNTIE exists the class designer can decide which parts of the
class functionality are really associated with C&lt;untie&gt; and which with
the object being destroyed. What makes sense for a given class depends
on whether the inner references are being kept so that non-tie-related
methods can be called on the object. But in most cases it probably makes
sense to move the functionality that would have been in DESTROY to the UNTIE
method.

If the UNTIE method exists then the warning above does not occur. Instead the
UNTIE method is passed the count of &quot;extra&quot; references and can issue its own
warning if appropriate. e.g. to replicate the no UNTIE case this method can
be used:

 sub UNTIE
 {
  my ($obj,$count) = @_;
  carp &quot;untie attempted while $count inner references still exist&quot;
                                                              if $count;
 }

=head1 SEE ALSO

See L&lt;DB_File&gt; or L&lt;Config&gt; for some interesting tie() implementations.
A good starting point for many tie() implementations is with one of the
modules L&lt;Tie::Scalar&gt;, L&lt;Tie::Array&gt;, L&lt;Tie::Hash&gt;, or L&lt;Tie::Handle&gt;.

=head1 BUGS

The normal return provided by C&lt;scalar(%hash)&gt; is not
available.  What this means is that using %tied_hash in boolean
context doesn&#39;t work right (currently this always tests false,
regardless of whether the hash is empty or hash elements).
[ This paragraph needs review in light of changes in 5.25 ]

Localizing tied arrays or hashes does not work.  After exiting the
scope the arrays or the hashes are not restored.

Counting the number of entries in a hash via C&lt;scalar(keys(%hash))&gt;
or C&lt;scalar(values(%hash)&gt;) is inefficient since it needs to iterate
through all the entries with FIRSTKEY/NEXTKEY.

Tied hash/array slices cause multiple FETCH/STORE pairs, there are no
tie methods for slice operations.

You cannot easily tie a multilevel data structure (such as a hash of
hashes) to a dbm file.  The first problem is that all but GDBM and
Berkeley DB have size limitations, but beyond that, you also have problems
with how references are to be represented on disk.  One
module that does attempt to address this need is DBM::Deep.  Check your
nearest CPAN site as described in L&lt;perlmodlib&gt; for source code.  Note
that despite its name, DBM::Deep does not use dbm.  Another earlier attempt
at solving the problem is MLDBM, which is also available on the CPAN, but
which has some fairly serious limitations.

Tied filehandles are still incomplete.  sysopen(), truncate(),
flock(), fcntl(), stat() and -X can&#39;t currently be trapped.

=head1 AUTHOR

Tom Christiansen

TIEHANDLE by Sven Verdoolaege &lt;F&lt;skimo@dns.ufsia.ac.be&gt;&gt; and Doug MacEachern &lt;F&lt;dougm@osf.org&gt;&gt;

UNTIE by Nick Ing-Simmons &lt;F&lt;nick@ing-simmons.net&gt;&gt;

SCALAR by Tassilo von Parseval &lt;F&lt;tassilo.von.parseval@rwth-aachen.de&gt;&gt;

Tying Arrays by Casey West &lt;F&lt;casey@geeknest.com&gt;&gt;
</pre>

                    </main>
                </div>

        </div>
        <footer>
        
        </footer>


    </body>
</html>

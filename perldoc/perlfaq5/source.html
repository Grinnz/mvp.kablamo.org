<!DOCTYPE html>
<html>
    <head>
        <meta charset="utf-8">
        <meta content="width=device-width, initial-scale=1" name="viewport">
        <link href="/theme/css/normalize.css" rel="stylesheet">
        <!--<link rel="stylesheet" href="/theme/css/skeleton.css" />-->
        <link href="/theme/css/statocles-default.css" rel="stylesheet">
        <link href="//maxcdn.bootstrapcdn.com/font-awesome/4.3.0/css/font-awesome.min.css" rel="stylesheet">
        <script crossorigin="anonymous" integrity="sha256-hwg4gsxgFZhOsEEamdOYGBf13FyQuiTwlAQgxVSNgt4=" src="http://code.jquery.com/jquery-3.2.1.min.js"></script>
        <title>perlfaq5 (source) - Minimum Viable Perl</title>
        <meta content="Statocles 0.086" name="generator">
        
        <script>
            $( document ).ready(function() {
                $('.sub-menu ul').hide();
                $('.sub-menu.perldoc ul').show();
                $('.sub-menu.perldoc div').toggleClass("fa-caret-up fa-caret-down");
                $(".sub-menu a").click(function () {
                    $(this).parent(".sub-menu").siblings().children('ul').hide();
                    $(this).parent(".sub-menu").children("ul").slideToggle("100");
                    $(this).find(".right").toggleClass("fa-caret-up fa-caret-down");
                });
            });
        </script>
    </head>
    <body>
        <header>
        </header>
        <div class="container">

                <div class="left column full-height">
                    <div class="sidebar">
                    
<a class="home" href="/">Minimum<br>Viable<br>Perl</a><br><br><br>

<div class="animated bounceInDown">
  <ul>
    <li class="basics sub-menu">
      <a href="/perldoc/perlfaq5/#basics">The basics<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/basics/hello">Hello world</a></li>
        <li><a href="/basics/variables">Variables</a></li>
        <li><a href="/basics/references">References</a></li>
        <li><a href="/basics/functions">Functions</a></li>
        <li><a href="/basics/flow">Conditionals</a></li>
        <li><a href="/basics/loops">Loops</a></li>
        <li><a href="/basics/exceptions">Exceptions</a></li>
        <li><a href="/basics/regexps">Regexps</a></li>
        <li><a href="/basics/files">Files</a></li>
      </ul>
    </li>
    <li class="oo sub-menu">
      <a href="/perldoc/perlfaq5/#oo">Object oriented programming<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/oo/story">The (weird) story of Perl OO</a></li>
        <li><a href="/oo/classes">Classes</a></li>
        <li><a href="/oo/attributes">Attributes</a></li>
        <li><a href="/oo/modifiers">Method modifiers</a></li>
        <li><a href="/oo/roles">Roles</a></li>
        <li><a href="/oo/inheritance">Inheritance</a></li>
      </ul>
    </li>
    <li class="debugging sub-menu">
      <a href="/perldoc/perlfaq5/#basics">Debugging<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/debugging/data-printer">Examining data structures</a></li>
        <li><a href="/debugging/stack-trace">Forcing stack traces</a></li>
      </ul>
    </li>
    <li class="dependencies sub-menu">
      <a href="/perldoc/perlfaq5/#dependencies">Perl libraries<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/dependencies/perl5lib">Loading libraries from custom locations</a></li>
        <li><a href="/dependencies/cpanm">Installing libraries with cpanm</a></li>
        <li><a href="/dependencies/metacpan">Choosing libraries from MetaCPAN</a></li>
        <li><a href="/dependencies/carton">Managing project dependencies with Carton</a></li>
        <li><a href="/dependencies/pmtools">What version is installed and where?</a></li>
      </ul>
    </li>
    <li class="testing sub-menu">
      <a href="/perldoc/perlfaq5/#testing">Testing<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/testing/intro">Introduction to testing</a></li>
        <li><a href="/testing/warnings">Testing warnings and exceptions</a></li>
        <li><a href="/testing/data">Testing data structures</a></li>
        <li><a href="/testing/mock">Testing with mock objects</a></li>
        <li><a href="/testing/libs">Build your own test libraries</a></li>
      </ul>
    </li>
    <li><a href="/perl">Perl design goals</a></li>
    <li><a href="/see-also">Book recommendations</a></li>
    <li><a href="/about">About this book</a></li>
  </ul>
</div>

                    
                    
                    </div>
                </div>

                <div class="right column">
                    <main>
                    <a class="button" href="/perldoc/perlfaq5/">
    Back to documentation
</a>
<pre>=head1 NAME

perlfaq5 - Files and Formats

=head1 VERSION

version 5.021011

=head1 DESCRIPTION

This section deals with I/O and the &quot;f&quot; issues: filehandles, flushing,
formats, and footers.

=head2 How do I flush/unbuffer an output filehandle? Why must I do this?
X&lt;flush&gt; X&lt;buffer&gt; X&lt;unbuffer&gt; X&lt;autoflush&gt;

(contributed by brian d foy)

You might like to read Mark Jason Dominus&#39;s &quot;Suffering From Buffering&quot;
at L&lt;http://perl.plover.com/FAQs/Buffering.html&gt; .

Perl normally buffers output so it doesn&#39;t make a system call for every
bit of output. By saving up output, it makes fewer expensive system calls.
For instance, in this little bit of code, you want to print a dot to the
screen for every line you process to watch the progress of your program.
Instead of seeing a dot for every line, Perl buffers the output and you
have a long wait before you see a row of 50 dots all at once:

    # long wait, then row of dots all at once
    while( &lt;&gt; ) {
        print &quot;.&quot;;
        print &quot;\n&quot; unless ++$count % 50;

        #... expensive line processing operations
    }

To get around this, you have to unbuffer the output filehandle, in this
case, C&lt;STDOUT&gt;. You can set the special variable C&lt;$|&gt; to a true value
(mnemonic: making your filehandles &quot;piping hot&quot;):

    $|++;

    # dot shown immediately
    while( &lt;&gt; ) {
        print &quot;.&quot;;
        print &quot;\n&quot; unless ++$count % 50;

        #... expensive line processing operations
    }

The C&lt;$|&gt; is one of the per-filehandle special variables, so each
filehandle has its own copy of its value. If you want to merge
standard output and standard error for instance, you have to unbuffer
each (although STDERR might be unbuffered by default):

    {
        my $previous_default = select(STDOUT);  # save previous default
        $|++;                                   # autoflush STDOUT
        select(STDERR);
        $|++;                                   # autoflush STDERR, to be sure
        select($previous_default);              # restore previous default
    }

    # now should alternate . and +
    while( 1 ) {
        sleep 1;
        print STDOUT &quot;.&quot;;
        print STDERR &quot;+&quot;;
        print STDOUT &quot;\n&quot; unless ++$count % 25;
    }

Besides the C&lt;$|&gt; special variable, you can use C&lt;binmode&gt; to give
your filehandle a C&lt;:unix&gt; layer, which is unbuffered:

    binmode( STDOUT, &quot;:unix&quot; );

    while( 1 ) {
        sleep 1;
        print &quot;.&quot;;
        print &quot;\n&quot; unless ++$count % 50;
    }

For more information on output layers, see the entries for C&lt;binmode&gt;
and L&lt;open&gt; in L&lt;perlfunc&gt;, and the L&lt;PerlIO&gt; module documentation.

If you are using L&lt;IO::Handle&gt; or one of its subclasses, you can
call the C&lt;autoflush&gt; method to change the settings of the
filehandle:

    use IO::Handle;
    open my( $io_fh ), &quot;&gt;&quot;, &quot;output.txt&quot;;
    $io_fh-&gt;autoflush(1);

The L&lt;IO::Handle&gt; objects also have a C&lt;flush&gt; method. You can flush
the buffer any time you want without auto-buffering

    $io_fh-&gt;flush;

=head2 How do I change, delete, or insert a line in a file, or append to the beginning of a file?
X&lt;file, editing&gt;

(contributed by brian d foy)

The basic idea of inserting, changing, or deleting a line from a text
file involves reading and printing the file to the point you want to
make the change, making the change, then reading and printing the rest
of the file. Perl doesn&#39;t provide random access to lines (especially
since the record input separator, C&lt;$/&gt;, is mutable), although modules
such as L&lt;Tie::File&gt; can fake it.

A Perl program to do these tasks takes the basic form of opening a
file, printing its lines, then closing the file:

    open my $in,  &#39;&lt;&#39;,  $file      or die &quot;Can&#39;t read old file: $!&quot;;
    open my $out, &#39;&gt;&#39;, &quot;$file.new&quot; or die &quot;Can&#39;t write new file: $!&quot;;

    while( &lt;$in&gt; ) {
            print $out $_;
    }

    close $out;

Within that basic form, add the parts that you need to insert, change,
or delete lines.

To prepend lines to the beginning, print those lines before you enter
the loop that prints the existing lines.

    open my $in,  &#39;&lt;&#39;,  $file      or die &quot;Can&#39;t read old file: $!&quot;;
    open my $out, &#39;&gt;&#39;, &quot;$file.new&quot; or die &quot;Can&#39;t write new file: $!&quot;;

    print $out &quot;# Add this line to the top\n&quot;; # &lt;--- HERE&#39;S THE MAGIC

    while( &lt;$in&gt; ) {
            print $out $_;
    }

    close $out;

To change existing lines, insert the code to modify the lines inside
the C&lt;while&gt; loop. In this case, the code finds all lowercased
versions of &quot;perl&quot; and uppercases them. The happens for every line, so
be sure that you&#39;re supposed to do that on every line!

    open my $in,  &#39;&lt;&#39;,  $file      or die &quot;Can&#39;t read old file: $!&quot;;
    open my $out, &#39;&gt;&#39;, &quot;$file.new&quot; or die &quot;Can&#39;t write new file: $!&quot;;

    print $out &quot;# Add this line to the top\n&quot;;

    while( &lt;$in&gt; ) {
        s/\b(perl)\b/Perl/g;
        print $out $_;
    }

    close $out;

To change only a particular line, the input line number, C&lt;$.&gt;, is
useful. First read and print the lines up to the one you  want to
change. Next, read the single line you want to change, change it, and
print it. After that, read the rest of the lines and print those:

    while( &lt;$in&gt; ) { # print the lines before the change
        print $out $_;
        last if $. == 4; # line number before change
    }

    my $line = &lt;$in&gt;;
    $line =~ s/\b(perl)\b/Perl/g;
    print $out $line;

    while( &lt;$in&gt; ) { # print the rest of the lines
        print $out $_;
    }

To skip lines, use the looping controls. The C&lt;next&gt; in this example
skips comment lines, and the C&lt;last&gt; stops all processing once it
encounters either C&lt;__END__&gt; or C&lt;__DATA__&gt;.

    while( &lt;$in&gt; ) {
        next if /^\s+#/;             # skip comment lines
        last if /^__(END|DATA)__$/;  # stop at end of code marker
        print $out $_;
    }

Do the same sort of thing to delete a particular line by using C&lt;next&gt;
to skip the lines you don&#39;t want to show up in the output. This
example skips every fifth line:

    while( &lt;$in&gt; ) {
        next unless $. % 5;
        print $out $_;
    }

If, for some odd reason, you really want to see the whole file at once
rather than processing line-by-line, you can slurp it in (as long as
you can fit the whole thing in memory!):

    open my $in,  &#39;&lt;&#39;,  $file      or die &quot;Can&#39;t read old file: $!&quot;
    open my $out, &#39;&gt;&#39;, &quot;$file.new&quot; or die &quot;Can&#39;t write new file: $!&quot;;

    my $content = do { local $/; &lt;$in&gt; }; # slurp!

        # do your magic here

    print $out $content;

Modules such as L&lt;Path::Tiny&gt; and L&lt;Tie::File&gt; can help with that
too. If you can, however, avoid reading the entire file at once. Perl
won&#39;t give that memory back to the operating system until the process
finishes.

You can also use Perl one-liners to modify a file in-place. The
following changes all &#39;Fred&#39; to &#39;Barney&#39; in F&lt;inFile.txt&gt;, overwriting
the file with the new contents. With the C&lt;-p&gt; switch, Perl wraps a
C&lt;while&gt; loop around the code you specify with C&lt;-e&gt;, and C&lt;-i&gt; turns
on in-place editing. The current line is in C&lt;$_&gt;. With C&lt;-p&gt;, Perl
automatically prints the value of C&lt;$_&gt; at the end of the loop. See
L&lt;perlrun&gt; for more details.

    perl -pi -e &#39;s/Fred/Barney/&#39; inFile.txt

To make a backup of C&lt;inFile.txt&gt;, give C&lt;-i&gt; a file extension to add:

    perl -pi.bak -e &#39;s/Fred/Barney/&#39; inFile.txt

To change only the fifth line, you can add a test checking C&lt;$.&gt;, the
input line number, then only perform the operation when the test
passes:

    perl -pi -e &#39;s/Fred/Barney/ if $. == 5&#39; inFile.txt

To add lines before a certain line, you can add a line (or lines!)
before Perl prints C&lt;$_&gt;:

    perl -pi -e &#39;print &quot;Put before third line\n&quot; if $. == 3&#39; inFile.txt

You can even add a line to the beginning of a file, since the current
line prints at the end of the loop:

    perl -pi -e &#39;print &quot;Put before first line\n&quot; if $. == 1&#39; inFile.txt

To insert a line after one already in the file, use the C&lt;-n&gt; switch.
It&#39;s just like C&lt;-p&gt; except that it doesn&#39;t print C&lt;$_&gt; at the end of
the loop, so you have to do that yourself. In this case, print C&lt;$_&gt;
first, then print the line that you want to add.

    perl -ni -e &#39;print; print &quot;Put after fifth line\n&quot; if $. == 5&#39; inFile.txt

To delete lines, only print the ones that you want.

    perl -ni -e &#39;print if /d/&#39; inFile.txt

=head2 How do I count the number of lines in a file?
X&lt;file, counting lines&gt; X&lt;lines&gt; X&lt;line&gt;

(contributed by brian d foy)

Conceptually, the easiest way to count the lines in a file is to
simply read them and count them:

    my $count = 0;
    while( &lt;$fh&gt; ) { $count++; }

You don&#39;t really have to count them yourself, though, since Perl
already does that with the C&lt;$.&gt; variable, which is the current line
number from the last filehandle read:

    1 while( &lt;$fh&gt; );
    my $count = $.;

If you want to use C&lt;$.&gt;, you can reduce it to a simple one-liner,
like one of these:

    % perl -lne &#39;} print $.; {&#39;    file

    % perl -lne &#39;END { print $. }&#39; file

Those can be rather inefficient though. If they aren&#39;t fast enough for
you, you might just read chunks of data and count the number of
newlines:

    my $lines = 0;
    open my($fh), &#39;&lt;:raw&#39;, $filename or die &quot;Can&#39;t open $filename: $!&quot;;
    while( sysread $fh, $buffer, 4096 ) {
        $lines += ( $buffer =~ tr/\n// );
    }
    close $fh;

However, that doesn&#39;t work if the line ending isn&#39;t a newline. You
might change that C&lt;tr///&gt; to a C&lt;s///&gt; so you can count the number of
times the input record separator, C&lt;$/&gt;, shows up:

    my $lines = 0;
    open my($fh), &#39;&lt;:raw&#39;, $filename or die &quot;Can&#39;t open $filename: $!&quot;;
    while( sysread $fh, $buffer, 4096 ) {
        $lines += ( $buffer =~ s|$/||g; );
    }
    close $fh;

If you don&#39;t mind shelling out, the C&lt;wc&gt; command is usually the
fastest, even with the extra interprocess overhead. Ensure that you
have an untainted filename though:

    #!perl -T

    $ENV{PATH} = undef;

    my $lines;
    if( $filename =~ /^([0-9a-z_.]+)\z/ ) {
        $lines = `/usr/bin/wc -l $1`
        chomp $lines;
    }

=head2 How do I delete the last N lines from a file?
X&lt;lines&gt; X&lt;file&gt;

(contributed by brian d foy)

The easiest conceptual solution is to count the lines in the
file then start at the beginning and print the number of lines
(minus the last N) to a new file.

Most often, the real question is how you can delete the last N lines
without making more than one pass over the file, or how to do it
without a lot of copying. The easy concept is the hard reality when
you might have millions of lines in your file.

One trick is to use L&lt;File::ReadBackwards&gt;, which starts at the end of
the file. That module provides an object that wraps the real filehandle
to make it easy for you to move around the file. Once you get to the
spot you need, you can get the actual filehandle and work with it as
normal. In this case, you get the file position at the end of the last
line you want to keep and truncate the file to that point:

    use File::ReadBackwards;

    my $filename = &#39;test.txt&#39;;
    my $Lines_to_truncate = 2;

    my $bw = File::ReadBackwards-&gt;new( $filename )
        or die &quot;Could not read backwards in [$filename]: $!&quot;;

    my $lines_from_end = 0;
    until( $bw-&gt;eof or $lines_from_end == $Lines_to_truncate ) {
        print &quot;Got: &quot;, $bw-&gt;readline;
        $lines_from_end++;
    }

    truncate( $filename, $bw-&gt;tell );

The L&lt;File::ReadBackwards&gt; module also has the advantage of setting
the input record separator to a regular expression.

You can also use the L&lt;Tie::File&gt; module which lets you access
the lines through a tied array. You can use normal array operations
to modify your file, including setting the last index and using
C&lt;splice&gt;.

=head2 How can I use Perl&#39;s C&lt;-i&gt; option from within a program?
X&lt;-i&gt; X&lt;in-place&gt;

C&lt;-i&gt; sets the value of Perl&#39;s C&lt;$^I&gt; variable, which in turn affects
the behavior of C&lt;&lt; &lt;&gt; &gt;&gt;; see L&lt;perlrun&gt; for more details. By
modifying the appropriate variables directly, you can get the same
behavior within a larger program. For example:

    # ...
    {
        local($^I, @ARGV) = (&#39;.orig&#39;, glob(&quot;*.c&quot;));
        while (&lt;&gt;) {
            if ($. == 1) {
                print &quot;This line should appear at the top of each file\n&quot;;
            }
            s/\b(p)earl\b/${1}erl/i;        # Correct typos, preserving case
            print;
            close ARGV if eof;              # Reset $.
        }
    }
    # $^I and @ARGV return to their old values here

This block modifies all the C&lt;.c&gt; files in the current directory,
leaving a backup of the original data from each file in a new
C&lt;.c.orig&gt; file.

=head2 How can I copy a file?
X&lt;copy&gt; X&lt;file, copy&gt; X&lt;File::Copy&gt;

(contributed by brian d foy)

Use the L&lt;File::Copy&gt; module. It comes with Perl and can do a
true copy across file systems, and it does its magic in
a portable fashion.

    use File::Copy;

    copy( $original, $new_copy ) or die &quot;Copy failed: $!&quot;;

If you can&#39;t use L&lt;File::Copy&gt;, you&#39;ll have to do the work yourself:
open the original file, open the destination file, then print
to the destination file as you read the original. You also have to
remember to copy the permissions, owner, and group to the new file.

=head2 How do I make a temporary file name?
X&lt;file, temporary&gt;

If you don&#39;t need to know the name of the file, you can use C&lt;open()&gt;
with C&lt;undef&gt; in place of the file name. In Perl 5.8 or later, the
C&lt;open()&gt; function creates an anonymous temporary file:

    open my $tmp, &#39;+&gt;&#39;, undef or die $!;

Otherwise, you can use the File::Temp module.

    use File::Temp qw/ tempfile tempdir /;

    my $dir = tempdir( CLEANUP =&gt; 1 );
    ($fh, $filename) = tempfile( DIR =&gt; $dir );

    # or if you don&#39;t need to know the filename

    my $fh = tempfile( DIR =&gt; $dir );

The File::Temp has been a standard module since Perl 5.6.1. If you
don&#39;t have a modern enough Perl installed, use the C&lt;new_tmpfile&gt;
class method from the IO::File module to get a filehandle opened for
reading and writing. Use it if you don&#39;t need to know the file&#39;s name:

    use IO::File;
    my $fh = IO::File-&gt;new_tmpfile()
        or die &quot;Unable to make new temporary file: $!&quot;;

If you&#39;re committed to creating a temporary file by hand, use the
process ID and/or the current time-value. If you need to have many
temporary files in one process, use a counter:

    BEGIN {
        use Fcntl;
        use File::Spec;
        my $temp_dir  = File::Spec-&gt;tmpdir();
        my $file_base = sprintf &quot;%d-%d-0000&quot;, $$, time;
        my $base_name = File::Spec-&gt;catfile($temp_dir, $file_base);

        sub temp_file {
            my $fh;
            my $count = 0;
            until( defined(fileno($fh)) || $count++ &gt; 100 ) {
                $base_name =~ s/-(\d+)$/&quot;-&quot; . (1 + $1)/e;
                # O_EXCL is required for security reasons.
                sysopen $fh, $base_name, O_WRONLY|O_EXCL|O_CREAT;
            }

            if( defined fileno($fh) ) {
                return ($fh, $base_name);
            }
            else {
                return ();
            }
        }
    }

=head2 How can I manipulate fixed-record-length files?
X&lt;fixed-length&gt; X&lt;file, fixed-length records&gt;

The most efficient way is using L&lt;pack()|perlfunc/&quot;pack&quot;&gt; and
L&lt;unpack()|perlfunc/&quot;unpack&quot;&gt;. This is faster than using
L&lt;substr()|perlfunc/&quot;substr&quot;&gt; when taking many, many strings. It is
slower for just a few.

Here is a sample chunk of code to break up and put back together again
some fixed-format input lines, in this case from the output of a normal,
Berkeley-style ps:

    # sample input line:
    #   15158 p5  T      0:00 perl /home/tchrist/scripts/now-what
    my $PS_T = &#39;A6 A4 A7 A5 A*&#39;;
    open my $ps, &#39;-|&#39;, &#39;ps&#39;;
    print scalar &lt;$ps&gt;;
    my @fields = qw( pid tt stat time command );
    while (&lt;$ps&gt;) {
        my %process;
        @process{@fields} = unpack($PS_T, $_);
        for my $field ( @fields ) {
            print &quot;$field: &lt;$process{$field}&gt;\n&quot;;
        }
        print &#39;line=&#39;, pack($PS_T, @process{@fields} ), &quot;\n&quot;;
    }

We&#39;ve used a hash slice in order to easily handle the fields of each row.
Storing the keys in an array makes it easy to operate on them as a
group or loop over them with C&lt;for&gt;. It also avoids polluting the program
with global variables and using symbolic references.

=head2 How can I make a filehandle local to a subroutine? How do I pass filehandles between subroutines? How do I make an array of filehandles?
X&lt;filehandle, local&gt; X&lt;filehandle, passing&gt; X&lt;filehandle, reference&gt;

As of perl5.6, open() autovivifies file and directory handles
as references if you pass it an uninitialized scalar variable.
You can then pass these references just like any other scalar,
and use them in the place of named handles.

    open my    $fh, $file_name;

    open local $fh, $file_name;

    print $fh &quot;Hello World!\n&quot;;

    process_file( $fh );

If you like, you can store these filehandles in an array or a hash.
If you access them directly, they aren&#39;t simple scalars and you
need to give C&lt;print&gt; a little help by placing the filehandle
reference in braces. Perl can only figure it out on its own when
the filehandle reference is a simple scalar.

    my @fhs = ( $fh1, $fh2, $fh3 );

    for( $i = 0; $i &lt;= $#fhs; $i++ ) {
        print {$fhs[$i]} &quot;just another Perl answer, \n&quot;;
    }

Before perl5.6, you had to deal with various typeglob idioms
which you may see in older code.

    open FILE, &quot;&gt; $filename&quot;;
    process_typeglob(   *FILE );
    process_reference( \*FILE );

    sub process_typeglob  { local *FH = shift; print FH  &quot;Typeglob!&quot; }
    sub process_reference { local $fh = shift; print $fh &quot;Reference!&quot; }

If you want to create many anonymous handles, you should
check out the Symbol or IO::Handle modules.

=head2 How can I use a filehandle indirectly?
X&lt;filehandle, indirect&gt;

An indirect filehandle is the use of something other than a symbol
in a place that a filehandle is expected. Here are ways
to get indirect filehandles:

    $fh =   SOME_FH;       # bareword is strict-subs hostile
    $fh =  &quot;SOME_FH&quot;;      # strict-refs hostile; same package only
    $fh =  *SOME_FH;       # typeglob
    $fh = \*SOME_FH;       # ref to typeglob (bless-able)
    $fh =  *SOME_FH{IO};   # blessed IO::Handle from *SOME_FH typeglob

Or, you can use the C&lt;new&gt; method from one of the IO::* modules to
create an anonymous filehandle and store that in a scalar variable.

    use IO::Handle;                     # 5.004 or higher
    my $fh = IO::Handle-&gt;new();

Then use any of those as you would a normal filehandle. Anywhere that
Perl is expecting a filehandle, an indirect filehandle may be used
instead. An indirect filehandle is just a scalar variable that contains
a filehandle. Functions like C&lt;print&gt;, C&lt;open&gt;, C&lt;seek&gt;, or
the C&lt;&lt; &lt;FH&gt; &gt;&gt; diamond operator will accept either a named filehandle
or a scalar variable containing one:

    ($ifh, $ofh, $efh) = (*STDIN, *STDOUT, *STDERR);
    print $ofh &quot;Type it: &quot;;
    my $got = &lt;$ifh&gt;
    print $efh &quot;What was that: $got&quot;;

If you&#39;re passing a filehandle to a function, you can write
the function in two ways:

    sub accept_fh {
        my $fh = shift;
        print $fh &quot;Sending to indirect filehandle\n&quot;;
    }

Or it can localize a typeglob and use the filehandle directly:

    sub accept_fh {
        local *FH = shift;
        print  FH &quot;Sending to localized filehandle\n&quot;;
    }

Both styles work with either objects or typeglobs of real filehandles.
(They might also work with strings under some circumstances, but this
is risky.)

    accept_fh(*STDOUT);
    accept_fh($handle);

In the examples above, we assigned the filehandle to a scalar variable
before using it. That is because only simple scalar variables, not
expressions or subscripts of hashes or arrays, can be used with
built-ins like C&lt;print&gt;, C&lt;printf&gt;, or the diamond operator. Using
something other than a simple scalar variable as a filehandle is
illegal and won&#39;t even compile:

    my @fd = (*STDIN, *STDOUT, *STDERR);
    print $fd[1] &quot;Type it: &quot;;                           # WRONG
    my $got = &lt;$fd[0]&gt;                                  # WRONG
    print $fd[2] &quot;What was that: $got&quot;;                 # WRONG

With C&lt;print&gt; and C&lt;printf&gt;, you get around this by using a block and
an expression where you would place the filehandle:

    print  { $fd[1] } &quot;funny stuff\n&quot;;
    printf { $fd[1] } &quot;Pity the poor %x.\n&quot;, 3_735_928_559;
    # Pity the poor deadbeef.

That block is a proper block like any other, so you can put more
complicated code there. This sends the message out to one of two places:

    my $ok = -x &quot;/bin/cat&quot;;
    print { $ok ? $fd[1] : $fd[2] } &quot;cat stat $ok\n&quot;;
    print { $fd[ 1+ ($ok || 0) ]  } &quot;cat stat $ok\n&quot;;

This approach of treating C&lt;print&gt; and C&lt;printf&gt; like object methods
calls doesn&#39;t work for the diamond operator. That&#39;s because it&#39;s a
real operator, not just a function with a comma-less argument. Assuming
you&#39;ve been storing typeglobs in your structure as we did above, you
can use the built-in function named C&lt;readline&gt; to read a record just
as C&lt;&lt; &lt;&gt; &gt;&gt; does. Given the initialization shown above for @fd, this
would work, but only because readline() requires a typeglob. It doesn&#39;t
work with objects or strings, which might be a bug we haven&#39;t fixed yet.

    $got = readline($fd[0]);

Let it be noted that the flakiness of indirect filehandles is not
related to whether they&#39;re strings, typeglobs, objects, or anything else.
It&#39;s the syntax of the fundamental operators. Playing the object
game doesn&#39;t help you at all here.

=head2 How can I set up a footer format to be used with write()?
X&lt;footer&gt;

There&#39;s no builtin way to do this, but L&lt;perlform&gt; has a couple of
techniques to make it possible for the intrepid hacker.

=head2 How can I write() into a string?
X&lt;write, into a string&gt;

(contributed by brian d foy)

If you want to C&lt;write&gt; into a string, you just have to &lt;open&gt; a
filehandle to a string, which Perl has been able to do since Perl 5.6:

    open FH, &#39;&gt;&#39;, \my $string;
    write( FH );

Since you want to be a good programmer, you probably want to use a lexical
filehandle, even though formats are designed to work with bareword filehandles
since the default format names take the filehandle name. However, you can
control this with some Perl special per-filehandle variables: C&lt;$^&gt;, which
names the top-of-page format, and C&lt;$~&gt; which shows the line format. You have
to change the default filehandle to set these variables:

    open my($fh), &#39;&gt;&#39;, \my $string;

    { # set per-filehandle variables
        my $old_fh = select( $fh );
        $~ = &#39;ANIMAL&#39;;
        $^ = &#39;ANIMAL_TOP&#39;;
        select( $old_fh );
    }

    format ANIMAL_TOP =
     ID  Type    Name
    .

    format ANIMAL =
    @##   @&lt;&lt;&lt;    @&lt;&lt;&lt;&lt;&lt;&lt;&lt;&lt;&lt;&lt;&lt;&lt;&lt;&lt;
    $id,  $type,  $name
    .

Although write can work with lexical or package variables, whatever variables
you use have to scope in the format. That most likely means you&#39;ll want to
localize some package variables:

    {
        local( $id, $type, $name ) = qw( 12 cat Buster );
        write( $fh );
    }

    print $string;

There are also some tricks that you can play with C&lt;formline&gt; and the
accumulator variable C&lt;$^A&gt;, but you lose a lot of the value of formats
since C&lt;formline&gt; won&#39;t handle paging and so on. You end up reimplementing
formats when you use them.

=head2 How can I open a filehandle to a string?
X&lt;string&gt; X&lt;open&gt; X&lt;IO::String&gt; X&lt;filehandle&gt;

(contributed by Peter J. Holzer, hjp-usenet2@hjp.at)

Since Perl 5.8.0 a file handle referring to a string can be created by
calling open with a reference to that string instead of the filename.
This file handle can then be used to read from or write to the string:

    open(my $fh, &#39;&gt;&#39;, \$string) or die &quot;Could not open string for writing&quot;;
    print $fh &quot;foo\n&quot;;
    print $fh &quot;bar\n&quot;;    # $string now contains &quot;foo\nbar\n&quot;

    open(my $fh, &#39;&lt;&#39;, \$string) or die &quot;Could not open string for reading&quot;;
    my $x = &lt;$fh&gt;;    # $x now contains &quot;foo\n&quot;

With older versions of Perl, the L&lt;IO::String&gt; module provides similar
functionality.

=head2 How can I output my numbers with commas added?
X&lt;number, commify&gt;

(contributed by brian d foy and Benjamin Goldberg)

You can use L&lt;Number::Format&gt; to separate places in a number.
It handles locale information for those of you who want to insert
full stops instead (or anything else that they want to use,
really).

This subroutine will add commas to your number:

    sub commify {
        local $_  = shift;
        1 while s/^([-+]?\d+)(\d{3})/$1,$2/;
        return $_;
    }

This regex from Benjamin Goldberg will add commas to numbers:

    s/(^[-+]?\d+?(?=(?&gt;(?:\d{3})+)(?!\d))|\G\d{3}(?=\d))/$1,/g;

It is easier to see with comments:

    s/(
        ^[-+]?             # beginning of number.
        \d+?               # first digits before first comma
        (?=                # followed by, (but not included in the match) :
            (?&gt;(?:\d{3})+) # some positive multiple of three digits.
            (?!\d)         # an *exact* multiple, not x * 3 + 1 or whatever.
        )
        |                  # or:
        \G\d{3}            # after the last group, get three digits
        (?=\d)             # but they have to have more digits after them.
    )/$1,/xg;

=head2 How can I translate tildes (~) in a filename?
X&lt;tilde&gt; X&lt;tilde expansion&gt;

Use the E&lt;lt&gt;E&lt;gt&gt; (C&lt;glob()&gt;) operator, documented in L&lt;perlfunc&gt;.
Versions of Perl older than 5.6 require that you have a shell
installed that groks tildes. Later versions of Perl have this feature
built in. The L&lt;File::KGlob&gt; module (available from CPAN) gives more
portable glob functionality.

Within Perl, you may use this directly:

    $filename =~ s{
      ^ ~             # find a leading tilde
      (               # save this in $1
          [^/]        # a non-slash character
                *     # repeated 0 or more times (0 means me)
      )
    }{
      $1
          ? (getpwnam($1))[7]
          : ( $ENV{HOME} || $ENV{LOGDIR} )
    }ex;

=head2 How come when I open a file read-write it wipes it out?
X&lt;clobber&gt; X&lt;read-write&gt; X&lt;clobbering&gt; X&lt;truncate&gt; X&lt;truncating&gt;

Because you&#39;re using something like this, which truncates the file
I&lt;then&gt; gives you read-write access:

    open my $fh, &#39;+&gt;&#39;, &#39;/path/name&#39;; # WRONG (almost always)

Whoops. You should instead use this, which will fail if the file
doesn&#39;t exist:

    open my $fh, &#39;+&lt;&#39;, &#39;/path/name&#39;; # open for update

Using &quot;&gt;&quot; always clobbers or creates. Using &quot;&lt;&quot; never does
either. The &quot;+&quot; doesn&#39;t change this.

Here are examples of many kinds of file opens. Those using C&lt;sysopen&gt;
all assume that you&#39;ve pulled in the constants from L&lt;Fcntl&gt;:

    use Fcntl;

To open file for reading:

    open my $fh, &#39;&lt;&#39;, $path                               or die $!;
    sysopen my $fh, $path, O_RDONLY                       or die $!;

To open file for writing, create new file if needed or else truncate old file:

    open my $fh, &#39;&gt;&#39;, $path                               or die $!;
    sysopen my $fh, $path, O_WRONLY|O_TRUNC|O_CREAT       or die $!;
    sysopen my $fh, $path, O_WRONLY|O_TRUNC|O_CREAT, 0666 or die $!;

To open file for writing, create new file, file must not exist:

    sysopen my $fh, $path, O_WRONLY|O_EXCL|O_CREAT        or die $!;
    sysopen my $fh, $path, O_WRONLY|O_EXCL|O_CREAT, 0666  or die $!;

To open file for appending, create if necessary:

    open my $fh, &#39;&gt;&gt;&#39; $path                               or die $!;
    sysopen my $fh, $path, O_WRONLY|O_APPEND|O_CREAT      or die $!;
    sysopen my $fh, $path, O_WRONLY|O_APPEND|O_CREAT, 0666 or die $!;

To open file for appending, file must exist:

    sysopen my $fh, $path, O_WRONLY|O_APPEND              or die $!;

To open file for update, file must exist:

    open my $fh, &#39;+&lt;&#39;, $path                              or die $!;
    sysopen my $fh, $path, O_RDWR                         or die $!;

To open file for update, create file if necessary:

    sysopen my $fh, $path, O_RDWR|O_CREAT                 or die $!;
    sysopen my $fh, $path, O_RDWR|O_CREAT, 0666           or die $!;

To open file for update, file must not exist:

    sysopen my $fh, $path, O_RDWR|O_EXCL|O_CREAT          or die $!;
    sysopen my $fh, $path, O_RDWR|O_EXCL|O_CREAT, 0666    or die $!;

To open a file without blocking, creating if necessary:

    sysopen my $fh, &#39;/foo/somefile&#39;, O_WRONLY|O_NDELAY|O_CREAT
        or die &quot;can&#39;t open /foo/somefile: $!&quot;:

Be warned that neither creation nor deletion of files is guaranteed to
be an atomic operation over NFS. That is, two processes might both
successfully create or unlink the same file! Therefore O_EXCL
isn&#39;t as exclusive as you might wish.

See also L&lt;perlopentut&gt;.

=head2 Why do I sometimes get an &quot;Argument list too long&quot; when I use E&lt;lt&gt;*E&lt;gt&gt;?
X&lt;argument list too long&gt;

The C&lt;&lt; &lt;&gt; &gt;&gt; operator performs a globbing operation (see above).
In Perl versions earlier than v5.6.0, the internal glob() operator forks
csh(1) to do the actual glob expansion, but
csh can&#39;t handle more than 127 items and so gives the error message
C&lt;Argument list too long&gt;. People who installed tcsh as csh won&#39;t
have this problem, but their users may be surprised by it.

To get around this, either upgrade to Perl v5.6.0 or later, do the glob
yourself with readdir() and patterns, or use a module like L&lt;File::Glob&gt;,
one that doesn&#39;t use the shell to do globbing.

=head2 How can I open a file with a leading &quot;&gt;&quot; or trailing blanks?
X&lt;filename, special characters&gt;

(contributed by Brian McCauley)

The special two-argument form of Perl&#39;s open() function ignores
trailing blanks in filenames and infers the mode from certain leading
characters (or a trailing &quot;|&quot;). In older versions of Perl this was the
only version of open() and so it is prevalent in old code and books.

Unless you have a particular reason to use the two-argument form you
should use the three-argument form of open() which does not treat any
characters in the filename as special.

    open my $fh, &quot;&lt;&quot;, &quot;  file  &quot;;  # filename is &quot;   file   &quot;
    open my $fh, &quot;&gt;&quot;, &quot;&gt;file&quot;;     # filename is &quot;&gt;file&quot;

=head2 How can I reliably rename a file?
X&lt;rename&gt; X&lt;mv&gt; X&lt;move&gt; X&lt;file, rename&gt;

If your operating system supports a proper mv(1) utility or its
functional equivalent, this works:

    rename($old, $new) or system(&quot;mv&quot;, $old, $new);

It may be more portable to use the L&lt;File::Copy&gt; module instead.
You just copy to the new file to the new name (checking return
values), then delete the old one. This isn&#39;t really the same
semantically as a C&lt;rename()&gt;, which preserves meta-information like
permissions, timestamps, inode info, etc.

=head2 How can I lock a file?
X&lt;lock&gt; X&lt;file, lock&gt; X&lt;flock&gt;

Perl&#39;s builtin flock() function (see L&lt;perlfunc&gt; for details) will call
flock(2) if that exists, fcntl(2) if it doesn&#39;t (on perl version 5.004 and
later), and lockf(3) if neither of the two previous system calls exists.
On some systems, it may even use a different form of native locking.
Here are some gotchas with Perl&#39;s flock():

=over 4

=item 1

Produces a fatal error if none of the three system calls (or their
close equivalent) exists.

=item 2

lockf(3) does not provide shared locking, and requires that the
filehandle be open for writing (or appending, or read/writing).

=item 3

Some versions of flock() can&#39;t lock files over a network (e.g. on NFS file
systems), so you&#39;d need to force the use of fcntl(2) when you build Perl.
But even this is dubious at best. See the flock entry of L&lt;perlfunc&gt;
and the F&lt;INSTALL&gt; file in the source distribution for information on
building Perl to do this.

Two potentially non-obvious but traditional flock semantics are that
it waits indefinitely until the lock is granted, and that its locks are
I&lt;merely advisory&gt;. Such discretionary locks are more flexible, but
offer fewer guarantees. This means that files locked with flock() may
be modified by programs that do not also use flock(). Cars that stop
for red lights get on well with each other, but not with cars that don&#39;t
stop for red lights. See the perlport manpage, your port&#39;s specific
documentation, or your system-specific local manpages for details. It&#39;s
best to assume traditional behavior if you&#39;re writing portable programs.
(If you&#39;re not, you should as always feel perfectly free to write
for your own system&#39;s idiosyncrasies (sometimes called &quot;features&quot;).
Slavish adherence to portability concerns shouldn&#39;t get in the way of
your getting your job done.)

For more information on file locking, see also
L&lt;perlopentut/&quot;File Locking&quot;&gt; if you have it (new for 5.6).

=back

=head2 Why can&#39;t I just open(FH, &quot;E&lt;gt&gt;file.lock&quot;)?
X&lt;lock, lockfile race condition&gt;

A common bit of code B&lt;NOT TO USE&gt; is this:

    sleep(3) while -e &#39;file.lock&#39;;    # PLEASE DO NOT USE
    open my $lock, &#39;&gt;&#39;, &#39;file.lock&#39;; # THIS BROKEN CODE

This is a classic race condition: you take two steps to do something
which must be done in one. That&#39;s why computer hardware provides an
atomic test-and-set instruction. In theory, this &quot;ought&quot; to work:

    sysopen my $fh, &quot;file.lock&quot;, O_WRONLY|O_EXCL|O_CREAT
        or die &quot;can&#39;t open  file.lock: $!&quot;;

except that lamentably, file creation (and deletion) is not atomic
over NFS, so this won&#39;t work (at least, not every time) over the net.
Various schemes involving link() have been suggested, but
these tend to involve busy-wait, which is also less than desirable.

=head2 I still don&#39;t get locking. I just want to increment the number in the file. How can I do this?
X&lt;counter&gt; X&lt;file, counter&gt;

Didn&#39;t anyone ever tell you web-page hit counters were useless?
They don&#39;t count number of hits, they&#39;re a waste of time, and they serve
only to stroke the writer&#39;s vanity. It&#39;s better to pick a random number;
they&#39;re more realistic.

Anyway, this is what you can do if you can&#39;t help yourself.

    use Fcntl qw(:DEFAULT :flock);
    sysopen my $fh, &quot;numfile&quot;, O_RDWR|O_CREAT or die &quot;can&#39;t open numfile: $!&quot;;
    flock $fh, LOCK_EX                        or die &quot;can&#39;t flock numfile: $!&quot;;
    my $num = &lt;$fh&gt; || 0;
    seek $fh, 0, 0                            or die &quot;can&#39;t rewind numfile: $!&quot;;
    truncate $fh, 0                           or die &quot;can&#39;t truncate numfile: $!&quot;;
    (print $fh $num+1, &quot;\n&quot;)                  or die &quot;can&#39;t write numfile: $!&quot;;
    close $fh                                 or die &quot;can&#39;t close numfile: $!&quot;;

Here&#39;s a much better web-page hit counter:

    $hits = int( (time() - 850_000_000) / rand(1_000) );

If the count doesn&#39;t impress your friends, then the code might. :-)

=head2 All I want to do is append a small amount of text to the end of a file. Do I still have to use locking?
X&lt;append&gt; X&lt;file, append&gt;

If you are on a system that correctly implements C&lt;flock&gt; and you use
the example appending code from &quot;perldoc -f flock&quot; everything will be
OK even if the OS you are on doesn&#39;t implement append mode correctly
(if such a system exists). So if you are happy to restrict yourself to
OSs that implement C&lt;flock&gt; (and that&#39;s not really much of a
restriction) then that is what you should do.

If you know you are only going to use a system that does correctly
implement appending (i.e. not Win32) then you can omit the C&lt;seek&gt;
from the code in the previous answer.

If you know you are only writing code to run on an OS and filesystem
that does implement append mode correctly (a local filesystem on a
modern Unix for example), and you keep the file in block-buffered mode
and you write less than one buffer-full of output between each manual
flushing of the buffer then each bufferload is almost guaranteed to be
written to the end of the file in one chunk without getting
intermingled with anyone else&#39;s output. You can also use the
C&lt;syswrite&gt; function which is simply a wrapper around your system&#39;s
C&lt;write(2)&gt; system call.

There is still a small theoretical chance that a signal will interrupt
the system-level C&lt;write()&gt; operation before completion. There is also
a possibility that some STDIO implementations may call multiple system
level C&lt;write()&gt;s even if the buffer was empty to start. There may be
some systems where this probability is reduced to zero, and this is
not a concern when using C&lt;:perlio&gt; instead of your system&#39;s STDIO.

=head2 How do I randomly update a binary file?
X&lt;file, binary patch&gt;

If you&#39;re just trying to patch a binary, in many cases something as
simple as this works:

    perl -i -pe &#39;s{window manager}{window mangler}g&#39; /usr/bin/emacs

However, if you have fixed sized records, then you might do something more
like this:

    my $RECSIZE = 220; # size of record, in bytes
    my $recno   = 37;  # which record to update
    open my $fh, &#39;+&lt;&#39;, &#39;somewhere&#39; or die &quot;can&#39;t update somewhere: $!&quot;;
    seek $fh, $recno * $RECSIZE, 0;
    read $fh, $record, $RECSIZE == $RECSIZE or die &quot;can&#39;t read record $recno: $!&quot;;
    # munge the record
    seek $fh, -$RECSIZE, 1;
    print $fh $record;
    close $fh;

Locking and error checking are left as an exercise for the reader.
Don&#39;t forget them or you&#39;ll be quite sorry.

=head2 How do I get a file&#39;s timestamp in perl?
X&lt;timestamp&gt; X&lt;file, timestamp&gt;

If you want to retrieve the time at which the file was last read,
written, or had its meta-data (owner, etc) changed, you use the B&lt;-A&gt;,
B&lt;-M&gt;, or B&lt;-C&gt; file test operations as documented in L&lt;perlfunc&gt;.
These retrieve the age of the file (measured against the start-time of
your program) in days as a floating point number. Some platforms may
not have all of these times. See L&lt;perlport&gt; for details. To retrieve
the &quot;raw&quot; time in seconds since the epoch, you would call the stat
function, then use C&lt;localtime()&gt;, C&lt;gmtime()&gt;, or
C&lt;POSIX::strftime()&gt; to convert this into human-readable form.

Here&#39;s an example:

    my $write_secs = (stat($file))[9];
    printf &quot;file %s updated at %s\n&quot;, $file,
        scalar localtime($write_secs);

If you prefer something more legible, use the File::stat module
(part of the standard distribution in version 5.004 and later):

    # error checking left as an exercise for reader.
    use File::stat;
    use Time::localtime;
    my $date_string = ctime(stat($file)-&gt;mtime);
    print &quot;file $file updated at $date_string\n&quot;;

The POSIX::strftime() approach has the benefit of being,
in theory, independent of the current locale. See L&lt;perllocale&gt;
for details.

=head2 How do I set a file&#39;s timestamp in perl?
X&lt;timestamp&gt; X&lt;file, timestamp&gt;

You use the utime() function documented in L&lt;perlfunc/utime&gt;.
By way of example, here&#39;s a little program that copies the
read and write times from its first argument to all the rest
of them.

    if (@ARGV &lt; 2) {
        die &quot;usage: cptimes timestamp_file other_files ...\n&quot;;
    }
    my $timestamp = shift;
    my($atime, $mtime) = (stat($timestamp))[8,9];
    utime $atime, $mtime, @ARGV;

Error checking is, as usual, left as an exercise for the reader.

The perldoc for utime also has an example that has the same
effect as touch(1) on files that I&lt;already exist&gt;.

Certain file systems have a limited ability to store the times
on a file at the expected level of precision. For example, the
FAT and HPFS filesystem are unable to create dates on files with
a finer granularity than two seconds. This is a limitation of
the filesystems, not of utime().

=head2 How do I print to more than one file at once?
X&lt;print, to multiple files&gt;

To connect one filehandle to several output filehandles,
you can use the L&lt;IO::Tee&gt; or L&lt;Tie::FileHandle::Multiplex&gt; modules.

If you only have to do this once, you can print individually
to each filehandle.

    for my $fh ($fh1, $fh2, $fh3) { print $fh &quot;whatever\n&quot; }

=head2 How can I read in an entire file all at once?
X&lt;slurp&gt; X&lt;file, slurping&gt;

The customary Perl approach for processing all the lines in a file is to
do so one line at a time:

    open my $input, &#39;&lt;&#39;, $file or die &quot;can&#39;t open $file: $!&quot;;
    while (&lt;$input&gt;) {
        chomp;
        # do something with $_
    }
    close $input or die &quot;can&#39;t close $file: $!&quot;;

This is tremendously more efficient than reading the entire file into
memory as an array of lines and then processing it one element at a time,
which is often--if not almost always--the wrong approach. Whenever
you see someone do this:

    my @lines = &lt;INPUT&gt;;

You should think long and hard about why you need everything loaded at
once. It&#39;s just not a scalable solution.

If you &quot;mmap&quot; the file with the File::Map module from
CPAN, you can virtually load the entire file into a
string without actually storing it in memory:

    use File::Map qw(map_file);

    map_file my $string, $filename;

Once mapped, you can treat C&lt;$string&gt; as you would any other string.
Since you don&#39;t necessarily have to load the data, mmap-ing can be
very fast and may not increase your memory footprint.

You might also find it more
fun to use the standard L&lt;Tie::File&gt; module, or the L&lt;DB_File&gt; module&#39;s
C&lt;$DB_RECNO&gt; bindings, which allow you to tie an array to a file so that
accessing an element of the array actually accesses the corresponding
line in the file.

If you want to load the entire file, you can use the L&lt;Path::Tiny&gt;
module to do it in one simple and efficient step:

    use Path::Tiny;

    my $all_of_it = path($filename)-&gt;slurp; # entire file in scalar
    my @all_lines = path($filename)-&gt;lines; # one line per element

Or you can read the entire file contents into a scalar like this:

    my $var;
    {
        local $/;
        open my $fh, &#39;&lt;&#39;, $file or die &quot;can&#39;t open $file: $!&quot;;
        $var = &lt;$fh&gt;;
    }

That temporarily undefs your record separator, and will automatically
close the file at block exit. If the file is already open, just use this:

    my $var = do { local $/; &lt;$fh&gt; };

You can also use a localized C&lt;@ARGV&gt; to eliminate the C&lt;open&gt;:

    my $var = do { local( @ARGV, $/ ) = $file; &lt;&gt; };

For ordinary files you can also use the C&lt;read&gt; function.

    read( $fh, $var, -s $fh );

That third argument tests the byte size of the data on the C&lt;$fh&gt; filehandle
and reads that many bytes into the buffer C&lt;$var&gt;.

=head2 How can I read in a file by paragraphs?
X&lt;file, reading by paragraphs&gt;

Use the C&lt;$/&gt; variable (see L&lt;perlvar&gt; for details). You can either
set it to C&lt;&quot;&quot;&gt; to eliminate empty paragraphs (C&lt;&quot;abc\n\n\n\ndef&quot;&gt;,
for instance, gets treated as two paragraphs and not three), or
C&lt;&quot;\n\n&quot;&gt; to accept empty paragraphs.

Note that a blank line must have no blanks in it. Thus
S&lt;C&lt;&quot;fred\n \nstuff\n\n&quot;&gt;&gt; is one paragraph, but C&lt;&quot;fred\n\nstuff\n\n&quot;&gt; is two.

=head2 How can I read a single character from a file? From the keyboard?
X&lt;getc&gt; X&lt;file, reading one character at a time&gt;

You can use the builtin C&lt;getc()&gt; function for most filehandles, but
it won&#39;t (easily) work on a terminal device. For STDIN, either use
the Term::ReadKey module from CPAN or use the sample code in
L&lt;perlfunc/getc&gt;.

If your system supports the portable operating system programming
interface (POSIX), you can use the following code, which you&#39;ll note
turns off echo processing as well.

    #!/usr/bin/perl -w
    use strict;
    $| = 1;
    for (1..4) {
        print &quot;gimme: &quot;;
        my $got = getone();
        print &quot;--&gt; $got\n&quot;;
    }
    exit;

    BEGIN {
        use POSIX qw(:termios_h);

        my ($term, $oterm, $echo, $noecho, $fd_stdin);

        my $fd_stdin = fileno(STDIN);

        $term     = POSIX::Termios-&gt;new();
        $term-&gt;getattr($fd_stdin);
        $oterm     = $term-&gt;getlflag();

        $echo     = ECHO | ECHOK | ICANON;
        $noecho   = $oterm &amp; ~$echo;

        sub cbreak {
            $term-&gt;setlflag($noecho);
            $term-&gt;setcc(VTIME, 1);
            $term-&gt;setattr($fd_stdin, TCSANOW);
        }

        sub cooked {
            $term-&gt;setlflag($oterm);
            $term-&gt;setcc(VTIME, 0);
            $term-&gt;setattr($fd_stdin, TCSANOW);
        }

        sub getone {
            my $key = &#39;&#39;;
            cbreak();
            sysread(STDIN, $key, 1);
            cooked();
            return $key;
        }
    }

    END { cooked() }

The Term::ReadKey module from CPAN may be easier to use. Recent versions
include also support for non-portable systems as well.

    use Term::ReadKey;
    open my $tty, &#39;&lt;&#39;, &#39;/dev/tty&#39;;
    print &quot;Gimme a char: &quot;;
    ReadMode &quot;raw&quot;;
    my $key = ReadKey 0, $tty;
    ReadMode &quot;normal&quot;;
    printf &quot;\nYou said %s, char number %03d\n&quot;,
        $key, ord $key;

=head2 How can I tell whether there&#39;s a character waiting on a filehandle?

The very first thing you should do is look into getting the Term::ReadKey
extension from CPAN. As we mentioned earlier, it now even has limited
support for non-portable (read: not open systems, closed, proprietary,
not POSIX, not Unix, etc.) systems.

You should also check out the Frequently Asked Questions list in
comp.unix.* for things like this: the answer is essentially the same.
It&#39;s very system-dependent. Here&#39;s one solution that works on BSD
systems:

    sub key_ready {
        my($rin, $nfd);
        vec($rin, fileno(STDIN), 1) = 1;
        return $nfd = select($rin,undef,undef,0);
    }

If you want to find out how many characters are waiting, there&#39;s
also the FIONREAD ioctl call to be looked at. The I&lt;h2ph&gt; tool that
comes with Perl tries to convert C include files to Perl code, which
can be C&lt;require&gt;d. FIONREAD ends up defined as a function in the
I&lt;sys/ioctl.ph&gt; file:

    require &#39;./sys/ioctl.ph&#39;;

    $size = pack(&quot;L&quot;, 0);
    ioctl(FH, FIONREAD(), $size)    or die &quot;Couldn&#39;t call ioctl: $!\n&quot;;
    $size = unpack(&quot;L&quot;, $size);

If I&lt;h2ph&gt; wasn&#39;t installed or doesn&#39;t work for you, you can
I&lt;grep&gt; the include files by hand:

    % grep FIONREAD /usr/include/*/*
    /usr/include/asm/ioctls.h:#define FIONREAD      0x541B

Or write a small C program using the editor of champions:

    % cat &gt; fionread.c
    #include &lt;sys/ioctl.h&gt;
    main() {
        printf(&quot;%#08x\n&quot;, FIONREAD);
    }
    ^D
    % cc -o fionread fionread.c
    % ./fionread
    0x4004667f

And then hard-code it, leaving porting as an exercise to your successor.

    $FIONREAD = 0x4004667f;         # XXX: opsys dependent

    $size = pack(&quot;L&quot;, 0);
    ioctl(FH, $FIONREAD, $size)     or die &quot;Couldn&#39;t call ioctl: $!\n&quot;;
    $size = unpack(&quot;L&quot;, $size);

FIONREAD requires a filehandle connected to a stream, meaning that sockets,
pipes, and tty devices work, but I&lt;not&gt; files.

=head2 How do I do a C&lt;tail -f&gt; in perl?
X&lt;tail&gt; X&lt;IO::Handle&gt; X&lt;File::Tail&gt; X&lt;clearerr&gt;

First try

    seek($gw_fh, 0, 1);

The statement C&lt;seek($gw_fh, 0, 1)&gt; doesn&#39;t change the current position,
but it does clear the end-of-file condition on the handle, so that the
next C&lt;&lt; &lt;$gw_fh&gt; &gt;&gt; makes Perl try again to read something.

If that doesn&#39;t work (it relies on features of your stdio implementation),
then you need something more like this:

    for (;;) {
      for ($curpos = tell($gw_fh); &lt;$gw_fh&gt;; $curpos =tell($gw_fh)) {
        # search for some stuff and put it into files
      }
      # sleep for a while
      seek($gw_fh, $curpos, 0);  # seek to where we had been
    }

If this still doesn&#39;t work, look into the C&lt;clearerr&gt; method
from L&lt;IO::Handle&gt;, which resets the error and end-of-file states
on the handle.

There&#39;s also a L&lt;File::Tail&gt; module from CPAN.

=head2 How do I dup() a filehandle in Perl?
X&lt;dup&gt;

If you check L&lt;perlfunc/open&gt;, you&#39;ll see that several of the ways
to call open() should do the trick. For example:

    open my $log, &#39;&gt;&gt;&#39;, &#39;/foo/logfile&#39;;
    open STDERR, &#39;&gt;&amp;&#39;, $log;

Or even with a literal numeric descriptor:

    my $fd = $ENV{MHCONTEXTFD};
    open $mhcontext, &quot;&lt;&amp;=$fd&quot;;  # like fdopen(3S)

Note that &quot;&lt;&amp;STDIN&quot; makes a copy, but &quot;&lt;&amp;=STDIN&quot; makes
an alias. That means if you close an aliased handle, all
aliases become inaccessible. This is not true with
a copied one.

Error checking, as always, has been left as an exercise for the reader.

=head2 How do I close a file descriptor by number?
X&lt;file, closing file descriptors&gt; X&lt;POSIX&gt; X&lt;close&gt;

If, for some reason, you have a file descriptor instead of a
filehandle (perhaps you used C&lt;POSIX::open&gt;), you can use the
C&lt;close()&gt; function from the L&lt;POSIX&gt; module:

    use POSIX ();

    POSIX::close( $fd );

This should rarely be necessary, as the Perl C&lt;close()&gt; function is to be
used for things that Perl opened itself, even if it was a dup of a
numeric descriptor as with C&lt;MHCONTEXT&gt; above. But if you really have
to, you may be able to do this:

    require &#39;./sys/syscall.ph&#39;;
    my $rc = syscall(SYS_close(), $fd + 0);  # must force numeric
    die &quot;can&#39;t sysclose $fd: $!&quot; unless $rc == -1;

Or, just use the fdopen(3S) feature of C&lt;open()&gt;:

    {
        open my $fh, &quot;&lt;&amp;=$fd&quot; or die &quot;Cannot reopen fd=$fd: $!&quot;;
        close $fh;
    }

=head2 Why can&#39;t I use &quot;C:\temp\foo&quot; in DOS paths? Why doesn&#39;t `C:\temp\foo.exe` work?
X&lt;filename, DOS issues&gt;

Whoops!  You just put a tab and a formfeed into that filename!
Remember that within double quoted strings (&quot;like\this&quot;), the
backslash is an escape character. The full list of these is in
L&lt;perlop/Quote and Quote-like Operators&gt;. Unsurprisingly, you don&#39;t
have a file called &quot;c:(tab)emp(formfeed)oo&quot; or
&quot;c:(tab)emp(formfeed)oo.exe&quot; on your legacy DOS filesystem.

Either single-quote your strings, or (preferably) use forward slashes.
Since all DOS and Windows versions since something like MS-DOS 2.0 or so
have treated C&lt;/&gt; and C&lt;\&gt; the same in a path, you might as well use the
one that doesn&#39;t clash with Perl--or the POSIX shell, ANSI C and C++,
awk, Tcl, Java, or Python, just to mention a few. POSIX paths
are more portable, too.

=head2 Why doesn&#39;t glob(&quot;*.*&quot;) get all the files?
X&lt;glob&gt;

Because even on non-Unix ports, Perl&#39;s glob function follows standard
Unix globbing semantics. You&#39;ll need C&lt;glob(&quot;*&quot;)&gt; to get all (non-hidden)
files. This makes glob() portable even to legacy systems. Your
port may include proprietary globbing functions as well. Check its
documentation for details.

=head2 Why does Perl let me delete read-only files? Why does C&lt;-i&gt; clobber protected files? Isn&#39;t this a bug in Perl?

This is elaborately and painstakingly described in the
F&lt;file-dir-perms&gt; article in the &quot;Far More Than You Ever Wanted To
Know&quot; collection in L&lt;http://www.cpan.org/misc/olddoc/FMTEYEWTK.tgz&gt; .

The executive summary: learn how your filesystem works. The
permissions on a file say what can happen to the data in that file.
The permissions on a directory say what can happen to the list of
files in that directory. If you delete a file, you&#39;re removing its
name from the directory (so the operation depends on the permissions
of the directory, not of the file). If you try to write to the file,
the permissions of the file govern whether you&#39;re allowed to.

=head2 How do I select a random line from a file?
X&lt;file, selecting a random line&gt;

Short of loading the file into a database or pre-indexing the lines in
the file, there are a couple of things that you can do.

Here&#39;s a reservoir-sampling algorithm from the Camel Book:

    srand;
    rand($.) &lt; 1 &amp;&amp; ($line = $_) while &lt;&gt;;

This has a significant advantage in space over reading the whole file
in. You can find a proof of this method in I&lt;The Art of Computer
Programming&gt;, Volume 2, Section 3.4.2, by Donald E. Knuth.

You can use the L&lt;File::Random&gt; module which provides a function
for that algorithm:

    use File::Random qw/random_line/;
    my $line = random_line($filename);

Another way is to use the L&lt;Tie::File&gt; module, which treats the entire
file as an array. Simply access a random array element.

=head2 Why do I get weird spaces when I print an array of lines?

(contributed by brian d foy)

If you are seeing spaces between the elements of your array when
you print the array, you are probably interpolating the array in
double quotes:

    my @animals = qw(camel llama alpaca vicuna);
    print &quot;animals are: @animals\n&quot;;

It&#39;s the double quotes, not the C&lt;print&gt;, doing this. Whenever you
interpolate an array in a double quote context, Perl joins the
elements with spaces (or whatever is in C&lt;$&quot;&gt;, which is a space by
default):

    animals are: camel llama alpaca vicuna

This is different than printing the array without the interpolation:

    my @animals = qw(camel llama alpaca vicuna);
    print &quot;animals are: &quot;, @animals, &quot;\n&quot;;

Now the output doesn&#39;t have the spaces between the elements because
the elements of C&lt;@animals&gt; simply become part of the list to
C&lt;print&gt;:

    animals are: camelllamaalpacavicuna

You might notice this when each of the elements of C&lt;@array&gt; end with
a newline. You expect to print one element per line, but notice that
every line after the first is indented:

    this is a line
     this is another line
     this is the third line

That extra space comes from the interpolation of the array. If you
don&#39;t want to put anything between your array elements, don&#39;t use the
array in double quotes. You can send it to print without them:

    print @lines;

=head2 How do I traverse a directory tree?

(contributed by brian d foy)

The L&lt;File::Find&gt; module, which comes with Perl, does all of the hard
work to traverse a directory structure. It comes with Perl. You simply
call the C&lt;find&gt; subroutine with a callback subroutine and the
directories you want to traverse:

    use File::Find;

    find( \&amp;wanted, @directories );

    sub wanted {
        # full path in $File::Find::name
        # just filename in $_
        ... do whatever you want to do ...
    }

The L&lt;File::Find::Closures&gt;, which you can download from CPAN, provides
many ready-to-use subroutines that you can use with L&lt;File::Find&gt;.

The L&lt;File::Finder&gt;, which you can download from CPAN, can help you
create the callback subroutine using something closer to the syntax of
the C&lt;find&gt; command-line utility:

    use File::Find;
    use File::Finder;

    my $deep_dirs = File::Finder-&gt;depth-&gt;type(&#39;d&#39;)-&gt;ls-&gt;exec(&#39;rmdir&#39;,&#39;{}&#39;);

    find( $deep_dirs-&gt;as_options, @places );

The L&lt;File::Find::Rule&gt; module, which you can download from CPAN, has
a similar interface, but does the traversal for you too:

    use File::Find::Rule;

    my @files = File::Find::Rule-&gt;file()
                             -&gt;name( &#39;*.pm&#39; )
                             -&gt;in( @INC );

=head2 How do I delete a directory tree?

(contributed by brian d foy)

If you have an empty directory, you can use Perl&#39;s built-in C&lt;rmdir&gt;.
If the directory is not empty (so, no files or subdirectories), you
either have to empty it yourself (a lot of work) or use a module to
help you.

The L&lt;File::Path&gt; module, which comes with Perl, has a C&lt;remove_tree&gt;
which can take care of all of the hard work for you:

    use File::Path qw(remove_tree);

    remove_tree( @directories );

The L&lt;File::Path&gt; module also has a legacy interface to the older
C&lt;rmtree&gt; subroutine.

=head2 How do I copy an entire directory?

(contributed by Shlomi Fish)

To do the equivalent of C&lt;cp -R&gt; (i.e. copy an entire directory tree
recursively) in portable Perl, you&#39;ll either need to write something yourself
or find a good CPAN module such as  L&lt;File::Copy::Recursive&gt;.

=head1 AUTHOR AND COPYRIGHT

Copyright (c) 1997-2010 Tom Christiansen, Nathan Torkington, and
other authors as noted. All rights reserved.

This documentation is free; you can redistribute it and/or modify it
under the same terms as Perl itself.

Irrespective of its distribution, all code examples here are in the public
domain. You are permitted and encouraged to use this code and any
derivatives thereof in your own programs for fun or for profit as you
see fit. A simple comment in the code giving credit to the FAQ would
be courteous but is not required.
</pre>

                    </main>
                </div>

        </div>
        <footer>
        
        </footer>


    </body>
</html>

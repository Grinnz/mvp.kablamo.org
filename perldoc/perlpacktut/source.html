<!DOCTYPE html>
<html>
    <head>
        <meta charset="utf-8">
        <meta content="width=device-width, initial-scale=1" name="viewport">
        <link href="/theme/css/normalize.css" rel="stylesheet">
        <!--<link rel="stylesheet" href="/theme/css/skeleton.css" />-->
        <link href="/theme/css/statocles-default.css" rel="stylesheet">
        <link href="//maxcdn.bootstrapcdn.com/font-awesome/4.3.0/css/font-awesome.min.css" rel="stylesheet">
        <script crossorigin="anonymous" integrity="sha256-hwg4gsxgFZhOsEEamdOYGBf13FyQuiTwlAQgxVSNgt4=" src="http://code.jquery.com/jquery-3.2.1.min.js"></script>
        <title>perlpacktut (source) - Minimum Viable Perl</title>
        <meta content="Statocles 0.086" name="generator">
        
        <script>
            $( document ).ready(function() {
                $('.sub-menu ul').hide();
                $('.sub-menu.perldoc ul').show();
                $('.sub-menu.perldoc div').toggleClass("fa-caret-up fa-caret-down");
                $(".sub-menu a").click(function () {
                    $(this).parent(".sub-menu").siblings().children('ul').hide();
                    $(this).parent(".sub-menu").children("ul").slideToggle("100");
                    $(this).find(".right").toggleClass("fa-caret-up fa-caret-down");
                });
            });
        </script>
        <!-- Global site tag (gtag.js) - Google Analytics -->
        <script async src="https://www.googletagmanager.com/gtag/js?id=UA-110455056-1"></script>
        <script>
            window.dataLayer = window.dataLayer || [];
            function gtag(){dataLayer.push(arguments);}
            gtag('js', new Date());
            gtag('config', 'UA-110455056-1');
        </script>
    </head>
    <body>
        <header>
        </header>
        <div class="container">

                <div class="left column full-height">
                    <div class="sidebar">
                    
<a class="home" href="/">Minimum<br>Viable<br>Perl</a><br><br><br>

<div class="animated bounceInDown">
  <ul>
    <li class="basics sub-menu">
      <a href="/perldoc/perlpacktut/#basics">The basics<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/basics/hello">Hello world</a></li>
        <li><a href="/basics/variables">Variables</a></li>
        <li><a href="/basics/references">References</a></li>
        <li><a href="/basics/functions">Functions</a></li>
        <li><a href="/basics/flow">Conditionals</a></li>
        <li><a href="/basics/loops">Loops</a></li>
        <li><a href="/basics/exceptions">Exceptions</a></li>
        <li><a href="/basics/regexps">Regexps</a></li>
        <li><a href="/basics/files">Files</a></li>
      </ul>
    </li>
    <li class="oo sub-menu">
      <a href="/perldoc/perlpacktut/#oo">Object oriented programming<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/oo/story">The (weird) story of Perl OO</a></li>
        <li><a href="/oo/classes">Classes</a></li>
        <li><a href="/oo/attributes">Attributes</a></li>
        <li><a href="/oo/modifiers">Method modifiers</a></li>
        <li><a href="/oo/roles">Roles</a></li>
        <li><a href="/oo/inheritance">Inheritance</a></li>
      </ul>
    </li>
    <li class="debugging sub-menu">
      <a href="/perldoc/perlpacktut/#basics">Debugging<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/debugging/data-printer">Examining data structures</a></li>
        <li><a href="/debugging/stack-trace">Forcing stack traces</a></li>
      </ul>
    </li>
    <li class="dependencies sub-menu">
      <a href="/perldoc/perlpacktut/#dependencies">Perl libraries<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/dependencies/perl5lib">Loading libraries from custom locations</a></li>
        <li><a href="/dependencies/cpanm">Installing libraries with cpanm</a></li>
        <li><a href="/dependencies/metacpan">Choosing libraries from MetaCPAN</a></li>
        <li><a href="/dependencies/carton">Managing project dependencies with Carton</a></li>
        <li><a href="/dependencies/pmtools">What version is installed and where?</a></li>
      </ul>
    </li>
    <li class="testing sub-menu">
      <a href="/perldoc/perlpacktut/#testing">Testing<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/testing/intro">Introduction to testing</a></li>
        <li><a href="/testing/warnings">Testing warnings and exceptions</a></li>
        <li><a href="/testing/data">Testing data structures</a></li>
        <li><a href="/testing/mock">Testing with mock objects</a></li>
        <li><a href="/testing/libs">Build your own test libraries</a></li>
      </ul>
    </li>
    <li><a href="/perl">Perl design goals</a></li>
    <li><a href="/see-also">Book recommendations</a></li>
    <li><a href="/about">About this book</a></li>
  </ul>
</div>

                    
                    
                    </div>
                </div>

                <div class="right column">
                    <main>
                    <a class="button" href="/perldoc/perlpacktut/">
    Back to documentation
</a>
<pre>=head1 NAME

perlpacktut - tutorial on C&lt;pack&gt; and C&lt;unpack&gt;

=head1 DESCRIPTION

C&lt;pack&gt; and C&lt;unpack&gt; are two functions for transforming data according
to a user-defined template, between the guarded way Perl stores values
and some well-defined representation as might be required in the 
environment of a Perl program. Unfortunately, they&#39;re also two of 
the most misunderstood and most often overlooked functions that Perl
provides. This tutorial will demystify them for you.


=head1 The Basic Principle

Most programming languages don&#39;t shelter the memory where variables are
stored. In C, for instance, you can take the address of some variable,
and the C&lt;sizeof&gt; operator tells you how many bytes are allocated to
the variable. Using the address and the size, you may access the storage
to your heart&#39;s content.

In Perl, you just can&#39;t access memory at random, but the structural and
representational conversion provided by C&lt;pack&gt; and C&lt;unpack&gt; is an
excellent alternative. The C&lt;pack&gt; function converts values to a byte
sequence containing representations according to a given specification,
the so-called &quot;template&quot; argument. C&lt;unpack&gt; is the reverse process,
deriving some values from the contents of a string of bytes. (Be cautioned,
however, that not all that has been packed together can be neatly unpacked - 
a very common experience as seasoned travellers are likely to confirm.)

Why, you may ask, would you need a chunk of memory containing some values
in binary representation? One good reason is input and output accessing
some file, a device, or a network connection, whereby this binary
representation is either forced on you or will give you some benefit
in processing. Another cause is passing data to some system call that
is not available as a Perl function: C&lt;syscall&gt; requires you to provide
parameters stored in the way it happens in a C program. Even text processing 
(as shown in the next section) may be simplified with judicious usage 
of these two functions.

To see how (un)packing works, we&#39;ll start with a simple template
code where the conversion is in low gear: between the contents of a byte
sequence and a string of hexadecimal digits. Let&#39;s use C&lt;unpack&gt;, since
this is likely to remind you of a dump program, or some desperate last
message unfortunate programs are wont to throw at you before they expire
into the wild blue yonder. Assuming that the variable C&lt;$mem&gt; holds a 
sequence of bytes that we&#39;d like to inspect without assuming anything 
about its meaning, we can write

   my( $hex ) = unpack( &#39;H*&#39;, $mem );
   print &quot;$hex\n&quot;;

whereupon we might see something like this, with each pair of hex digits
corresponding to a byte:

   41204d414e204120504c414e20412043414e414c2050414e414d41

What was in this chunk of memory? Numbers, characters, or a mixture of
both? Assuming that we&#39;re on a computer where ASCII (or some similar)
encoding is used: hexadecimal values in the range C&lt;0x40&gt; - C&lt;0x5A&gt;
indicate an uppercase letter, and C&lt;0x20&gt; encodes a space. So we might
assume it is a piece of text, which some are able to read like a tabloid;
but others will have to get hold of an ASCII table and relive that
firstgrader feeling. Not caring too much about which way to read this,
we note that C&lt;unpack&gt; with the template code C&lt;H&gt; converts the contents
of a sequence of bytes into the customary hexadecimal notation. Since
&quot;a sequence of&quot; is a pretty vague indication of quantity, C&lt;H&gt; has been
defined to convert just a single hexadecimal digit unless it is followed
by a repeat count. An asterisk for the repeat count means to use whatever
remains.

The inverse operation - packing byte contents from a string of hexadecimal
digits - is just as easily written. For instance:

   my $s = pack( &#39;H2&#39; x 10, 30..39 );
   print &quot;$s\n&quot;;

Since we feed a list of ten 2-digit hexadecimal strings to C&lt;pack&gt;, the
pack template should contain ten pack codes. If this is run on a computer
with ASCII character coding, it will print C&lt;0123456789&gt;.

=head1 Packing Text

Let&#39;s suppose you&#39;ve got to read in a data file like this:

    Date      |Description                | Income|Expenditure
    01/24/2001 Zed&#39;s Camel Emporium                    1147.99
    01/28/2001 Flea spray                                24.99
    01/29/2001 Camel rides to tourists      235.00

How do we do it? You might think first to use C&lt;split&gt;; however, since
C&lt;split&gt; collapses blank fields, you&#39;ll never know whether a record was
income or expenditure. Oops. Well, you could always use C&lt;substr&gt;:

    while (&lt;&gt;) { 
        my $date   = substr($_,  0, 11);
        my $desc   = substr($_, 12, 27);
        my $income = substr($_, 40,  7);
        my $expend = substr($_, 52,  7);
        ...
    }

It&#39;s not really a barrel of laughs, is it? In fact, it&#39;s worse than it
may seem; the eagle-eyed may notice that the first field should only be
10 characters wide, and the error has propagated right through the other
numbers - which we&#39;ve had to count by hand. So it&#39;s error-prone as well
as horribly unfriendly.

Or maybe we could use regular expressions:

    while (&lt;&gt;) { 
        my($date, $desc, $income, $expend) = 
            m|(\d\d/\d\d/\d{4}) (.{27}) (.{7})(.*)|;
        ...
    }

Urgh. Well, it&#39;s a bit better, but - well, would you want to maintain
that?

Hey, isn&#39;t Perl supposed to make this sort of thing easy? Well, it does,
if you use the right tools. C&lt;pack&gt; and C&lt;unpack&gt; are designed to help
you out when dealing with fixed-width data like the above. Let&#39;s have a
look at a solution with C&lt;unpack&gt;:

    while (&lt;&gt;) { 
        my($date, $desc, $income, $expend) = unpack(&quot;A10xA27xA7A*&quot;, $_);
        ...
    }

That looks a bit nicer; but we&#39;ve got to take apart that weird template.
Where did I pull that out of? 

OK, let&#39;s have a look at some of our data again; in fact, we&#39;ll include
the headers, and a handy ruler so we can keep track of where we are.

             1         2         3         4         5        
    1234567890123456789012345678901234567890123456789012345678
    Date      |Description                | Income|Expenditure
    01/28/2001 Flea spray                                24.99
    01/29/2001 Camel rides to tourists      235.00

From this, we can see that the date column stretches from column 1 to
column 10 - ten characters wide. The C&lt;pack&gt;-ese for &quot;character&quot; is
C&lt;A&gt;, and ten of them are C&lt;A10&gt;. So if we just wanted to extract the
dates, we could say this:

    my($date) = unpack(&quot;A10&quot;, $_);

OK, what&#39;s next? Between the date and the description is a blank column;
we want to skip over that. The C&lt;x&gt; template means &quot;skip forward&quot;, so we
want one of those. Next, we have another batch of characters, from 12 to
38. That&#39;s 27 more characters, hence C&lt;A27&gt;. (Don&#39;t make the fencepost
error - there are 27 characters between 12 and 38, not 26. Count &#39;em!)

Now we skip another character and pick up the next 7 characters:

    my($date,$description,$income) = unpack(&quot;A10xA27xA7&quot;, $_);

Now comes the clever bit. Lines in our ledger which are just income and
not expenditure might end at column 46. Hence, we don&#39;t want to tell our
C&lt;unpack&gt; pattern that we B&lt;need&gt; to find another 12 characters; we&#39;ll
just say &quot;if there&#39;s anything left, take it&quot;. As you might guess from
regular expressions, that&#39;s what the C&lt;*&gt; means: &quot;use everything
remaining&quot;.

=over 3

=item *

Be warned, though, that unlike regular expressions, if the C&lt;unpack&gt;
template doesn&#39;t match the incoming data, Perl will scream and die.

=back


Hence, putting it all together:

    my ($date, $description, $income, $expend) =
        unpack(&quot;A10xA27xA7xA*&quot;, $_);

Now, that&#39;s our data parsed. I suppose what we might want to do now is
total up our income and expenditure, and add another line to the end of
our ledger - in the same format - saying how much we&#39;ve brought in and
how much we&#39;ve spent:

    while (&lt;&gt;) {
        my ($date, $desc, $income, $expend) =
            unpack(&quot;A10xA27xA7xA*&quot;, $_);
        $tot_income += $income;
        $tot_expend += $expend;
    }

    $tot_income = sprintf(&quot;%.2f&quot;, $tot_income); # Get them into 
    $tot_expend = sprintf(&quot;%.2f&quot;, $tot_expend); # &quot;financial&quot; format

    $date = POSIX::strftime(&quot;%m/%d/%Y&quot;, localtime); 

    # OK, let&#39;s go:

    print pack(&quot;A10xA27xA7xA*&quot;, $date, &quot;Totals&quot;,
        $tot_income, $tot_expend);

Oh, hmm. That didn&#39;t quite work. Let&#39;s see what happened:

    01/24/2001 Zed&#39;s Camel Emporium                     1147.99
    01/28/2001 Flea spray                                 24.99
    01/29/2001 Camel rides to tourists     1235.00
    03/23/2001Totals                     1235.001172.98

OK, it&#39;s a start, but what happened to the spaces? We put C&lt;x&gt;, didn&#39;t
we? Shouldn&#39;t it skip forward? Let&#39;s look at what L&lt;perlfunc/pack&gt; says:

    x   A null byte.

Urgh. No wonder. There&#39;s a big difference between &quot;a null byte&quot;,
character zero, and &quot;a space&quot;, character 32. Perl&#39;s put something
between the date and the description - but unfortunately, we can&#39;t see
it! 

What we actually need to do is expand the width of the fields. The C&lt;A&gt;
format pads any non-existent characters with spaces, so we can use the
additional spaces to line up our fields, like this:

    print pack(&quot;A11 A28 A8 A*&quot;, $date, &quot;Totals&quot;,
        $tot_income, $tot_expend);

(Note that you can put spaces in the template to make it more readable,
but they don&#39;t translate to spaces in the output.) Here&#39;s what we got
this time:

    01/24/2001 Zed&#39;s Camel Emporium                     1147.99
    01/28/2001 Flea spray                                 24.99
    01/29/2001 Camel rides to tourists     1235.00
    03/23/2001 Totals                      1235.00 1172.98

That&#39;s a bit better, but we still have that last column which needs to
be moved further over. There&#39;s an easy way to fix this up:
unfortunately, we can&#39;t get C&lt;pack&gt; to right-justify our fields, but we
can get C&lt;sprintf&gt; to do it:

    $tot_income = sprintf(&quot;%.2f&quot;, $tot_income); 
    $tot_expend = sprintf(&quot;%12.2f&quot;, $tot_expend);
    $date = POSIX::strftime(&quot;%m/%d/%Y&quot;, localtime); 
    print pack(&quot;A11 A28 A8 A*&quot;, $date, &quot;Totals&quot;,
        $tot_income, $tot_expend);

This time we get the right answer:

    01/28/2001 Flea spray                                 24.99
    01/29/2001 Camel rides to tourists     1235.00
    03/23/2001 Totals                      1235.00      1172.98

So that&#39;s how we consume and produce fixed-width data. Let&#39;s recap what
we&#39;ve seen of C&lt;pack&gt; and C&lt;unpack&gt; so far:

=over 3

=item *

Use C&lt;pack&gt; to go from several pieces of data to one fixed-width
version; use C&lt;unpack&gt; to turn a fixed-width-format string into several
pieces of data. 

=item *

The pack format C&lt;A&gt; means &quot;any character&quot;; if you&#39;re C&lt;pack&gt;ing and
you&#39;ve run out of things to pack, C&lt;pack&gt; will fill the rest up with
spaces.

=item *

C&lt;x&gt; means &quot;skip a byte&quot; when C&lt;unpack&gt;ing; when C&lt;pack&gt;ing, it means
&quot;introduce a null byte&quot; - that&#39;s probably not what you mean if you&#39;re
dealing with plain text.

=item *

You can follow the formats with numbers to say how many characters
should be affected by that format: C&lt;A12&gt; means &quot;take 12 characters&quot;;
C&lt;x6&gt; means &quot;skip 6 bytes&quot; or &quot;character 0, 6 times&quot;.

=item *

Instead of a number, you can use C&lt;*&gt; to mean &quot;consume everything else
left&quot;. 

B&lt;Warning&gt;: when packing multiple pieces of data, C&lt;*&gt; only means
&quot;consume all of the current piece of data&quot;. That&#39;s to say

    pack(&quot;A*A*&quot;, $one, $two)

packs all of C&lt;$one&gt; into the first C&lt;A*&gt; and then all of C&lt;$two&gt; into
the second. This is a general principle: each format character
corresponds to one piece of data to be C&lt;pack&gt;ed.

=back



=head1 Packing Numbers

So much for textual data. Let&#39;s get onto the meaty stuff that C&lt;pack&gt;
and C&lt;unpack&gt; are best at: handling binary formats for numbers. There is,
of course, not just one binary format  - life would be too simple - but
Perl will do all the finicky labor for you.


=head2 Integers

Packing and unpacking numbers implies conversion to and from some
I&lt;specific&gt; binary representation. Leaving floating point numbers
aside for the moment, the salient properties of any such representation
are:

=over 4

=item *

the number of bytes used for storing the integer,

=item *

whether the contents are interpreted as a signed or unsigned number,

=item *

the byte ordering: whether the first byte is the least or most
significant byte (or: little-endian or big-endian, respectively).

=back

So, for instance, to pack 20302 to a signed 16 bit integer in your
computer&#39;s representation you write

   my $ps = pack( &#39;s&#39;, 20302 );

Again, the result is a string, now containing 2 bytes. If you print 
this string (which is, generally, not recommended) you might see
C&lt;ON&gt; or C&lt;NO&gt; (depending on your system&#39;s byte ordering) - or something
entirely different if your computer doesn&#39;t use ASCII character encoding.
Unpacking C&lt;$ps&gt; with the same template returns the original integer value:

   my( $s ) = unpack( &#39;s&#39;, $ps );

This is true for all numeric template codes. But don&#39;t expect miracles:
if the packed value exceeds the allotted byte capacity, high order bits
are silently discarded, and unpack certainly won&#39;t be able to pull them
back out of some magic hat. And, when you pack using a signed template
code such as C&lt;s&gt;, an excess value may result in the sign bit
getting set, and unpacking this will smartly return a negative value.

16 bits won&#39;t get you too far with integers, but there is C&lt;l&gt; and C&lt;L&gt;
for signed and unsigned 32-bit integers. And if this is not enough and
your system supports 64 bit integers you can push the limits much closer
to infinity with pack codes C&lt;q&gt; and C&lt;Q&gt;. A notable exception is provided
by pack codes C&lt;i&gt; and C&lt;I&gt; for signed and unsigned integers of the 
&quot;local custom&quot; variety: Such an integer will take up as many bytes as
a local C compiler returns for C&lt;sizeof(int)&gt;, but it&#39;ll use I&lt;at least&gt;
32 bits.

Each of the integer pack codes C&lt;sSlLqQ&gt; results in a fixed number of bytes,
no matter where you execute your program. This may be useful for some 
applications, but it does not provide for a portable way to pass data 
structures between Perl and C programs (bound to happen when you call 
XS extensions or the Perl function C&lt;syscall&gt;), or when you read or
write binary files. What you&#39;ll need in this case are template codes that
depend on what your local C compiler compiles when you code C&lt;short&gt; or
C&lt;unsigned long&gt;, for instance. These codes and their corresponding
byte lengths are shown in the table below.  Since the C standard leaves
much leeway with respect to the relative sizes of these data types, actual
values may vary, and that&#39;s why the values are given as expressions in
C and Perl. (If you&#39;d like to use values from C&lt;%Config&gt; in your program
you have to import it with C&lt;use Config&gt;.)

   signed unsigned  byte length in C   byte length in Perl       
     s!     S!      sizeof(short)      $Config{shortsize}
     i!     I!      sizeof(int)        $Config{intsize}
     l!     L!      sizeof(long)       $Config{longsize}
     q!     Q!      sizeof(long long)  $Config{longlongsize}

The C&lt;i!&gt; and C&lt;I!&gt; codes aren&#39;t different from C&lt;i&gt; and C&lt;I&gt;; they are
tolerated for completeness&#39; sake.


=head2 Unpacking a Stack Frame

Requesting a particular byte ordering may be necessary when you work with
binary data coming from some specific architecture whereas your program could
run on a totally different system. As an example, assume you have 24 bytes
containing a stack frame as it happens on an Intel 8086:

      +---------+        +----+----+               +---------+
 TOS: |   IP    |  TOS+4:| FL | FH | FLAGS  TOS+14:|   SI    |
      +---------+        +----+----+               +---------+
      |   CS    |        | AL | AH | AX            |   DI    |
      +---------+        +----+----+               +---------+
                         | BL | BH | BX            |   BP    |
                         +----+----+               +---------+
                         | CL | CH | CX            |   DS    |
                         +----+----+               +---------+
                         | DL | DH | DX            |   ES    |
                         +----+----+               +---------+

First, we note that this time-honored 16-bit CPU uses little-endian order,
and that&#39;s why the low order byte is stored at the lower address. To
unpack such a (unsigned) short we&#39;ll have to use code C&lt;v&gt;. A repeat
count unpacks all 12 shorts:

   my( $ip, $cs, $flags, $ax, $bx, $cd, $dx, $si, $di, $bp, $ds, $es ) =
     unpack( &#39;v12&#39;, $frame );

Alternatively, we could have used C&lt;C&gt; to unpack the individually
accessible byte registers FL, FH, AL, AH, etc.:

   my( $fl, $fh, $al, $ah, $bl, $bh, $cl, $ch, $dl, $dh ) =
     unpack( &#39;C10&#39;, substr( $frame, 4, 10 ) );

It would be nice if we could do this in one fell swoop: unpack a short,
back up a little, and then unpack 2 bytes. Since Perl I&lt;is&gt; nice, it
proffers the template code C&lt;X&gt; to back up one byte. Putting this all
together, we may now write:

   my( $ip, $cs,
       $flags,$fl,$fh,
       $ax,$al,$ah, $bx,$bl,$bh, $cx,$cl,$ch, $dx,$dl,$dh, 
       $si, $di, $bp, $ds, $es ) =
   unpack( &#39;v2&#39; . (&#39;vXXCC&#39; x 5) . &#39;v5&#39;, $frame );

(The clumsy construction of the template can be avoided - just read on!)  

We&#39;ve taken some pains to construct the template so that it matches
the contents of our frame buffer. Otherwise we&#39;d either get undefined values,
or C&lt;unpack&gt; could not unpack all. If C&lt;pack&gt; runs out of items, it will
supply null strings (which are coerced into zeroes whenever the pack code
says so).


=head2 How to Eat an Egg on a Net

The pack code for big-endian (high order byte at the lowest address) is
C&lt;n&gt; for 16 bit and C&lt;N&gt; for 32 bit integers. You use these codes
if you know that your data comes from a compliant architecture, but,
surprisingly enough, you should also use these pack codes if you
exchange binary data, across the network, with some system that you
know next to nothing about. The simple reason is that this
order has been chosen as the I&lt;network order&gt;, and all standard-fearing
programs ought to follow this convention. (This is, of course, a stern
backing for one of the Lilliputian parties and may well influence the
political development there.) So, if the protocol expects you to send
a message by sending the length first, followed by just so many bytes,
you could write:

   my $buf = pack( &#39;N&#39;, length( $msg ) ) . $msg;

or even:

   my $buf = pack( &#39;NA*&#39;, length( $msg ), $msg );

and pass C&lt;$buf&gt; to your send routine. Some protocols demand that the
count should include the length of the count itself: then just add 4
to the data length. (But make sure to read L&lt;/&quot;Lengths and Widths&quot;&gt; before
you really code this!)


=head2 Byte-order modifiers

In the previous sections we&#39;ve learned how to use C&lt;n&gt;, C&lt;N&gt;, C&lt;v&gt; and
C&lt;V&gt; to pack and unpack integers with big- or little-endian byte-order.
While this is nice, it&#39;s still rather limited because it leaves out all
kinds of signed integers as well as 64-bit integers. For example, if you
wanted to unpack a sequence of signed big-endian 16-bit integers in a
platform-independent way, you would have to write:

   my @data = unpack &#39;s*&#39;, pack &#39;S*&#39;, unpack &#39;n*&#39;, $buf;

This is ugly. As of Perl 5.9.2, there&#39;s a much nicer way to express your
desire for a certain byte-order: the C&lt;E&lt;gt&gt;&gt; and C&lt;E&lt;lt&gt;&gt; modifiers.
C&lt;E&lt;gt&gt;&gt; is the big-endian modifier, while C&lt;E&lt;lt&gt;&gt; is the little-endian
modifier. Using them, we could rewrite the above code as:

   my @data = unpack &#39;s&gt;*&#39;, $buf;

As you can see, the &quot;big end&quot; of the arrow touches the C&lt;s&gt;, which is a
nice way to remember that C&lt;E&lt;gt&gt;&gt; is the big-endian modifier. The same
obviously works for C&lt;E&lt;lt&gt;&gt;, where the &quot;little end&quot; touches the code.

You will probably find these modifiers even more useful if you have
to deal with big- or little-endian C structures. Be sure to read
L&lt;/&quot;Packing and Unpacking C Structures&quot;&gt; for more on that.


=head2 Floating point Numbers

For packing floating point numbers you have the choice between the
pack codes C&lt;f&gt;, C&lt;d&gt;, C&lt;F&gt; and C&lt;D&gt;. C&lt;f&gt; and C&lt;d&gt; pack into (or unpack
from) single-precision or double-precision representation as it is provided
by your system. If your systems supports it, C&lt;D&gt; can be used to pack and
unpack (C&lt;long double&gt;) values, which can offer even more resolution
than C&lt;f&gt; or C&lt;d&gt;.  B&lt;Note that there are different long double formats.&gt;

C&lt;F&gt; packs an C&lt;NV&gt;, which is the floating point type used by Perl
internally.

There is no such thing as a network representation for reals, so if
you want to send your real numbers across computer boundaries, you&#39;d
better stick to text representation, possibly using the hexadecimal
float format (avoiding the decimal conversion loss), unless you&#39;re
absolutely sure what&#39;s on the other end of the line. For the even more
adventuresome, you can use the byte-order modifiers from the previous
section also on floating point codes.



=head1 Exotic Templates


=head2 Bit Strings

Bits are the atoms in the memory world. Access to individual bits may
have to be used either as a last resort or because it is the most
convenient way to handle your data. Bit string (un)packing converts
between strings containing a series of C&lt;0&gt; and C&lt;1&gt; characters and
a sequence of bytes each containing a group of 8 bits. This is almost
as simple as it sounds, except that there are two ways the contents of
a byte may be written as a bit string. Let&#39;s have a look at an annotated
byte:

     7 6 5 4 3 2 1 0
   +-----------------+
   | 1 0 0 0 1 1 0 0 |
   +-----------------+
    MSB           LSB

It&#39;s egg-eating all over again: Some think that as a bit string this should
be written &quot;10001100&quot; i.e. beginning with the most significant bit, others
insist on &quot;00110001&quot;. Well, Perl isn&#39;t biased, so that&#39;s why we have two bit
string codes:

   $byte = pack( &#39;B8&#39;, &#39;10001100&#39; ); # start with MSB
   $byte = pack( &#39;b8&#39;, &#39;00110001&#39; ); # start with LSB

It is not possible to pack or unpack bit fields - just integral bytes.
C&lt;pack&gt; always starts at the next byte boundary and &quot;rounds up&quot; to the
next multiple of 8 by adding zero bits as required. (If you do want bit
fields, there is L&lt;perlfunc/vec&gt;. Or you could implement bit field 
handling at the character string level, using split, substr, and
concatenation on unpacked bit strings.)

To illustrate unpacking for bit strings, we&#39;ll decompose a simple
status register (a &quot;-&quot; stands for a &quot;reserved&quot; bit):

   +-----------------+-----------------+
   | S Z - A - P - C | - - - - O D I T |
   +-----------------+-----------------+
    MSB           LSB MSB           LSB

Converting these two bytes to a string can be done with the unpack 
template C&lt;&#39;b16&#39;&gt;. To obtain the individual bit values from the bit
string we use C&lt;split&gt; with the &quot;empty&quot; separator pattern which dissects
into individual characters. Bit values from the &quot;reserved&quot; positions are
simply assigned to C&lt;undef&gt;, a convenient notation for &quot;I don&#39;t care where
this goes&quot;.

   ($carry, undef, $parity, undef, $auxcarry, undef, $zero, $sign,
    $trace, $interrupt, $direction, $overflow) =
      split( //, unpack( &#39;b16&#39;, $status ) );

We could have used an unpack template C&lt;&#39;b12&#39;&gt; just as well, since the
last 4 bits can be ignored anyway. 


=head2 Uuencoding

Another odd-man-out in the template alphabet is C&lt;u&gt;, which packs a
&quot;uuencoded string&quot;. (&quot;uu&quot; is short for Unix-to-Unix.) Chances are that
you won&#39;t ever need this encoding technique which was invented to overcome
the shortcomings of old-fashioned transmission mediums that do not support
other than simple ASCII data. The essential recipe is simple: Take three 
bytes, or 24 bits. Split them into 4 six-packs, adding a space (0x20) to 
each. Repeat until all of the data is blended. Fold groups of 4 bytes into 
lines no longer than 60 and garnish them in front with the original byte count 
(incremented by 0x20) and a C&lt;&quot;\n&quot;&gt; at the end. - The C&lt;pack&gt; chef will
prepare this for you, a la minute, when you select pack code C&lt;u&gt; on the menu:

   my $uubuf = pack( &#39;u&#39;, $bindat );

A repeat count after C&lt;u&gt; sets the number of bytes to put into an
uuencoded line, which is the maximum of 45 by default, but could be
set to some (smaller) integer multiple of three. C&lt;unpack&gt; simply ignores
the repeat count.


=head2 Doing Sums

An even stranger template code is C&lt;%&gt;E&lt;lt&gt;I&lt;number&gt;E&lt;gt&gt;. First, because 
it&#39;s used as a prefix to some other template code. Second, because it
cannot be used in C&lt;pack&gt; at all, and third, in C&lt;unpack&gt;, doesn&#39;t return the
data as defined by the template code it precedes. Instead it&#39;ll give you an
integer of I&lt;number&gt; bits that is computed from the data value by 
doing sums. For numeric unpack codes, no big feat is achieved:

    my $buf = pack( &#39;iii&#39;, 100, 20, 3 );
    print unpack( &#39;%32i3&#39;, $buf ), &quot;\n&quot;;  # prints 123

For string values, C&lt;%&gt; returns the sum of the byte values saving
you the trouble of a sum loop with C&lt;substr&gt; and C&lt;ord&gt;:

    print unpack( &#39;%32A*&#39;, &quot;\x01\x10&quot; ), &quot;\n&quot;;  # prints 17

Although the C&lt;%&gt; code is documented as returning a &quot;checksum&quot;:
don&#39;t put your trust in such values! Even when applied to a small number
of bytes, they won&#39;t guarantee a noticeable Hamming distance.

In connection with C&lt;b&gt; or C&lt;B&gt;, C&lt;%&gt; simply adds bits, and this can be put
to good use to count set bits efficiently:

    my $bitcount = unpack( &#39;%32b*&#39;, $mask );

And an even parity bit can be determined like this:

    my $evenparity = unpack( &#39;%1b*&#39;, $mask );


=head2  Unicode

Unicode is a character set that can represent most characters in most of
the world&#39;s languages, providing room for over one million different
characters. Unicode 3.1 specifies 94,140 characters: The Basic Latin
characters are assigned to the numbers 0 - 127. The Latin-1 Supplement with
characters that are used in several European languages is in the next
range, up to 255. After some more Latin extensions we find the character
sets from languages using non-Roman alphabets, interspersed with a
variety of symbol sets such as currency symbols, Zapf Dingbats or Braille.
(You might want to visit L&lt;http://www.unicode.org/&gt; for a look at some of
them - my personal favourites are Telugu and Kannada.)

The Unicode character sets associates characters with integers. Encoding
these numbers in an equal number of bytes would more than double the
requirements for storing texts written in Latin alphabets.
The UTF-8 encoding avoids this by storing the most common (from a western
point of view) characters in a single byte while encoding the rarer
ones in three or more bytes.

Perl uses UTF-8, internally, for most Unicode strings.

So what has this got to do with C&lt;pack&gt;? Well, if you want to compose a
Unicode string (that is internally encoded as UTF-8), you can do so by
using template code C&lt;U&gt;. As an example, let&#39;s produce the Euro currency
symbol (code number 0x20AC):

   $UTF8{Euro} = pack( &#39;U&#39;, 0x20AC );
   # Equivalent to: $UTF8{Euro} = &quot;\x{20ac}&quot;;

Inspecting C&lt;$UTF8{Euro}&gt; shows that it contains 3 bytes:
&quot;\xe2\x82\xac&quot;. However, it contains only 1 character, number 0x20AC.
The round trip can be completed with C&lt;unpack&gt;:

   $Unicode{Euro} = unpack( &#39;U&#39;, $UTF8{Euro} );

Unpacking using the C&lt;U&gt; template code also works on UTF-8 encoded byte
strings.

Usually you&#39;ll want to pack or unpack UTF-8 strings:

   # pack and unpack the Hebrew alphabet
   my $alefbet = pack( &#39;U*&#39;, 0x05d0..0x05ea );
   my @hebrew = unpack( &#39;U*&#39;, $utf );

Please note: in the general case, you&#39;re better off using
L&lt;C&lt;Encode::decode(&#39;UTF-8&#39;, $utf)&gt;|Encode/decode&gt; to decode a UTF-8
encoded byte string to a Perl Unicode string, and
L&lt;C&lt;Encode::encode(&#39;UTF-8&#39;, $str)&gt;|Encode/encode&gt; to encode a Perl Unicode
string to UTF-8 bytes. These functions provide means of handling invalid byte
sequences and generally have a friendlier interface.

=head2 Another Portable Binary Encoding

The pack code C&lt;w&gt; has been added to support a portable binary data
encoding scheme that goes way beyond simple integers. (Details can
be found at L&lt;http://Casbah.org/&gt;, the Scarab project.)  A BER (Binary Encoded
Representation) compressed unsigned integer stores base 128
digits, most significant digit first, with as few digits as possible.
Bit eight (the high bit) is set on each byte except the last. There
is no size limit to BER encoding, but Perl won&#39;t go to extremes.

   my $berbuf = pack( &#39;w*&#39;, 1, 128, 128+1, 128*128+127 );

A hex dump of C&lt;$berbuf&gt;, with spaces inserted at the right places,
shows 01 8100 8101 81807F. Since the last byte is always less than
128, C&lt;unpack&gt; knows where to stop.


=head1 Template Grouping

Prior to Perl 5.8, repetitions of templates had to be made by
C&lt;x&gt;-multiplication of template strings. Now there is a better way as
we may use the pack codes C&lt;(&gt; and C&lt;)&gt; combined with a repeat count.
The C&lt;unpack&gt; template from the Stack Frame example can simply
be written like this:

   unpack( &#39;v2 (vXXCC)5 v5&#39;, $frame )

Let&#39;s explore this feature a little more. We&#39;ll begin with the equivalent of

   join( &#39;&#39;, map( substr( $_, 0, 1 ), @str ) )

which returns a string consisting of the first character from each string.
Using pack, we can write

   pack( &#39;(A)&#39;.@str, @str )

or, because a repeat count C&lt;*&gt; means &quot;repeat as often as required&quot;,
simply

   pack( &#39;(A)*&#39;, @str )

(Note that the template C&lt;A*&gt; would only have packed C&lt;$str[0]&gt; in full
length.)

To pack dates stored as triplets ( day, month, year ) in an array C&lt;@dates&gt;
into a sequence of byte, byte, short integer we can write

   $pd = pack( &#39;(CCS)*&#39;, map( @$_, @dates ) );

To swap pairs of characters in a string (with even length) one could use
several techniques. First, let&#39;s use C&lt;x&gt; and C&lt;X&gt; to skip forward and back:

   $s = pack( &#39;(A)*&#39;, unpack( &#39;(xAXXAx)*&#39;, $s ) );

We can also use C&lt;@&gt; to jump to an offset, with 0 being the position where
we were when the last C&lt;(&gt; was encountered:

   $s = pack( &#39;(A)*&#39;, unpack( &#39;(@1A @0A @2)*&#39;, $s ) );

Finally, there is also an entirely different approach by unpacking big
endian shorts and packing them in the reverse byte order:

   $s = pack( &#39;(v)*&#39;, unpack( &#39;(n)*&#39;, $s );


=head1 Lengths and Widths

=head2 String Lengths

In the previous section we&#39;ve seen a network message that was constructed
by prefixing the binary message length to the actual message. You&#39;ll find
that packing a length followed by so many bytes of data is a 
frequently used recipe since appending a null byte won&#39;t work
if a null byte may be part of the data. Here is an example where both
techniques are used: after two null terminated strings with source and
destination address, a Short Message (to a mobile phone) is sent after
a length byte:

   my $msg = pack( &#39;Z*Z*CA*&#39;, $src, $dst, length( $sm ), $sm );

Unpacking this message can be done with the same template:

   ( $src, $dst, $len, $sm ) = unpack( &#39;Z*Z*CA*&#39;, $msg );

There&#39;s a subtle trap lurking in the offing: Adding another field after
the Short Message (in variable C&lt;$sm&gt;) is all right when packing, but this
cannot be unpacked naively:

   # pack a message
   my $msg = pack( &#39;Z*Z*CA*C&#39;, $src, $dst, length( $sm ), $sm, $prio );

   # unpack fails - $prio remains undefined!
   ( $src, $dst, $len, $sm, $prio ) = unpack( &#39;Z*Z*CA*C&#39;, $msg );

The pack code C&lt;A*&gt; gobbles up all remaining bytes, and C&lt;$prio&gt; remains
undefined! Before we let disappointment dampen the morale: Perl&#39;s got
the trump card to make this trick too, just a little further up the sleeve.
Watch this:

   # pack a message: ASCIIZ, ASCIIZ, length/string, byte
   my $msg = pack( &#39;Z* Z* C/A* C&#39;, $src, $dst, $sm, $prio );

   # unpack
   ( $src, $dst, $sm, $prio ) = unpack( &#39;Z* Z* C/A* C&#39;, $msg );

Combining two pack codes with a slash (C&lt;/&gt;) associates them with a single
value from the argument list. In C&lt;pack&gt;, the length of the argument is
taken and packed according to the first code while the argument itself
is added after being converted with the template code after the slash.
This saves us the trouble of inserting the C&lt;length&gt; call, but it is 
in C&lt;unpack&gt; where we really score: The value of the length byte marks the
end of the string to be taken from the buffer. Since this combination
doesn&#39;t make sense except when the second pack code isn&#39;t C&lt;a*&gt;, C&lt;A*&gt;
or C&lt;Z*&gt;, Perl won&#39;t let you.

The pack code preceding C&lt;/&gt; may be anything that&#39;s fit to represent a
number: All the numeric binary pack codes, and even text codes such as
C&lt;A4&gt; or C&lt;Z*&gt;:

   # pack/unpack a string preceded by its length in ASCII
   my $buf = pack( &#39;A4/A*&#39;, &quot;Humpty-Dumpty&quot; );
   # unpack $buf: &#39;13  Humpty-Dumpty&#39;
   my $txt = unpack( &#39;A4/A*&#39;, $buf );

C&lt;/&gt; is not implemented in Perls before 5.6, so if your code is required to
work on older Perls you&#39;ll need to C&lt;unpack( &#39;Z* Z* C&#39;)&gt; to get the length,
then use it to make a new unpack string. For example

   # pack a message: ASCIIZ, ASCIIZ, length, string, byte
   # (5.005 compatible)
   my $msg = pack( &#39;Z* Z* C A* C&#39;, $src, $dst, length $sm, $sm, $prio );

   # unpack
   ( undef, undef, $len) = unpack( &#39;Z* Z* C&#39;, $msg );
   ($src, $dst, $sm, $prio) = unpack ( &quot;Z* Z* x A$len C&quot;, $msg );

But that second C&lt;unpack&gt; is rushing ahead. It isn&#39;t using a simple literal
string for the template. So maybe we should introduce...

=head2 Dynamic Templates

So far, we&#39;ve seen literals used as templates. If the list of pack
items doesn&#39;t have fixed length, an expression constructing the
template is required (whenever, for some reason, C&lt;()*&gt; cannot be used).
Here&#39;s an example: To store named string values in a way that can be
conveniently parsed by a C program, we create a sequence of names and
null terminated ASCII strings, with C&lt;=&gt; between the name and the value,
followed by an additional delimiting null byte. Here&#39;s how:

   my $env = pack( &#39;(A*A*Z*)&#39; . keys( %Env ) . &#39;C&#39;,
                   map( { ( $_, &#39;=&#39;, $Env{$_} ) } keys( %Env ) ), 0 );

Let&#39;s examine the cogs of this byte mill, one by one. There&#39;s the C&lt;map&gt;
call, creating the items we intend to stuff into the C&lt;$env&gt; buffer:
to each key (in C&lt;$_&gt;) it adds the C&lt;=&gt; separator and the hash entry value.
Each triplet is packed with the template code sequence C&lt;A*A*Z*&gt; that
is repeated according to the number of keys. (Yes, that&#39;s what the C&lt;keys&gt;
function returns in scalar context.) To get the very last null byte,
we add a C&lt;0&gt; at the end of the C&lt;pack&gt; list, to be packed with C&lt;C&gt;.
(Attentive readers may have noticed that we could have omitted the 0.)

For the reverse operation, we&#39;ll have to determine the number of items
in the buffer before we can let C&lt;unpack&gt; rip it apart:

   my $n = $env =~ tr/\0// - 1;
   my %env = map( split( /=/, $_ ), unpack( &quot;(Z*)$n&quot;, $env ) );

The C&lt;tr&gt; counts the null bytes. The C&lt;unpack&gt; call returns a list of
name-value pairs each of which is taken apart in the C&lt;map&gt; block. 


=head2 Counting Repetitions

Rather than storing a sentinel at the end of a data item (or a list of items),
we could precede the data with a count. Again, we pack keys and values of
a hash, preceding each with an unsigned short length count, and up front
we store the number of pairs:

   my $env = pack( &#39;S(S/A* S/A*)*&#39;, scalar keys( %Env ), %Env );

This simplifies the reverse operation as the number of repetitions can be
unpacked with the C&lt;/&gt; code:

   my %env = unpack( &#39;S/(S/A* S/A*)&#39;, $env );

Note that this is one of the rare cases where you cannot use the same
template for C&lt;pack&gt; and C&lt;unpack&gt; because C&lt;pack&gt; can&#39;t determine
a repeat count for a C&lt;()&gt;-group.


=head2 Intel HEX

Intel HEX is a file format for representing binary data, mostly for
programming various chips, as a text file. (See
L&lt;http://en.wikipedia.org/wiki/.hex&gt; for a detailed description, and
L&lt;http://en.wikipedia.org/wiki/SREC_(file_format)&gt; for the Motorola
S-record format, which can be unravelled using the same technique.)
Each line begins with a colon (&#39;:&#39;) and is followed by a sequence of
hexadecimal characters, specifying a byte count I&lt;n&gt; (8 bit),
an address (16 bit, big endian), a record type (8 bit), I&lt;n&gt; data bytes
and a checksum (8 bit) computed as the least significant byte of the two&#39;s
complement sum of the preceding bytes. Example: C&lt;:0300300002337A1E&gt;.

The first step of processing such a line is the conversion, to binary,
of the hexadecimal data, to obtain the four fields, while checking the
checksum. No surprise here: we&#39;ll start with a simple C&lt;pack&gt; call to 
convert everything to binary:

   my $binrec = pack( &#39;H*&#39;, substr( $hexrec, 1 ) );

The resulting byte sequence is most convenient for checking the checksum.
Don&#39;t slow your program down with a for loop adding the C&lt;ord&gt; values
of this string&#39;s bytes - the C&lt;unpack&gt; code C&lt;%&gt; is the thing to use
for computing the 8-bit sum of all bytes, which must be equal to zero:

   die unless unpack( &quot;%8C*&quot;, $binrec ) == 0;

Finally, let&#39;s get those four fields. By now, you shouldn&#39;t have any
problems with the first three fields - but how can we use the byte count
of the data in the first field as a length for the data field? Here
the codes C&lt;x&gt; and C&lt;X&gt; come to the rescue, as they permit jumping
back and forth in the string to unpack.

   my( $addr, $type, $data ) = unpack( &quot;x n C X4 C x3 /a&quot;, $bin ); 

Code C&lt;x&gt; skips a byte, since we don&#39;t need the count yet. Code C&lt;n&gt; takes
care of the 16-bit big-endian integer address, and C&lt;C&gt; unpacks the
record type. Being at offset 4, where the data begins, we need the count.
C&lt;X4&gt; brings us back to square one, which is the byte at offset 0.
Now we pick up the count, and zoom forth to offset 4, where we are
now fully furnished to extract the exact number of data bytes, leaving
the trailing checksum byte alone.



=head1 Packing and Unpacking C Structures

In previous sections we have seen how to pack numbers and character
strings. If it were not for a couple of snags we could conclude this
section right away with the terse remark that C structures don&#39;t
contain anything else, and therefore you already know all there is to it.
Sorry, no: read on, please.

If you have to deal with a lot of C structures, and don&#39;t want to
hack all your template strings manually, you&#39;ll probably want to have
a look at the CPAN module C&lt;Convert::Binary::C&gt;. Not only can it parse
your C source directly, but it also has built-in support for all the
odds and ends described further on in this section.

=head2 The Alignment Pit

In the consideration of speed against memory requirements the balance
has been tilted in favor of faster execution. This has influenced the
way C compilers allocate memory for structures: On architectures
where a 16-bit or 32-bit operand can be moved faster between places in
memory, or to or from a CPU register, if it is aligned at an even or 
multiple-of-four or even at a multiple-of eight address, a C compiler
will give you this speed benefit by stuffing extra bytes into structures.
If you don&#39;t cross the C shoreline this is not likely to cause you any
grief (although you should care when you design large data structures,
or you want your code to be portable between architectures (you do want
that, don&#39;t you?)).

To see how this affects C&lt;pack&gt; and C&lt;unpack&gt;, we&#39;ll compare these two
C structures:

   typedef struct {
     char     c1;
     short    s;
     char     c2;
     long     l;
   } gappy_t;

   typedef struct {
     long     l;
     short    s;
     char     c1;
     char     c2;
   } dense_t;

Typically, a C compiler allocates 12 bytes to a C&lt;gappy_t&gt; variable, but
requires only 8 bytes for a C&lt;dense_t&gt;. After investigating this further,
we can draw memory maps, showing where the extra 4 bytes are hidden:

   0           +4          +8          +12
   +--+--+--+--+--+--+--+--+--+--+--+--+
   |c1|xx|  s  |c2|xx|xx|xx|     l     |    xx = fill byte
   +--+--+--+--+--+--+--+--+--+--+--+--+
   gappy_t

   0           +4          +8
   +--+--+--+--+--+--+--+--+
   |     l     |  h  |c1|c2|
   +--+--+--+--+--+--+--+--+
   dense_t

And that&#39;s where the first quirk strikes: C&lt;pack&gt; and C&lt;unpack&gt;
templates have to be stuffed with C&lt;x&gt; codes to get those extra fill bytes.

The natural question: &quot;Why can&#39;t Perl compensate for the gaps?&quot; warrants
an answer. One good reason is that C compilers might provide (non-ANSI)
extensions permitting all sorts of fancy control over the way structures
are aligned, even at the level of an individual structure field. And, if
this were not enough, there is an insidious thing called C&lt;union&gt; where
the amount of fill bytes cannot be derived from the alignment of the next
item alone.

OK, so let&#39;s bite the bullet. Here&#39;s one way to get the alignment right
by inserting template codes C&lt;x&gt;, which don&#39;t take a corresponding item 
from the list:

  my $gappy = pack( &#39;cxs cxxx l!&#39;, $c1, $s, $c2, $l );

Note the C&lt;!&gt; after C&lt;l&gt;: We want to make sure that we pack a long
integer as it is compiled by our C compiler. And even now, it will only
work for the platforms where the compiler aligns things as above.
And somebody somewhere has a platform where it doesn&#39;t.
[Probably a Cray, where C&lt;short&gt;s, C&lt;int&gt;s and C&lt;long&gt;s are all 8 bytes. :-)]

Counting bytes and watching alignments in lengthy structures is bound to 
be a drag. Isn&#39;t there a way we can create the template with a simple
program? Here&#39;s a C program that does the trick:

   #include &lt;stdio.h&gt;
   #include &lt;stddef.h&gt;

   typedef struct {
     char     fc1;
     short    fs;
     char     fc2;
     long     fl;
   } gappy_t;

   #define Pt(struct,field,tchar) \
     printf( &quot;@%d%s &quot;, offsetof(struct,field), # tchar );

   int main() {
     Pt( gappy_t, fc1, c  );
     Pt( gappy_t, fs,  s! );
     Pt( gappy_t, fc2, c  );
     Pt( gappy_t, fl,  l! );
     printf( &quot;\n&quot; );
   }

The output line can be used as a template in a C&lt;pack&gt; or C&lt;unpack&gt; call:

  my $gappy = pack( &#39;@0c @2s! @4c @8l!&#39;, $c1, $s, $c2, $l );

Gee, yet another template code - as if we hadn&#39;t plenty. But 
C&lt;@&gt; saves our day by enabling us to specify the offset from the beginning
of the pack buffer to the next item: This is just the value
the C&lt;offsetof&gt; macro (defined in C&lt;E&lt;lt&gt;stddef.hE&lt;gt&gt;&gt;) returns when
given a C&lt;struct&gt; type and one of its field names (&quot;member-designator&quot; in 
C standardese).

Neither using offsets nor adding C&lt;x&gt;&#39;s to bridge the gaps is satisfactory.
(Just imagine what happens if the structure changes.) What we really need
is a way of saying &quot;skip as many bytes as required to the next multiple of N&quot;.
In fluent Templatese, you say this with C&lt;x!N&gt; where N is replaced by the
appropriate value. Here&#39;s the next version of our struct packaging:

  my $gappy = pack( &#39;c x!2 s c x!4 l!&#39;, $c1, $s, $c2, $l );

That&#39;s certainly better, but we still have to know how long all the
integers are, and portability is far away. Rather than C&lt;2&gt;,
for instance, we want to say &quot;however long a short is&quot;. But this can be
done by enclosing the appropriate pack code in brackets: C&lt;[s]&gt;. So, here&#39;s
the very best we can do:

  my $gappy = pack( &#39;c x![s] s c x![l!] l!&#39;, $c1, $s, $c2, $l );


=head2 Dealing with Endian-ness

Now, imagine that we want to pack the data for a machine with a
different byte-order. First, we&#39;ll have to figure out how big the data
types on the target machine really are. Let&#39;s assume that the longs are
32 bits wide and the shorts are 16 bits wide. You can then rewrite the
template as:

  my $gappy = pack( &#39;c x![s] s c x![l] l&#39;, $c1, $s, $c2, $l );

If the target machine is little-endian, we could write:

  my $gappy = pack( &#39;c x![s] s&lt; c x![l] l&lt;&#39;, $c1, $s, $c2, $l );

This forces the short and the long members to be little-endian, and is
just fine if you don&#39;t have too many struct members. But we could also
use the byte-order modifier on a group and write the following:

  my $gappy = pack( &#39;( c x![s] s c x![l] l )&lt;&#39;, $c1, $s, $c2, $l );

This is not as short as before, but it makes it more obvious that we
intend to have little-endian byte-order for a whole group, not only
for individual template codes. It can also be more readable and easier
to maintain.


=head2 Alignment, Take 2

I&#39;m afraid that we&#39;re not quite through with the alignment catch yet. The
hydra raises another ugly head when you pack arrays of structures:

   typedef struct {
     short    count;
     char     glyph;
   } cell_t;

   typedef cell_t buffer_t[BUFLEN];

Where&#39;s the catch? Padding is neither required before the first field C&lt;count&gt;,
nor between this and the next field C&lt;glyph&gt;, so why can&#39;t we simply pack
like this:

   # something goes wrong here:
   pack( &#39;s!a&#39; x @buffer,
         map{ ( $_-&gt;{count}, $_-&gt;{glyph} ) } @buffer );

This packs C&lt;3*@buffer&gt; bytes, but it turns out that the size of 
C&lt;buffer_t&gt; is four times C&lt;BUFLEN&gt;! The moral of the story is that
the required alignment of a structure or array is propagated to the
next higher level where we have to consider padding I&lt;at the end&gt;
of each component as well. Thus the correct template is:

   pack( &#39;s!ax&#39; x @buffer,
         map{ ( $_-&gt;{count}, $_-&gt;{glyph} ) } @buffer );

=head2 Alignment, Take 3

And even if you take all the above into account, ANSI still lets this:

   typedef struct {
     char     foo[2];
   } foo_t;

vary in size. The alignment constraint of the structure can be greater than
any of its elements. [And if you think that this doesn&#39;t affect anything
common, dismember the next cellphone that you see. Many have ARM cores, and
the ARM structure rules make C&lt;sizeof (foo_t)&gt; == 4]

=head2 Pointers for How to Use Them

The title of this section indicates the second problem you may run into
sooner or later when you pack C structures. If the function you intend
to call expects a, say, C&lt;void *&gt; value, you I&lt;cannot&gt; simply take
a reference to a Perl variable. (Although that value certainly is a
memory address, it&#39;s not the address where the variable&#39;s contents are
stored.)

Template code C&lt;P&gt; promises to pack a &quot;pointer to a fixed length string&quot;.
Isn&#39;t this what we want? Let&#39;s try:

    # allocate some storage and pack a pointer to it
    my $memory = &quot;\x00&quot; x $size;
    my $memptr = pack( &#39;P&#39;, $memory );

But wait: doesn&#39;t C&lt;pack&gt; just return a sequence of bytes? How can we pass this
string of bytes to some C code expecting a pointer which is, after all,
nothing but a number? The answer is simple: We have to obtain the numeric
address from the bytes returned by C&lt;pack&gt;.

    my $ptr = unpack( &#39;L!&#39;, $memptr );

Obviously this assumes that it is possible to typecast a pointer
to an unsigned long and vice versa, which frequently works but should not
be taken as a universal law. - Now that we have this pointer the next question
is: How can we put it to good use? We need a call to some C function
where a pointer is expected. The read(2) system call comes to mind:

    ssize_t read(int fd, void *buf, size_t count);

After reading L&lt;perlfunc&gt; explaining how to use C&lt;syscall&gt; we can write
this Perl function copying a file to standard output:

    require &#39;syscall.ph&#39;; # run h2ph to generate this file
    sub cat($){
        my $path = shift();
        my $size = -s $path;
        my $memory = &quot;\x00&quot; x $size;  # allocate some memory
        my $ptr = unpack( &#39;L&#39;, pack( &#39;P&#39;, $memory ) );
        open( F, $path ) || die( &quot;$path: cannot open ($!)\n&quot; );
        my $fd = fileno(F);
        my $res = syscall( &amp;SYS_read, fileno(F), $ptr, $size );
        print $memory;
        close( F );
    }

This is neither a specimen of simplicity nor a paragon of portability but
it illustrates the point: We are able to sneak behind the scenes and
access Perl&#39;s otherwise well-guarded memory! (Important note: Perl&#39;s
C&lt;syscall&gt; does I&lt;not&gt; require you to construct pointers in this roundabout
way. You simply pass a string variable, and Perl forwards the address.) 

How does C&lt;unpack&gt; with C&lt;P&gt; work? Imagine some pointer in the buffer
about to be unpacked: If it isn&#39;t the null pointer (which will smartly
produce the C&lt;undef&gt; value) we have a start address - but then what?
Perl has no way of knowing how long this &quot;fixed length string&quot; is, so
it&#39;s up to you to specify the actual size as an explicit length after C&lt;P&gt;.

   my $mem = &quot;abcdefghijklmn&quot;;
   print unpack( &#39;P5&#39;, pack( &#39;P&#39;, $mem ) ); # prints &quot;abcde&quot;

As a consequence, C&lt;pack&gt; ignores any number or C&lt;*&gt; after C&lt;P&gt;.


Now that we have seen C&lt;P&gt; at work, we might as well give C&lt;p&gt; a whirl.
Why do we need a second template code for packing pointers at all? The 
answer lies behind the simple fact that an C&lt;unpack&gt; with C&lt;p&gt; promises
a null-terminated string starting at the address taken from the buffer,
and that implies a length for the data item to be returned:

   my $buf = pack( &#39;p&#39;, &quot;abc\x00efhijklmn&quot; );
   print unpack( &#39;p&#39;, $buf );    # prints &quot;abc&quot;



Albeit this is apt to be confusing: As a consequence of the length being
implied by the string&#39;s length, a number after pack code C&lt;p&gt; is a repeat
count, not a length as after C&lt;P&gt;. 


Using C&lt;pack(..., $x)&gt; with C&lt;P&gt; or C&lt;p&gt; to get the address where C&lt;$x&gt; is
actually stored must be used with circumspection. Perl&#39;s internal machinery
considers the relation between a variable and that address as its very own 
private matter and doesn&#39;t really care that we have obtained a copy. Therefore:

=over 4

=item * 

Do not use C&lt;pack&gt; with C&lt;p&gt; or C&lt;P&gt; to obtain the address of variable
that&#39;s bound to go out of scope (and thereby freeing its memory) before you
are done with using the memory at that address.

=item * 

Be very careful with Perl operations that change the value of the
variable. Appending something to the variable, for instance, might require
reallocation of its storage, leaving you with a pointer into no-man&#39;s land.

=item * 

Don&#39;t think that you can get the address of a Perl variable
when it is stored as an integer or double number! C&lt;pack(&#39;P&#39;, $x)&gt; will
force the variable&#39;s internal representation to string, just as if you
had written something like C&lt;$x .= &#39;&#39;&gt;.

=back

It&#39;s safe, however, to P- or p-pack a string literal, because Perl simply
allocates an anonymous variable.



=head1 Pack Recipes

Here are a collection of (possibly) useful canned recipes for C&lt;pack&gt;
and C&lt;unpack&gt;:

    # Convert IP address for socket functions
    pack( &quot;C4&quot;, split /\./, &quot;123.4.5.6&quot; ); 

    # Count the bits in a chunk of memory (e.g. a select vector)
    unpack( &#39;%32b*&#39;, $mask );

    # Determine the endianness of your system
    $is_little_endian = unpack( &#39;c&#39;, pack( &#39;s&#39;, 1 ) );
    $is_big_endian = unpack( &#39;xc&#39;, pack( &#39;s&#39;, 1 ) );

    # Determine the number of bits in a native integer
    $bits = unpack( &#39;%32I!&#39;, ~0 );

    # Prepare argument for the nanosleep system call
    my $timespec = pack( &#39;L!L!&#39;, $secs, $nanosecs );

For a simple memory dump we unpack some bytes into just as 
many pairs of hex digits, and use C&lt;map&gt; to handle the traditional
spacing - 16 bytes to a line:

    my $i;
    print map( ++$i % 16 ? &quot;$_ &quot; : &quot;$_\n&quot;,
               unpack( &#39;H2&#39; x length( $mem ), $mem ) ),
          length( $mem ) % 16 ? &quot;\n&quot; : &#39;&#39;;


=head1 Funnies Section

    # Pulling digits out of nowhere...
    print unpack( &#39;C&#39;, pack( &#39;x&#39; ) ),
          unpack( &#39;%B*&#39;, pack( &#39;A&#39; ) ),
          unpack( &#39;H&#39;, pack( &#39;A&#39; ) ),
          unpack( &#39;A&#39;, unpack( &#39;C&#39;, pack( &#39;A&#39; ) ) ), &quot;\n&quot;;

    # One for the road ;-)
    my $advice = pack( &#39;all u can in a van&#39; );


=head1 Authors

Simon Cozens and Wolfgang Laun.

</pre>

                    </main>
                </div>

        </div>
        <footer>
        
        </footer>


    </body>
</html>

<!DOCTYPE html>
<html>
    <head>
        <meta charset="utf-8">
        <meta content="width=device-width, initial-scale=1" name="viewport">
        <link href="/theme/css/normalize.css" rel="stylesheet">
        <!--<link rel="stylesheet" href="/theme/css/skeleton.css" />-->
        <link href="/theme/css/statocles-default.css" rel="stylesheet">
        <link href="//maxcdn.bootstrapcdn.com/font-awesome/4.3.0/css/font-awesome.min.css" rel="stylesheet">
        <script crossorigin="anonymous" integrity="sha256-hwg4gsxgFZhOsEEamdOYGBf13FyQuiTwlAQgxVSNgt4=" src="http://code.jquery.com/jquery-3.2.1.min.js"></script>
        <title>perlsyn (source) - Minimum Viable Perl</title>
        <meta content="Statocles 0.086" name="generator">
        
        <script>
            $( document ).ready(function() {
                $('.sub-menu ul').hide();
                $('.sub-menu.perldoc ul').show();
                $('.sub-menu.perldoc div').toggleClass("fa-caret-up fa-caret-down");
                $(".sub-menu a").click(function () {
                    $(this).parent(".sub-menu").siblings().children('ul').hide();
                    $(this).parent(".sub-menu").children("ul").slideToggle("100");
                    $(this).find(".right").toggleClass("fa-caret-up fa-caret-down");
                });
            });
        </script>
        <!-- Global site tag (gtag.js) - Google Analytics -->
        <script async src="https://www.googletagmanager.com/gtag/js?id=UA-110455056-1"></script>
        <script>
            window.dataLayer = window.dataLayer || [];
            function gtag(){dataLayer.push(arguments);}
            gtag('js', new Date());
            gtag('config', 'UA-110455056-1');
        </script>
    </head>
    <body>
        <header>
        </header>
        <div class="container">

                <div class="left column full-height">
                    <div class="sidebar">
                    
<a class="home" href="/">Minimum<br>Viable<br>Perl</a><br><br><br>

<div class="animated bounceInDown">
  <ul>
    <li class="basics sub-menu">
      <a href="/perldoc/perlsyn/#basics">The basics<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/basics/hello">Hello world</a></li>
        <li><a href="/basics/variables">Variables</a></li>
        <li><a href="/basics/references">References</a></li>
        <li><a href="/basics/functions">Functions</a></li>
        <li><a href="/basics/flow">Conditionals</a></li>
        <li><a href="/basics/loops">Loops</a></li>
        <li><a href="/basics/exceptions">Exceptions</a></li>
        <li><a href="/basics/regexps">Regexps</a></li>
        <li><a href="/basics/files">Files</a></li>
      </ul>
    </li>
    <li class="oo sub-menu">
      <a href="/perldoc/perlsyn/#oo">Object oriented programming<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/oo/story">The (weird) story of Perl OO</a></li>
        <li><a href="/oo/classes">Classes</a></li>
        <li><a href="/oo/attributes">Attributes</a></li>
        <li><a href="/oo/modifiers">Method modifiers</a></li>
        <li><a href="/oo/roles">Roles</a></li>
        <li><a href="/oo/inheritance">Inheritance</a></li>
      </ul>
    </li>
    <li class="debugging sub-menu">
      <a href="/perldoc/perlsyn/#basics">Debugging<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/debugging/data-printer">Examining data structures</a></li>
        <li><a href="/debugging/stack-trace">Forcing stack traces</a></li>
      </ul>
    </li>
    <li class="dependencies sub-menu">
      <a href="/perldoc/perlsyn/#dependencies">Perl libraries<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/dependencies/perl5lib">Loading libraries from custom locations</a></li>
        <li><a href="/dependencies/cpanm">Installing libraries with cpanm</a></li>
        <li><a href="/dependencies/metacpan">Choosing libraries from MetaCPAN</a></li>
        <li><a href="/dependencies/carton">Managing project dependencies with Carton</a></li>
        <li><a href="/dependencies/pmtools">What version is installed and where?</a></li>
      </ul>
    </li>
    <li class="testing sub-menu">
      <a href="/perldoc/perlsyn/#testing">Testing<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/testing/intro">Introduction to testing</a></li>
        <li><a href="/testing/warnings">Testing warnings and exceptions</a></li>
        <li><a href="/testing/data">Testing data structures</a></li>
        <li><a href="/testing/mock">Testing with mock objects</a></li>
        <li><a href="/testing/libs">Build your own test libraries</a></li>
      </ul>
    </li>
    <li><a href="/perl">Perl design goals</a></li>
    <li><a href="/see-also">Book recommendations</a></li>
    <li><a href="/about">About this book</a></li>
  </ul>
</div>

                    
                    
                    </div>
                </div>

                <div class="right column">
                    <main>
                    <a class="button" href="/perldoc/perlsyn/">
    Back to documentation
</a>
<pre>=head1 NAME
X&lt;syntax&gt;

perlsyn - Perl syntax

=head1 DESCRIPTION

A Perl program consists of a sequence of declarations and statements
which run from the top to the bottom.  Loops, subroutines, and other
control structures allow you to jump around within the code.

Perl is a B&lt;free-form&gt; language: you can format and indent it however
you like.  Whitespace serves mostly to separate tokens, unlike
languages like Python where it is an important part of the syntax,
or Fortran where it is immaterial.

Many of Perl&#39;s syntactic elements are B&lt;optional&gt;.  Rather than
requiring you to put parentheses around every function call and
declare every variable, you can often leave such explicit elements off
and Perl will figure out what you meant.  This is known as B&lt;Do What I
Mean&gt;, abbreviated B&lt;DWIM&gt;.  It allows programmers to be B&lt;lazy&gt; and to
code in a style with which they are comfortable.

Perl B&lt;borrows syntax&gt; and concepts from many languages: awk, sed, C,
Bourne Shell, Smalltalk, Lisp and even English.  Other
languages have borrowed syntax from Perl, particularly its regular
expression extensions.  So if you have programmed in another language
you will see familiar pieces in Perl.  They often work the same, but
see L&lt;perltrap&gt; for information about how they differ.

=head2 Declarations
X&lt;declaration&gt; X&lt;undef&gt; X&lt;undefined&gt; X&lt;uninitialized&gt;

The only things you need to declare in Perl are report formats and
subroutines (and sometimes not even subroutines).  A scalar variable holds
the undefined value (C&lt;undef&gt;) until it has been assigned a defined
value, which is anything other than C&lt;undef&gt;.  When used as a number,
C&lt;undef&gt; is treated as C&lt;0&gt;; when used as a string, it is treated as
the empty string, C&lt;&quot;&quot;&gt;; and when used as a reference that isn&#39;t being
assigned to, it is treated as an error.  If you enable warnings,
you&#39;ll be notified of an uninitialized value whenever you treat
C&lt;undef&gt; as a string or a number.  Well, usually.  Boolean contexts,
such as:

    if ($a) {}

are exempt from warnings (because they care about truth rather than
definedness).  Operators such as C&lt;++&gt;, C&lt;--&gt;, C&lt;+=&gt;,
C&lt;-=&gt;, and C&lt;.=&gt;, that operate on undefined variables such as:

    undef $a;
    $a++;

are also always exempt from such warnings.

A declaration can be put anywhere a statement can, but has no effect on
the execution of the primary sequence of statements: declarations all
take effect at compile time.  All declarations are typically put at
the beginning or the end of the script.  However, if you&#39;re using
lexically-scoped private variables created with C&lt;my()&gt;,
C&lt;state()&gt;, or C&lt;our()&gt;, you&#39;ll have to make sure
your format or subroutine definition is within the same block scope
as the my if you expect to be able to access those private variables.

Declaring a subroutine allows a subroutine name to be used as if it were a
list operator from that point forward in the program.  You can declare a
subroutine without defining it by saying C&lt;sub name&gt;, thus:
X&lt;subroutine, declaration&gt;

    sub myname;
    $me = myname $0             or die &quot;can&#39;t get myname&quot;;

A bare declaration like that declares the function to be a list operator,
not a unary operator, so you have to be careful to use parentheses (or
C&lt;or&gt; instead of C&lt;||&gt;.)  The C&lt;||&gt; operator binds too tightly to use after
list operators; it becomes part of the last element.  You can always use
parentheses around the list operators arguments to turn the list operator
back into something that behaves more like a function call.  Alternatively,
you can use the prototype C&lt;($)&gt; to turn the subroutine into a unary
operator:

  sub myname ($);
  $me = myname $0             || die &quot;can&#39;t get myname&quot;;

That now parses as you&#39;d expect, but you still ought to get in the habit of
using parentheses in that situation.  For more on prototypes, see
L&lt;perlsub&gt;.

Subroutines declarations can also be loaded up with the C&lt;require&gt; statement
or both loaded and imported into your namespace with a C&lt;use&gt; statement.
See L&lt;perlmod&gt; for details on this.

A statement sequence may contain declarations of lexically-scoped
variables, but apart from declaring a variable name, the declaration acts
like an ordinary statement, and is elaborated within the sequence of
statements as if it were an ordinary statement.  That means it actually
has both compile-time and run-time effects.

=head2 Comments
X&lt;comment&gt; X&lt;#&gt;

Text from a C&lt;&quot;#&quot;&gt; character until the end of the line is a comment,
and is ignored.  Exceptions include C&lt;&quot;#&quot;&gt; inside a string or regular
expression.

=head2 Simple Statements
X&lt;statement&gt; X&lt;semicolon&gt; X&lt;expression&gt; X&lt;;&gt;

The only kind of simple statement is an expression evaluated for its
side-effects.  Every simple statement must be terminated with a
semicolon, unless it is the final statement in a block, in which case
the semicolon is optional.  But put the semicolon in anyway if the
block takes up more than one line, because you may eventually add
another line.  Note that there are operators like C&lt;eval {}&gt;, C&lt;sub {}&gt;, and
C&lt;do {}&gt; that I&lt;look&gt; like compound statements, but aren&#39;t--they&#39;re just
TERMs in an expression--and thus need an explicit termination when used
as the last item in a statement.

=head2 Truth and Falsehood
X&lt;truth&gt; X&lt;falsehood&gt; X&lt;true&gt; X&lt;false&gt; X&lt;!&gt; X&lt;not&gt; X&lt;negation&gt; X&lt;0&gt;

The number 0, the strings C&lt;&#39;0&#39;&gt; and C&lt;&quot;&quot;&gt;, the empty list C&lt;()&gt;, and
C&lt;undef&gt; are all false in a boolean context.  All other values are true.
Negation of a true value by C&lt;!&gt; or C&lt;not&gt; returns a special false value.
When evaluated as a string it is treated as C&lt;&quot;&quot;&gt;, but as a number, it
is treated as 0.  Most Perl operators
that return true or false behave this way.

=head2 Statement Modifiers
X&lt;statement modifier&gt; X&lt;modifier&gt; X&lt;if&gt; X&lt;unless&gt; X&lt;while&gt;
X&lt;until&gt; X&lt;when&gt; X&lt;foreach&gt; X&lt;for&gt;

Any simple statement may optionally be followed by a I&lt;SINGLE&gt; modifier,
just before the terminating semicolon (or block ending).  The possible
modifiers are:

    if EXPR
    unless EXPR
    while EXPR
    until EXPR
    for LIST
    foreach LIST
    when EXPR

The C&lt;EXPR&gt; following the modifier is referred to as the &quot;condition&quot;.
Its truth or falsehood determines how the modifier will behave.

C&lt;if&gt; executes the statement once I&lt;if&gt; and only if the condition is
true.  C&lt;unless&gt; is the opposite, it executes the statement I&lt;unless&gt;
the condition is true (that is, if the condition is false).

    print &quot;Basset hounds got long ears&quot; if length $ear &gt;= 10;
    go_outside() and play() unless $is_raining;

The C&lt;for(each)&gt; modifier is an iterator: it executes the statement once
for each item in the LIST (with C&lt;$_&gt; aliased to each item in turn).

    print &quot;Hello $_!\n&quot; for qw(world Dolly nurse);

C&lt;while&gt; repeats the statement I&lt;while&gt; the condition is true.
C&lt;until&gt; does the opposite, it repeats the statement I&lt;until&gt; the
condition is true (or while the condition is false):

    # Both of these count from 0 to 10.
    print $i++ while $i &lt;= 10;
    print $j++ until $j &gt;  10;

The C&lt;while&gt; and C&lt;until&gt; modifiers have the usual &quot;C&lt;while&gt; loop&quot;
semantics (conditional evaluated first), except when applied to a
C&lt;do&gt;-BLOCK (or to the Perl4 C&lt;do&gt;-SUBROUTINE statement), in
which case the block executes once before the conditional is
evaluated.

This is so that you can write loops like:

    do {
        $line = &lt;STDIN&gt;;
        ...
    } until !defined($line) || $line eq &quot;.\n&quot;

See L&lt;perlfunc/do&gt;.  Note also that the loop control statements described
later will I&lt;NOT&gt; work in this construct, because modifiers don&#39;t take
loop labels.  Sorry.  You can always put another block inside of it
(for C&lt;next&gt;/C&lt;redo&gt;) or around it (for C&lt;last&gt;) to do that sort of thing.
X&lt;next&gt; X&lt;last&gt; X&lt;redo&gt;

For C&lt;next&gt; or C&lt;redo&gt;, just double the braces:

    do {{
        next if $x == $y;
        # do something here
    }} until $x++ &gt; $z;

For C&lt;last&gt;, you have to be more elaborate and put braces around it:
X&lt;last&gt;

    {
        do {
            last if $x == $y**2;
            # do something here
        } while $x++ &lt;= $z;
    }

If you need both C&lt;next&gt; and C&lt;last&gt;, you have to do both and also use a
loop label:

    LOOP: {
        do {{
            next if $x == $y;
            last LOOP if $x == $y**2;
            # do something here
        }} until $x++ &gt; $z;
    }

B&lt;NOTE:&gt; The behaviour of a C&lt;my&gt;, C&lt;state&gt;, or
C&lt;our&gt; modified with a statement modifier conditional
or loop construct (for example, C&lt;my $x if ...&gt;) is
B&lt;undefined&gt;.  The value of the C&lt;my&gt; variable may be C&lt;undef&gt;, any
previously assigned value, or possibly anything else.  Don&#39;t rely on
it.  Future versions of perl might do something different from the
version of perl you try it out on.  Here be dragons.
X&lt;my&gt;

The C&lt;when&gt; modifier is an experimental feature that first appeared in Perl
5.14.  To use it, you should include a C&lt;use v5.14&gt; declaration.
(Technically, it requires only the C&lt;switch&gt; feature, but that aspect of it
was not available before 5.14.)  Operative only from within a C&lt;foreach&gt;
loop or a C&lt;given&gt; block, it executes the statement only if the smartmatch
C&lt;&lt; $_ ~~ I&lt;EXPR&gt; &gt;&gt; is true.  If the statement executes, it is followed by
a C&lt;next&gt; from inside a C&lt;foreach&gt; and C&lt;break&gt; from inside a C&lt;given&gt;.

Under the current implementation, the C&lt;foreach&gt; loop can be
anywhere within the C&lt;when&gt; modifier&#39;s dynamic scope, but must be
within the C&lt;given&gt; block&#39;s lexical scope.  This restriction may
be relaxed in a future release.  See L&lt;/&quot;Switch Statements&quot;&gt; below.

=head2 Compound Statements
X&lt;statement, compound&gt; X&lt;block&gt; X&lt;bracket, curly&gt; X&lt;curly bracket&gt; X&lt;brace&gt;
X&lt;{&gt; X&lt;}&gt; X&lt;if&gt; X&lt;unless&gt; X&lt;given&gt; X&lt;while&gt; X&lt;until&gt; X&lt;foreach&gt; X&lt;for&gt; X&lt;continue&gt;

In Perl, a sequence of statements that defines a scope is called a block.
Sometimes a block is delimited by the file containing it (in the case
of a required file, or the program as a whole), and sometimes a block
is delimited by the extent of a string (in the case of an eval).

But generally, a block is delimited by curly brackets, also known as braces.
We will call this syntactic construct a BLOCK.

The following compound statements may be used to control flow:

    if (EXPR) BLOCK
    if (EXPR) BLOCK else BLOCK
    if (EXPR) BLOCK elsif (EXPR) BLOCK ...
    if (EXPR) BLOCK elsif (EXPR) BLOCK ... else BLOCK

    unless (EXPR) BLOCK
    unless (EXPR) BLOCK else BLOCK
    unless (EXPR) BLOCK elsif (EXPR) BLOCK ...
    unless (EXPR) BLOCK elsif (EXPR) BLOCK ... else BLOCK

    given (EXPR) BLOCK

    LABEL while (EXPR) BLOCK
    LABEL while (EXPR) BLOCK continue BLOCK

    LABEL until (EXPR) BLOCK
    LABEL until (EXPR) BLOCK continue BLOCK

    LABEL for (EXPR; EXPR; EXPR) BLOCK
    LABEL for VAR (LIST) BLOCK
    LABEL for VAR (LIST) BLOCK continue BLOCK

    LABEL foreach (EXPR; EXPR; EXPR) BLOCK
    LABEL foreach VAR (LIST) BLOCK
    LABEL foreach VAR (LIST) BLOCK continue BLOCK

    LABEL BLOCK
    LABEL BLOCK continue BLOCK

    PHASE BLOCK

The experimental C&lt;given&gt; statement is I&lt;not automatically enabled&gt;; see
L&lt;/&quot;Switch Statements&quot;&gt; below for how to do so, and the attendant caveats.

Unlike in C and Pascal, in Perl these are all defined in terms of BLOCKs,
not statements.  This means that the curly brackets are I&lt;required&gt;--no
dangling statements allowed.  If you want to write conditionals without
curly brackets, there are several other ways to do it.  The following
all do the same thing:

    if (!open(FOO)) { die &quot;Can&#39;t open $FOO: $!&quot; }
    die &quot;Can&#39;t open $FOO: $!&quot; unless open(FOO);
    open(FOO)  || die &quot;Can&#39;t open $FOO: $!&quot;;
    open(FOO) ? () : die &quot;Can&#39;t open $FOO: $!&quot;;
        # a bit exotic, that last one

The C&lt;if&gt; statement is straightforward.  Because BLOCKs are always
bounded by curly brackets, there is never any ambiguity about which
C&lt;if&gt; an C&lt;else&gt; goes with.  If you use C&lt;unless&gt; in place of C&lt;if&gt;,
the sense of the test is reversed.  Like C&lt;if&gt;, C&lt;unless&gt; can be followed
by C&lt;else&gt;.  C&lt;unless&gt; can even be followed by one or more C&lt;elsif&gt;
statements, though you may want to think twice before using that particular
language construct, as everyone reading your code will have to think at least
twice before they can understand what&#39;s going on.

The C&lt;while&gt; statement executes the block as long as the expression is
L&lt;true|/&quot;Truth and Falsehood&quot;&gt;.
The C&lt;until&gt; statement executes the block as long as the expression is
false.
The LABEL is optional, and if present, consists of an identifier followed
by a colon.  The LABEL identifies the loop for the loop control
statements C&lt;next&gt;, C&lt;last&gt;, and C&lt;redo&gt;.
If the LABEL is omitted, the loop control statement
refers to the innermost enclosing loop.  This may include dynamically
looking back your call-stack at run time to find the LABEL.  Such
desperate behavior triggers a warning if you use the C&lt;use warnings&gt;
pragma or the B&lt;-w&gt; flag.

If there is a C&lt;continue&gt; BLOCK, it is always executed just before the
conditional is about to be evaluated again.  Thus it can be used to
increment a loop variable, even when the loop has been continued via
the C&lt;next&gt; statement.

When a block is preceding by a compilation phase keyword such as C&lt;BEGIN&gt;,
C&lt;END&gt;, C&lt;INIT&gt;, C&lt;CHECK&gt;, or C&lt;UNITCHECK&gt;, then the block will run only
during the corresponding phase of execution.  See L&lt;perlmod&gt; for more details.

Extension modules can also hook into the Perl parser to define new
kinds of compound statements.  These are introduced by a keyword which
the extension recognizes, and the syntax following the keyword is
defined entirely by the extension.  If you are an implementor, see
L&lt;perlapi/PL_keyword_plugin&gt; for the mechanism.  If you are using such
a module, see the module&#39;s documentation for details of the syntax that
it defines.

=head2 Loop Control
X&lt;loop control&gt; X&lt;loop, control&gt; X&lt;next&gt; X&lt;last&gt; X&lt;redo&gt; X&lt;continue&gt;

The C&lt;next&gt; command starts the next iteration of the loop:

    LINE: while (&lt;STDIN&gt;) {
        next LINE if /^#/;      # discard comments
        ...
    }

The C&lt;last&gt; command immediately exits the loop in question.  The
C&lt;continue&gt; block, if any, is not executed:

    LINE: while (&lt;STDIN&gt;) {
        last LINE if /^$/;      # exit when done with header
        ...
    }

The C&lt;redo&gt; command restarts the loop block without evaluating the
conditional again.  The C&lt;continue&gt; block, if any, is I&lt;not&gt; executed.
This command is normally used by programs that want to lie to themselves
about what was just input.

For example, when processing a file like F&lt;/etc/termcap&gt;.
If your input lines might end in backslashes to indicate continuation, you
want to skip ahead and get the next record.

    while (&lt;&gt;) {
        chomp;
        if (s/\\$//) {
            $_ .= &lt;&gt;;
            redo unless eof();
        }
        # now process $_
    }

which is Perl shorthand for the more explicitly written version:

    LINE: while (defined($line = &lt;ARGV&gt;)) {
        chomp($line);
        if ($line =~ s/\\$//) {
            $line .= &lt;ARGV&gt;;
            redo LINE unless eof(); # not eof(ARGV)!
        }
        # now process $line
    }

Note that if there were a C&lt;continue&gt; block on the above code, it would
get executed only on lines discarded by the regex (since redo skips the
continue block).  A continue block is often used to reset line counters
or C&lt;m?pat?&gt; one-time matches:

    # inspired by :1,$g/fred/s//WILMA/
    while (&lt;&gt;) {
        m?(fred)?    &amp;&amp; s//WILMA $1 WILMA/;
        m?(barney)?  &amp;&amp; s//BETTY $1 BETTY/;
        m?(homer)?   &amp;&amp; s//MARGE $1 MARGE/;
    } continue {
        print &quot;$ARGV $.: $_&quot;;
        close ARGV  if eof;             # reset $.
        reset       if eof;             # reset ?pat?
    }

If the word C&lt;while&gt; is replaced by the word C&lt;until&gt;, the sense of the
test is reversed, but the conditional is still tested before the first
iteration.

Loop control statements don&#39;t work in an C&lt;if&gt; or C&lt;unless&gt;, since
they aren&#39;t loops.  You can double the braces to make them such, though.

    if (/pattern/) {{
        last if /fred/;
        next if /barney/; # same effect as &quot;last&quot;,
                          # but doesn&#39;t document as well
        # do something here
    }}

This is caused by the fact that a block by itself acts as a loop that
executes once, see L&lt;/&quot;Basic BLOCKs&quot;&gt;.

The form C&lt;while/if BLOCK BLOCK&gt;, available in Perl 4, is no longer
available.   Replace any occurrence of C&lt;if BLOCK&gt; by C&lt;if (do BLOCK)&gt;.

=head2 For Loops
X&lt;for&gt; X&lt;foreach&gt;

Perl&#39;s C-style C&lt;for&gt; loop works like the corresponding C&lt;while&gt; loop;
that means that this:

    for ($i = 1; $i &lt; 10; $i++) {
        ...
    }

is the same as this:

    $i = 1;
    while ($i &lt; 10) {
        ...
    } continue {
        $i++;
    }

There is one minor difference: if variables are declared with C&lt;my&gt;
in the initialization section of the C&lt;for&gt;, the lexical scope of
those variables is exactly the C&lt;for&gt; loop (the body of the loop
and the control sections).
X&lt;my&gt;

As a special case, if the test in the C&lt;for&gt; loop (or the corresponding
C&lt;while&gt; loop) is empty, it is treated as true.  That is, both

    for (;;) {
        ...
    }

and

    while () {
        ...
    }

are treated as infinite loops.

Besides the normal array index looping, C&lt;for&gt; can lend itself
to many other interesting applications.  Here&#39;s one that avoids the
problem you get into if you explicitly test for end-of-file on
an interactive file descriptor causing your program to appear to
hang.
X&lt;eof&gt; X&lt;end-of-file&gt; X&lt;end of file&gt;

    $on_a_tty = -t STDIN &amp;&amp; -t STDOUT;
    sub prompt { print &quot;yes? &quot; if $on_a_tty }
    for ( prompt(); &lt;STDIN&gt;; prompt() ) {
        # do something
    }

Using C&lt;readline&gt; (or the operator form, C&lt;&lt; &lt;EXPR&gt; &gt;&gt;) as the
conditional of a C&lt;for&gt; loop is shorthand for the following.  This
behaviour is the same as a C&lt;while&gt; loop conditional.
X&lt;readline&gt; X&lt;&lt; &lt;&gt; &gt;&gt;

    for ( prompt(); defined( $_ = &lt;STDIN&gt; ); prompt() ) {
        # do something
    }

=head2 Foreach Loops
X&lt;for&gt; X&lt;foreach&gt;

The C&lt;foreach&gt; loop iterates over a normal list value and sets the scalar
variable VAR to be each element of the list in turn.  If the variable
is preceded with the keyword C&lt;my&gt;, then it is lexically scoped, and
is therefore visible only within the loop.  Otherwise, the variable is
implicitly local to the loop and regains its former value upon exiting
the loop.  If the variable was previously declared with C&lt;my&gt;, it uses
that variable instead of the global one, but it&#39;s still localized to
the loop.  This implicit localization occurs I&lt;only&gt; in a C&lt;foreach&gt;
loop.
X&lt;my&gt; X&lt;local&gt;

The C&lt;foreach&gt; keyword is actually a synonym for the C&lt;for&gt; keyword, so
you can use either.  If VAR is omitted, C&lt;$_&gt; is set to each value.
X&lt;$_&gt;

If any element of LIST is an lvalue, you can modify it by modifying
VAR inside the loop.  Conversely, if any element of LIST is NOT an
lvalue, any attempt to modify that element will fail.  In other words,
the C&lt;foreach&gt; loop index variable is an implicit alias for each item
in the list that you&#39;re looping over.
X&lt;alias&gt;

If any part of LIST is an array, C&lt;foreach&gt; will get very confused if
you add or remove elements within the loop body, for example with
C&lt;splice&gt;.   So don&#39;t do that.
X&lt;splice&gt;

C&lt;foreach&gt; probably won&#39;t do what you expect if VAR is a tied or other
special variable.   Don&#39;t do that either.

As of Perl 5.22, there is an experimental variant of this loop that accepts
a variable preceded by a backslash for VAR, in which case the items in the
LIST must be references.  The backslashed variable will become an alias
to each referenced item in the LIST, which must be of the correct type.
The variable needn&#39;t be a scalar in this case, and the backslash may be
followed by C&lt;my&gt;.  To use this form, you must enable the C&lt;refaliasing&gt;
feature via C&lt;use feature&gt;.  (See L&lt;feature&gt;.  See also L&lt;perlref/Assigning
to References&gt;.)

Examples:

    for (@ary) { s/foo/bar/ }

    for my $elem (@elements) {
        $elem *= 2;
    }

    for $count (reverse(1..10), &quot;BOOM&quot;) {
        print $count, &quot;\n&quot;;
        sleep(1);
    }

    for (1..15) { print &quot;Merry Christmas\n&quot;; }

    foreach $item (split(/:[\\\n:]*/, $ENV{TERMCAP})) {
        print &quot;Item: $item\n&quot;;
    }

    use feature &quot;refaliasing&quot;;
    no warnings &quot;experimental::refaliasing&quot;;
    foreach \my %hash (@array_of_hash_references) {
        # do something which each %hash
    }

Here&#39;s how a C programmer might code up a particular algorithm in Perl:

    for (my $i = 0; $i &lt; @ary1; $i++) {
        for (my $j = 0; $j &lt; @ary2; $j++) {
            if ($ary1[$i] &gt; $ary2[$j]) {
                last; # can&#39;t go to outer :-(
            }
            $ary1[$i] += $ary2[$j];
        }
        # this is where that last takes me
    }

Whereas here&#39;s how a Perl programmer more comfortable with the idiom might
do it:

    OUTER: for my $wid (@ary1) {
    INNER:   for my $jet (@ary2) {
                next OUTER if $wid &gt; $jet;
                $wid += $jet;
             }
          }

See how much easier this is?  It&#39;s cleaner, safer, and faster.  It&#39;s
cleaner because it&#39;s less noisy.  It&#39;s safer because if code gets added
between the inner and outer loops later on, the new code won&#39;t be
accidentally executed.  The C&lt;next&gt; explicitly iterates the other loop
rather than merely terminating the inner one.  And it&#39;s faster because
Perl executes a C&lt;foreach&gt; statement more rapidly than it would the
equivalent C&lt;for&gt; loop.

Perceptive Perl hackers may have noticed that a C&lt;for&gt; loop has a return
value, and that this value can be captured by wrapping the loop in a C&lt;do&gt;
block.  The reward for this discovery is this cautionary advice:  The
return value of a C&lt;for&gt; loop is unspecified and may change without notice.
Do not rely on it.

=head2 Basic BLOCKs
X&lt;block&gt;

A BLOCK by itself (labeled or not) is semantically equivalent to a
loop that executes once.  Thus you can use any of the loop control
statements in it to leave or restart the block.  (Note that this is
I&lt;NOT&gt; true in C&lt;eval{}&gt;, C&lt;sub{}&gt;, or contrary to popular belief
C&lt;do{}&gt; blocks, which do I&lt;NOT&gt; count as loops.)  The C&lt;continue&gt;
block is optional.

The BLOCK construct can be used to emulate case structures.

    SWITCH: {
        if (/^abc/) { $abc = 1; last SWITCH; }
        if (/^def/) { $def = 1; last SWITCH; }
        if (/^xyz/) { $xyz = 1; last SWITCH; }
        $nothing = 1;
    }

You&#39;ll also find that C&lt;foreach&gt; loop used to create a topicalizer
and a switch:

    SWITCH:
    for ($var) {
        if (/^abc/) { $abc = 1; last SWITCH; }
        if (/^def/) { $def = 1; last SWITCH; }
        if (/^xyz/) { $xyz = 1; last SWITCH; }
        $nothing = 1;
    }

Such constructs are quite frequently used, both because older versions of
Perl had no official C&lt;switch&gt; statement, and also because the new version
described immediately below remains experimental and can sometimes be confusing.

=head2 Switch Statements

X&lt;switch&gt; X&lt;case&gt; X&lt;given&gt; X&lt;when&gt; X&lt;default&gt;

Starting from Perl 5.10.1 (well, 5.10.0, but it didn&#39;t work
right), you can say

    use feature &quot;switch&quot;;

to enable an experimental switch feature.  This is loosely based on an
old version of a Perl 6 proposal, but it no longer resembles the Perl 6
construct.   You also get the switch feature whenever you declare that your
code prefers to run under a version of Perl that is 5.10 or later.  For
example:

    use v5.14;

Under the &quot;switch&quot; feature, Perl gains the experimental keywords
C&lt;given&gt;, C&lt;when&gt;, C&lt;default&gt;, C&lt;continue&gt;, and C&lt;break&gt;.
Starting from Perl 5.16, one can prefix the switch
keywords with C&lt;CORE::&gt; to access the feature without a C&lt;use feature&gt;
statement.  The keywords C&lt;given&gt; and
C&lt;when&gt; are analogous to C&lt;switch&gt; and
C&lt;case&gt; in other languages -- though C&lt;continue&gt; is not -- so the code
in the previous section could be rewritten as

    use v5.10.1;
    for ($var) {
        when (/^abc/) { $abc = 1 }
        when (/^def/) { $def = 1 }
        when (/^xyz/) { $xyz = 1 }
        default       { $nothing = 1 }
    }

The C&lt;foreach&gt; is the non-experimental way to set a topicalizer.
If you wish to use the highly experimental C&lt;given&gt;, that could be
written like this:

    use v5.10.1;
    given ($var) {
        when (/^abc/) { $abc = 1 }
        when (/^def/) { $def = 1 }
        when (/^xyz/) { $xyz = 1 }
        default       { $nothing = 1 }
    }

As of 5.14, that can also be written this way:

    use v5.14;
    for ($var) {
        $abc = 1 when /^abc/;
        $def = 1 when /^def/;
        $xyz = 1 when /^xyz/;
        default { $nothing = 1 }
    }

Or if you don&#39;t care to play it safe, like this:

    use v5.14;
    given ($var) {
        $abc = 1 when /^abc/;
        $def = 1 when /^def/;
        $xyz = 1 when /^xyz/;
        default { $nothing = 1 }
    }

The arguments to C&lt;given&gt; and C&lt;when&gt; are in scalar context,
and C&lt;given&gt; assigns the C&lt;$_&gt; variable its topic value.

Exactly what the I&lt;EXPR&gt; argument to C&lt;when&gt; does is hard to describe
precisely, but in general, it tries to guess what you want done.  Sometimes
it is interpreted as C&lt;&lt; $_ ~~ I&lt;EXPR&gt; &gt;&gt;, and sometimes it is not.  It
also behaves differently when lexically enclosed by a C&lt;given&gt; block than
it does when dynamically enclosed by a C&lt;foreach&gt; loop.  The rules are far
too difficult to understand to be described here.  See L&lt;/&quot;Experimental Details
on given and when&quot;&gt; later on.

Due to an unfortunate bug in how C&lt;given&gt; was implemented between Perl 5.10
and 5.16, under those implementations the version of C&lt;$_&gt; governed by
C&lt;given&gt; is merely a lexically scoped copy of the original, not a
dynamically scoped alias to the original, as it would be if it were a
C&lt;foreach&gt; or under both the original and the current Perl 6 language
specification.  This bug was fixed in Perl 5.18 (and lexicalized C&lt;$_&gt; itself
was removed in Perl 5.24).

If your code still needs to run on older versions,
stick to C&lt;foreach&gt; for your topicalizer and
you will be less unhappy.

=head2 Goto
X&lt;goto&gt;

Although not for the faint of heart, Perl does support a C&lt;goto&gt;
statement.  There are three forms: C&lt;goto&gt;-LABEL, C&lt;goto&gt;-EXPR, and
C&lt;goto&gt;-&amp;NAME.  A loop&#39;s LABEL is not actually a valid target for
a C&lt;goto&gt;; it&#39;s just the name of the loop.

The C&lt;goto&gt;-LABEL form finds the statement labeled with LABEL and resumes
execution there.  It may not be used to go into any construct that
requires initialization, such as a subroutine or a C&lt;foreach&gt; loop.  It
also can&#39;t be used to go into a construct that is optimized away.  It
can be used to go almost anywhere else within the dynamic scope,
including out of subroutines, but it&#39;s usually better to use some other
construct such as C&lt;last&gt; or C&lt;die&gt;.  The author of Perl has never felt the
need to use this form of C&lt;goto&gt; (in Perl, that is--C is another matter).

The C&lt;goto&gt;-EXPR form expects a label name, whose scope will be resolved
dynamically.  This allows for computed C&lt;goto&gt;s per FORTRAN, but isn&#39;t
necessarily recommended if you&#39;re optimizing for maintainability:

    goto((&quot;FOO&quot;, &quot;BAR&quot;, &quot;GLARCH&quot;)[$i]);

The C&lt;goto&gt;-&amp;NAME form is highly magical, and substitutes a call to the
named subroutine for the currently running subroutine.  This is used by
C&lt;AUTOLOAD()&gt; subroutines that wish to load another subroutine and then
pretend that the other subroutine had been called in the first place
(except that any modifications to C&lt;@_&gt; in the current subroutine are
propagated to the other subroutine.)  After the C&lt;goto&gt;, not even C&lt;caller()&gt;
will be able to tell that this routine was called first.

In almost all cases like this, it&#39;s usually a far, far better idea to use the
structured control flow mechanisms of C&lt;next&gt;, C&lt;last&gt;, or C&lt;redo&gt; instead of
resorting to a C&lt;goto&gt;.  For certain applications, the catch and throw pair of
C&lt;eval{}&gt; and die() for exception processing can also be a prudent approach.

=head2 The Ellipsis Statement
X&lt;...&gt;
X&lt;... statement&gt;
X&lt;ellipsis operator&gt;
X&lt;elliptical statement&gt;
X&lt;unimplemented statement&gt;
X&lt;unimplemented operator&gt;
X&lt;yada-yada&gt;
X&lt;yada-yada operator&gt;
X&lt;... operator&gt;
X&lt;whatever operator&gt;
X&lt;triple-dot operator&gt;

Beginning in Perl 5.12, Perl accepts an ellipsis, &quot;C&lt;...&gt;&quot;, as a
placeholder for code that you haven&#39;t implemented yet.  This form of
ellipsis, the unimplemented statement, should not be confused with the
binary flip-flop C&lt;...&gt; operator.  One is a statement and the other an
operator.  (Perl doesn&#39;t usually confuse them because usually Perl can tell
whether it wants an operator or a statement, but see below for exceptions.)

When Perl 5.12 or later encounters an ellipsis statement, it parses this
without error, but if and when you should actually try to execute it, Perl
throws an exception with the text C&lt;Unimplemented&gt;:

    use v5.12;
    sub unimplemented { ... }
    eval { unimplemented() };
    if ($@ =~ /^Unimplemented at /) {
        say &quot;I found an ellipsis!&quot;;
    }

You can only use the elliptical statement to stand in for a
complete statement.  These examples of how the ellipsis works:

    use v5.12;
    { ... }
    sub foo { ... }
    ...;
    eval { ... };
    sub somemeth {
        my $self = shift;
        ...;
    }
    $x = do {
        my $n;
        ...;
        say &quot;Hurrah!&quot;;
        $n;
    };

The elliptical statement cannot stand in for an expression that
is part of a larger statement, since the C&lt;...&gt; is also the three-dot
version of the flip-flop operator (see L&lt;perlop/&quot;Range Operators&quot;&gt;).

These examples of attempts to use an ellipsis are syntax errors:

    use v5.12;

    print ...;
    open(my $fh, &quot;&gt;&quot;, &quot;/dev/passwd&quot;) or ...;
    if ($condition &amp;&amp; ... ) { say &quot;Howdy&quot; };

There are some cases where Perl can&#39;t immediately tell the difference
between an expression and a statement.  For instance, the syntax for a
block and an anonymous hash reference constructor look the same unless
there&#39;s something in the braces to give Perl a hint.  The ellipsis is a
syntax error if Perl doesn&#39;t guess that the C&lt;{ ... }&gt; is a block.  In that
case, it doesn&#39;t think the C&lt;...&gt; is an ellipsis because it&#39;s expecting an
expression instead of a statement:

    @transformed = map { ... } @input;    # syntax error

Inside your block, you can use a C&lt;;&gt; before the ellipsis to denote that the
C&lt;{ ... }&gt; is a block and not a hash reference constructor.  Now the ellipsis
works:

    @transformed = map {; ... } @input;   # &#39;;&#39; disambiguates

Note: Some folks colloquially refer to this bit of punctuation as a
&quot;yada-yada&quot; or &quot;triple-dot&quot;, but its true name
is actually an ellipsis.

=head2 PODs: Embedded Documentation
X&lt;POD&gt; X&lt;documentation&gt;

Perl has a mechanism for intermixing documentation with source code.
While it&#39;s expecting the beginning of a new statement, if the compiler
encounters a line that begins with an equal sign and a word, like this

    =head1 Here There Be Pods!

Then that text and all remaining text up through and including a line
beginning with C&lt;=cut&gt; will be ignored.  The format of the intervening
text is described in L&lt;perlpod&gt;.

This allows you to intermix your source code
and your documentation text freely, as in

    =item snazzle($)

    The snazzle() function will behave in the most spectacular
    form that you can possibly imagine, not even excepting
    cybernetic pyrotechnics.

    =cut back to the compiler, nuff of this pod stuff!

    sub snazzle($) {
        my $thingie = shift;
        .........
    }

Note that pod translators should look at only paragraphs beginning
with a pod directive (it makes parsing easier), whereas the compiler
actually knows to look for pod escapes even in the middle of a
paragraph.  This means that the following secret stuff will be
ignored by both the compiler and the translators.

    $a=3;
    =secret stuff
     warn &quot;Neither POD nor CODE!?&quot;
    =cut back
    print &quot;got $a\n&quot;;

You probably shouldn&#39;t rely upon the C&lt;warn()&gt; being podded out forever.
Not all pod translators are well-behaved in this regard, and perhaps
the compiler will become pickier.

One may also use pod directives to quickly comment out a section
of code.

=head2 Plain Old Comments (Not!)
X&lt;comment&gt; X&lt;line&gt; X&lt;#&gt; X&lt;preprocessor&gt; X&lt;eval&gt;

Perl can process line directives, much like the C preprocessor.  Using
this, one can control Perl&#39;s idea of filenames and line numbers in
error or warning messages (especially for strings that are processed
with C&lt;eval()&gt;).  The syntax for this mechanism is almost the same as for
most C preprocessors: it matches the regular expression

    # example: &#39;# line 42 &quot;new_filename.plx&quot;&#39;
    /^\#   \s*
      line \s+ (\d+)   \s*
      (?:\s(&quot;?)([^&quot;]+)\g2)? \s*
     $/x

with C&lt;$1&gt; being the line number for the next line, and C&lt;$3&gt; being
the optional filename (specified with or without quotes).  Note that
no whitespace may precede the C&lt;&lt; # &gt;&gt;, unlike modern C preprocessors.

There is a fairly obvious gotcha included with the line directive:
Debuggers and profilers will only show the last source line to appear
at a particular line number in a given file.  Care should be taken not
to cause line number collisions in code you&#39;d like to debug later.

Here are some examples that you should be able to type into your command
shell:

    % perl
    # line 200 &quot;bzzzt&quot;
    # the &#39;#&#39; on the previous line must be the first char on line
    die &#39;foo&#39;;
    __END__
    foo at bzzzt line 201.

    % perl
    # line 200 &quot;bzzzt&quot;
    eval qq[\n#line 2001 &quot;&quot;\ndie &#39;foo&#39;]; print $@;
    __END__
    foo at - line 2001.

    % perl
    eval qq[\n#line 200 &quot;foo bar&quot;\ndie &#39;foo&#39;]; print $@;
    __END__
    foo at foo bar line 200.

    % perl
    # line 345 &quot;goop&quot;
    eval &quot;\n#line &quot; . __LINE__ . &#39; &quot;&#39; . __FILE__ .&quot;\&quot;\ndie &#39;foo&#39;&quot;;
    print $@;
    __END__
    foo at goop line 345.

=head2 Experimental Details on given and when

As previously mentioned, the &quot;switch&quot; feature is considered highly
experimental; it is subject to change with little notice.  In particular,
C&lt;when&gt; has tricky behaviours that are expected to change to become less
tricky in the future.  Do not rely upon its current (mis)implementation.
Before Perl 5.18, C&lt;given&gt; also had tricky behaviours that you should still
beware of if your code must run on older versions of Perl.

Here is a longer example of C&lt;given&gt;:

    use feature &quot;:5.10&quot;;
    given ($foo) {
        when (undef) {
            say &#39;$foo is undefined&#39;;
        }
        when (&quot;foo&quot;) {
            say &#39;$foo is the string &quot;foo&quot;&#39;;
        }
        when ([1,3,5,7,9]) {
            say &#39;$foo is an odd digit&#39;;
            continue; # Fall through
        }
        when ($_ &lt; 100) {
            say &#39;$foo is numerically less than 100&#39;;
        }
        when (\&amp;complicated_check) {
            say &#39;a complicated check for $foo is true&#39;;
        }
        default {
            die q(I don&#39;t know what to do with $foo);
        }
    }

Before Perl 5.18, C&lt;given(EXPR)&gt; assigned the value of I&lt;EXPR&gt; to
merely a lexically scoped I&lt;B&lt;copy&gt;&gt; (!) of C&lt;$_&gt;, not a dynamically
scoped alias the way C&lt;foreach&gt; does.  That made it similar to

        do { my $_ = EXPR; ... }

except that the block was automatically broken out of by a successful
C&lt;when&gt; or an explicit C&lt;break&gt;.  Because it was only a copy, and because
it was only lexically scoped, not dynamically scoped, you could not do the
things with it that you are used to in a C&lt;foreach&gt; loop.  In particular,
it did not work for arbitrary function calls if those functions might try
to access $_.  Best stick to C&lt;foreach&gt; for that.

Most of the power comes from the implicit smartmatching that can
sometimes apply.  Most of the time, C&lt;when(EXPR)&gt; is treated as an
implicit smartmatch of C&lt;$_&gt;, that is, C&lt;$_ ~~ EXPR&gt;.  (See
L&lt;perlop/&quot;Smartmatch Operator&quot;&gt; for more information on smartmatching.)
But when I&lt;EXPR&gt; is one of the 10 exceptional cases (or things like them)
listed below, it is used directly as a boolean.

=over 4

=item Z&lt;&gt;1.

A user-defined subroutine call or a method invocation.

=item Z&lt;&gt;2.

A regular expression match in the form of C&lt;/REGEX/&gt;, C&lt;$foo =~ /REGEX/&gt;,
or C&lt;$foo =~ EXPR&gt;.  Also, a negated regular expression match in
the form C&lt;!/REGEX/&gt;, C&lt;$foo !~ /REGEX/&gt;, or C&lt;$foo !~ EXPR&gt;.

=item Z&lt;&gt;3.

A smart match that uses an explicit C&lt;~~&gt; operator, such as C&lt;EXPR ~~ EXPR&gt;.

B&lt;NOTE:&gt; You will often have to use C&lt;$c ~~ $_&gt; because the default case
uses C&lt;$_ ~~ $c&gt; , which is frequentlythe opposite of what you want.

=item Z&lt;&gt;4.

A boolean comparison operator such as C&lt;$_ E&lt;lt&gt; 10&gt; or C&lt;$x eq &quot;abc&quot;&gt;.  The
relational operators that this applies to are the six numeric comparisons
(C&lt;&lt; &lt; &gt;&gt;, C&lt;&lt; &gt; &gt;&gt;, C&lt;&lt; &lt;= &gt;&gt;, C&lt;&lt; &gt;= &gt;&gt;, C&lt;&lt; == &gt;&gt;, and C&lt;&lt; != &gt;&gt;), and
the six string comparisons (C&lt;lt&gt;, C&lt;gt&gt;, C&lt;le&gt;, C&lt;ge&gt;, C&lt;eq&gt;, and C&lt;ne&gt;).

=item Z&lt;&gt;5.

At least the three builtin functions C&lt;defined(...)&gt;, C&lt;exists(...)&gt;, and
C&lt;eof(...)&gt;.  We might someday add more of these later if we think of them.

=item Z&lt;&gt;6.

A negated expression, whether C&lt;!(EXPR)&gt; or C&lt;not(EXPR)&gt;, or a logical
exclusive-or, C&lt;(EXPR1) xor (EXPR2)&gt;.  The bitwise versions (C&lt;~&gt; and C&lt;^&gt;)
are not included.

=item Z&lt;&gt;7.

A filetest operator, with exactly 4 exceptions: C&lt;-s&gt;, C&lt;-M&gt;, C&lt;-A&gt;, and
C&lt;-C&gt;, as these return numerical values, not boolean ones.  The C&lt;-z&gt;
filetest operator is not included in the exception list.

=item Z&lt;&gt;8.

The C&lt;..&gt; and C&lt;...&gt; flip-flop operators.  Note that the C&lt;...&gt; flip-flop
operator is completely different from the C&lt;...&gt; elliptical statement
just described.

=back

In those 8 cases above, the value of EXPR is used directly as a boolean, so
no smartmatching is done.  You may think of C&lt;when&gt; as a smartsmartmatch.

Furthermore, Perl inspects the operands of logical operators to
decide whether to use smartmatching for each one by applying the
above test to the operands:

=over 4

=item Z&lt;&gt;9.

If EXPR is C&lt;EXPR1 &amp;&amp; EXPR2&gt; or C&lt;EXPR1 and EXPR2&gt;, the test is applied
I&lt;recursively&gt; to both EXPR1 and EXPR2.
Only if I&lt;both&gt; operands also pass the
test, I&lt;recursively&gt;, will the expression be treated as boolean.  Otherwise,
smartmatching is used.

=item Z&lt;&gt;10.

If EXPR is C&lt;EXPR1 || EXPR2&gt;, C&lt;EXPR1 // EXPR2&gt;, or C&lt;EXPR1 or EXPR2&gt;, the
test is applied I&lt;recursively&gt; to EXPR1 only (which might itself be a
higher-precedence AND operator, for example, and thus subject to the
previous rule), not to EXPR2.  If EXPR1 is to use smartmatching, then EXPR2
also does so, no matter what EXPR2 contains.  But if EXPR2 does not get to
use smartmatching, then the second argument will not be either.  This is
quite different from the C&lt;&amp;&amp;&gt; case just described, so be careful.

=back

These rules are complicated, but the goal is for them to do what you want
(even if you don&#39;t quite understand why they are doing it).  For example:

    when (/^\d+$/ &amp;&amp; $_ &lt; 75) { ... }

will be treated as a boolean match because the rules say both
a regex match and an explicit test on C&lt;$_&gt; will be treated
as boolean.

Also:

    when ([qw(foo bar)] &amp;&amp; /baz/) { ... }

will use smartmatching because only I&lt;one&gt; of the operands is a boolean:
the other uses smartmatching, and that wins.

Further:

    when ([qw(foo bar)] || /^baz/) { ... }

will use smart matching (only the first operand is considered), whereas

    when (/^baz/ || [qw(foo bar)]) { ... }

will test only the regex, which causes both operands to be
treated as boolean.  Watch out for this one, then, because an
arrayref is always a true value, which makes it effectively
redundant.  Not a good idea.

Tautologous boolean operators are still going to be optimized
away.  Don&#39;t be tempted to write

    when (&quot;foo&quot; or &quot;bar&quot;) { ... }

This will optimize down to C&lt;&quot;foo&quot;&gt;, so C&lt;&quot;bar&quot;&gt; will never be considered (even
though the rules say to use a smartmatch
on C&lt;&quot;foo&quot;&gt;).  For an alternation like
this, an array ref will work, because this will instigate smartmatching:

    when ([qw(foo bar)] { ... }

This is somewhat equivalent to the C-style switch statement&#39;s fallthrough
functionality (not to be confused with I&lt;Perl&#39;s&gt; fallthrough
functionality--see below), wherein the same block is used for several
C&lt;case&gt; statements.

Another useful shortcut is that, if you use a literal array or hash as the
argument to C&lt;given&gt;, it is turned into a reference.  So C&lt;given(@foo)&gt; is
the same as C&lt;given(\@foo)&gt;, for example.

C&lt;default&gt; behaves exactly like C&lt;when(1 == 1)&gt;, which is
to say that it always matches.

=head3 Breaking out

You can use the C&lt;break&gt; keyword to break out of the enclosing
C&lt;given&gt; block.  Every C&lt;when&gt; block is implicitly ended with
a C&lt;break&gt;.

=head3 Fall-through

You can use the C&lt;continue&gt; keyword to fall through from one
case to the next immediate C&lt;when&gt; or C&lt;default&gt;:

    given($foo) {
        when (/x/) { say &#39;$foo contains an x&#39;; continue }
        when (/y/) { say &#39;$foo contains a y&#39;            }
        default    { say &#39;$foo does not contain a y&#39;    }
    }

=head3 Return value

When a C&lt;given&gt; statement is also a valid expression (for example,
when it&#39;s the last statement of a block), it evaluates to:

=over 4

=item *

An empty list as soon as an explicit C&lt;break&gt; is encountered.

=item *

The value of the last evaluated expression of the successful
C&lt;when&gt;/C&lt;default&gt; clause, if there happens to be one.

=item *

The value of the last evaluated expression of the C&lt;given&gt; block if no
condition is true.

=back

In both last cases, the last expression is evaluated in the context that
was applied to the C&lt;given&gt; block.

Note that, unlike C&lt;if&gt; and C&lt;unless&gt;, failed C&lt;when&gt; statements always
evaluate to an empty list.

    my $price = do {
        given ($item) {
            when ([&quot;pear&quot;, &quot;apple&quot;]) { 1 }
            break when &quot;vote&quot;;      # My vote cannot be bought
            1e10  when /Mona Lisa/;
            &quot;unknown&quot;;
        }
    };

Currently, C&lt;given&gt; blocks can&#39;t always
be used as proper expressions.  This
may be addressed in a future version of Perl.

=head3 Switching in a loop

Instead of using C&lt;given()&gt;, you can use a C&lt;foreach()&gt; loop.
For example, here&#39;s one way to count how many times a particular
string occurs in an array:

    use v5.10.1;
    my $count = 0;
    for (@array) {
        when (&quot;foo&quot;) { ++$count }
    }
    print &quot;\@array contains $count copies of &#39;foo&#39;\n&quot;;

Or in a more recent version:

    use v5.14;
    my $count = 0;
    for (@array) {
        ++$count when &quot;foo&quot;;
    }
    print &quot;\@array contains $count copies of &#39;foo&#39;\n&quot;;

At the end of all C&lt;when&gt; blocks, there is an implicit C&lt;next&gt;.
You can override that with an explicit C&lt;last&gt; if you&#39;re
interested in only the first match alone.

This doesn&#39;t work if you explicitly specify a loop variable, as
in C&lt;for $item (@array)&gt;.  You have to use the default variable C&lt;$_&gt;.

=head3 Differences from Perl 6

The Perl 5 smartmatch and C&lt;given&gt;/C&lt;when&gt; constructs are not compatible
with their Perl 6 analogues.  The most visible difference and least
important difference is that, in Perl 5, parentheses are required around
the argument to C&lt;given()&gt; and C&lt;when()&gt; (except when this last one is used
as a statement modifier).  Parentheses in Perl 6 are always optional in a
control construct such as C&lt;if()&gt;, C&lt;while()&gt;, or C&lt;when()&gt;; they can&#39;t be
made optional in Perl 5 without a great deal of potential confusion,
because Perl 5 would parse the expression

    given $foo {
        ...
    }

as though the argument to C&lt;given&gt; were an element of the hash
C&lt;%foo&gt;, interpreting the braces as hash-element syntax.

However, their are many, many other differences.  For example,
this works in Perl 5:

    use v5.12;
    my @primary = (&quot;red&quot;, &quot;blue&quot;, &quot;green&quot;);

    if (@primary ~~ &quot;red&quot;) {
        say &quot;primary smartmatches red&quot;;
    }

    if (&quot;red&quot; ~~ @primary) {
        say &quot;red smartmatches primary&quot;;
    }

    say &quot;that&#39;s all, folks!&quot;;

But it doesn&#39;t work at all in Perl 6.  Instead, you should
use the (parallelizable) C&lt;any&gt; operator:

   if any(@primary) eq &quot;red&quot; {
       say &quot;primary smartmatches red&quot;;
   }

   if &quot;red&quot; eq any(@primary) {
       say &quot;red smartmatches primary&quot;;
   }

The table of smartmatches in L&lt;perlop/&quot;Smartmatch Operator&quot;&gt; is not
identical to that proposed by the Perl 6 specification, mainly due to
differences between Perl 6&#39;s and Perl 5&#39;s data models, but also because
the Perl 6 spec has changed since Perl 5 rushed into early adoption.

In Perl 6, C&lt;when()&gt; will always do an implicit smartmatch with its
argument, while in Perl 5 it is convenient (albeit potentially confusing) to
suppress this implicit smartmatch in various rather loosely-defined
situations, as roughly outlined above.  (The difference is largely because
Perl 5 does not have, even internally, a boolean type.)

=cut
</pre>

                    </main>
                </div>

        </div>
        <footer>
        
        </footer>


    </body>
</html>

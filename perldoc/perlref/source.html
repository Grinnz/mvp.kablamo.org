<!DOCTYPE html>
<html>
    <head>
        <meta charset="utf-8">
        <meta content="width=device-width, initial-scale=1" name="viewport">
        <link href="/theme/css/normalize.css" rel="stylesheet">
        <!--<link rel="stylesheet" href="/theme/css/skeleton.css" />-->
        <link href="/theme/css/statocles-default.css" rel="stylesheet">
        <link href="//maxcdn.bootstrapcdn.com/font-awesome/4.3.0/css/font-awesome.min.css" rel="stylesheet">
        <script crossorigin="anonymous" integrity="sha256-hwg4gsxgFZhOsEEamdOYGBf13FyQuiTwlAQgxVSNgt4=" src="http://code.jquery.com/jquery-3.2.1.min.js"></script>
        <title>perlref (source) - Minimum Viable Perl</title>
        <meta content="Statocles 0.086" name="generator">
        
        <script>
            $( document ).ready(function() {
                $('.sub-menu ul').hide();
                $('.sub-menu.perldoc ul').show();
                $('.sub-menu.perldoc div').toggleClass("fa-caret-up fa-caret-down");
                $(".sub-menu a").click(function () {
                    $(this).parent(".sub-menu").siblings().children('ul').hide();
                    $(this).parent(".sub-menu").children("ul").slideToggle("100");
                    $(this).find(".right").toggleClass("fa-caret-up fa-caret-down");
                });
            });
        </script>
    </head>
    <body>
        <header>
        </header>
        <div class="container">

                <div class="left column full-height">
                    <div class="sidebar">
                    
<a class="home" href="/">Minimum<br>Viable<br>Perl</a><br><br><br>

<div class="animated bounceInDown">
  <ul>
    <li class="basics sub-menu">
      <a href="/perldoc/perlref/#basics">The basics<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/basics/hello">Hello world</a></li>
        <li><a href="/basics/variables">Variables</a></li>
        <li><a href="/basics/references">References</a></li>
        <li><a href="/basics/functions">Functions</a></li>
        <li><a href="/basics/flow">Conditionals</a></li>
        <li><a href="/basics/loops">Loops</a></li>
        <li><a href="/basics/exceptions">Exceptions</a></li>
        <li><a href="/basics/regexps">Regexps</a></li>
        <li><a href="/basics/files">Files</a></li>
      </ul>
    </li>
    <li class="oo sub-menu">
      <a href="/perldoc/perlref/#oo">Object oriented programming<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/oo/story">The (weird) story of Perl OO</a></li>
        <li><a href="/oo/classes">Classes</a></li>
        <li><a href="/oo/attributes">Attributes</a></li>
        <li><a href="/oo/modifiers">Method modifiers</a></li>
        <li><a href="/oo/roles">Roles</a></li>
        <li><a href="/oo/inheritance">Inheritance</a></li>
      </ul>
    </li>
    <li class="debugging sub-menu">
      <a href="/perldoc/perlref/#basics">Debugging<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/debugging/data-printer">Examining data structures</a></li>
        <li><a href="/debugging/stack-trace">Forcing stack traces</a></li>
      </ul>
    </li>
    <li class="dependencies sub-menu">
      <a href="/perldoc/perlref/#dependencies">Perl libraries<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/dependencies/perl5lib">Loading libraries from custom locations</a></li>
        <li><a href="/dependencies/cpanm">Installing libraries with cpanm</a></li>
        <li><a href="/dependencies/metacpan">Choosing libraries from MetaCPAN</a></li>
        <li><a href="/dependencies/carton">Managing project dependencies with Carton</a></li>
        <li><a href="/dependencies/pmtools">What version is installed and where?</a></li>
      </ul>
    </li>
    <li class="testing sub-menu">
      <a href="/perldoc/perlref/#testing">Testing<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/testing/intro">Introduction to testing</a></li>
        <li><a href="/testing/warnings">Testing warnings and exceptions</a></li>
        <li><a href="/testing/data">Testing data structures</a></li>
        <li><a href="/testing/mock">Testing with mock objects</a></li>
        <li><a href="/testing/libs">Build your own test libraries</a></li>
      </ul>
    </li>
    <li><a href="/perl">Perl design goals</a></li>
    <li><a href="/see-also">Book recommendations</a></li>
    <li><a href="/about">About this book</a></li>
  </ul>
</div>

                    
                    
                    </div>
                </div>

                <div class="right column">
                    <main>
                    <a class="button" href="/perldoc/perlref/">
    Back to documentation
</a>
<pre>=head1 NAME
X&lt;reference&gt; X&lt;pointer&gt; X&lt;data structure&gt; X&lt;structure&gt; X&lt;struct&gt;

perlref - Perl references and nested data structures

=head1 NOTE

This is complete documentation about all aspects of references.
For a shorter, tutorial introduction to just the essential features,
see L&lt;perlreftut&gt;.

=head1 DESCRIPTION

Before release 5 of Perl it was difficult to represent complex data
structures, because all references had to be symbolic--and even then
it was difficult to refer to a variable instead of a symbol table entry.
Perl now not only makes it easier to use symbolic references to variables,
but also lets you have &quot;hard&quot; references to any piece of data or code.
Any scalar may hold a hard reference.  Because arrays and hashes contain
scalars, you can now easily build arrays of arrays, arrays of hashes,
hashes of arrays, arrays of hashes of functions, and so on.

Hard references are smart--they keep track of reference counts for you,
automatically freeing the thing referred to when its reference count goes
to zero.  (Reference counts for values in self-referential or
cyclic data structures may not go to zero without a little help; see
L&lt;/&quot;Circular References&quot;&gt; for a detailed explanation.)
If that thing happens to be an object, the object is destructed.  See
L&lt;perlobj&gt; for more about objects.  (In a sense, everything in Perl is an
object, but we usually reserve the word for references to objects that
have been officially &quot;blessed&quot; into a class package.)

Symbolic references are names of variables or other objects, just as a
symbolic link in a Unix filesystem contains merely the name of a file.
The C&lt;*glob&gt; notation is something of a symbolic reference.  (Symbolic
references are sometimes called &quot;soft references&quot;, but please don&#39;t call
them that; references are confusing enough without useless synonyms.)
X&lt;reference, symbolic&gt; X&lt;reference, soft&gt;
X&lt;symbolic reference&gt; X&lt;soft reference&gt;

In contrast, hard references are more like hard links in a Unix file
system: They are used to access an underlying object without concern for
what its (other) name is.  When the word &quot;reference&quot; is used without an
adjective, as in the following paragraph, it is usually talking about a
hard reference.
X&lt;reference, hard&gt; X&lt;hard reference&gt;

References are easy to use in Perl.  There is just one overriding
principle: in general, Perl does no implicit referencing or dereferencing.
When a scalar is holding a reference, it always behaves as a simple scalar.
It doesn&#39;t magically start being an array or hash or subroutine; you have to
tell it explicitly to do so, by dereferencing it.

=head2 Making References
X&lt;reference, creation&gt; X&lt;referencing&gt;

References can be created in several ways.

=over 4

=item 1.
X&lt;\&gt; X&lt;backslash&gt;

By using the backslash operator on a variable, subroutine, or value.
(This works much like the &amp; (address-of) operator in C.)
This typically creates I&lt;another&gt; reference to a variable, because
there&#39;s already a reference to the variable in the symbol table.  But
the symbol table reference might go away, and you&#39;ll still have the
reference that the backslash returned.  Here are some examples:

    $scalarref = \$foo;
    $arrayref  = \@ARGV;
    $hashref   = \%ENV;
    $coderef   = \&amp;handler;
    $globref   = \*foo;

It isn&#39;t possible to create a true reference to an IO handle (filehandle
or dirhandle) using the backslash operator.  The most you can get is a
reference to a typeglob, which is actually a complete symbol table entry.
But see the explanation of the C&lt;*foo{THING}&gt; syntax below.  However,
you can still use type globs and globrefs as though they were IO handles.

=item 2.
X&lt;array, anonymous&gt; X&lt;[&gt; X&lt;[]&gt; X&lt;square bracket&gt;
X&lt;bracket, square&gt; X&lt;arrayref&gt; X&lt;array reference&gt; X&lt;reference, array&gt;

A reference to an anonymous array can be created using square
brackets:

    $arrayref = [1, 2, [&#39;a&#39;, &#39;b&#39;, &#39;c&#39;]];

Here we&#39;ve created a reference to an anonymous array of three elements
whose final element is itself a reference to another anonymous array of three
elements.  (The multidimensional syntax described later can be used to
access this.  For example, after the above, C&lt;&lt; $arrayref-&gt;[2][1] &gt;&gt; would have
the value &quot;b&quot;.)

Taking a reference to an enumerated list is not the same
as using square brackets--instead it&#39;s the same as creating
a list of references!

    @list = (\$a, \@b, \%c);
    @list = \($a, @b, %c);      # same thing!

As a special case, C&lt;\(@foo)&gt; returns a list of references to the contents
of C&lt;@foo&gt;, not a reference to C&lt;@foo&gt; itself.  Likewise for C&lt;%foo&gt;,
except that the key references are to copies (since the keys are just
strings rather than full-fledged scalars).

=item 3.
X&lt;hash, anonymous&gt; X&lt;{&gt; X&lt;{}&gt; X&lt;curly bracket&gt;
X&lt;bracket, curly&gt; X&lt;brace&gt; X&lt;hashref&gt; X&lt;hash reference&gt; X&lt;reference, hash&gt;

A reference to an anonymous hash can be created using curly
brackets:

    $hashref = {
        &#39;Adam&#39;  =&gt; &#39;Eve&#39;,
        &#39;Clyde&#39; =&gt; &#39;Bonnie&#39;,
    };

Anonymous hash and array composers like these can be intermixed freely to
produce as complicated a structure as you want.  The multidimensional
syntax described below works for these too.  The values above are
literals, but variables and expressions would work just as well, because
assignment operators in Perl (even within local() or my()) are executable
statements, not compile-time declarations.

Because curly brackets (braces) are used for several other things
including BLOCKs, you may occasionally have to disambiguate braces at the
beginning of a statement by putting a C&lt;+&gt; or a C&lt;return&gt; in front so
that Perl realizes the opening brace isn&#39;t starting a BLOCK.  The economy and
mnemonic value of using curlies is deemed worth this occasional extra
hassle.

For example, if you wanted a function to make a new hash and return a
reference to it, you have these options:

    sub hashem {        { @_ } }   # silently wrong
    sub hashem {       +{ @_ } }   # ok
    sub hashem { return { @_ } }   # ok

On the other hand, if you want the other meaning, you can do this:

    sub showem {        { @_ } }   # ambiguous (currently ok,
                                   # but may change)
    sub showem {       {; @_ } }   # ok
    sub showem { { return @_ } }   # ok

The leading C&lt;+{&gt; and C&lt;{;&gt; always serve to disambiguate
the expression to mean either the HASH reference, or the BLOCK.

=item 4.
X&lt;subroutine, anonymous&gt; X&lt;subroutine, reference&gt; X&lt;reference, subroutine&gt;
X&lt;scope, lexical&gt; X&lt;closure&gt; X&lt;lexical&gt; X&lt;lexical scope&gt;

A reference to an anonymous subroutine can be created by using
C&lt;sub&gt; without a subname:

    $coderef = sub { print &quot;Boink!\n&quot; };

Note the semicolon.  Except for the code
inside not being immediately executed, a C&lt;sub {}&gt; is not so much a
declaration as it is an operator, like C&lt;do{}&gt; or C&lt;eval{}&gt;.  (However, no
matter how many times you execute that particular line (unless you&#39;re in an
C&lt;eval(&quot;...&quot;)&gt;), $coderef will still have a reference to the I&lt;same&gt;
anonymous subroutine.)

Anonymous subroutines act as closures with respect to my() variables,
that is, variables lexically visible within the current scope.  Closure
is a notion out of the Lisp world that says if you define an anonymous
function in a particular lexical context, it pretends to run in that
context even when it&#39;s called outside the context.

In human terms, it&#39;s a funny way of passing arguments to a subroutine when
you define it as well as when you call it.  It&#39;s useful for setting up
little bits of code to run later, such as callbacks.  You can even
do object-oriented stuff with it, though Perl already provides a different
mechanism to do that--see L&lt;perlobj&gt;.

You might also think of closure as a way to write a subroutine
template without using eval().  Here&#39;s a small example of how
closures work:

    sub newprint {
        my $x = shift;
        return sub { my $y = shift; print &quot;$x, $y!\n&quot;; };
    }
    $h = newprint(&quot;Howdy&quot;);
    $g = newprint(&quot;Greetings&quot;);

    # Time passes...

    &amp;$h(&quot;world&quot;);
    &amp;$g(&quot;earthlings&quot;);

This prints

    Howdy, world!
    Greetings, earthlings!

Note particularly that $x continues to refer to the value passed
into newprint() I&lt;despite&gt; &quot;my $x&quot; having gone out of scope by the
time the anonymous subroutine runs.  That&#39;s what a closure is all
about.

This applies only to lexical variables, by the way.  Dynamic variables
continue to work as they have always worked.  Closure is not something
that most Perl programmers need trouble themselves about to begin with.

=item 5.
X&lt;constructor&gt; X&lt;new&gt;

References are often returned by special subroutines called constructors.  Perl
objects are just references to a special type of object that happens to know
which package it&#39;s associated with.  Constructors are just special subroutines
that know how to create that association.  They do so by starting with an
ordinary reference, and it remains an ordinary reference even while it&#39;s also
being an object.  Constructors are often named C&lt;new()&gt;.  You I&lt;can&gt; call them
indirectly:

    $objref = new Doggie( Tail =&gt; &#39;short&#39;, Ears =&gt; &#39;long&#39; );

But that can produce ambiguous syntax in certain cases, so it&#39;s often
better to use the direct method invocation approach:

    $objref   = Doggie-&gt;new(Tail =&gt; &#39;short&#39;, Ears =&gt; &#39;long&#39;);

    use Term::Cap;
    $terminal = Term::Cap-&gt;Tgetent( { OSPEED =&gt; 9600 });

    use Tk;
    $main    = MainWindow-&gt;new();
    $menubar = $main-&gt;Frame(-relief              =&gt; &quot;raised&quot;,
                            -borderwidth         =&gt; 2)

=item 6.
X&lt;autovivification&gt;

References of the appropriate type can spring into existence if you
dereference them in a context that assumes they exist.  Because we haven&#39;t
talked about dereferencing yet, we can&#39;t show you any examples yet.

=item 7.
X&lt;*foo{THING}&gt; X&lt;*&gt;

A reference can be created by using a special syntax, lovingly known as
the *foo{THING} syntax.  *foo{THING} returns a reference to the THING
slot in *foo (which is the symbol table entry which holds everything
known as foo).

    $scalarref = *foo{SCALAR};
    $arrayref  = *ARGV{ARRAY};
    $hashref   = *ENV{HASH};
    $coderef   = *handler{CODE};
    $ioref     = *STDIN{IO};
    $globref   = *foo{GLOB};
    $formatref = *foo{FORMAT};
    $globname  = *foo{NAME};    # &quot;foo&quot;
    $pkgname   = *foo{PACKAGE}; # &quot;main&quot;

Most of these are self-explanatory, but C&lt;*foo{IO}&gt;
deserves special attention.  It returns
the IO handle, used for file handles (L&lt;perlfunc/open&gt;), sockets
(L&lt;perlfunc/socket&gt; and L&lt;perlfunc/socketpair&gt;), and directory
handles (L&lt;perlfunc/opendir&gt;).  For compatibility with previous
versions of Perl, C&lt;*foo{FILEHANDLE}&gt; is a synonym for C&lt;*foo{IO}&gt;, though it
is discouraged, to encourage a consistent use of one name: IO.  On perls
between v5.8 and v5.22, it will issue a deprecation warning, but this
deprecation has since been rescinded.

C&lt;*foo{THING}&gt; returns undef if that particular THING hasn&#39;t been used yet,
except in the case of scalars.  C&lt;*foo{SCALAR}&gt; returns a reference to an
anonymous scalar if $foo hasn&#39;t been used yet.  This might change in a
future release.

C&lt;*foo{NAME}&gt; and C&lt;*foo{PACKAGE}&gt; are the exception, in that they return
strings, rather than references.  These return the package and name of the
typeglob itself, rather than one that has been assigned to it.  So, after
C&lt;*foo=*Foo::bar&gt;, C&lt;*foo&gt; will become &quot;*Foo::bar&quot; when used as a string,
but C&lt;*foo{PACKAGE}&gt; and C&lt;*foo{NAME}&gt; will continue to produce &quot;main&quot; and
&quot;foo&quot;, respectively.

C&lt;*foo{IO}&gt; is an alternative to the C&lt;*HANDLE&gt; mechanism given in
L&lt;perldata/&quot;Typeglobs and Filehandles&quot;&gt; for passing filehandles
into or out of subroutines, or storing into larger data structures.
Its disadvantage is that it won&#39;t create a new filehandle for you.
Its advantage is that you have less risk of clobbering more than
you want to with a typeglob assignment.  (It still conflates file
and directory handles, though.)  However, if you assign the incoming
value to a scalar instead of a typeglob as we do in the examples
below, there&#39;s no risk of that happening.

    splutter(*STDOUT);          # pass the whole glob
    splutter(*STDOUT{IO});      # pass both file and dir handles

    sub splutter {
        my $fh = shift;
        print $fh &quot;her um well a hmmm\n&quot;;
    }

    $rec = get_rec(*STDIN);     # pass the whole glob
    $rec = get_rec(*STDIN{IO}); # pass both file and dir handles

    sub get_rec {
        my $fh = shift;
        return scalar &lt;$fh&gt;;
    }

=back

=head2 Using References
X&lt;reference, use&gt; X&lt;dereferencing&gt; X&lt;dereference&gt;

That&#39;s it for creating references.  By now you&#39;re probably dying to
know how to use references to get back to your long-lost data.  There
are several basic methods.

=over 4

=item 1.

Anywhere you&#39;d put an identifier (or chain of identifiers) as part
of a variable or subroutine name, you can replace the identifier with
a simple scalar variable containing a reference of the correct type:

    $bar = $$scalarref;
    push(@$arrayref, $filename);
    $$arrayref[0] = &quot;January&quot;;
    $$hashref{&quot;KEY&quot;} = &quot;VALUE&quot;;
    &amp;$coderef(1,2,3);
    print $globref &quot;output\n&quot;;

It&#39;s important to understand that we are specifically I&lt;not&gt; dereferencing
C&lt;$arrayref[0]&gt; or C&lt;$hashref{&quot;KEY&quot;}&gt; there.  The dereference of the
scalar variable happens I&lt;before&gt; it does any key lookups.  Anything more
complicated than a simple scalar variable must use methods 2 or 3 below.
However, a &quot;simple scalar&quot; includes an identifier that itself uses method
1 recursively.  Therefore, the following prints &quot;howdy&quot;.

    $refrefref = \\\&quot;howdy&quot;;
    print $$$$refrefref;

=item 2.

Anywhere you&#39;d put an identifier (or chain of identifiers) as part of a
variable or subroutine name, you can replace the identifier with a
BLOCK returning a reference of the correct type.  In other words, the
previous examples could be written like this:

    $bar = ${$scalarref};
    push(@{$arrayref}, $filename);
    ${$arrayref}[0] = &quot;January&quot;;
    ${$hashref}{&quot;KEY&quot;} = &quot;VALUE&quot;;
    &amp;{$coderef}(1,2,3);
    $globref-&gt;print(&quot;output\n&quot;);  # iff IO::Handle is loaded

Admittedly, it&#39;s a little silly to use the curlies in this case, but
the BLOCK can contain any arbitrary expression, in particular,
subscripted expressions:

    &amp;{ $dispatch{$index} }(1,2,3);      # call correct routine

Because of being able to omit the curlies for the simple case of C&lt;$$x&gt;,
people often make the mistake of viewing the dereferencing symbols as
proper operators, and wonder about their precedence.  If they were,
though, you could use parentheses instead of braces.  That&#39;s not the case.
Consider the difference below; case 0 is a short-hand version of case 1,
I&lt;not&gt; case 2:

    $$hashref{&quot;KEY&quot;}   = &quot;VALUE&quot;;       # CASE 0
    ${$hashref}{&quot;KEY&quot;} = &quot;VALUE&quot;;       # CASE 1
    ${$hashref{&quot;KEY&quot;}} = &quot;VALUE&quot;;       # CASE 2
    ${$hashref-&gt;{&quot;KEY&quot;}} = &quot;VALUE&quot;;     # CASE 3

Case 2 is also deceptive in that you&#39;re accessing a variable
called %hashref, not dereferencing through $hashref to the hash
it&#39;s presumably referencing.  That would be case 3.

=item 3.

Subroutine calls and lookups of individual array elements arise often
enough that it gets cumbersome to use method 2.  As a form of
syntactic sugar, the examples for method 2 may be written:

    $arrayref-&gt;[0] = &quot;January&quot;;   # Array element
    $hashref-&gt;{&quot;KEY&quot;} = &quot;VALUE&quot;;  # Hash element
    $coderef-&gt;(1,2,3);            # Subroutine call

The left side of the arrow can be any expression returning a reference,
including a previous dereference.  Note that C&lt;$array[$x]&gt; is I&lt;not&gt; the
same thing as C&lt;&lt; $array-&gt;[$x] &gt;&gt; here:

    $array[$x]-&gt;{&quot;foo&quot;}-&gt;[0] = &quot;January&quot;;

This is one of the cases we mentioned earlier in which references could
spring into existence when in an lvalue context.  Before this
statement, C&lt;$array[$x]&gt; may have been undefined.  If so, it&#39;s
automatically defined with a hash reference so that we can look up
C&lt;{&quot;foo&quot;}&gt; in it.  Likewise C&lt;&lt; $array[$x]-&gt;{&quot;foo&quot;} &gt;&gt; will automatically get
defined with an array reference so that we can look up C&lt;[0]&gt; in it.
This process is called I&lt;autovivification&gt;.

One more thing here.  The arrow is optional I&lt;between&gt; brackets
subscripts, so you can shrink the above down to

    $array[$x]{&quot;foo&quot;}[0] = &quot;January&quot;;

Which, in the degenerate case of using only ordinary arrays, gives you
multidimensional arrays just like C&#39;s:

    $score[$x][$y][$z] += 42;

Well, okay, not entirely like C&#39;s arrays, actually.  C doesn&#39;t know how
to grow its arrays on demand.  Perl does.

=item 4.

If a reference happens to be a reference to an object, then there are
probably methods to access the things referred to, and you should probably
stick to those methods unless you&#39;re in the class package that defines the
object&#39;s methods.  In other words, be nice, and don&#39;t violate the object&#39;s
encapsulation without a very good reason.  Perl does not enforce
encapsulation.  We are not totalitarians here.  We do expect some basic
civility though.

=back

Using a string or number as a reference produces a symbolic reference,
as explained above.  Using a reference as a number produces an
integer representing its storage location in memory.  The only
useful thing to be done with this is to compare two references
numerically to see whether they refer to the same location.
X&lt;reference, numeric context&gt;

    if ($ref1 == $ref2) {  # cheap numeric compare of references
        print &quot;refs 1 and 2 refer to the same thing\n&quot;;
    }

Using a reference as a string produces both its referent&#39;s type,
including any package blessing as described in L&lt;perlobj&gt;, as well
as the numeric address expressed in hex.  The ref() operator returns
just the type of thing the reference is pointing to, without the
address.  See L&lt;perlfunc/ref&gt; for details and examples of its use.
X&lt;reference, string context&gt;

The bless() operator may be used to associate the object a reference
points to with a package functioning as an object class.  See L&lt;perlobj&gt;.

A typeglob may be dereferenced the same way a reference can, because
the dereference syntax always indicates the type of reference desired.
So C&lt;${*foo}&gt; and C&lt;${\$foo}&gt; both indicate the same scalar variable.

Here&#39;s a trick for interpolating a subroutine call into a string:

    print &quot;My sub returned @{[mysub(1,2,3)]} that time.\n&quot;;

The way it works is that when the C&lt;@{...}&gt; is seen in the double-quoted
string, it&#39;s evaluated as a block.  The block creates a reference to an
anonymous array containing the results of the call to C&lt;mysub(1,2,3)&gt;.  So
the whole block returns a reference to an array, which is then
dereferenced by C&lt;@{...}&gt; and stuck into the double-quoted string. This
chicanery is also useful for arbitrary expressions:

    print &quot;That yields @{[$n + 5]} widgets\n&quot;;

Similarly, an expression that returns a reference to a scalar can be
dereferenced via C&lt;${...}&gt;. Thus, the above expression may be written
as:

    print &quot;That yields ${\($n + 5)} widgets\n&quot;;

=head2 Circular References
X&lt;circular reference&gt; X&lt;reference, circular&gt;

It is possible to create a &quot;circular reference&quot; in Perl, which can lead
to memory leaks. A circular reference occurs when two references
contain a reference to each other, like this:

    my $foo = {};
    my $bar = { foo =&gt; $foo };
    $foo-&gt;{bar} = $bar;

You can also create a circular reference with a single variable:

    my $foo;
    $foo = \$foo;

In this case, the reference count for the variables will never reach 0,
and the references will never be garbage-collected. This can lead to
memory leaks.

Because objects in Perl are implemented as references, it&#39;s possible to
have circular references with objects as well. Imagine a TreeNode class
where each node references its parent and child nodes. Any node with a
parent will be part of a circular reference.

You can break circular references by creating a &quot;weak reference&quot;. A
weak reference does not increment the reference count for a variable,
which means that the object can go out of scope and be destroyed. You
can weaken a reference with the C&lt;weaken&gt; function exported by the
L&lt;Scalar::Util&gt; module.

Here&#39;s how we can make the first example safer:

    use Scalar::Util &#39;weaken&#39;;

    my $foo = {};
    my $bar = { foo =&gt; $foo };
    $foo-&gt;{bar} = $bar;

    weaken $foo-&gt;{bar};

The reference from C&lt;$foo&gt; to C&lt;$bar&gt; has been weakened. When the
C&lt;$bar&gt; variable goes out of scope, it will be garbage-collected. The
next time you look at the value of the C&lt;&lt; $foo-&gt;{bar} &gt;&gt; key, it will
be C&lt;undef&gt;.

This action at a distance can be confusing, so you should be careful
with your use of weaken. You should weaken the reference in the
variable that will go out of scope I&lt;first&gt;. That way, the longer-lived
variable will contain the expected reference until it goes out of
scope.

=head2 Symbolic references
X&lt;reference, symbolic&gt; X&lt;reference, soft&gt;
X&lt;symbolic reference&gt; X&lt;soft reference&gt;

We said that references spring into existence as necessary if they are
undefined, but we didn&#39;t say what happens if a value used as a
reference is already defined, but I&lt;isn&#39;t&gt; a hard reference.  If you
use it as a reference, it&#39;ll be treated as a symbolic
reference.  That is, the value of the scalar is taken to be the I&lt;name&gt;
of a variable, rather than a direct link to a (possibly) anonymous
value.

People frequently expect it to work like this.  So it does.

    $name = &quot;foo&quot;;
    $$name = 1;                 # Sets $foo
    ${$name} = 2;               # Sets $foo
    ${$name x 2} = 3;           # Sets $foofoo
    $name-&gt;[0] = 4;             # Sets $foo[0]
    @$name = ();                # Clears @foo
    &amp;$name();                   # Calls &amp;foo()
    $pack = &quot;THAT&quot;;
    ${&quot;${pack}::$name&quot;} = 5;    # Sets $THAT::foo without eval

This is powerful, and slightly dangerous, in that it&#39;s possible
to intend (with the utmost sincerity) to use a hard reference, and
accidentally use a symbolic reference instead.  To protect against
that, you can say

    use strict &#39;refs&#39;;

and then only hard references will be allowed for the rest of the enclosing
block.  An inner block may countermand that with

    no strict &#39;refs&#39;;

Only package variables (globals, even if localized) are visible to
symbolic references.  Lexical variables (declared with my()) aren&#39;t in
a symbol table, and thus are invisible to this mechanism.  For example:

    local $value = 10;
    $ref = &quot;value&quot;;
    {
        my $value = 20;
        print $$ref;
    }

This will still print 10, not 20.  Remember that local() affects package
variables, which are all &quot;global&quot; to the package.

=head2 Not-so-symbolic references

Brackets around a symbolic reference can simply
serve to isolate an identifier or variable name from the rest of an
expression, just as they always have within a string.  For example,

    $push = &quot;pop on &quot;;
    print &quot;${push}over&quot;;

has always meant to print &quot;pop on over&quot;, even though push is
a reserved word.  This is generalized to work the same
without the enclosing double quotes, so that

    print ${push} . &quot;over&quot;;

and even

    print ${ push } . &quot;over&quot;;

will have the same effect.  This
construct is I&lt;not&gt; considered to be a symbolic reference when you&#39;re
using strict refs:

    use strict &#39;refs&#39;;
    ${ bareword };      # Okay, means $bareword.
    ${ &quot;bareword&quot; };    # Error, symbolic reference.

Similarly, because of all the subscripting that is done using single words,
the same rule applies to any bareword that is used for subscripting a hash.
So now, instead of writing

    $array{ &quot;aaa&quot; }{ &quot;bbb&quot; }{ &quot;ccc&quot; }

you can write just

    $array{ aaa }{ bbb }{ ccc }

and not worry about whether the subscripts are reserved words.  In the
rare event that you do wish to do something like

    $array{ shift }

you can force interpretation as a reserved word by adding anything that
makes it more than a bareword:

    $array{ shift() }
    $array{ +shift }
    $array{ shift @_ }

The C&lt;use warnings&gt; pragma or the B&lt;-w&gt; switch will warn you if it
interprets a reserved word as a string.
But it will no longer warn you about using lowercase words, because the
string is effectively quoted.

=head2 Pseudo-hashes: Using an array as a hash
X&lt;pseudo-hash&gt; X&lt;pseudo hash&gt; X&lt;pseudohash&gt;

Pseudo-hashes have been removed from Perl.  The &#39;fields&#39; pragma
remains available.

=head2 Function Templates
X&lt;scope, lexical&gt; X&lt;closure&gt; X&lt;lexical&gt; X&lt;lexical scope&gt;
X&lt;subroutine, nested&gt; X&lt;sub, nested&gt; X&lt;subroutine, local&gt; X&lt;sub, local&gt;

As explained above, an anonymous function with access to the lexical
variables visible when that function was compiled, creates a closure.  It
retains access to those variables even though it doesn&#39;t get run until
later, such as in a signal handler or a Tk callback.

Using a closure as a function template allows us to generate many functions
that act similarly.  Suppose you wanted functions named after the colors
that generated HTML font changes for the various colors:

    print &quot;Be &quot;, red(&quot;careful&quot;), &quot;with that &quot;, green(&quot;light&quot;);

The red() and green() functions would be similar.  To create these,
we&#39;ll assign a closure to a typeglob of the name of the function we&#39;re
trying to build.

    @colors = qw(red blue green yellow orange purple violet);
    for my $name (@colors) {
        no strict &#39;refs&#39;;       # allow symbol table manipulation
        *$name = *{uc $name} = sub { &quot;&lt;FONT COLOR=&#39;$name&#39;&gt;@_&lt;/FONT&gt;&quot; };
    }

Now all those different functions appear to exist independently.  You can
call red(), RED(), blue(), BLUE(), green(), etc.  This technique saves on
both compile time and memory use, and is less error-prone as well, since
syntax checks happen at compile time.  It&#39;s critical that any variables in
the anonymous subroutine be lexicals in order to create a proper closure.
That&#39;s the reasons for the C&lt;my&gt; on the loop iteration variable.

This is one of the only places where giving a prototype to a closure makes
much sense.  If you wanted to impose scalar context on the arguments of
these functions (probably not a wise idea for this particular example),
you could have written it this way instead:

    *$name = sub ($) { &quot;&lt;FONT COLOR=&#39;$name&#39;&gt;$_[0]&lt;/FONT&gt;&quot; };

However, since prototype checking happens at compile time, the assignment
above happens too late to be of much use.  You could address this by
putting the whole loop of assignments within a BEGIN block, forcing it
to occur during compilation.

Access to lexicals that change over time--like those in the C&lt;for&gt; loop
above, basically aliases to elements from the surrounding lexical scopes--
only works with anonymous subs, not with named subroutines. Generally
said, named subroutines do not nest properly and should only be declared
in the main package scope.

This is because named subroutines are created at compile time so their
lexical variables get assigned to the parent lexicals from the first
execution of the parent block. If a parent scope is entered a second
time, its lexicals are created again, while the nested subs still
reference the old ones.

Anonymous subroutines get to capture each time you execute the C&lt;sub&gt;
operator, as they are created on the fly. If you are accustomed to using
nested subroutines in other programming languages with their own private
variables, you&#39;ll have to work at it a bit in Perl.  The intuitive coding
of this type of thing incurs mysterious warnings about &quot;will not stay
shared&quot; due to the reasons explained above.
For example, this won&#39;t work:

    sub outer {
        my $x = $_[0] + 35;
        sub inner { return $x * 19 }   # WRONG
        return $x + inner();
    }

A work-around is the following:

    sub outer {
        my $x = $_[0] + 35;
        local *inner = sub { return $x * 19 };
        return $x + inner();
    }

Now inner() can only be called from within outer(), because of the
temporary assignments of the anonymous subroutine. But when it does,
it has normal access to the lexical variable $x from the scope of
outer() at the time outer is invoked.

This has the interesting effect of creating a function local to another
function, something not normally supported in Perl.

=head1 WARNING: Don&#39;t use references as hash keys
X&lt;reference, string context&gt; X&lt;reference, use as hash key&gt;

You may not (usefully) use a reference as the key to a hash.  It will be
converted into a string:

    $x{ \$a } = $a;

If you try to dereference the key, it won&#39;t do a hard dereference, and
you won&#39;t accomplish what you&#39;re attempting.  You might want to do something
more like

    $r = \@a;
    $x{ $r } = $r;

And then at least you can use the values(), which will be
real refs, instead of the keys(), which won&#39;t.

The standard Tie::RefHash module provides a convenient workaround to this.

=head2 Postfix Dereference Syntax

Beginning in v5.20.0, a postfix syntax for using references is
available.  It behaves as described in L&lt;/Using References&gt;, but instead
of a prefixed sigil, a postfixed sigil-and-star is used.

For example:

    $r = \@a;
    @b = $r-&gt;@*; # equivalent to @$r or @{ $r }

    $r = [ 1, [ 2, 3 ], 4 ];
    $r-&gt;[1]-&gt;@*;  # equivalent to @{ $r-&gt;[1] }

In Perl 5.20 and 5.22, this syntax must be enabled with C&lt;use feature
&#39;postderef&#39;&gt;. As of Perl 5.24, no feature declarations are required to make
it available.

Postfix dereference should work in all circumstances where block
(circumfix) dereference worked, and should be entirely equivalent.  This
syntax allows dereferencing to be written and read entirely
left-to-right.  The following equivalencies are defined:

  $sref-&gt;$*;  # same as  ${ $sref }
  $aref-&gt;@*;  # same as  @{ $aref }
  $aref-&gt;$#*; # same as $#{ $aref }
  $href-&gt;%*;  # same as  %{ $href }
  $cref-&gt;&amp;*;  # same as  &amp;{ $cref }
  $gref-&gt;**;  # same as  *{ $gref }

Note especially that C&lt;&lt; $cref-&gt;&amp;* &gt;&gt; is I&lt;not&gt; equivalent to C&lt;&lt;
$cref-&gt;() &gt;&gt;, and can serve different purposes.

Glob elements can be extracted through the postfix dereferencing feature:

  $gref-&gt;*{SCALAR}; # same as *{ $gref }{SCALAR}

Postfix array and scalar dereferencing I&lt;can&gt; be used in interpolating
strings (double quotes or the C&lt;qq&gt; operator), but only if the
C&lt;postderef_qq&gt; feature is enabled.

=head2 Postfix Reference Slicing

Value slices of arrays and hashes may also be taken with postfix
dereferencing notation, with the following equivalencies:

  $aref-&gt;@[ ... ];  # same as @$aref[ ... ]
  $href-&gt;@{ ... };  # same as @$href{ ... }

Postfix key/value pair slicing, added in 5.20.0 and documented in
L&lt;the KeyE&lt;sol&gt;Value Hash Slices section of perldata|perldata/&quot;Key/Value Hash
Slices&quot;&gt;, also behaves as expected:

  $aref-&gt;%[ ... ];  # same as %$aref[ ... ]
  $href-&gt;%{ ... };  # same as %$href{ ... }

As with postfix array, postfix value slice dereferencing I&lt;can&gt; be used
in interpolating strings (double quotes or the C&lt;qq&gt; operator), but only
if the C&lt;postderef_qq&gt; L&lt;feature&gt; is enabled.

=head2 Assigning to References

Beginning in v5.22.0, the referencing operator can be assigned to.  It
performs an aliasing operation, so that the variable name referenced on the
left-hand side becomes an alias for the thing referenced on the right-hand
side:

    \$a = \$b; # $a and $b now point to the same scalar
    \&amp;foo = \&amp;bar; # foo() now means bar()

This syntax must be enabled with C&lt;use feature &#39;refaliasing&#39;&gt;.  It is
experimental, and will warn by default unless C&lt;no warnings
&#39;experimental::refaliasing&#39;&gt; is in effect.

These forms may be assigned to, and cause the right-hand side to be
evaluated in scalar context:

    \$scalar
    \@array
    \%hash
    \&amp;sub
    \my $scalar
    \my @array
    \my %hash
    \state $scalar # or @array, etc.
    \our $scalar   # etc.
    \local $scalar # etc.
    \local our $scalar # etc.
    \$some_array[$index]
    \$some_hash{$key}
    \local $some_array[$index]
    \local $some_hash{$key}
    condition ? \$this : \$that[0] # etc.

Slicing operations and parentheses cause
the right-hand side to be evaluated in
list context:

    \@array[5..7]
    (\@array[5..7])
    \(@array[5..7])
    \@hash{&#39;foo&#39;,&#39;bar&#39;}
    (\@hash{&#39;foo&#39;,&#39;bar&#39;})
    \(@hash{&#39;foo&#39;,&#39;bar&#39;})
    (\$scalar)
    \($scalar)
    \(my $scalar)
    \my($scalar)
    (\@array)
    (\%hash)
    (\&amp;sub)
    \(&amp;sub)
    \($foo, @bar, %baz)
    (\$foo, \@bar, \%baz)

Each element on the right-hand side must be a reference to a datum of the
right type.  Parentheses immediately surrounding an array (and possibly
also C&lt;my&gt;/C&lt;state&gt;/C&lt;our&gt;/C&lt;local&gt;) will make each element of the array an
alias to the corresponding scalar referenced on the right-hand side:

    \(@a) = \(@b); # @a and @b now have the same elements
    \my(@a) = \(@b); # likewise
    \(my @a) = \(@b); # likewise
    push @a, 3; # but now @a has an extra element that @b lacks
    \(@a) = (\$a, \$b, \$c); # @a now contains $a, $b, and $c

Combining that form with C&lt;local&gt; and putting parentheses immediately
around a hash are forbidden (because it is not clear what they should do):

    \local(@array) = foo(); # WRONG
    \(%hash)       = bar(); # wRONG

Assignment to references and non-references may be combined in lists and
conditional ternary expressions, as long as the values on the right-hand
side are the right type for each element on the left, though this may make
for obfuscated code:

    (my $tom, \my $dick, \my @harry) = (\1, \2, [1..3]);
    # $tom is now \1
    # $dick is now 2 (read-only)
    # @harry is (1,2,3)

    my $type = ref $thingy;
    ($type ? $type eq &#39;ARRAY&#39; ? \@foo : \$bar : $baz) = $thingy;

The C&lt;foreach&gt; loop can also take a reference constructor for its loop
variable, though the syntax is limited to one of the following, with an
optional C&lt;my&gt;, C&lt;state&gt;, or C&lt;our&gt; after the backslash:

    \$s
    \@a
    \%h
    \&amp;c

No parentheses are permitted.  This feature is particularly useful for
arrays-of-arrays, or arrays-of-hashes:

    foreach \my @a (@array_of_arrays) {
        frobnicate($a[0], $a[-1]);
    }

    foreach \my %h (@array_of_hashes) {
        $h{gelastic}++ if $h{type} eq &#39;funny&#39;;
    }

B&lt;CAVEAT:&gt; Aliasing does not work correctly with closures.  If you try to
alias lexical variables from an inner subroutine or C&lt;eval&gt;, the aliasing
will only be visible within that inner sub, and will not affect the outer
subroutine where the variables are declared.  This bizarre behavior is
subject to change.

=head1 Declaring a Reference to a Variable

Beginning in v5.26.0, the referencing operator can come after C&lt;my&gt;,
C&lt;state&gt;, C&lt;our&gt;, or C&lt;local&gt;.  This syntax must be enabled with C&lt;use
feature &#39;declared_refs&#39;&gt;.  It is experimental, and will warn by default
unless C&lt;no warnings &#39;experimental::refaliasing&#39;&gt; is in effect.

This feature makes these:

    my \$x;
    our \$y;

equivalent to:

    \my $x;
    \our $x;

It is intended mainly for use in assignments to references (see
L&lt;/Assigning to References&gt;, above).  It also allows the backslash to be
used on just some items in a list of declared variables:

    my ($foo, \@bar, \%baz); # equivalent to:  my $foo, \my(@bar, %baz);

=head1 SEE ALSO

Besides the obvious documents, source code can be instructive.
Some pathological examples of the use of references can be found
in the F&lt;t/op/ref.t&gt; regression test in the Perl source directory.

See also L&lt;perldsc&gt; and L&lt;perllol&gt; for how to use references to create
complex data structures, and L&lt;perlootut&gt; and L&lt;perlobj&gt;
for how to use them to create objects.
</pre>

                    </main>
                </div>

        </div>
        <footer>
        
        </footer>


    </body>
</html>

<!DOCTYPE html>
<html>
    <head>
        <meta charset="utf-8">
        <meta content="width=device-width, initial-scale=1" name="viewport">
        <link href="/theme/css/normalize.css" rel="stylesheet">
        <!--<link rel="stylesheet" href="/theme/css/skeleton.css" />-->
        <link href="/theme/css/statocles-default.css" rel="stylesheet">
        <link href="//maxcdn.bootstrapcdn.com/font-awesome/4.3.0/css/font-awesome.min.css" rel="stylesheet">
        <script crossorigin="anonymous" integrity="sha256-hwg4gsxgFZhOsEEamdOYGBf13FyQuiTwlAQgxVSNgt4=" src="http://code.jquery.com/jquery-3.2.1.min.js"></script>
        <title>perlretut (source) - Minimum Viable Perl</title>
        <meta content="Statocles 0.086" name="generator">
        
        <script>
            $( document ).ready(function() {
                $('.sub-menu ul').hide();
                $('.sub-menu.perldoc ul').show();
                $('.sub-menu.perldoc div').toggleClass("fa-caret-up fa-caret-down");
                $(".sub-menu a").click(function () {
                    $(this).parent(".sub-menu").siblings().children('ul').hide();
                    $(this).parent(".sub-menu").children("ul").slideToggle("100");
                    $(this).find(".right").toggleClass("fa-caret-up fa-caret-down");
                });
            });
        </script>
        <!-- Global site tag (gtag.js) - Google Analytics -->
        <script async src="https://www.googletagmanager.com/gtag/js?id=UA-110455056-1"></script>
        <script>
            window.dataLayer = window.dataLayer || [];
            function gtag(){dataLayer.push(arguments);}
            gtag('js', new Date());
            gtag('config', 'UA-110455056-1');
        </script>
    </head>
    <body>
        <header>
        </header>
        <div class="container">

                <div class="left column full-height">
                    <div class="sidebar">
                    
<a class="home" href="/">Minimum<br>Viable<br>Perl</a><br><br><br>

<div class="animated bounceInDown">
  <ul>
    <li class="basics sub-menu">
      <a href="/perldoc/perlretut/#basics">The basics<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/basics/hello">Hello world</a></li>
        <li><a href="/basics/variables">Variables</a></li>
        <li><a href="/basics/references">References</a></li>
        <li><a href="/basics/functions">Functions</a></li>
        <li><a href="/basics/flow">Conditionals</a></li>
        <li><a href="/basics/loops">Loops</a></li>
        <li><a href="/basics/exceptions">Exceptions</a></li>
        <li><a href="/basics/regexps">Regexps</a></li>
        <li><a href="/basics/files">Files</a></li>
      </ul>
    </li>
    <li class="oo sub-menu">
      <a href="/perldoc/perlretut/#oo">Object oriented programming<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/oo/story">The (weird) story of Perl OO</a></li>
        <li><a href="/oo/classes">Classes</a></li>
        <li><a href="/oo/attributes">Attributes</a></li>
        <li><a href="/oo/modifiers">Method modifiers</a></li>
        <li><a href="/oo/roles">Roles</a></li>
        <li><a href="/oo/inheritance">Inheritance</a></li>
      </ul>
    </li>
    <li class="debugging sub-menu">
      <a href="/perldoc/perlretut/#basics">Debugging<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/debugging/data-printer">Examining data structures</a></li>
        <li><a href="/debugging/stack-trace">Forcing stack traces</a></li>
      </ul>
    </li>
    <li class="dependencies sub-menu">
      <a href="/perldoc/perlretut/#dependencies">Perl libraries<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/dependencies/perl5lib">Loading libraries from custom locations</a></li>
        <li><a href="/dependencies/cpanm">Installing libraries with cpanm</a></li>
        <li><a href="/dependencies/metacpan">Choosing libraries from MetaCPAN</a></li>
        <li><a href="/dependencies/carton">Managing project dependencies with Carton</a></li>
        <li><a href="/dependencies/pmtools">What version is installed and where?</a></li>
      </ul>
    </li>
    <li class="testing sub-menu">
      <a href="/perldoc/perlretut/#testing">Testing<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/testing/intro">Introduction to testing</a></li>
        <li><a href="/testing/warnings">Testing warnings and exceptions</a></li>
        <li><a href="/testing/data">Testing data structures</a></li>
        <li><a href="/testing/mock">Testing with mock objects</a></li>
        <li><a href="/testing/libs">Build your own test libraries</a></li>
      </ul>
    </li>
    <li><a href="/perl">Perl design goals</a></li>
    <li><a href="/see-also">Book recommendations</a></li>
    <li><a href="/about">About this book</a></li>
  </ul>
</div>

                    
                    
                    </div>
                </div>

                <div class="right column">
                    <main>
                    <a class="button" href="/perldoc/perlretut/">
    Back to documentation
</a>
<pre>=head1 NAME

perlretut - Perl regular expressions tutorial

=head1 DESCRIPTION

This page provides a basic tutorial on understanding, creating and
using regular expressions in Perl.  It serves as a complement to the
reference page on regular expressions L&lt;perlre&gt;.  Regular expressions
are an integral part of the C&lt;m//&gt;, C&lt;s///&gt;, C&lt;qr//&gt; and C&lt;split&gt;
operators and so this tutorial also overlaps with
L&lt;perlop/&quot;Regexp Quote-Like Operators&quot;&gt; and L&lt;perlfunc/split&gt;.

Perl is widely renowned for excellence in text processing, and regular
expressions are one of the big factors behind this fame.  Perl regular
expressions display an efficiency and flexibility unknown in most
other computer languages.  Mastering even the basics of regular
expressions will allow you to manipulate text with surprising ease.

What is a regular expression?  At its most basic, a regular expression
is a template that is used to determine if a string has certain
characteristics.  The string is most often some text, such as a line,
sentence, web page, or even a whole book, but less commonly it could be
some binary data as well.
Suppose we want to determine if the text in variable, C&lt;$var&gt; contains
the sequence of characters S&lt;C&lt;m u s h r o o m&gt;&gt;
(blanks added for legibility).  We can write in Perl

 $var =~ m/mushroom/

The value of this expression will be TRUE if C&lt;$var&gt; contains that
sequence of characters, and FALSE otherwise.  The portion enclosed in
C&lt;&#39;E&lt;sol&gt;&#39;&gt; characters denotes the characteristic we are looking for.
We use the term I&lt;pattern&gt; for it.  The process of looking to see if the
pattern occurs in the string is called I&lt;matching&gt;, and the C&lt;&quot;=~&quot;&gt;
operator along with the C&lt;m//&gt; tell Perl to try to match the pattern
against the string.  Note that the pattern is also a string, but a very
special kind of one, as we will see.  Patterns are in common use these
days;
examples are the patterns typed into a search engine to find web pages
and the patterns used to list files in a directory, I&lt;e.g.&gt;, &quot;C&lt;ls *.txt&gt;&quot;
or &quot;C&lt;dir *.*&gt;&quot;.  In Perl, the patterns described by regular expressions
are used not only to search strings, but to also extract desired parts
of strings, and to do search and replace operations.

Regular expressions have the undeserved reputation of being abstract
and difficult to understand.  This really stems simply because the
notation used to express them tends to be terse and dense, and not
because of inherent complexity.  We recommend using the C&lt;/x&gt; regular
expression modifier (described below) along with plenty of white space
to make them less dense, and easier to read.  Regular expressions are
constructed using
simple concepts like conditionals and loops and are no more difficult
to understand than the corresponding C&lt;if&gt; conditionals and C&lt;while&gt;
loops in the Perl language itself.

This tutorial flattens the learning curve by discussing regular
expression concepts, along with their notation, one at a time and with
many examples.  The first part of the tutorial will progress from the
simplest word searches to the basic regular expression concepts.  If
you master the first part, you will have all the tools needed to solve
about 98% of your needs.  The second part of the tutorial is for those
comfortable with the basics and hungry for more power tools.  It
discusses the more advanced regular expression operators and
introduces the latest cutting-edge innovations.

A note: to save time, &quot;regular expression&quot; is often abbreviated as
regexp or regex.  Regexp is a more natural abbreviation than regex, but
is harder to pronounce.  The Perl pod documentation is evenly split on
regexp vs regex; in Perl, there is more than one way to abbreviate it.
We&#39;ll use regexp in this tutorial.

New in v5.22, L&lt;C&lt;use re &#39;strict&#39;&gt;|re/&#39;strict&#39; mode&gt; applies stricter
rules than otherwise when compiling regular expression patterns.  It can
find things that, while legal, may not be what you intended.

=head1 Part 1: The basics

=head2 Simple word matching

The simplest regexp is simply a word, or more generally, a string of
characters.  A regexp consisting of just a word matches any string that
contains that word:

    &quot;Hello World&quot; =~ /World/;  # matches

What is this Perl statement all about? C&lt;&quot;Hello World&quot;&gt; is a simple
double-quoted string.  C&lt;World&gt; is the regular expression and the
C&lt;//&gt; enclosing C&lt;/World/&gt; tells Perl to search a string for a match.
The operator C&lt;=~&gt; associates the string with the regexp match and
produces a true value if the regexp matched, or false if the regexp
did not match.  In our case, C&lt;World&gt; matches the second word in
C&lt;&quot;Hello World&quot;&gt;, so the expression is true.  Expressions like this
are useful in conditionals:

    if (&quot;Hello World&quot; =~ /World/) {
        print &quot;It matches\n&quot;;
    }
    else {
        print &quot;It doesn&#39;t match\n&quot;;
    }

There are useful variations on this theme.  The sense of the match can
be reversed by using the C&lt;!~&gt; operator:

    if (&quot;Hello World&quot; !~ /World/) {
        print &quot;It doesn&#39;t match\n&quot;;
    }
    else {
        print &quot;It matches\n&quot;;
    }

The literal string in the regexp can be replaced by a variable:

    my $greeting = &quot;World&quot;;
    if (&quot;Hello World&quot; =~ /$greeting/) {
        print &quot;It matches\n&quot;;
    }
    else {
        print &quot;It doesn&#39;t match\n&quot;;
    }

If you&#39;re matching against the special default variable C&lt;$_&gt;, the
C&lt;$_ =~&gt; part can be omitted:

    $_ = &quot;Hello World&quot;;
    if (/World/) {
        print &quot;It matches\n&quot;;
    }
    else {
        print &quot;It doesn&#39;t match\n&quot;;
    }

And finally, the C&lt;//&gt; default delimiters for a match can be changed
to arbitrary delimiters by putting an C&lt;&#39;m&#39;&gt; out front:

    &quot;Hello World&quot; =~ m!World!;   # matches, delimited by &#39;!&#39;
    &quot;Hello World&quot; =~ m{World};   # matches, note the matching &#39;{}&#39;
    &quot;/usr/bin/perl&quot; =~ m&quot;/perl&quot;; # matches after &#39;/usr/bin&#39;,
                                 # &#39;/&#39; becomes an ordinary char

C&lt;/World/&gt;, C&lt;m!World!&gt;, and C&lt;m{World}&gt; all represent the
same thing.  When, I&lt;e.g.&gt;, the quote (C&lt;&#39;&quot;&#39;&gt;) is used as a delimiter, the forward
slash C&lt;&#39;/&#39;&gt; becomes an ordinary character and can be used in this regexp
without trouble.

Let&#39;s consider how different regexps would match C&lt;&quot;Hello World&quot;&gt;:

    &quot;Hello World&quot; =~ /world/;  # doesn&#39;t match
    &quot;Hello World&quot; =~ /o W/;    # matches
    &quot;Hello World&quot; =~ /oW/;     # doesn&#39;t match
    &quot;Hello World&quot; =~ /World /; # doesn&#39;t match

The first regexp C&lt;world&gt; doesn&#39;t match because regexps are
case-sensitive.  The second regexp matches because the substring
S&lt;C&lt;&#39;o W&#39;&gt;&gt; occurs in the string S&lt;C&lt;&quot;Hello World&quot;&gt;&gt;.  The space
character C&lt;&#39; &#39;&gt; is treated like any other character in a regexp and is
needed to match in this case.  The lack of a space character is the
reason the third regexp C&lt;&#39;oW&#39;&gt; doesn&#39;t match.  The fourth regexp
&quot;C&lt;World &gt;&quot; doesn&#39;t match because there is a space at the end of the
regexp, but not at the end of the string.  The lesson here is that
regexps must match a part of the string I&lt;exactly&gt; in order for the
statement to be true.

If a regexp matches in more than one place in the string, Perl will
always match at the earliest possible point in the string:

    &quot;Hello World&quot; =~ /o/;       # matches &#39;o&#39; in &#39;Hello&#39;
    &quot;That hat is red&quot; =~ /hat/; # matches &#39;hat&#39; in &#39;That&#39;

With respect to character matching, there are a few more points you
need to know about.   First of all, not all characters can be used &quot;as
is&quot; in a match.  Some characters, called I&lt;metacharacters&gt;, are reserved
for use in regexp notation.  The metacharacters are

    {}[]()^$.|*+?-\

The significance of each of these will be explained
in the rest of the tutorial, but for now, it is important only to know
that a metacharacter can be matched as-is by putting a backslash before
it:

    &quot;2+2=4&quot; =~ /2+2/;    # doesn&#39;t match, + is a metacharacter
    &quot;2+2=4&quot; =~ /2\+2/;   # matches, \+ is treated like an ordinary +
    &quot;The interval is [0,1).&quot; =~ /[0,1)./     # is a syntax error!
    &quot;The interval is [0,1).&quot; =~ /\[0,1\)\./  # matches
    &quot;#!/usr/bin/perl&quot; =~ /#!\/usr\/bin\/perl/;  # matches

In the last regexp, the forward slash C&lt;&#39;/&#39;&gt; is also backslashed,
because it is used to delimit the regexp.  This can lead to LTS
(leaning toothpick syndrome), however, and it is often more readable
to change delimiters.

    &quot;#!/usr/bin/perl&quot; =~ m!#\!/usr/bin/perl!;  # easier to read

The backslash character C&lt;&#39;\&#39;&gt; is a metacharacter itself and needs to
be backslashed:

    &#39;C:\WIN32&#39; =~ /C:\\WIN/;   # matches

In situations where it doesn&#39;t make sense for a particular metacharacter
to mean what it normally does, it automatically loses its
metacharacter-ness and becomes an ordinary character that is to be
matched literally.  For example, the C&lt;&#39;}&#39;&gt; is a metacharacter only when
it is the mate of a C&lt;&#39;{&#39;&gt; metacharacter.  Otherwise it is treated as a
literal RIGHT CURLY BRACKET.  This may lead to unexpected results.
L&lt;C&lt;use re &#39;strict&#39;&gt;|re/&#39;strict&#39; mode&gt; can catch some of these.

In addition to the metacharacters, there are some ASCII characters
which don&#39;t have printable character equivalents and are instead
represented by I&lt;escape sequences&gt;.  Common examples are C&lt;\t&gt; for a
tab, C&lt;\n&gt; for a newline, C&lt;\r&gt; for a carriage return and C&lt;\a&gt; for a
bell (or alert).  If your string is better thought of as a sequence of arbitrary
bytes, the octal escape sequence, I&lt;e.g.&gt;, C&lt;\033&gt;, or hexadecimal escape
sequence, I&lt;e.g.&gt;, C&lt;\x1B&gt; may be a more natural representation for your
bytes.  Here are some examples of escapes:

    &quot;1000\t2000&quot; =~ m(0\t2)   # matches
    &quot;1000\n2000&quot; =~ /0\n20/   # matches
    &quot;1000\t2000&quot; =~ /\000\t2/ # doesn&#39;t match, &quot;0&quot; ne &quot;\000&quot;
    &quot;cat&quot;   =~ /\o{143}\x61\x74/ # matches in ASCII, but a weird way
                                 # to spell cat

If you&#39;ve been around Perl a while, all this talk of escape sequences
may seem familiar.  Similar escape sequences are used in double-quoted
strings and in fact the regexps in Perl are mostly treated as
double-quoted strings.  This means that variables can be used in
regexps as well.  Just like double-quoted strings, the values of the
variables in the regexp will be substituted in before the regexp is
evaluated for matching purposes.  So we have:

    $foo = &#39;house&#39;;
    &#39;housecat&#39; =~ /$foo/;      # matches
    &#39;cathouse&#39; =~ /cat$foo/;   # matches
    &#39;housecat&#39; =~ /${foo}cat/; # matches

So far, so good.  With the knowledge above you can already perform
searches with just about any literal string regexp you can dream up.
Here is a I&lt;very simple&gt; emulation of the Unix grep program:

    % cat &gt; simple_grep
    #!/usr/bin/perl
    $regexp = shift;
    while (&lt;&gt;) {
        print if /$regexp/;
    }
    ^D

    % chmod +x simple_grep

    % simple_grep abba /usr/dict/words
    Babbage
    cabbage
    cabbages
    sabbath
    Sabbathize
    Sabbathizes
    sabbatical
    scabbard
    scabbards

This program is easy to understand.  C&lt;#!/usr/bin/perl&gt; is the standard
way to invoke a perl program from the shell.
S&lt;C&lt;$regexp = shift;&gt;&gt; saves the first command line argument as the
regexp to be used, leaving the rest of the command line arguments to
be treated as files.  S&lt;C&lt;&lt; while (&lt;&gt;) &gt;&gt;&gt; loops over all the lines in
all the files.  For each line, S&lt;C&lt;print if /$regexp/;&gt;&gt; prints the
line if the regexp matches the line.  In this line, both C&lt;print&gt; and
C&lt;/$regexp/&gt; use the default variable C&lt;$_&gt; implicitly.

With all of the regexps above, if the regexp matched anywhere in the
string, it was considered a match.  Sometimes, however, we&#39;d like to
specify I&lt;where&gt; in the string the regexp should try to match.  To do
this, we would use the I&lt;anchor&gt; metacharacters C&lt;&#39;^&#39;&gt; and C&lt;&#39;$&#39;&gt;.  The
anchor C&lt;&#39;^&#39;&gt; means match at the beginning of the string and the anchor
C&lt;&#39;$&#39;&gt; means match at the end of the string, or before a newline at the
end of the string.  Here is how they are used:

    &quot;housekeeper&quot; =~ /keeper/;    # matches
    &quot;housekeeper&quot; =~ /^keeper/;   # doesn&#39;t match
    &quot;housekeeper&quot; =~ /keeper$/;   # matches
    &quot;housekeeper\n&quot; =~ /keeper$/; # matches

The second regexp doesn&#39;t match because C&lt;&#39;^&#39;&gt; constrains C&lt;keeper&gt; to
match only at the beginning of the string, but C&lt;&quot;housekeeper&quot;&gt; has
keeper starting in the middle.  The third regexp does match, since the
C&lt;&#39;$&#39;&gt; constrains C&lt;keeper&gt; to match only at the end of the string.

When both C&lt;&#39;^&#39;&gt; and C&lt;&#39;$&#39;&gt; are used at the same time, the regexp has to
match both the beginning and the end of the string, I&lt;i.e.&gt;, the regexp
matches the whole string.  Consider

    &quot;keeper&quot; =~ /^keep$/;      # doesn&#39;t match
    &quot;keeper&quot; =~ /^keeper$/;    # matches
    &quot;&quot;       =~ /^$/;          # ^$ matches an empty string

The first regexp doesn&#39;t match because the string has more to it than
C&lt;keep&gt;.  Since the second regexp is exactly the string, it
matches.  Using both C&lt;&#39;^&#39;&gt; and C&lt;&#39;$&#39;&gt; in a regexp forces the complete
string to match, so it gives you complete control over which strings
match and which don&#39;t.  Suppose you are looking for a fellow named
bert, off in a string by himself:

    &quot;dogbert&quot; =~ /bert/;   # matches, but not what you want

    &quot;dilbert&quot; =~ /^bert/;  # doesn&#39;t match, but ..
    &quot;bertram&quot; =~ /^bert/;  # matches, so still not good enough

    &quot;bertram&quot; =~ /^bert$/; # doesn&#39;t match, good
    &quot;dilbert&quot; =~ /^bert$/; # doesn&#39;t match, good
    &quot;bert&quot;    =~ /^bert$/; # matches, perfect

Of course, in the case of a literal string, one could just as easily
use the string comparison S&lt;C&lt;$string eq &#39;bert&#39;&gt;&gt; and it would be
more efficient.   The  C&lt;^...$&gt; regexp really becomes useful when we
add in the more powerful regexp tools below.

=head2 Using character classes

Although one can already do quite a lot with the literal string
regexps above, we&#39;ve only scratched the surface of regular expression
technology.  In this and subsequent sections we will introduce regexp
concepts (and associated metacharacter notations) that will allow a
regexp to represent not just a single character sequence, but a I&lt;whole
class&gt; of them.

One such concept is that of a I&lt;character class&gt;.  A character class
allows a set of possible characters, rather than just a single
character, to match at a particular point in a regexp.  You can define
your own custom character classes.  These
are denoted by brackets C&lt;[...]&gt;, with the set of characters
to be possibly matched inside.  Here are some examples:

    /cat/;       # matches &#39;cat&#39;
    /[bcr]at/;   # matches &#39;bat, &#39;cat&#39;, or &#39;rat&#39;
    /item[0123456789]/;  # matches &#39;item0&#39; or ... or &#39;item9&#39;
    &quot;abc&quot; =~ /[cab]/;    # matches &#39;a&#39;

In the last statement, even though C&lt;&#39;c&#39;&gt; is the first character in
the class, C&lt;&#39;a&#39;&gt; matches because the first character position in the
string is the earliest point at which the regexp can match.

    /[yY][eE][sS]/;      # match &#39;yes&#39; in a case-insensitive way
                         # &#39;yes&#39;, &#39;Yes&#39;, &#39;YES&#39;, etc.

This regexp displays a common task: perform a case-insensitive
match.  Perl provides a way of avoiding all those brackets by simply
appending an C&lt;&#39;i&#39;&gt; to the end of the match.  Then C&lt;/[yY][eE][sS]/;&gt;
can be rewritten as C&lt;/yes/i;&gt;.  The C&lt;&#39;i&#39;&gt; stands for
case-insensitive and is an example of a I&lt;modifier&gt; of the matching
operation.  We will meet other modifiers later in the tutorial.

We saw in the section above that there were ordinary characters, which
represented themselves, and special characters, which needed a
backslash C&lt;&#39;\&#39;&gt; to represent themselves.  The same is true in a
character class, but the sets of ordinary and special characters
inside a character class are different than those outside a character
class.  The special characters for a character class are C&lt;-]\^$&gt; (and
the pattern delimiter, whatever it is).
C&lt;&#39;]&#39;&gt; is special because it denotes the end of a character class.  C&lt;&#39;$&#39;&gt; is
special because it denotes a scalar variable.  C&lt;&#39;\&#39;&gt; is special because
it is used in escape sequences, just like above.  Here is how the
special characters C&lt;]$\&gt; are handled:

   /[\]c]def/; # matches &#39;]def&#39; or &#39;cdef&#39;
   $x = &#39;bcr&#39;;
   /[$x]at/;   # matches &#39;bat&#39;, &#39;cat&#39;, or &#39;rat&#39;
   /[\$x]at/;  # matches &#39;$at&#39; or &#39;xat&#39;
   /[\\$x]at/; # matches &#39;\at&#39;, &#39;bat, &#39;cat&#39;, or &#39;rat&#39;

The last two are a little tricky.  In C&lt;[\$x]&gt;, the backslash protects
the dollar sign, so the character class has two members C&lt;&#39;$&#39;&gt; and C&lt;&#39;x&#39;&gt;.
In C&lt;[\\$x]&gt;, the backslash is protected, so C&lt;$x&gt; is treated as a
variable and substituted in double quote fashion.

The special character C&lt;&#39;-&#39;&gt; acts as a range operator within character
classes, so that a contiguous set of characters can be written as a
range.  With ranges, the unwieldy C&lt;[0123456789]&gt; and C&lt;[abc...xyz]&gt;
become the svelte C&lt;[0-9]&gt; and C&lt;[a-z]&gt;.  Some examples are

    /item[0-9]/;  # matches &#39;item0&#39; or ... or &#39;item9&#39;
    /[0-9bx-z]aa/;  # matches &#39;0aa&#39;, ..., &#39;9aa&#39;,
                    # &#39;baa&#39;, &#39;xaa&#39;, &#39;yaa&#39;, or &#39;zaa&#39;
    /[0-9a-fA-F]/;  # matches a hexadecimal digit
    /[0-9a-zA-Z_]/; # matches a &quot;word&quot; character,
                    # like those in a Perl variable name

If C&lt;&#39;-&#39;&gt; is the first or last character in a character class, it is
treated as an ordinary character; C&lt;[-ab]&gt;, C&lt;[ab-]&gt; and C&lt;[a\-b]&gt; are
all equivalent.

The special character C&lt;&#39;^&#39;&gt; in the first position of a character class
denotes a I&lt;negated character class&gt;, which matches any character but
those in the brackets.  Both C&lt;[...]&gt; and C&lt;[^...]&gt; must match a
character, or the match fails.  Then

    /[^a]at/;  # doesn&#39;t match &#39;aat&#39; or &#39;at&#39;, but matches
               # all other &#39;bat&#39;, &#39;cat, &#39;0at&#39;, &#39;%at&#39;, etc.
    /[^0-9]/;  # matches a non-numeric character
    /[a^]at/;  # matches &#39;aat&#39; or &#39;^at&#39;; here &#39;^&#39; is ordinary

Now, even C&lt;[0-9]&gt; can be a bother to write multiple times, so in the
interest of saving keystrokes and making regexps more readable, Perl
has several abbreviations for common character classes, as shown below.
Since the introduction of Unicode, unless the C&lt;/a&gt; modifier is in
effect, these character classes match more than just a few characters in
the ASCII range.

=over 4

=item *

C&lt;\d&gt; matches a digit, not just C&lt;[0-9]&gt; but also digits from non-roman scripts

=item *

C&lt;\s&gt; matches a whitespace character, the set C&lt;[\ \t\r\n\f]&gt; and others

=item *

C&lt;\w&gt; matches a word character (alphanumeric or C&lt;&#39;_&#39;&gt;), not just C&lt;[0-9a-zA-Z_]&gt;
but also digits and characters from non-roman scripts

=item *

C&lt;\D&gt; is a negated C&lt;\d&gt;; it represents any other character than a digit, or C&lt;[^\d]&gt;

=item *

C&lt;\S&gt; is a negated C&lt;\s&gt;; it represents any non-whitespace character C&lt;[^\s]&gt;

=item *

C&lt;\W&gt; is a negated C&lt;\w&gt;; it represents any non-word character C&lt;[^\w]&gt;

=item *

The period C&lt;&#39;.&#39;&gt; matches any character but C&lt;&quot;\n&quot;&gt; (unless the modifier C&lt;/s&gt; is
in effect, as explained below).

=item *

C&lt;\N&gt;, like the period, matches any character but C&lt;&quot;\n&quot;&gt;, but it does so
regardless of whether the modifier C&lt;/s&gt; is in effect.

=back

The C&lt;/a&gt; modifier, available starting in Perl 5.14,  is used to
restrict the matches of C&lt;\d&gt;, C&lt;\s&gt;, and C&lt;\w&gt; to just those in the ASCII range.
It is useful to keep your program from being needlessly exposed to full
Unicode (and its accompanying security considerations) when all you want
is to process English-like text.  (The &quot;a&quot; may be doubled, C&lt;/aa&gt;, to
provide even more restrictions, preventing case-insensitive matching of
ASCII with non-ASCII characters; otherwise a Unicode &quot;Kelvin Sign&quot;
would caselessly match a &quot;k&quot; or &quot;K&quot;.)

The C&lt;\d\s\w\D\S\W&gt; abbreviations can be used both inside and outside
of bracketed character classes.  Here are some in use:

    /\d\d:\d\d:\d\d/; # matches a hh:mm:ss time format
    /[\d\s]/;         # matches any digit or whitespace character
    /\w\W\w/;         # matches a word char, followed by a
                      # non-word char, followed by a word char
    /..rt/;           # matches any two chars, followed by &#39;rt&#39;
    /end\./;          # matches &#39;end.&#39;
    /end[.]/;         # same thing, matches &#39;end.&#39;

Because a period is a metacharacter, it needs to be escaped to match
as an ordinary period. Because, for example, C&lt;\d&gt; and C&lt;\w&gt; are sets
of characters, it is incorrect to think of C&lt;[^\d\w]&gt; as C&lt;[\D\W]&gt;; in
fact C&lt;[^\d\w]&gt; is the same as C&lt;[^\w]&gt;, which is the same as
C&lt;[\W]&gt;. Think DeMorgan&#39;s laws.

In actuality, the period and C&lt;\d\s\w\D\S\W&gt; abbreviations are
themselves types of character classes, so the ones surrounded by
brackets are just one type of character class.  When we need to make a
distinction, we refer to them as &quot;bracketed character classes.&quot;

An anchor useful in basic regexps is the I&lt;word anchor&gt;
C&lt;\b&gt;.  This matches a boundary between a word character and a non-word
character C&lt;\w\W&gt; or C&lt;\W\w&gt;:

    $x = &quot;Housecat catenates house and cat&quot;;
    $x =~ /cat/;    # matches cat in &#39;housecat&#39;
    $x =~ /\bcat/;  # matches cat in &#39;catenates&#39;
    $x =~ /cat\b/;  # matches cat in &#39;housecat&#39;
    $x =~ /\bcat\b/;  # matches &#39;cat&#39; at end of string

Note in the last example, the end of the string is considered a word
boundary.

For natural language processing (so that, for example, apostrophes are
included in words), use instead C&lt;\b{wb}&gt;

    &quot;don&#39;t&quot; =~ / .+? \b{wb} /x;  # matches the whole string

You might wonder why C&lt;&#39;.&#39;&gt; matches everything but C&lt;&quot;\n&quot;&gt; - why not
every character? The reason is that often one is matching against
lines and would like to ignore the newline characters.  For instance,
while the string C&lt;&quot;\n&quot;&gt; represents one line, we would like to think
of it as empty.  Then

    &quot;&quot;   =~ /^$/;    # matches
    &quot;\n&quot; =~ /^$/;    # matches, $ anchors before &quot;\n&quot;

    &quot;&quot;   =~ /./;      # doesn&#39;t match; it needs a char
    &quot;&quot;   =~ /^.$/;    # doesn&#39;t match; it needs a char
    &quot;\n&quot; =~ /^.$/;    # doesn&#39;t match; it needs a char other than &quot;\n&quot;
    &quot;a&quot;  =~ /^.$/;    # matches
    &quot;a\n&quot;  =~ /^.$/;  # matches, $ anchors before &quot;\n&quot;

This behavior is convenient, because we usually want to ignore
newlines when we count and match characters in a line.  Sometimes,
however, we want to keep track of newlines.  We might even want C&lt;&#39;^&#39;&gt;
and C&lt;&#39;$&#39;&gt; to anchor at the beginning and end of lines within the
string, rather than just the beginning and end of the string.  Perl
allows us to choose between ignoring and paying attention to newlines
by using the C&lt;/s&gt; and C&lt;/m&gt; modifiers.  C&lt;/s&gt; and C&lt;/m&gt; stand for
single line and multi-line and they determine whether a string is to
be treated as one continuous string, or as a set of lines.  The two
modifiers affect two aspects of how the regexp is interpreted: 1) how
the C&lt;&#39;.&#39;&gt; character class is defined, and 2) where the anchors C&lt;&#39;^&#39;&gt;
and C&lt;&#39;$&#39;&gt; are able to match.  Here are the four possible combinations:

=over 4

=item *

no modifiers: Default behavior.  C&lt;&#39;.&#39;&gt; matches any character
except C&lt;&quot;\n&quot;&gt;.  C&lt;&#39;^&#39;&gt; matches only at the beginning of the string and
C&lt;&#39;$&#39;&gt; matches only at the end or before a newline at the end.

=item *

s modifier (C&lt;/s&gt;): Treat string as a single long line.  C&lt;&#39;.&#39;&gt; matches
any character, even C&lt;&quot;\n&quot;&gt;.  C&lt;&#39;^&#39;&gt; matches only at the beginning of
the string and C&lt;&#39;$&#39;&gt; matches only at the end or before a newline at the
end.

=item *

m modifier (C&lt;/m&gt;): Treat string as a set of multiple lines.  C&lt;&#39;.&#39;&gt;
matches any character except C&lt;&quot;\n&quot;&gt;.  C&lt;&#39;^&#39;&gt; and C&lt;&#39;$&#39;&gt; are able to match
at the start or end of I&lt;any&gt; line within the string.

=item *

both s and m modifiers (C&lt;/sm&gt;): Treat string as a single long line, but
detect multiple lines.  C&lt;&#39;.&#39;&gt; matches any character, even
C&lt;&quot;\n&quot;&gt;.  C&lt;&#39;^&#39;&gt; and C&lt;&#39;$&#39;&gt;, however, are able to match at the start or end
of I&lt;any&gt; line within the string.

=back

Here are examples of C&lt;/s&gt; and C&lt;/m&gt; in action:

    $x = &quot;There once was a girl\nWho programmed in Perl\n&quot;;

    $x =~ /^Who/;   # doesn&#39;t match, &quot;Who&quot; not at start of string
    $x =~ /^Who/s;  # doesn&#39;t match, &quot;Who&quot; not at start of string
    $x =~ /^Who/m;  # matches, &quot;Who&quot; at start of second line
    $x =~ /^Who/sm; # matches, &quot;Who&quot; at start of second line

    $x =~ /girl.Who/;   # doesn&#39;t match, &quot;.&quot; doesn&#39;t match &quot;\n&quot;
    $x =~ /girl.Who/s;  # matches, &quot;.&quot; matches &quot;\n&quot;
    $x =~ /girl.Who/m;  # doesn&#39;t match, &quot;.&quot; doesn&#39;t match &quot;\n&quot;
    $x =~ /girl.Who/sm; # matches, &quot;.&quot; matches &quot;\n&quot;

Most of the time, the default behavior is what is wanted, but C&lt;/s&gt; and
C&lt;/m&gt; are occasionally very useful.  If C&lt;/m&gt; is being used, the start
of the string can still be matched with C&lt;\A&gt; and the end of the string
can still be matched with the anchors C&lt;\Z&gt; (matches both the end and
the newline before, like C&lt;&#39;$&#39;&gt;), and C&lt;\z&gt; (matches only the end):

    $x =~ /^Who/m;   # matches, &quot;Who&quot; at start of second line
    $x =~ /\AWho/m;  # doesn&#39;t match, &quot;Who&quot; is not at start of string

    $x =~ /girl$/m;  # matches, &quot;girl&quot; at end of first line
    $x =~ /girl\Z/m; # doesn&#39;t match, &quot;girl&quot; is not at end of string

    $x =~ /Perl\Z/m; # matches, &quot;Perl&quot; is at newline before end
    $x =~ /Perl\z/m; # doesn&#39;t match, &quot;Perl&quot; is not at end of string

We now know how to create choices among classes of characters in a
regexp.  What about choices among words or character strings? Such
choices are described in the next section.

=head2 Matching this or that

Sometimes we would like our regexp to be able to match different
possible words or character strings.  This is accomplished by using
the I&lt;alternation&gt; metacharacter C&lt;&#39;|&#39;&gt;.  To match C&lt;dog&gt; or C&lt;cat&gt;, we
form the regexp C&lt;dog|cat&gt;.  As before, Perl will try to match the
regexp at the earliest possible point in the string.  At each
character position, Perl will first try to match the first
alternative, C&lt;dog&gt;.  If C&lt;dog&gt; doesn&#39;t match, Perl will then try the
next alternative, C&lt;cat&gt;.  If C&lt;cat&gt; doesn&#39;t match either, then the
match fails and Perl moves to the next position in the string.  Some
examples:

    &quot;cats and dogs&quot; =~ /cat|dog|bird/;  # matches &quot;cat&quot;
    &quot;cats and dogs&quot; =~ /dog|cat|bird/;  # matches &quot;cat&quot;

Even though C&lt;dog&gt; is the first alternative in the second regexp,
C&lt;cat&gt; is able to match earlier in the string.

    &quot;cats&quot;          =~ /c|ca|cat|cats/; # matches &quot;c&quot;
    &quot;cats&quot;          =~ /cats|cat|ca|c/; # matches &quot;cats&quot;

Here, all the alternatives match at the first string position, so the
first alternative is the one that matches.  If some of the
alternatives are truncations of the others, put the longest ones first
to give them a chance to match.

    &quot;cab&quot; =~ /a|b|c/ # matches &quot;c&quot;
                     # /a|b|c/ == /[abc]/

The last example points out that character classes are like
alternations of characters.  At a given character position, the first
alternative that allows the regexp match to succeed will be the one
that matches.

=head2 Grouping things and hierarchical matching

Alternation allows a regexp to choose among alternatives, but by
itself it is unsatisfying.  The reason is that each alternative is a whole
regexp, but sometime we want alternatives for just part of a
regexp.  For instance, suppose we want to search for housecats or
housekeepers.  The regexp C&lt;housecat|housekeeper&gt; fits the bill, but is
inefficient because we had to type C&lt;house&gt; twice.  It would be nice to
have parts of the regexp be constant, like C&lt;house&gt;, and some
parts have alternatives, like C&lt;cat|keeper&gt;.

The I&lt;grouping&gt; metacharacters C&lt;()&gt; solve this problem.  Grouping
allows parts of a regexp to be treated as a single unit.  Parts of a
regexp are grouped by enclosing them in parentheses.  Thus we could solve
the C&lt;housecat|housekeeper&gt; by forming the regexp as
C&lt;house(cat|keeper)&gt;.  The regexp C&lt;house(cat|keeper)&gt; means match
C&lt;house&gt; followed by either C&lt;cat&gt; or C&lt;keeper&gt;.  Some more examples
are

    /(a|b)b/;    # matches &#39;ab&#39; or &#39;bb&#39;
    /(ac|b)b/;   # matches &#39;acb&#39; or &#39;bb&#39;
    /(^a|b)c/;   # matches &#39;ac&#39; at start of string or &#39;bc&#39; anywhere
    /(a|[bc])d/; # matches &#39;ad&#39;, &#39;bd&#39;, or &#39;cd&#39;

    /house(cat|)/;  # matches either &#39;housecat&#39; or &#39;house&#39;
    /house(cat(s|)|)/;  # matches either &#39;housecats&#39; or &#39;housecat&#39; or
                        # &#39;house&#39;.  Note groups can be nested.

    /(19|20|)\d\d/;  # match years 19xx, 20xx, or the Y2K problem, xx
    &quot;20&quot; =~ /(19|20|)\d\d/;  # matches the null alternative &#39;()\d\d&#39;,
                             # because &#39;20\d\d&#39; can&#39;t match

Alternations behave the same way in groups as out of them: at a given
string position, the leftmost alternative that allows the regexp to
match is taken.  So in the last example at the first string position,
C&lt;&quot;20&quot;&gt; matches the second alternative, but there is nothing left over
to match the next two digits C&lt;\d\d&gt;.  So Perl moves on to the next
alternative, which is the null alternative and that works, since
C&lt;&quot;20&quot;&gt; is two digits.

The process of trying one alternative, seeing if it matches, and
moving on to the next alternative, while going back in the string
from where the previous alternative was tried, if it doesn&#39;t, is called
I&lt;backtracking&gt;.  The term &quot;backtracking&quot; comes from the idea that
matching a regexp is like a walk in the woods.  Successfully matching
a regexp is like arriving at a destination.  There are many possible
trailheads, one for each string position, and each one is tried in
order, left to right.  From each trailhead there may be many paths,
some of which get you there, and some which are dead ends.  When you
walk along a trail and hit a dead end, you have to backtrack along the
trail to an earlier point to try another trail.  If you hit your
destination, you stop immediately and forget about trying all the
other trails.  You are persistent, and only if you have tried all the
trails from all the trailheads and not arrived at your destination, do
you declare failure.  To be concrete, here is a step-by-step analysis
of what Perl does when it tries to match the regexp

    &quot;abcde&quot; =~ /(abd|abc)(df|d|de)/;

=over 4

=item Z&lt;&gt;0. Start with the first letter in the string C&lt;&#39;a&#39;&gt;.

E&lt;nbsp&gt;

=item Z&lt;&gt;1. Try the first alternative in the first group C&lt;&#39;abd&#39;&gt;.

E&lt;nbsp&gt;

=item Z&lt;&gt;2.  Match C&lt;&#39;a&#39;&gt; followed by C&lt;&#39;b&#39;&gt;. So far so good.

E&lt;nbsp&gt;

=item Z&lt;&gt;3.  C&lt;&#39;d&#39;&gt; in the regexp doesn&#39;t match C&lt;&#39;c&#39;&gt; in the string - a
dead end.  So backtrack two characters and pick the second alternative
in the first group C&lt;&#39;abc&#39;&gt;.

E&lt;nbsp&gt;

=item Z&lt;&gt;4.  Match C&lt;&#39;a&#39;&gt; followed by C&lt;&#39;b&#39;&gt; followed by C&lt;&#39;c&#39;&gt;.  We are on a roll
and have satisfied the first group. Set C&lt;$1&gt; to C&lt;&#39;abc&#39;&gt;.

E&lt;nbsp&gt;

=item Z&lt;&gt;5 Move on to the second group and pick the first alternative C&lt;&#39;df&#39;&gt;.

E&lt;nbsp&gt;

=item Z&lt;&gt;6 Match the C&lt;&#39;d&#39;&gt;.

E&lt;nbsp&gt;

=item Z&lt;&gt;7.  C&lt;&#39;f&#39;&gt; in the regexp doesn&#39;t match C&lt;&#39;e&#39;&gt; in the string, so a dead
end.  Backtrack one character and pick the second alternative in the
second group C&lt;&#39;d&#39;&gt;.

E&lt;nbsp&gt;

=item Z&lt;&gt;8.  C&lt;&#39;d&#39;&gt; matches. The second grouping is satisfied, so set
C&lt;$2&gt; to C&lt;&#39;d&#39;&gt;.

E&lt;nbsp&gt;

=item Z&lt;&gt;9.  We are at the end of the regexp, so we are done! We have
matched C&lt;&#39;abcd&#39;&gt; out of the string C&lt;&quot;abcde&quot;&gt;.

=back

There are a couple of things to note about this analysis.  First, the
third alternative in the second group C&lt;&#39;de&#39;&gt; also allows a match, but we
stopped before we got to it - at a given character position, leftmost
wins.  Second, we were able to get a match at the first character
position of the string C&lt;&#39;a&#39;&gt;.  If there were no matches at the first
position, Perl would move to the second character position C&lt;&#39;b&#39;&gt; and
attempt the match all over again.  Only when all possible paths at all
possible character positions have been exhausted does Perl give
up and declare S&lt;C&lt;$string =~ /(abd|abc)(df|d|de)/;&gt;&gt; to be false.

Even with all this work, regexp matching happens remarkably fast.  To
speed things up, Perl compiles the regexp into a compact sequence of
opcodes that can often fit inside a processor cache.  When the code is
executed, these opcodes can then run at full throttle and search very
quickly.

=head2 Extracting matches

The grouping metacharacters C&lt;()&gt; also serve another completely
different function: they allow the extraction of the parts of a string
that matched.  This is very useful to find out what matched and for
text processing in general.  For each grouping, the part that matched
inside goes into the special variables C&lt;$1&gt;, C&lt;$2&gt;, I&lt;etc&gt;.  They can be
used just as ordinary variables:

    # extract hours, minutes, seconds
    if ($time =~ /(\d\d):(\d\d):(\d\d)/) {    # match hh:mm:ss format
	$hours = $1;
	$minutes = $2;
	$seconds = $3;
    }

Now, we know that in scalar context,
S&lt;C&lt;$time =~ /(\d\d):(\d\d):(\d\d)/&gt;&gt; returns a true or false
value.  In list context, however, it returns the list of matched values
C&lt;($1,$2,$3)&gt;.  So we could write the code more compactly as

    # extract hours, minutes, seconds
    ($hours, $minutes, $second) = ($time =~ /(\d\d):(\d\d):(\d\d)/);

If the groupings in a regexp are nested, C&lt;$1&gt; gets the group with the
leftmost opening parenthesis, C&lt;$2&gt; the next opening parenthesis,
I&lt;etc&gt;.  Here is a regexp with nested groups:

    /(ab(cd|ef)((gi)|j))/;
     1  2      34

If this regexp matches, C&lt;$1&gt; contains a string starting with
C&lt;&#39;ab&#39;&gt;, C&lt;$2&gt; is either set to C&lt;&#39;cd&#39;&gt; or C&lt;&#39;ef&#39;&gt;, C&lt;$3&gt; equals either
C&lt;&#39;gi&#39;&gt; or C&lt;&#39;j&#39;&gt;, and C&lt;$4&gt; is either set to C&lt;&#39;gi&#39;&gt;, just like C&lt;$3&gt;,
or it remains undefined.

For convenience, Perl sets C&lt;$+&gt; to the string held by the highest numbered
C&lt;$1&gt;, C&lt;$2&gt;,... that got assigned (and, somewhat related, C&lt;$^N&gt; to the
value of the C&lt;$1&gt;, C&lt;$2&gt;,... most-recently assigned; I&lt;i.e.&gt; the C&lt;$1&gt;,
C&lt;$2&gt;,... associated with the rightmost closing parenthesis used in the
match).


=head2 Backreferences

Closely associated with the matching variables C&lt;$1&gt;, C&lt;$2&gt;, ... are
the I&lt;backreferences&gt; C&lt;\g1&gt;, C&lt;\g2&gt;,...  Backreferences are simply
matching variables that can be used I&lt;inside&gt; a regexp.  This is a
really nice feature; what matches later in a regexp is made to depend on
what matched earlier in the regexp.  Suppose we wanted to look
for doubled words in a text, like &quot;the the&quot;.  The following regexp finds
all 3-letter doubles with a space in between:

    /\b(\w\w\w)\s\g1\b/;

The grouping assigns a value to C&lt;\g1&gt;, so that the same 3-letter sequence
is used for both parts.

A similar task is to find words consisting of two identical parts:

    % simple_grep &#39;^(\w\w\w\w|\w\w\w|\w\w|\w)\g1$&#39; /usr/dict/words
    beriberi
    booboo
    coco
    mama
    murmur
    papa

The regexp has a single grouping which considers 4-letter
combinations, then 3-letter combinations, I&lt;etc&gt;., and uses C&lt;\g1&gt; to look for
a repeat.  Although C&lt;$1&gt; and C&lt;\g1&gt; represent the same thing, care should be
taken to use matched variables C&lt;$1&gt;, C&lt;$2&gt;,... only I&lt;outside&gt; a regexp
and backreferences C&lt;\g1&gt;, C&lt;\g2&gt;,... only I&lt;inside&gt; a regexp; not doing
so may lead to surprising and unsatisfactory results.


=head2 Relative backreferences

Counting the opening parentheses to get the correct number for a
backreference is error-prone as soon as there is more than one
capturing group.  A more convenient technique became available
with Perl 5.10: relative backreferences. To refer to the immediately
preceding capture group one now may write C&lt;\g{-1}&gt;, the next but
last is available via C&lt;\g{-2}&gt;, and so on.

Another good reason in addition to readability and maintainability
for using relative backreferences is illustrated by the following example,
where a simple pattern for matching peculiar strings is used:

    $a99a = &#39;([a-z])(\d)\g2\g1&#39;;   # matches a11a, g22g, x33x, etc.

Now that we have this pattern stored as a handy string, we might feel
tempted to use it as a part of some other pattern:

    $line = &quot;code=e99e&quot;;
    if ($line =~ /^(\w+)=$a99a$/){   # unexpected behavior!
        print &quot;$1 is valid\n&quot;;
    } else {
        print &quot;bad line: &#39;$line&#39;\n&quot;;
    }

But this doesn&#39;t match, at least not the way one might expect. Only
after inserting the interpolated C&lt;$a99a&gt; and looking at the resulting
full text of the regexp is it obvious that the backreferences have
backfired. The subexpression C&lt;(\w+)&gt; has snatched number 1 and
demoted the groups in C&lt;$a99a&gt; by one rank. This can be avoided by
using relative backreferences:

    $a99a = &#39;([a-z])(\d)\g{-1}\g{-2}&#39;;  # safe for being interpolated


=head2 Named backreferences

Perl 5.10 also introduced named capture groups and named backreferences.
To attach a name to a capturing group, you write either
C&lt;&lt; (?&lt;name&gt;...) &gt;&gt; or C&lt;&lt; (?&#39;name&#39;...) &gt;&gt;.  The backreference may
then be written as C&lt;\g{name}&gt;.  It is permissible to attach the
same name to more than one group, but then only the leftmost one of the
eponymous set can be referenced.  Outside of the pattern a named
capture group is accessible through the C&lt;%+&gt; hash.

Assuming that we have to match calendar dates which may be given in one
of the three formats yyyy-mm-dd, mm/dd/yyyy or dd.mm.yyyy, we can write
three suitable patterns where we use C&lt;&#39;d&#39;&gt;, C&lt;&#39;m&#39;&gt; and C&lt;&#39;y&#39;&gt; respectively as the
names of the groups capturing the pertaining components of a date. The
matching operation combines the three patterns as alternatives:

    $fmt1 = &#39;(?&lt;y&gt;\d\d\d\d)-(?&lt;m&gt;\d\d)-(?&lt;d&gt;\d\d)&#39;;
    $fmt2 = &#39;(?&lt;m&gt;\d\d)/(?&lt;d&gt;\d\d)/(?&lt;y&gt;\d\d\d\d)&#39;;
    $fmt3 = &#39;(?&lt;d&gt;\d\d)\.(?&lt;m&gt;\d\d)\.(?&lt;y&gt;\d\d\d\d)&#39;;
    for my $d qw( 2006-10-21 15.01.2007 10/31/2005 ){
        if ( $d =~ m{$fmt1|$fmt2|$fmt3} ){
            print &quot;day=$+{d} month=$+{m} year=$+{y}\n&quot;;
        }
    }

If any of the alternatives matches, the hash C&lt;%+&gt; is bound to contain the
three key-value pairs.


=head2 Alternative capture group numbering

Yet another capturing group numbering technique (also as from Perl 5.10)
deals with the problem of referring to groups within a set of alternatives.
Consider a pattern for matching a time of the day, civil or military style:

    if ( $time =~ /(\d\d|\d):(\d\d)|(\d\d)(\d\d)/ ){
        # process hour and minute
    }

Processing the results requires an additional if statement to determine
whether C&lt;$1&gt; and C&lt;$2&gt; or C&lt;$3&gt; and C&lt;$4&gt; contain the goodies. It would
be easier if we could use group numbers 1 and 2 in second alternative as
well, and this is exactly what the parenthesized construct C&lt;(?|...)&gt;,
set around an alternative achieves. Here is an extended version of the
previous pattern:

  if($time =~ /(?|(\d\d|\d):(\d\d)|(\d\d)(\d\d))\s+([A-Z][A-Z][A-Z])/){
      print &quot;hour=$1 minute=$2 zone=$3\n&quot;;
  }

Within the alternative numbering group, group numbers start at the same
position for each alternative. After the group, numbering continues
with one higher than the maximum reached across all the alternatives.

=head2 Position information

In addition to what was matched, Perl also provides the
positions of what was matched as contents of the C&lt;@-&gt; and C&lt;@+&gt;
arrays. C&lt;$-[0]&gt; is the position of the start of the entire match and
C&lt;$+[0]&gt; is the position of the end. Similarly, C&lt;$-[n]&gt; is the
position of the start of the C&lt;$n&gt; match and C&lt;$+[n]&gt; is the position
of the end. If C&lt;$n&gt; is undefined, so are C&lt;$-[n]&gt; and C&lt;$+[n]&gt;. Then
this code

    $x = &quot;Mmm...donut, thought Homer&quot;;
    $x =~ /^(Mmm|Yech)\.\.\.(donut|peas)/; # matches
    foreach $exp (1..$#-) {
        print &quot;Match $exp: &#39;${$exp}&#39; at position ($-[$exp],$+[$exp])\n&quot;;
    }

prints

    Match 1: &#39;Mmm&#39; at position (0,3)
    Match 2: &#39;donut&#39; at position (6,11)

Even if there are no groupings in a regexp, it is still possible to
find out what exactly matched in a string.  If you use them, Perl
will set C&lt;$`&gt; to the part of the string before the match, will set C&lt;$&amp;&gt;
to the part of the string that matched, and will set C&lt;&#39;$&#39;&gt; to the part
of the string after the match.  An example:

    $x = &quot;the cat caught the mouse&quot;;
    $x =~ /cat/;  # $` = &#39;the &#39;, $&amp; = &#39;cat&#39;, $&#39; = &#39; caught the mouse&#39;
    $x =~ /the/;  # $` = &#39;&#39;, $&amp; = &#39;the&#39;, $&#39; = &#39; cat caught the mouse&#39;

In the second match, C&lt;$`&gt; equals C&lt;&#39;&#39;&gt; because the regexp matched at the
first character position in the string and stopped; it never saw the
second &quot;the&quot;.

If your code is to run on Perl versions earlier than
5.20, it is worthwhile to note that using C&lt;$`&gt; and C&lt;&#39;$&#39;&gt;
slows down regexp matching quite a bit, while C&lt;$&amp;&gt; slows it down to a
lesser extent, because if they are used in one regexp in a program,
they are generated for I&lt;all&gt; regexps in the program.  So if raw
performance is a goal of your application, they should be avoided.
If you need to extract the corresponding substrings, use C&lt;@-&gt; and
C&lt;@+&gt; instead:

    $` is the same as substr( $x, 0, $-[0] )
    $&amp; is the same as substr( $x, $-[0], $+[0]-$-[0] )
    $&#39; is the same as substr( $x, $+[0] )

As of Perl 5.10, the C&lt;${^PREMATCH}&gt;, C&lt;${^MATCH}&gt; and C&lt;${^POSTMATCH}&gt;
variables may be used.  These are only set if the C&lt;/p&gt; modifier is
present.  Consequently they do not penalize the rest of the program.  In
Perl 5.20, C&lt;${^PREMATCH}&gt;, C&lt;${^MATCH}&gt; and C&lt;${^POSTMATCH}&gt; are available
whether the C&lt;/p&gt; has been used or not (the modifier is ignored), and
C&lt;$`&gt;, C&lt;&#39;$&#39;&gt; and C&lt;$&amp;&gt; do not cause any speed difference.

=head2 Non-capturing groupings

A group that is required to bundle a set of alternatives may or may not be
useful as a capturing group.  If it isn&#39;t, it just creates a superfluous
addition to the set of available capture group values, inside as well as
outside the regexp.  Non-capturing groupings, denoted by C&lt;(?:regexp)&gt;,
still allow the regexp to be treated as a single unit, but don&#39;t establish
a capturing group at the same time.  Both capturing and non-capturing
groupings are allowed to co-exist in the same regexp.  Because there is
no extraction, non-capturing groupings are faster than capturing
groupings.  Non-capturing groupings are also handy for choosing exactly
which parts of a regexp are to be extracted to matching variables:

    # match a number, $1-$4 are set, but we only want $1
    /([+-]?\ *(\d+(\.\d*)?|\.\d+)([eE][+-]?\d+)?)/;

    # match a number faster , only $1 is set
    /([+-]?\ *(?:\d+(?:\.\d*)?|\.\d+)(?:[eE][+-]?\d+)?)/;

    # match a number, get $1 = whole number, $2 = exponent
    /([+-]?\ *(?:\d+(?:\.\d*)?|\.\d+)(?:[eE]([+-]?\d+))?)/;

Non-capturing groupings are also useful for removing nuisance
elements gathered from a split operation where parentheses are
required for some reason:

    $x = &#39;12aba34ba5&#39;;
    @num = split /(a|b)+/, $x;    # @num = (&#39;12&#39;,&#39;a&#39;,&#39;34&#39;,&#39;a&#39;,&#39;5&#39;)
    @num = split /(?:a|b)+/, $x;  # @num = (&#39;12&#39;,&#39;34&#39;,&#39;5&#39;)

In Perl 5.22 and later, all groups within a regexp can be set to
non-capturing by using the new C&lt;/n&gt; flag:

    &quot;hello&quot; =~ /(hi|hello)/n; # $1 is not set!

See L&lt;perlre/&quot;n&quot;&gt; for more information.

=head2 Matching repetitions

The examples in the previous section display an annoying weakness.  We
were only matching 3-letter words, or chunks of words of 4 letters or
less.  We&#39;d like to be able to match words or, more generally, strings
of any length, without writing out tedious alternatives like
C&lt;\w\w\w\w|\w\w\w|\w\w|\w&gt;.

This is exactly the problem the I&lt;quantifier&gt; metacharacters C&lt;&#39;?&#39;&gt;,
C&lt;&#39;*&#39;&gt;, C&lt;&#39;+&#39;&gt;, and C&lt;{}&gt; were created for.  They allow us to delimit the
number of repeats for a portion of a regexp we consider to be a
match.  Quantifiers are put immediately after the character, character
class, or grouping that we want to specify.  They have the following
meanings:

=over 4

=item *

C&lt;a?&gt; means: match C&lt;&#39;a&#39;&gt; 1 or 0 times

=item *

C&lt;a*&gt; means: match C&lt;&#39;a&#39;&gt; 0 or more times, I&lt;i.e.&gt;, any number of times

=item *

C&lt;a+&gt; means: match C&lt;&#39;a&#39;&gt; 1 or more times, I&lt;i.e.&gt;, at least once

=item *

C&lt;a{n,m}&gt; means: match at least C&lt;n&gt; times, but not more than C&lt;m&gt;
times.

=item *

C&lt;a{n,}&gt; means: match at least C&lt;n&gt; or more times

=item *

C&lt;a{n}&gt; means: match exactly C&lt;n&gt; times

=back

Here are some examples:

    /[a-z]+\s+\d*/;  # match a lowercase word, at least one space, and
                     # any number of digits
    /(\w+)\s+\g1/;    # match doubled words of arbitrary length
    /y(es)?/i;       # matches &#39;y&#39;, &#39;Y&#39;, or a case-insensitive &#39;yes&#39;
    $year =~ /^\d{2,4}$/;  # make sure year is at least 2 but not more
                           # than 4 digits
    $year =~ /^\d{4}$|^\d{2}$/; # better match; throw out 3-digit dates
    $year =~ /^\d{2}(\d{2})?$/; # same thing written differently.
                                # However, this captures the last two
                                # digits in $1 and the other does not.

    % simple_grep &#39;^(\w+)\g1$&#39; /usr/dict/words   # isn&#39;t this easier?
    beriberi
    booboo
    coco
    mama
    murmur
    papa

For all of these quantifiers, Perl will try to match as much of the
string as possible, while still allowing the regexp to succeed.  Thus
with C&lt;/a?.../&gt;, Perl will first try to match the regexp with the C&lt;&#39;a&#39;&gt;
present; if that fails, Perl will try to match the regexp without the
C&lt;&#39;a&#39;&gt; present.  For the quantifier C&lt;&#39;*&#39;&gt;, we get the following:

    $x = &quot;the cat in the hat&quot;;
    $x =~ /^(.*)(cat)(.*)$/; # matches,
                             # $1 = &#39;the &#39;
                             # $2 = &#39;cat&#39;
                             # $3 = &#39; in the hat&#39;

Which is what we might expect, the match finds the only C&lt;cat&gt; in the
string and locks onto it.  Consider, however, this regexp:

    $x =~ /^(.*)(at)(.*)$/; # matches,
                            # $1 = &#39;the cat in the h&#39;
                            # $2 = &#39;at&#39;
                            # $3 = &#39;&#39;   (0 characters match)

One might initially guess that Perl would find the C&lt;at&gt; in C&lt;cat&gt; and
stop there, but that wouldn&#39;t give the longest possible string to the
first quantifier C&lt;.*&gt;.  Instead, the first quantifier C&lt;.*&gt; grabs as
much of the string as possible while still having the regexp match.  In
this example, that means having the C&lt;at&gt; sequence with the final C&lt;at&gt;
in the string.  The other important principle illustrated here is that,
when there are two or more elements in a regexp, the I&lt;leftmost&gt;
quantifier, if there is one, gets to grab as much of the string as
possible, leaving the rest of the regexp to fight over scraps.  Thus in
our example, the first quantifier C&lt;.*&gt; grabs most of the string, while
the second quantifier C&lt;.*&gt; gets the empty string.   Quantifiers that
grab as much of the string as possible are called I&lt;maximal match&gt; or
I&lt;greedy&gt; quantifiers.

When a regexp can match a string in several different ways, we can use
the principles above to predict which way the regexp will match:

=over 4

=item *

Principle 0: Taken as a whole, any regexp will be matched at the
earliest possible position in the string.

=item *

Principle 1: In an alternation C&lt;a|b|c...&gt;, the leftmost alternative
that allows a match for the whole regexp will be the one used.

=item *

Principle 2: The maximal matching quantifiers C&lt;&#39;?&#39;&gt;, C&lt;&#39;*&#39;&gt;, C&lt;&#39;+&#39;&gt; and
C&lt;{n,m}&gt; will in general match as much of the string as possible while
still allowing the whole regexp to match.

=item *

Principle 3: If there are two or more elements in a regexp, the
leftmost greedy quantifier, if any, will match as much of the string
as possible while still allowing the whole regexp to match.  The next
leftmost greedy quantifier, if any, will try to match as much of the
string remaining available to it as possible, while still allowing the
whole regexp to match.  And so on, until all the regexp elements are
satisfied.

=back

As we have seen above, Principle 0 overrides the others. The regexp
will be matched as early as possible, with the other principles
determining how the regexp matches at that earliest character
position.

Here is an example of these principles in action:

    $x = &quot;The programming republic of Perl&quot;;
    $x =~ /^(.+)(e|r)(.*)$/;  # matches,
                              # $1 = &#39;The programming republic of Pe&#39;
                              # $2 = &#39;r&#39;
                              # $3 = &#39;l&#39;

This regexp matches at the earliest string position, C&lt;&#39;T&#39;&gt;.  One
might think that C&lt;&#39;e&#39;&gt;, being leftmost in the alternation, would be
matched, but C&lt;&#39;r&#39;&gt; produces the longest string in the first quantifier.

    $x =~ /(m{1,2})(.*)$/;  # matches,
                            # $1 = &#39;mm&#39;
                            # $2 = &#39;ing republic of Perl&#39;

Here, The earliest possible match is at the first C&lt;&#39;m&#39;&gt; in
C&lt;programming&gt;. C&lt;m{1,2}&gt; is the first quantifier, so it gets to match
a maximal C&lt;mm&gt;.

    $x =~ /.*(m{1,2})(.*)$/;  # matches,
                              # $1 = &#39;m&#39;
                              # $2 = &#39;ing republic of Perl&#39;

Here, the regexp matches at the start of the string. The first
quantifier C&lt;.*&gt; grabs as much as possible, leaving just a single
C&lt;&#39;m&#39;&gt; for the second quantifier C&lt;m{1,2}&gt;.

    $x =~ /(.?)(m{1,2})(.*)$/;  # matches,
                                # $1 = &#39;a&#39;
                                # $2 = &#39;mm&#39;
                                # $3 = &#39;ing republic of Perl&#39;

Here, C&lt;.?&gt; eats its maximal one character at the earliest possible
position in the string, C&lt;&#39;a&#39;&gt; in C&lt;programming&gt;, leaving C&lt;m{1,2}&gt;
the opportunity to match both C&lt;&#39;m&#39;&gt;&#39;s. Finally,

    &quot;aXXXb&quot; =~ /(X*)/; # matches with $1 = &#39;&#39;

because it can match zero copies of C&lt;&#39;X&#39;&gt; at the beginning of the
string.  If you definitely want to match at least one C&lt;&#39;X&#39;&gt;, use
C&lt;X+&gt;, not C&lt;X*&gt;.

Sometimes greed is not good.  At times, we would like quantifiers to
match a I&lt;minimal&gt; piece of string, rather than a maximal piece.  For
this purpose, Larry Wall created the I&lt;minimal match&gt; or
I&lt;non-greedy&gt; quantifiers C&lt;??&gt;, C&lt;*?&gt;, C&lt;+?&gt;, and C&lt;{}?&gt;.  These are
the usual quantifiers with a C&lt;&#39;?&#39;&gt; appended to them.  They have the
following meanings:

=over 4

=item *

C&lt;a??&gt; means: match C&lt;&#39;a&#39;&gt; 0 or 1 times. Try 0 first, then 1.

=item *

C&lt;a*?&gt; means: match C&lt;&#39;a&#39;&gt; 0 or more times, I&lt;i.e.&gt;, any number of times,
but as few times as possible

=item *

C&lt;a+?&gt; means: match C&lt;&#39;a&#39;&gt; 1 or more times, I&lt;i.e.&gt;, at least once, but
as few times as possible

=item *

C&lt;a{n,m}?&gt; means: match at least C&lt;n&gt; times, not more than C&lt;m&gt;
times, as few times as possible

=item *

C&lt;a{n,}?&gt; means: match at least C&lt;n&gt; times, but as few times as
possible

=item *

C&lt;a{n}?&gt; means: match exactly C&lt;n&gt; times.  Because we match exactly
C&lt;n&gt; times, C&lt;a{n}?&gt; is equivalent to C&lt;a{n}&gt; and is just there for
notational consistency.

=back

Let&#39;s look at the example above, but with minimal quantifiers:

    $x = &quot;The programming republic of Perl&quot;;
    $x =~ /^(.+?)(e|r)(.*)$/; # matches,
                              # $1 = &#39;Th&#39;
                              # $2 = &#39;e&#39;
                              # $3 = &#39; programming republic of Perl&#39;

The minimal string that will allow both the start of the string C&lt;&#39;^&#39;&gt;
and the alternation to match is C&lt;Th&gt;, with the alternation C&lt;e|r&gt;
matching C&lt;&#39;e&#39;&gt;.  The second quantifier C&lt;.*&gt; is free to gobble up the
rest of the string.

    $x =~ /(m{1,2}?)(.*?)$/;  # matches,
                              # $1 = &#39;m&#39;
                              # $2 = &#39;ming republic of Perl&#39;

The first string position that this regexp can match is at the first
C&lt;&#39;m&#39;&gt; in C&lt;programming&gt;. At this position, the minimal C&lt;m{1,2}?&gt;
matches just one C&lt;&#39;m&#39;&gt;.  Although the second quantifier C&lt;.*?&gt; would
prefer to match no characters, it is constrained by the end-of-string
anchor C&lt;&#39;$&#39;&gt; to match the rest of the string.

    $x =~ /(.*?)(m{1,2}?)(.*)$/;  # matches,
                                  # $1 = &#39;The progra&#39;
                                  # $2 = &#39;m&#39;
                                  # $3 = &#39;ming republic of Perl&#39;

In this regexp, you might expect the first minimal quantifier C&lt;.*?&gt;
to match the empty string, because it is not constrained by a C&lt;&#39;^&#39;&gt;
anchor to match the beginning of the word.  Principle 0 applies here,
however.  Because it is possible for the whole regexp to match at the
start of the string, it I&lt;will&gt; match at the start of the string.  Thus
the first quantifier has to match everything up to the first C&lt;&#39;m&#39;&gt;.  The
second minimal quantifier matches just one C&lt;&#39;m&#39;&gt; and the third
quantifier matches the rest of the string.

    $x =~ /(.??)(m{1,2})(.*)$/;  # matches,
                                 # $1 = &#39;a&#39;
                                 # $2 = &#39;mm&#39;
                                 # $3 = &#39;ing republic of Perl&#39;

Just as in the previous regexp, the first quantifier C&lt;.??&gt; can match
earliest at position C&lt;&#39;a&#39;&gt;, so it does.  The second quantifier is
greedy, so it matches C&lt;mm&gt;, and the third matches the rest of the
string.

We can modify principle 3 above to take into account non-greedy
quantifiers:

=over 4

=item *

Principle 3: If there are two or more elements in a regexp, the
leftmost greedy (non-greedy) quantifier, if any, will match as much
(little) of the string as possible while still allowing the whole
regexp to match.  The next leftmost greedy (non-greedy) quantifier, if
any, will try to match as much (little) of the string remaining
available to it as possible, while still allowing the whole regexp to
match.  And so on, until all the regexp elements are satisfied.

=back

Just like alternation, quantifiers are also susceptible to
backtracking.  Here is a step-by-step analysis of the example

    $x = &quot;the cat in the hat&quot;;
    $x =~ /^(.*)(at)(.*)$/; # matches,
                            # $1 = &#39;the cat in the h&#39;
                            # $2 = &#39;at&#39;
                            # $3 = &#39;&#39;   (0 matches)

=over 4

=item Z&lt;&gt;0.  Start with the first letter in the string C&lt;&#39;t&#39;&gt;.

E&lt;nbsp&gt;

=item Z&lt;&gt;1.  The first quantifier C&lt;&#39;.*&#39;&gt; starts out by matching the whole
string &quot;C&lt;the cat in the hat&gt;&quot;.

E&lt;nbsp&gt;

=item Z&lt;&gt;2.  C&lt;&#39;a&#39;&gt; in the regexp element C&lt;&#39;at&#39;&gt; doesn&#39;t match the end
of the string.  Backtrack one character.

E&lt;nbsp&gt;

=item Z&lt;&gt;3.  C&lt;&#39;a&#39;&gt; in the regexp element C&lt;&#39;at&#39;&gt; still doesn&#39;t match
the last letter of the string C&lt;&#39;t&#39;&gt;, so backtrack one more character.

E&lt;nbsp&gt;

=item Z&lt;&gt;4.  Now we can match the C&lt;&#39;a&#39;&gt; and the C&lt;&#39;t&#39;&gt;.

E&lt;nbsp&gt;

=item Z&lt;&gt;5.  Move on to the third element C&lt;&#39;.*&#39;&gt;.  Since we are at the
end of the string and C&lt;&#39;.*&#39;&gt; can match 0 times, assign it the empty
string.

E&lt;nbsp&gt;

=item Z&lt;&gt;6.  We are done!

=back

Most of the time, all this moving forward and backtracking happens
quickly and searching is fast. There are some pathological regexps,
however, whose execution time exponentially grows with the size of the
string.  A typical structure that blows up in your face is of the form

    /(a|b+)*/;

The problem is the nested indeterminate quantifiers.  There are many
different ways of partitioning a string of length n between the C&lt;&#39;+&#39;&gt;
and C&lt;&#39;*&#39;&gt;: one repetition with C&lt;b+&gt; of length n, two repetitions with
the first C&lt;b+&gt; length k and the second with length n-k, m repetitions
whose bits add up to length n, I&lt;etc&gt;.  In fact there are an exponential
number of ways to partition a string as a function of its length.  A
regexp may get lucky and match early in the process, but if there is
no match, Perl will try I&lt;every&gt; possibility before giving up.  So be
careful with nested C&lt;&#39;*&#39;&gt;&#39;s, C&lt;{n,m}&gt;&#39;s, and C&lt;&#39;+&#39;&gt;&#39;s.  The book
I&lt;Mastering Regular Expressions&gt; by Jeffrey Friedl gives a wonderful
discussion of this and other efficiency issues.


=head2 Possessive quantifiers

Backtracking during the relentless search for a match may be a waste
of time, particularly when the match is bound to fail.  Consider
the simple pattern

    /^\w+\s+\w+$/; # a word, spaces, a word

Whenever this is applied to a string which doesn&#39;t quite meet the
pattern&#39;s expectations such as S&lt;C&lt;&quot;abc  &quot;&gt;&gt; or S&lt;C&lt;&quot;abc  def &quot;&gt;&gt;,
the regexp engine will backtrack, approximately once for each character
in the string.  But we know that there is no way around taking I&lt;all&gt;
of the initial word characters to match the first repetition, that I&lt;all&gt;
spaces must be eaten by the middle part, and the same goes for the second
word.

With the introduction of the I&lt;possessive quantifiers&gt; in Perl 5.10, we
have a way of instructing the regexp engine not to backtrack, with the
usual quantifiers with a C&lt;&#39;+&#39;&gt; appended to them.  This makes them greedy as
well as stingy; once they succeed they won&#39;t give anything back to permit
another solution. They have the following meanings:

=over 4

=item *

C&lt;a{n,m}+&gt; means: match at least C&lt;n&gt; times, not more than C&lt;m&gt; times,
as many times as possible, and don&#39;t give anything up. C&lt;a?+&gt; is short
for C&lt;a{0,1}+&gt;

=item *

C&lt;a{n,}+&gt; means: match at least C&lt;n&gt; times, but as many times as possible,
and don&#39;t give anything up. C&lt;a*+&gt; is short for C&lt;a{0,}+&gt; and C&lt;a++&gt; is
short for C&lt;a{1,}+&gt;.

=item *

C&lt;a{n}+&gt; means: match exactly C&lt;n&gt; times.  It is just there for
notational consistency.

=back

These possessive quantifiers represent a special case of a more general
concept, the I&lt;independent subexpression&gt;, see below.

As an example where a possessive quantifier is suitable we consider
matching a quoted string, as it appears in several programming languages.
The backslash is used as an escape character that indicates that the
next character is to be taken literally, as another character for the
string.  Therefore, after the opening quote, we expect a (possibly
empty) sequence of alternatives: either some character except an
unescaped quote or backslash or an escaped character.

    /&quot;(?:[^&quot;\\]++|\\.)*+&quot;/;


=head2 Building a regexp

At this point, we have all the basic regexp concepts covered, so let&#39;s
give a more involved example of a regular expression.  We will build a
regexp that matches numbers.

The first task in building a regexp is to decide what we want to match
and what we want to exclude.  In our case, we want to match both
integers and floating point numbers and we want to reject any string
that isn&#39;t a number.

The next task is to break the problem down into smaller problems that
are easily converted into a regexp.

The simplest case is integers.  These consist of a sequence of digits,
with an optional sign in front.  The digits we can represent with
C&lt;\d+&gt; and the sign can be matched with C&lt;[+-]&gt;.  Thus the integer
regexp is

    /[+-]?\d+/;  # matches integers

A floating point number potentially has a sign, an integral part, a
decimal point, a fractional part, and an exponent.  One or more of these
parts is optional, so we need to check out the different
possibilities.  Floating point numbers which are in proper form include
123., 0.345, .34, -1e6, and 25.4E-72.  As with integers, the sign out
front is completely optional and can be matched by C&lt;[+-]?&gt;.  We can
see that if there is no exponent, floating point numbers must have a
decimal point, otherwise they are integers.  We might be tempted to
model these with C&lt;\d*\.\d*&gt;, but this would also match just a single
decimal point, which is not a number.  So the three cases of floating
point number without exponent are

   /[+-]?\d+\./;  # 1., 321., etc.
   /[+-]?\.\d+/;  # .1, .234, etc.
   /[+-]?\d+\.\d+/;  # 1.0, 30.56, etc.

These can be combined into a single regexp with a three-way alternation:

   /[+-]?(\d+\.\d+|\d+\.|\.\d+)/;  # floating point, no exponent

In this alternation, it is important to put C&lt;&#39;\d+\.\d+&#39;&gt; before
C&lt;&#39;\d+\.&#39;&gt;.  If C&lt;&#39;\d+\.&#39;&gt; were first, the regexp would happily match that
and ignore the fractional part of the number.

Now consider floating point numbers with exponents.  The key
observation here is that I&lt;both&gt; integers and numbers with decimal
points are allowed in front of an exponent.  Then exponents, like the
overall sign, are independent of whether we are matching numbers with
or without decimal points, and can be &quot;decoupled&quot; from the
mantissa.  The overall form of the regexp now becomes clear:

    /^(optional sign)(integer | f.p. mantissa)(optional exponent)$/;

The exponent is an C&lt;&#39;e&#39;&gt; or C&lt;&#39;E&#39;&gt;, followed by an integer.  So the
exponent regexp is

   /[eE][+-]?\d+/;  # exponent

Putting all the parts together, we get a regexp that matches numbers:

   /^[+-]?(\d+\.\d+|\d+\.|\.\d+|\d+)([eE][+-]?\d+)?$/;  # Ta da!

Long regexps like this may impress your friends, but can be hard to
decipher.  In complex situations like this, the C&lt;/x&gt; modifier for a
match is invaluable.  It allows one to put nearly arbitrary whitespace
and comments into a regexp without affecting their meaning.  Using it,
we can rewrite our &quot;extended&quot; regexp in the more pleasing form

   /^
      [+-]?         # first, match an optional sign
      (             # then match integers or f.p. mantissas:
          \d+\.\d+  # mantissa of the form a.b
         |\d+\.     # mantissa of the form a.
         |\.\d+     # mantissa of the form .b
         |\d+       # integer of the form a
      )
      ( [eE] [+-]? \d+ )?  # finally, optionally match an exponent
   $/x;

If whitespace is mostly irrelevant, how does one include space
characters in an extended regexp? The answer is to backslash it
S&lt;C&lt;&#39;\ &#39;&gt;&gt; or put it in a character class S&lt;C&lt;[ ]&gt;&gt;.  The same thing
goes for pound signs: use C&lt;\#&gt; or C&lt;[#]&gt;.  For instance, Perl allows
a space between the sign and the mantissa or integer, and we could add
this to our regexp as follows:

   /^
      [+-]?\ *      # first, match an optional sign *and space*
      (             # then match integers or f.p. mantissas:
          \d+\.\d+  # mantissa of the form a.b
         |\d+\.     # mantissa of the form a.
         |\.\d+     # mantissa of the form .b
         |\d+       # integer of the form a
      )
      ( [eE] [+-]? \d+ )?  # finally, optionally match an exponent
   $/x;

In this form, it is easier to see a way to simplify the
alternation.  Alternatives 1, 2, and 4 all start with C&lt;\d+&gt;, so it
could be factored out:

   /^
      [+-]?\ *      # first, match an optional sign
      (             # then match integers or f.p. mantissas:
          \d+       # start out with a ...
          (
              \.\d* # mantissa of the form a.b or a.
          )?        # ? takes care of integers of the form a
         |\.\d+     # mantissa of the form .b
      )
      ( [eE] [+-]? \d+ )?  # finally, optionally match an exponent
   $/x;

Starting in Perl v5.26, specifying C&lt;/xx&gt; changes the square-bracketed
portions of a pattern to ignore tabs and space characters unless they
are escaped by preceding them with a backslash.  So, we could write

   /^
      [ + - ]?\ *   # first, match an optional sign
      (             # then match integers or f.p. mantissas:
          \d+       # start out with a ...
          (
              \.\d* # mantissa of the form a.b or a.
          )?        # ? takes care of integers of the form a
         |\.\d+     # mantissa of the form .b
      )
      ( [ e E ] [ + - ]? \d+ )?  # finally, optionally match an exponent
   $/xx;

This doesn&#39;t really improve the legibility of this example, but it&#39;s
available in case you want it.  Squashing the pattern down to the
compact form, we have

    /^[+-]?\ *(\d+(\.\d*)?|\.\d+)([eE][+-]?\d+)?$/;

This is our final regexp.  To recap, we built a regexp by

=over 4

=item *

specifying the task in detail,

=item *

breaking down the problem into smaller parts,

=item *

translating the small parts into regexps,

=item *

combining the regexps,

=item *

and optimizing the final combined regexp.

=back

These are also the typical steps involved in writing a computer
program.  This makes perfect sense, because regular expressions are
essentially programs written in a little computer language that specifies
patterns.

=head2 Using regular expressions in Perl

The last topic of Part 1 briefly covers how regexps are used in Perl
programs.  Where do they fit into Perl syntax?

We have already introduced the matching operator in its default
C&lt;/regexp/&gt; and arbitrary delimiter C&lt;m!regexp!&gt; forms.  We have used
the binding operator C&lt;=~&gt; and its negation C&lt;!~&gt; to test for string
matches.  Associated with the matching operator, we have discussed the
single line C&lt;/s&gt;, multi-line C&lt;/m&gt;, case-insensitive C&lt;/i&gt; and
extended C&lt;/x&gt; modifiers.  There are a few more things you might
want to know about matching operators.

=head3 Prohibiting substitution

If you change C&lt;$pattern&gt; after the first substitution happens, Perl
will ignore it.  If you don&#39;t want any substitutions at all, use the
special delimiter C&lt;m&#39;&#39;&gt;:

    @pattern = (&#39;Seuss&#39;);
    while (&lt;&gt;) {
        print if m&#39;@pattern&#39;;  # matches literal &#39;@pattern&#39;, not &#39;Seuss&#39;
    }

Similar to strings, C&lt;m&#39;&#39;&gt; acts like apostrophes on a regexp; all other
C&lt;&#39;m&#39;&gt; delimiters act like quotes.  If the regexp evaluates to the empty string,
the regexp in the I&lt;last successful match&gt; is used instead.  So we have

    &quot;dog&quot; =~ /d/;  # &#39;d&#39; matches
    &quot;dogbert =~ //;  # this matches the &#39;d&#39; regexp used before


=head3 Global matching

The final two modifiers we will discuss here,
C&lt;/g&gt; and C&lt;/c&gt;, concern multiple matches.
The modifier C&lt;/g&gt; stands for global matching and allows the
matching operator to match within a string as many times as possible.
In scalar context, successive invocations against a string will have
C&lt;/g&gt; jump from match to match, keeping track of position in the
string as it goes along.  You can get or set the position with the
C&lt;pos()&gt; function.

The use of C&lt;/g&gt; is shown in the following example.  Suppose we have
a string that consists of words separated by spaces.  If we know how
many words there are in advance, we could extract the words using
groupings:

    $x = &quot;cat dog house&quot;; # 3 words
    $x =~ /^\s*(\w+)\s+(\w+)\s+(\w+)\s*$/; # matches,
                                           # $1 = &#39;cat&#39;
                                           # $2 = &#39;dog&#39;
                                           # $3 = &#39;house&#39;

But what if we had an indeterminate number of words? This is the sort
of task C&lt;/g&gt; was made for.  To extract all words, form the simple
regexp C&lt;(\w+)&gt; and loop over all matches with C&lt;/(\w+)/g&gt;:

    while ($x =~ /(\w+)/g) {
        print &quot;Word is $1, ends at position &quot;, pos $x, &quot;\n&quot;;
    }

prints

    Word is cat, ends at position 3
    Word is dog, ends at position 7
    Word is house, ends at position 13

A failed match or changing the target string resets the position.  If
you don&#39;t want the position reset after failure to match, add the
C&lt;/c&gt;, as in C&lt;/regexp/gc&gt;.  The current position in the string is
associated with the string, not the regexp.  This means that different
strings have different positions and their respective positions can be
set or read independently.

In list context, C&lt;/g&gt; returns a list of matched groupings, or if
there are no groupings, a list of matches to the whole regexp.  So if
we wanted just the words, we could use

    @words = ($x =~ /(\w+)/g);  # matches,
                                # $words[0] = &#39;cat&#39;
                                # $words[1] = &#39;dog&#39;
                                # $words[2] = &#39;house&#39;

Closely associated with the C&lt;/g&gt; modifier is the C&lt;\G&gt; anchor.  The
C&lt;\G&gt; anchor matches at the point where the previous C&lt;/g&gt; match left
off.  C&lt;\G&gt; allows us to easily do context-sensitive matching:

    $metric = 1;  # use metric units
    ...
    $x = &lt;FILE&gt;;  # read in measurement
    $x =~ /^([+-]?\d+)\s*/g;  # get magnitude
    $weight = $1;
    if ($metric) { # error checking
        print &quot;Units error!&quot; unless $x =~ /\Gkg\./g;
    }
    else {
        print &quot;Units error!&quot; unless $x =~ /\Glbs\./g;
    }
    $x =~ /\G\s+(widget|sprocket)/g;  # continue processing

The combination of C&lt;/g&gt; and C&lt;\G&gt; allows us to process the string a
bit at a time and use arbitrary Perl logic to decide what to do next.
Currently, the C&lt;\G&gt; anchor is only fully supported when used to anchor
to the start of the pattern.

C&lt;\G&gt; is also invaluable in processing fixed-length records with
regexps.  Suppose we have a snippet of coding region DNA, encoded as
base pair letters C&lt;ATCGTTGAAT...&gt; and we want to find all the stop
codons C&lt;TGA&gt;.  In a coding region, codons are 3-letter sequences, so
we can think of the DNA snippet as a sequence of 3-letter records.  The
naive regexp

    # expanded, this is &quot;ATC GTT GAA TGC AAA TGA CAT GAC&quot;
    $dna = &quot;ATCGTTGAATGCAAATGACATGAC&quot;;
    $dna =~ /TGA/;

doesn&#39;t work; it may match a C&lt;TGA&gt;, but there is no guarantee that
the match is aligned with codon boundaries, I&lt;e.g.&gt;, the substring
S&lt;C&lt;GTT GAA&gt;&gt; gives a match.  A better solution is

    while ($dna =~ /(\w\w\w)*?TGA/g) {  # note the minimal *?
        print &quot;Got a TGA stop codon at position &quot;, pos $dna, &quot;\n&quot;;
    }

which prints

    Got a TGA stop codon at position 18
    Got a TGA stop codon at position 23

Position 18 is good, but position 23 is bogus.  What happened?

The answer is that our regexp works well until we get past the last
real match.  Then the regexp will fail to match a synchronized C&lt;TGA&gt;
and start stepping ahead one character position at a time, not what we
want.  The solution is to use C&lt;\G&gt; to anchor the match to the codon
alignment:

    while ($dna =~ /\G(\w\w\w)*?TGA/g) {
        print &quot;Got a TGA stop codon at position &quot;, pos $dna, &quot;\n&quot;;
    }

This prints

    Got a TGA stop codon at position 18

which is the correct answer.  This example illustrates that it is
important not only to match what is desired, but to reject what is not
desired.

(There are other regexp modifiers that are available, such as
C&lt;/o&gt;, but their specialized uses are beyond the
scope of this introduction.  )

=head3 Search and replace

Regular expressions also play a big role in I&lt;search and replace&gt;
operations in Perl.  Search and replace is accomplished with the
C&lt;s///&gt; operator.  The general form is
C&lt;s/regexp/replacement/modifiers&gt;, with everything we know about
regexps and modifiers applying in this case as well.  The
I&lt;replacement&gt; is a Perl double-quoted string that replaces in the
string whatever is matched with the C&lt;regexp&gt;.  The operator C&lt;=~&gt; is
also used here to associate a string with C&lt;s///&gt;.  If matching
against C&lt;$_&gt;, the S&lt;C&lt;$_ =~&gt;&gt; can be dropped.  If there is a match,
C&lt;s///&gt; returns the number of substitutions made; otherwise it returns
false.  Here are a few examples:

    $x = &quot;Time to feed the cat!&quot;;
    $x =~ s/cat/hacker/;   # $x contains &quot;Time to feed the hacker!&quot;
    if ($x =~ s/^(Time.*hacker)!$/$1 now!/) {
        $more_insistent = 1;
    }
    $y = &quot;&#39;quoted words&#39;&quot;;
    $y =~ s/^&#39;(.*)&#39;$/$1/;  # strip single quotes,
                           # $y contains &quot;quoted words&quot;

In the last example, the whole string was matched, but only the part
inside the single quotes was grouped.  With the C&lt;s///&gt; operator, the
matched variables C&lt;$1&gt;, C&lt;$2&gt;, I&lt;etc&gt;. are immediately available for use
in the replacement expression, so we use C&lt;$1&gt; to replace the quoted
string with just what was quoted.  With the global modifier, C&lt;s///g&gt;
will search and replace all occurrences of the regexp in the string:

    $x = &quot;I batted 4 for 4&quot;;
    $x =~ s/4/four/;   # doesn&#39;t do it all:
                       # $x contains &quot;I batted four for 4&quot;
    $x = &quot;I batted 4 for 4&quot;;
    $x =~ s/4/four/g;  # does it all:
                       # $x contains &quot;I batted four for four&quot;

If you prefer &quot;regex&quot; over &quot;regexp&quot; in this tutorial, you could use
the following program to replace it:

    % cat &gt; simple_replace
    #!/usr/bin/perl
    $regexp = shift;
    $replacement = shift;
    while (&lt;&gt;) {
        s/$regexp/$replacement/g;
        print;
    }
    ^D

    % simple_replace regexp regex perlretut.pod

In C&lt;simple_replace&gt; we used the C&lt;s///g&gt; modifier to replace all
occurrences of the regexp on each line.  (Even though the regular
expression appears in a loop, Perl is smart enough to compile it
only once.)  As with C&lt;simple_grep&gt;, both the
C&lt;print&gt; and the C&lt;s/$regexp/$replacement/g&gt; use C&lt;$_&gt; implicitly.

If you don&#39;t want C&lt;s///&gt; to change your original variable you can use
the non-destructive substitute modifier, C&lt;s///r&gt;.  This changes the
behavior so that C&lt;s///r&gt; returns the final substituted string
(instead of the number of substitutions):

    $x = &quot;I like dogs.&quot;;
    $y = $x =~ s/dogs/cats/r;
    print &quot;$x $y\n&quot;;

That example will print &quot;I like dogs. I like cats&quot;. Notice the original
C&lt;$x&gt; variable has not been affected. The overall
result of the substitution is instead stored in C&lt;$y&gt;. If the
substitution doesn&#39;t affect anything then the original string is
returned:

    $x = &quot;I like dogs.&quot;;
    $y = $x =~ s/elephants/cougars/r;
    print &quot;$x $y\n&quot;; # prints &quot;I like dogs. I like dogs.&quot;

One other interesting thing that the C&lt;s///r&gt; flag allows is chaining
substitutions:

    $x = &quot;Cats are great.&quot;;
    print $x =~ s/Cats/Dogs/r =~ s/Dogs/Frogs/r =~
        s/Frogs/Hedgehogs/r, &quot;\n&quot;;
    # prints &quot;Hedgehogs are great.&quot;

A modifier available specifically to search and replace is the
C&lt;s///e&gt; evaluation modifier.  C&lt;s///e&gt; treats the
replacement text as Perl code, rather than a double-quoted
string.  The value that the code returns is substituted for the
matched substring.  C&lt;s///e&gt; is useful if you need to do a bit of
computation in the process of replacing text.  This example counts
character frequencies in a line:

    $x = &quot;Bill the cat&quot;;
    $x =~ s/(.)/$chars{$1}++;$1/eg; # final $1 replaces char with itself
    print &quot;frequency of &#39;$_&#39; is $chars{$_}\n&quot;
        foreach (sort {$chars{$b} &lt;=&gt; $chars{$a}} keys %chars);

This prints

    frequency of &#39; &#39; is 2
    frequency of &#39;t&#39; is 2
    frequency of &#39;l&#39; is 2
    frequency of &#39;B&#39; is 1
    frequency of &#39;c&#39; is 1
    frequency of &#39;e&#39; is 1
    frequency of &#39;h&#39; is 1
    frequency of &#39;i&#39; is 1
    frequency of &#39;a&#39; is 1

As with the match C&lt;m//&gt; operator, C&lt;s///&gt; can use other delimiters,
such as C&lt;s!!!&gt; and C&lt;s{}{}&gt;, and even C&lt;s{}//&gt;.  If single quotes are
used C&lt;s&#39;&#39;&#39;&gt;, then the regexp and replacement are
treated as single-quoted strings and there are no
variable substitutions.  C&lt;s///&gt; in list context
returns the same thing as in scalar context, I&lt;i.e.&gt;, the number of
matches.

=head3 The split function

The C&lt;split()&gt; function is another place where a regexp is used.
C&lt;split /regexp/, string, limit&gt; separates the C&lt;string&gt; operand into
a list of substrings and returns that list.  The regexp must be designed
to match whatever constitutes the separators for the desired substrings.
The C&lt;limit&gt;, if present, constrains splitting into no more than C&lt;limit&gt;
number of strings.  For example, to split a string into words, use

    $x = &quot;Calvin and Hobbes&quot;;
    @words = split /\s+/, $x;  # $word[0] = &#39;Calvin&#39;
                               # $word[1] = &#39;and&#39;
                               # $word[2] = &#39;Hobbes&#39;

If the empty regexp C&lt;//&gt; is used, the regexp always matches and
the string is split into individual characters.  If the regexp has
groupings, then the resulting list contains the matched substrings from the
groupings as well.  For instance,

    $x = &quot;/usr/bin/perl&quot;;
    @dirs = split m!/!, $x;  # $dirs[0] = &#39;&#39;
                             # $dirs[1] = &#39;usr&#39;
                             # $dirs[2] = &#39;bin&#39;
                             # $dirs[3] = &#39;perl&#39;
    @parts = split m!(/)!, $x;  # $parts[0] = &#39;&#39;
                                # $parts[1] = &#39;/&#39;
                                # $parts[2] = &#39;usr&#39;
                                # $parts[3] = &#39;/&#39;
                                # $parts[4] = &#39;bin&#39;
                                # $parts[5] = &#39;/&#39;
                                # $parts[6] = &#39;perl&#39;

Since the first character of C&lt;$x&gt; matched the regexp, C&lt;split&gt; prepended
an empty initial element to the list.

If you have read this far, congratulations! You now have all the basic
tools needed to use regular expressions to solve a wide range of text
processing problems.  If this is your first time through the tutorial,
why not stop here and play around with regexps a while....  S&lt;Part 2&gt;
concerns the more esoteric aspects of regular expressions and those
concepts certainly aren&#39;t needed right at the start.

=head1 Part 2: Power tools

OK, you know the basics of regexps and you want to know more.  If
matching regular expressions is analogous to a walk in the woods, then
the tools discussed in Part 1 are analogous to topo maps and a
compass, basic tools we use all the time.  Most of the tools in part 2
are analogous to flare guns and satellite phones.  They aren&#39;t used
too often on a hike, but when we are stuck, they can be invaluable.

What follows are the more advanced, less used, or sometimes esoteric
capabilities of Perl regexps.  In Part 2, we will assume you are
comfortable with the basics and concentrate on the advanced features.

=head2 More on characters, strings, and character classes

There are a number of escape sequences and character classes that we
haven&#39;t covered yet.

There are several escape sequences that convert characters or strings
between upper and lower case, and they are also available within
patterns.  C&lt;\l&gt; and C&lt;\u&gt; convert the next character to lower or
upper case, respectively:

    $x = &quot;perl&quot;;
    $string =~ /\u$x/;  # matches &#39;Perl&#39; in $string
    $x = &quot;M(rs?|s)\\.&quot;; # note the double backslash
    $string =~ /\l$x/;  # matches &#39;mr.&#39;, &#39;mrs.&#39;, and &#39;ms.&#39;,

A C&lt;\L&gt; or C&lt;\U&gt; indicates a lasting conversion of case, until
terminated by C&lt;\E&gt; or thrown over by another C&lt;\U&gt; or C&lt;\L&gt;:

    $x = &quot;This word is in lower case:\L SHOUT\E&quot;;
    $x =~ /shout/;       # matches
    $x = &quot;I STILL KEYPUNCH CARDS FOR MY 360&quot;
    $x =~ /\Ukeypunch/;  # matches punch card string

If there is no C&lt;\E&gt;, case is converted until the end of the
string. The regexps C&lt;\L\u$word&gt; or C&lt;\u\L$word&gt; convert the first
character of C&lt;$word&gt; to uppercase and the rest of the characters to
lowercase.

Control characters can be escaped with C&lt;\c&gt;, so that a control-Z
character would be matched with C&lt;\cZ&gt;.  The escape sequence
C&lt;\Q&gt;...C&lt;\E&gt; quotes, or protects most non-alphabetic characters.   For
instance,

    $x = &quot;\QThat !^*&amp;%~&amp; cat!&quot;;
    $x =~ /\Q!^*&amp;%~&amp;\E/;  # check for rough language

It does not protect C&lt;&#39;$&#39;&gt; or C&lt;&#39;@&#39;&gt;, so that variables can still be
substituted.

C&lt;\Q&gt;, C&lt;\L&gt;, C&lt;\l&gt;, C&lt;\U&gt;, C&lt;\u&gt; and C&lt;\E&gt; are actually part of
double-quotish syntax, and not part of regexp syntax proper.  They will
work if they appear in a regular expression embedded directly in a
program, but not when contained in a string that is interpolated in a
pattern.

Perl regexps can handle more than just the
standard ASCII character set.  Perl supports I&lt;Unicode&gt;, a standard
for representing the alphabets from virtually all of the world&#39;s written
languages, and a host of symbols.  Perl&#39;s text strings are Unicode strings, so
they can contain characters with a value (codepoint or character number) higher
than 255.

What does this mean for regexps? Well, regexp users don&#39;t need to know
much about Perl&#39;s internal representation of strings.  But they do need
to know 1) how to represent Unicode characters in a regexp and 2) that
a matching operation will treat the string to be searched as a sequence
of characters, not bytes.  The answer to 1) is that Unicode characters
greater than C&lt;chr(255)&gt; are represented using the C&lt;\x{hex}&gt; notation, because
C&lt;\x&gt;I&lt;XY&gt; (without curly braces and I&lt;XY&gt; are two hex digits) doesn&#39;t
go further than 255.  (Starting in Perl 5.14, if you&#39;re an octal fan,
you can also use C&lt;\o{oct}&gt;.)

    /\x{263a}/;  # match a Unicode smiley face :)

B&lt;NOTE&gt;: In Perl 5.6.0 it used to be that one needed to say C&lt;use
utf8&gt; to use any Unicode features.  This is no more the case: for
almost all Unicode processing, the explicit C&lt;utf8&gt; pragma is not
needed.  (The only case where it matters is if your Perl script is in
Unicode and encoded in UTF-8, then an explicit C&lt;use utf8&gt; is needed.)

Figuring out the hexadecimal sequence of a Unicode character you want
or deciphering someone else&#39;s hexadecimal Unicode regexp is about as
much fun as programming in machine code.  So another way to specify
Unicode characters is to use the I&lt;named character&gt; escape
sequence C&lt;\N{I&lt;name&gt;}&gt;.  I&lt;name&gt; is a name for the Unicode character, as
specified in the Unicode standard.  For instance, if we wanted to
represent or match the astrological sign for the planet Mercury, we
could use

    $x = &quot;abc\N{MERCURY}def&quot;;
    $x =~ /\N{MERCURY}/;   # matches

One can also use &quot;short&quot; names:

    print &quot;\N{GREEK SMALL LETTER SIGMA} is called sigma.\n&quot;;
    print &quot;\N{greek:Sigma} is an upper-case sigma.\n&quot;;

You can also restrict names to a certain alphabet by specifying the
L&lt;charnames&gt; pragma:

    use charnames qw(greek);
    print &quot;\N{sigma} is Greek sigma\n&quot;;

An index of character names is available on-line from the Unicode
Consortium, L&lt;http://www.unicode.org/charts/charindex.html&gt;; explanatory
material with links to other resources at
L&lt;http://www.unicode.org/standard/where&gt;.

The answer to requirement 2) is that a regexp (mostly)
uses Unicode characters.  The &quot;mostly&quot; is for messy backward
compatibility reasons, but starting in Perl 5.14, any regexp compiled in
the scope of a C&lt;use feature &#39;unicode_strings&#39;&gt; (which is automatically
turned on within the scope of a C&lt;use 5.012&gt; or higher) will turn that
&quot;mostly&quot; into &quot;always&quot;.  If you want to handle Unicode properly, you
should ensure that C&lt;&#39;unicode_strings&#39;&gt; is turned on.
Internally, this is encoded to bytes using either UTF-8 or a native 8
bit encoding, depending on the history of the string, but conceptually
it is a sequence of characters, not bytes. See L&lt;perlunitut&gt; for a
tutorial about that.

Let us now discuss Unicode character classes, most usually called
&quot;character properties&quot;.  These are represented by the C&lt;\p{I&lt;name&gt;}&gt;
escape sequence.  The negation of this is C&lt;\P{I&lt;name&gt;}&gt;.  For example,
to match lower and uppercase characters,

    $x = &quot;BOB&quot;;
    $x =~ /^\p{IsUpper}/;   # matches, uppercase char class
    $x =~ /^\P{IsUpper}/;   # doesn&#39;t match, char class sans uppercase
    $x =~ /^\p{IsLower}/;   # doesn&#39;t match, lowercase char class
    $x =~ /^\P{IsLower}/;   # matches, char class sans lowercase

(The &quot;C&lt;Is&gt;&quot; is optional.)

There are many, many Unicode character properties.  For the full list
see L&lt;perluniprops&gt;.  Most of them have synonyms with shorter names,
also listed there.  Some synonyms are a single character.  For these,
you can drop the braces.  For instance, C&lt;\pM&gt; is the same thing as
C&lt;\p{Mark}&gt;, meaning things like accent marks.

The Unicode C&lt;\p{Script}&gt; and C&lt;\p{Script_Extensions}&gt; properties are
used to categorize every Unicode character into the language script it
is written in.  (C&lt;Script_Extensions&gt; is an improved version of
C&lt;Script&gt;, which is retained for backward compatibility, and so you
should generally use C&lt;Script_Extensions&gt;.)
For example,
English, French, and a bunch of other European languages are written in
the Latin script.  But there is also the Greek script, the Thai script,
the Katakana script, I&lt;etc&gt;.  You can test whether a character is in a
particular script (based on C&lt;Script_Extensions&gt;) with, for example
C&lt;\p{Latin}&gt;, C&lt;\p{Greek}&gt;, or C&lt;\p{Katakana}&gt;.  To test if it isn&#39;t in
the Balinese script, you would use C&lt;\P{Balinese}&gt;.

What we have described so far is the single form of the C&lt;\p{...}&gt; character
classes.  There is also a compound form which you may run into.  These
look like C&lt;\p{I&lt;name&gt;=I&lt;value&gt;}&gt; or C&lt;\p{I&lt;name&gt;:I&lt;value&gt;}&gt; (the equals sign and colon
can be used interchangeably).  These are more general than the single form,
and in fact most of the single forms are just Perl-defined shortcuts for common
compound forms.  For example, the script examples in the previous paragraph
could be written equivalently as C&lt;\p{Script_Extensions=Latin}&gt;, C&lt;\p{Script_Extensions:Greek}&gt;,
C&lt;\p{script_extensions=katakana}&gt;, and C&lt;\P{script_extensions=balinese}&gt; (case is irrelevant
between the C&lt;{}&gt; braces).  You may
never have to use the compound forms, but sometimes it is necessary, and their
use can make your code easier to understand.

C&lt;\X&gt; is an abbreviation for a character class that comprises
a Unicode I&lt;extended grapheme cluster&gt;.  This represents a &quot;logical character&quot;:
what appears to be a single character, but may be represented internally by more
than one.  As an example, using the Unicode full names, I&lt;e.g.&gt;, &quot;S&lt;A + COMBINING
RING&gt;&quot; is a grapheme cluster with base character &quot;A&quot; and combining character
&quot;S&lt;COMBINING RING&gt;, which translates in Danish to &quot;A&quot; with the circle atop it,
as in the word E&lt;Aring&gt;ngstrom.

For the full and latest information about Unicode see the latest
Unicode standard, or the Unicode Consortium&#39;s website L&lt;http://www.unicode.org&gt;

As if all those classes weren&#39;t enough, Perl also defines POSIX-style
character classes.  These have the form C&lt;[:I&lt;name&gt;:]&gt;, with I&lt;name&gt; the
name of the POSIX class.  The POSIX classes are C&lt;alpha&gt;, C&lt;alnum&gt;,
C&lt;ascii&gt;, C&lt;cntrl&gt;, C&lt;digit&gt;, C&lt;graph&gt;, C&lt;lower&gt;, C&lt;print&gt;, C&lt;punct&gt;,
C&lt;space&gt;, C&lt;upper&gt;, and C&lt;xdigit&gt;, and two extensions, C&lt;word&gt; (a Perl
extension to match C&lt;\w&gt;), and C&lt;blank&gt; (a GNU extension).  The C&lt;/a&gt;
modifier restricts these to matching just in the ASCII range; otherwise
they can match the same as their corresponding Perl Unicode classes:
C&lt;[:upper:]&gt; is the same as C&lt;\p{IsUpper}&gt;, I&lt;etc&gt;.  (There are some
exceptions and gotchas with this; see L&lt;perlrecharclass&gt; for a full
discussion.) The C&lt;[:digit:]&gt;, C&lt;[:word:]&gt;, and
C&lt;[:space:]&gt; correspond to the familiar C&lt;\d&gt;, C&lt;\w&gt;, and C&lt;\s&gt;
character classes.  To negate a POSIX class, put a C&lt;&#39;^&#39;&gt; in front of
the name, so that, I&lt;e.g.&gt;, C&lt;[:^digit:]&gt; corresponds to C&lt;\D&gt; and, under
Unicode, C&lt;\P{IsDigit}&gt;.  The Unicode and POSIX character classes can
be used just like C&lt;\d&gt;, with the exception that POSIX character
classes can only be used inside of a character class:

    /\s+[abc[:digit:]xyz]\s*/;  # match a,b,c,x,y,z, or a digit
    /^=item\s[[:digit:]]/;      # match &#39;=item&#39;,
                                # followed by a space and a digit
    /\s+[abc\p{IsDigit}xyz]\s+/;  # match a,b,c,x,y,z, or a digit
    /^=item\s\p{IsDigit}/;        # match &#39;=item&#39;,
                                  # followed by a space and a digit

Whew! That is all the rest of the characters and character classes.

=head2 Compiling and saving regular expressions

In Part 1 we mentioned that Perl compiles a regexp into a compact
sequence of opcodes.  Thus, a compiled regexp is a data structure
that can be stored once and used again and again.  The regexp quote
C&lt;qr//&gt; does exactly that: C&lt;qr/string/&gt; compiles the C&lt;string&gt; as a
regexp and transforms the result into a form that can be assigned to a
variable:

    $reg = qr/foo+bar?/;  # reg contains a compiled regexp

Then C&lt;$reg&gt; can be used as a regexp:

    $x = &quot;fooooba&quot;;
    $x =~ $reg;     # matches, just like /foo+bar?/
    $x =~ /$reg/;   # same thing, alternate form

C&lt;$reg&gt; can also be interpolated into a larger regexp:

    $x =~ /(abc)?$reg/;  # still matches

As with the matching operator, the regexp quote can use different
delimiters, I&lt;e.g.&gt;, C&lt;qr!!&gt;, C&lt;qr{}&gt; or C&lt;qr~~&gt;.  Apostrophes
as delimiters (C&lt;qr&#39;&#39;&gt;) inhibit any interpolation.

Pre-compiled regexps are useful for creating dynamic matches that
don&#39;t need to be recompiled each time they are encountered.  Using
pre-compiled regexps, we write a C&lt;grep_step&gt; program which greps
for a sequence of patterns, advancing to the next pattern as soon
as one has been satisfied.

    % cat &gt; grep_step
    #!/usr/bin/perl
    # grep_step - match &lt;number&gt; regexps, one after the other
    # usage: multi_grep &lt;number&gt; regexp1 regexp2 ... file1 file2 ...

    $number = shift;
    $regexp[$_] = shift foreach (0..$number-1);
    @compiled = map qr/$_/, @regexp;
    while ($line = &lt;&gt;) {
        if ($line =~ /$compiled[0]/) {
            print $line;
            shift @compiled;
            last unless @compiled;
        }
    }
    ^D

    % grep_step 3 shift print last grep_step
    $number = shift;
            print $line;
            last unless @compiled;

Storing pre-compiled regexps in an array C&lt;@compiled&gt; allows us to
simply loop through the regexps without any recompilation, thus gaining
flexibility without sacrificing speed.


=head2 Composing regular expressions at runtime

Backtracking is more efficient than repeated tries with different regular
expressions.  If there are several regular expressions and a match with
any of them is acceptable, then it is possible to combine them into a set
of alternatives.  If the individual expressions are input data, this
can be done by programming a join operation.  We&#39;ll exploit this idea in
an improved version of the C&lt;simple_grep&gt; program: a program that matches
multiple patterns:

    % cat &gt; multi_grep
    #!/usr/bin/perl
    # multi_grep - match any of &lt;number&gt; regexps
    # usage: multi_grep &lt;number&gt; regexp1 regexp2 ... file1 file2 ...

    $number = shift;
    $regexp[$_] = shift foreach (0..$number-1);
    $pattern = join &#39;|&#39;, @regexp;

    while ($line = &lt;&gt;) {
        print $line if $line =~ /$pattern/;
    }
    ^D

    % multi_grep 2 shift for multi_grep
    $number = shift;
    $regexp[$_] = shift foreach (0..$number-1);

Sometimes it is advantageous to construct a pattern from the I&lt;input&gt;
that is to be analyzed and use the permissible values on the left
hand side of the matching operations.  As an example for this somewhat
paradoxical situation, let&#39;s assume that our input contains a command
verb which should match one out of a set of available command verbs,
with the additional twist that commands may be abbreviated as long as
the given string is unique. The program below demonstrates the basic
algorithm.

    % cat &gt; keymatch
    #!/usr/bin/perl
    $kwds = &#39;copy compare list print&#39;;
    while( $cmd = &lt;&gt; ){
        $cmd =~ s/^\s+|\s+$//g;  # trim leading and trailing spaces
        if( ( @matches = $kwds =~ /\b$cmd\w*/g ) == 1 ){
            print &quot;command: &#39;@matches&#39;\n&quot;;
        } elsif( @matches == 0 ){
            print &quot;no such command: &#39;$cmd&#39;\n&quot;;
        } else {
            print &quot;not unique: &#39;$cmd&#39; (could be one of: @matches)\n&quot;;
        }
    }
    ^D

    % keymatch
    li
    command: &#39;list&#39;
    co
    not unique: &#39;co&#39; (could be one of: copy compare)
    printer
    no such command: &#39;printer&#39;

Rather than trying to match the input against the keywords, we match the
combined set of keywords against the input.  The pattern matching
operation S&lt;C&lt;$kwds =~ /\b($cmd\w*)/g&gt;&gt; does several things at the
same time. It makes sure that the given command begins where a keyword
begins (C&lt;\b&gt;). It tolerates abbreviations due to the added C&lt;\w*&gt;. It
tells us the number of matches (C&lt;scalar @matches&gt;) and all the keywords
that were actually matched.  You could hardly ask for more.

=head2 Embedding comments and modifiers in a regular expression

Starting with this section, we will be discussing Perl&#39;s set of
I&lt;extended patterns&gt;.  These are extensions to the traditional regular
expression syntax that provide powerful new tools for pattern
matching.  We have already seen extensions in the form of the minimal
matching constructs C&lt;??&gt;, C&lt;*?&gt;, C&lt;+?&gt;, C&lt;{n,m}?&gt;, and C&lt;{n,}?&gt;.  Most
of the extensions below have the form C&lt;(?char...)&gt;, where the
C&lt;char&gt; is a character that determines the type of extension.

The first extension is an embedded comment C&lt;(?#text)&gt;.  This embeds a
comment into the regular expression without affecting its meaning.  The
comment should not have any closing parentheses in the text.  An
example is

    /(?# Match an integer:)[+-]?\d+/;

This style of commenting has been largely superseded by the raw,
freeform commenting that is allowed with the C&lt;/x&gt; modifier.

Most modifiers, such as C&lt;/i&gt;, C&lt;/m&gt;, C&lt;/s&gt; and C&lt;/x&gt; (or any
combination thereof) can also be embedded in
a regexp using C&lt;(?i)&gt;, C&lt;(?m)&gt;, C&lt;(?s)&gt;, and C&lt;(?x)&gt;.  For instance,

    /(?i)yes/;  # match &#39;yes&#39; case insensitively
    /yes/i;     # same thing
    /(?x)(          # freeform version of an integer regexp
             [+-]?  # match an optional sign
             \d+    # match a sequence of digits
         )
    /x;

Embedded modifiers can have two important advantages over the usual
modifiers.  Embedded modifiers allow a custom set of modifiers for
I&lt;each&gt; regexp pattern.  This is great for matching an array of regexps
that must have different modifiers:

    $pattern[0] = &#39;(?i)doctor&#39;;
    $pattern[1] = &#39;Johnson&#39;;
    ...
    while (&lt;&gt;) {
        foreach $patt (@pattern) {
            print if /$patt/;
        }
    }

The second advantage is that embedded modifiers (except C&lt;/p&gt;, which
modifies the entire regexp) only affect the regexp
inside the group the embedded modifier is contained in.  So grouping
can be used to localize the modifier&#39;s effects:

    /Answer: ((?i)yes)/;  # matches &#39;Answer: yes&#39;, &#39;Answer: YES&#39;, etc.

Embedded modifiers can also turn off any modifiers already present
by using, I&lt;e.g.&gt;, C&lt;(?-i)&gt;.  Modifiers can also be combined into
a single expression, I&lt;e.g.&gt;, C&lt;(?s-i)&gt; turns on single line mode and
turns off case insensitivity.

Embedded modifiers may also be added to a non-capturing grouping.
C&lt;(?i-m:regexp)&gt; is a non-capturing grouping that matches C&lt;regexp&gt;
case insensitively and turns off multi-line mode.


=head2 Looking ahead and looking behind

This section concerns the lookahead and lookbehind assertions.  First,
a little background.

In Perl regular expressions, most regexp elements &quot;eat up&quot; a certain
amount of string when they match.  For instance, the regexp element
C&lt;[abc]&gt; eats up one character of the string when it matches, in the
sense that Perl moves to the next character position in the string
after the match.  There are some elements, however, that don&#39;t eat up
characters (advance the character position) if they match.  The examples
we have seen so far are the anchors.  The anchor C&lt;&#39;^&#39;&gt; matches the
beginning of the line, but doesn&#39;t eat any characters.  Similarly, the
word boundary anchor C&lt;\b&gt; matches wherever a character matching C&lt;\w&gt;
is next to a character that doesn&#39;t, but it doesn&#39;t eat up any
characters itself.  Anchors are examples of I&lt;zero-width assertions&gt;:
zero-width, because they consume
no characters, and assertions, because they test some property of the
string.  In the context of our walk in the woods analogy to regexp
matching, most regexp elements move us along a trail, but anchors have
us stop a moment and check our surroundings.  If the local environment
checks out, we can proceed forward.  But if the local environment
doesn&#39;t satisfy us, we must backtrack.

Checking the environment entails either looking ahead on the trail,
looking behind, or both.  C&lt;&#39;^&#39;&gt; looks behind, to see that there are no
characters before.  C&lt;&#39;$&#39;&gt; looks ahead, to see that there are no
characters after.  C&lt;\b&gt; looks both ahead and behind, to see if the
characters on either side differ in their &quot;word-ness&quot;.

The lookahead and lookbehind assertions are generalizations of the
anchor concept.  Lookahead and lookbehind are zero-width assertions
that let us specify which characters we want to test for.  The
lookahead assertion is denoted by C&lt;(?=regexp)&gt; and the lookbehind
assertion is denoted by C&lt;&lt; (?&lt;=fixed-regexp) &gt;&gt;.  Some examples are

    $x = &quot;I catch the housecat &#39;Tom-cat&#39; with catnip&quot;;
    $x =~ /cat(?=\s)/;   # matches &#39;cat&#39; in &#39;housecat&#39;
    @catwords = ($x =~ /(?&lt;=\s)cat\w+/g);  # matches,
                                           # $catwords[0] = &#39;catch&#39;
                                           # $catwords[1] = &#39;catnip&#39;
    $x =~ /\bcat\b/;  # matches &#39;cat&#39; in &#39;Tom-cat&#39;
    $x =~ /(?&lt;=\s)cat(?=\s)/; # doesn&#39;t match; no isolated &#39;cat&#39; in
                              # middle of $x

Note that the parentheses in C&lt;(?=regexp)&gt; and C&lt;&lt; (?&lt;=regexp) &gt;&gt; are
non-capturing, since these are zero-width assertions.  Thus in the
second regexp, the substrings captured are those of the whole regexp
itself.  Lookahead C&lt;(?=regexp)&gt; can match arbitrary regexps, but
lookbehind C&lt;&lt; (?&lt;=fixed-regexp) &gt;&gt; only works for regexps of fixed
width, I&lt;i.e.&gt;, a fixed number of characters long.  Thus
C&lt;&lt; (?&lt;=(ab|bc)) &gt;&gt; is fine, but C&lt;&lt; (?&lt;=(ab)*) &gt;&gt; is not.  The
negated versions of the lookahead and lookbehind assertions are
denoted by C&lt;(?!regexp)&gt; and C&lt;&lt; (?&lt;!fixed-regexp) &gt;&gt; respectively.
They evaluate true if the regexps do I&lt;not&gt; match:

    $x = &quot;foobar&quot;;
    $x =~ /foo(?!bar)/;  # doesn&#39;t match, &#39;bar&#39; follows &#39;foo&#39;
    $x =~ /foo(?!baz)/;  # matches, &#39;baz&#39; doesn&#39;t follow &#39;foo&#39;
    $x =~ /(?&lt;!\s)foo/;  # matches, there is no \s before &#39;foo&#39;

Here is an example where a string containing blank-separated words,
numbers and single dashes is to be split into its components.
Using C&lt;/\s+/&gt; alone won&#39;t work, because spaces are not required between
dashes, or a word or a dash. Additional places for a split are established
by looking ahead and behind:

    $str = &quot;one two - --6-8&quot;;
    @toks = split / \s+              # a run of spaces
                  | (?&lt;=\S) (?=-)    # any non-space followed by &#39;-&#39;
                  | (?&lt;=-)  (?=\S)   # a &#39;-&#39; followed by any non-space
                  /x, $str;          # @toks = qw(one two - - - 6 - 8)


=head2 Using independent subexpressions to prevent backtracking

I&lt;Independent subexpressions&gt; are regular expressions, in the
context of a larger regular expression, that function independently of
the larger regular expression.  That is, they consume as much or as
little of the string as they wish without regard for the ability of
the larger regexp to match.  Independent subexpressions are represented
by C&lt;&lt; (?&gt;regexp) &gt;&gt;.  We can illustrate their behavior by first
considering an ordinary regexp:

    $x = &quot;ab&quot;;
    $x =~ /a*ab/;  # matches

This obviously matches, but in the process of matching, the
subexpression C&lt;a*&gt; first grabbed the C&lt;&#39;a&#39;&gt;.  Doing so, however,
wouldn&#39;t allow the whole regexp to match, so after backtracking, C&lt;a*&gt;
eventually gave back the C&lt;&#39;a&#39;&gt; and matched the empty string.  Here, what
C&lt;a*&gt; matched was I&lt;dependent&gt; on what the rest of the regexp matched.

Contrast that with an independent subexpression:

    $x =~ /(?&gt;a*)ab/;  # doesn&#39;t match!

The independent subexpression C&lt;&lt; (?&gt;a*) &gt;&gt; doesn&#39;t care about the rest
of the regexp, so it sees an C&lt;&#39;a&#39;&gt; and grabs it.  Then the rest of the
regexp C&lt;ab&gt; cannot match.  Because C&lt;&lt; (?&gt;a*) &gt;&gt; is independent, there
is no backtracking and the independent subexpression does not give
up its C&lt;&#39;a&#39;&gt;.  Thus the match of the regexp as a whole fails.  A similar
behavior occurs with completely independent regexps:

    $x = &quot;ab&quot;;
    $x =~ /a*/g;   # matches, eats an &#39;a&#39;
    $x =~ /\Gab/g; # doesn&#39;t match, no &#39;a&#39; available

Here C&lt;/g&gt; and C&lt;\G&gt; create a &quot;tag team&quot; handoff of the string from
one regexp to the other.  Regexps with an independent subexpression are
much like this, with a handoff of the string to the independent
subexpression, and a handoff of the string back to the enclosing
regexp.

The ability of an independent subexpression to prevent backtracking
can be quite useful.  Suppose we want to match a non-empty string
enclosed in parentheses up to two levels deep.  Then the following
regexp matches:

    $x = &quot;abc(de(fg)h&quot;;  # unbalanced parentheses
    $x =~ /\( ( [ ^ () ]+ | \( [ ^ () ]* \) )+ \)/xx;

The regexp matches an open parenthesis, one or more copies of an
alternation, and a close parenthesis.  The alternation is two-way, with
the first alternative C&lt;[^()]+&gt; matching a substring with no
parentheses and the second alternative C&lt;\([^()]*\)&gt;  matching a
substring delimited by parentheses.  The problem with this regexp is
that it is pathological: it has nested indeterminate quantifiers
of the form C&lt;(a+|b)+&gt;.  We discussed in Part 1 how nested quantifiers
like this could take an exponentially long time to execute if there
was no match possible.  To prevent the exponential blowup, we need to
prevent useless backtracking at some point.  This can be done by
enclosing the inner quantifier as an independent subexpression:

    $x =~ /\( ( (?&gt; [ ^ () ]+ ) | \([ ^ () ]* \) )+ \)/xx;

Here, C&lt;&lt; (?&gt;[^()]+) &gt;&gt; breaks the degeneracy of string partitioning
by gobbling up as much of the string as possible and keeping it.   Then
match failures fail much more quickly.


=head2 Conditional expressions

A I&lt;conditional expression&gt; is a form of if-then-else statement
that allows one to choose which patterns are to be matched, based on
some condition.  There are two types of conditional expression:
C&lt;(?(I&lt;condition&gt;)I&lt;yes-regexp&gt;)&gt; and
C&lt;(?(condition)I&lt;yes-regexp&gt;|I&lt;no-regexp&gt;)&gt;.
C&lt;(?(I&lt;condition&gt;)I&lt;yes-regexp&gt;)&gt; is
like an S&lt;C&lt;&#39;if () {}&#39;&gt;&gt; statement in Perl.  If the I&lt;condition&gt; is true,
the I&lt;yes-regexp&gt; will be matched.  If the I&lt;condition&gt; is false, the
I&lt;yes-regexp&gt; will be skipped and Perl will move onto the next regexp
element.  The second form is like an S&lt;C&lt;&#39;if () {} else {}&#39;&gt;&gt; statement
in Perl.  If the I&lt;condition&gt; is true, the I&lt;yes-regexp&gt; will be
matched, otherwise the I&lt;no-regexp&gt; will be matched.

The I&lt;condition&gt; can have several forms.  The first form is simply an
integer in parentheses C&lt;(I&lt;integer&gt;)&gt;.  It is true if the corresponding
backreference C&lt;\I&lt;integer&gt;&gt; matched earlier in the regexp.  The same
thing can be done with a name associated with a capture group, written
as C&lt;&lt;&lt; (E&lt;lt&gt;I&lt;name&gt;E&lt;gt&gt;) &gt;&gt;&gt; or C&lt;&lt; (&#39;I&lt;name&gt;&#39;) &gt;&gt;.  The second form is a bare
zero-width assertion C&lt;(?...)&gt;, either a lookahead, a lookbehind, or a
code assertion (discussed in the next section).  The third set of forms
provides tests that return true if the expression is executed within
a recursion (C&lt;(R)&gt;) or is being called from some capturing group,
referenced either by number (C&lt;(R1)&gt;, C&lt;(R2)&gt;,...) or by name
(C&lt;(R&amp;I&lt;name&gt;)&gt;).

The integer or name form of the C&lt;condition&gt; allows us to choose,
with more flexibility, what to match based on what matched earlier in the
regexp. This searches for words of the form C&lt;&quot;$x$x&quot;&gt; or C&lt;&quot;$x$y$y$x&quot;&gt;:

    % simple_grep &#39;^(\w+)(\w+)?(?(2)\g2\g1|\g1)$&#39; /usr/dict/words
    beriberi
    coco
    couscous
    deed
    ...
    toot
    toto
    tutu

The lookbehind C&lt;condition&gt; allows, along with backreferences,
an earlier part of the match to influence a later part of the
match.  For instance,

    /[ATGC]+(?(?&lt;=AA)G|C)$/;

matches a DNA sequence such that it either ends in C&lt;AAG&gt;, or some
other base pair combination and C&lt;&#39;C&#39;&gt;.  Note that the form is
C&lt;&lt; (?(?&lt;=AA)G|C) &gt;&gt; and not C&lt;&lt; (?((?&lt;=AA))G|C) &gt;&gt;; for the
lookahead, lookbehind or code assertions, the parentheses around the
conditional are not needed.


=head2 Defining named patterns

Some regular expressions use identical subpatterns in several places.
Starting with Perl 5.10, it is possible to define named subpatterns in
a section of the pattern so that they can be called up by name
anywhere in the pattern.  This syntactic pattern for this definition
group is C&lt;&lt; (?(DEFINE)(?&lt;I&lt;name&gt;&gt;I&lt;pattern&gt;)...) &gt;&gt;.  An insertion
of a named pattern is written as C&lt;(?&amp;I&lt;name&gt;)&gt;.

The example below illustrates this feature using the pattern for
floating point numbers that was presented earlier on.  The three
subpatterns that are used more than once are the optional sign, the
digit sequence for an integer and the decimal fraction.  The C&lt;DEFINE&gt;
group at the end of the pattern contains their definition.  Notice
that the decimal fraction pattern is the first place where we can
reuse the integer pattern.

   /^ (?&amp;osg)\ * ( (?&amp;int)(?&amp;dec)? | (?&amp;dec) )
      (?: [eE](?&amp;osg)(?&amp;int) )?
    $
    (?(DEFINE)
      (?&lt;osg&gt;[-+]?)         # optional sign
      (?&lt;int&gt;\d++)          # integer
      (?&lt;dec&gt;\.(?&amp;int))     # decimal fraction
    )/x


=head2 Recursive patterns

This feature (introduced in Perl 5.10) significantly extends the
power of Perl&#39;s pattern matching.  By referring to some other
capture group anywhere in the pattern with the construct
C&lt;(?I&lt;group-ref&gt;)&gt;, the I&lt;pattern&gt; within the referenced group is used
as an independent subpattern in place of the group reference itself.
Because the group reference may be contained I&lt;within&gt; the group it
refers to, it is now possible to apply pattern matching to tasks that
hitherto required a recursive parser.

To illustrate this feature, we&#39;ll design a pattern that matches if
a string contains a palindrome. (This is a word or a sentence that,
while ignoring spaces, interpunctuation and case, reads the same backwards
as forwards. We begin by observing that the empty string or a string
containing just one word character is a palindrome. Otherwise it must
have a word character up front and the same at its end, with another
palindrome in between.

    /(?: (\w) (?...Here be a palindrome...) \g{-1} | \w? )/x

Adding C&lt;\W*&gt; at either end to eliminate what is to be ignored, we already
have the full pattern:

    my $pp = qr/^(\W* (?: (\w) (?1) \g{-1} | \w? ) \W*)$/ix;
    for $s ( &quot;saippuakauppias&quot;, &quot;A man, a plan, a canal: Panama!&quot; ){
        print &quot;&#39;$s&#39; is a palindrome\n&quot; if $s =~ /$pp/;
    }

In C&lt;(?...)&gt; both absolute and relative backreferences may be used.
The entire pattern can be reinserted with C&lt;(?R)&gt; or C&lt;(?0)&gt;.
If you prefer to name your groups, you can use C&lt;(?&amp;I&lt;name&gt;)&gt; to
recurse into that group.


=head2 A bit of magic: executing Perl code in a regular expression

Normally, regexps are a part of Perl expressions.
I&lt;Code evaluation&gt; expressions turn that around by allowing
arbitrary Perl code to be a part of a regexp.  A code evaluation
expression is denoted C&lt;(?{I&lt;code&gt;})&gt;, with I&lt;code&gt; a string of Perl
statements.

Code expressions are zero-width assertions, and the value they return
depends on their environment.  There are two possibilities: either the
code expression is used as a conditional in a conditional expression
C&lt;(?(I&lt;condition&gt;)...)&gt;, or it is not.  If the code expression is a
conditional, the code is evaluated and the result (I&lt;i.e.&gt;, the result of
the last statement) is used to determine truth or falsehood.  If the
code expression is not used as a conditional, the assertion always
evaluates true and the result is put into the special variable
C&lt;$^R&gt;.  The variable C&lt;$^R&gt; can then be used in code expressions later
in the regexp.  Here are some silly examples:

    $x = &quot;abcdef&quot;;
    $x =~ /abc(?{print &quot;Hi Mom!&quot;;})def/; # matches,
                                         # prints &#39;Hi Mom!&#39;
    $x =~ /aaa(?{print &quot;Hi Mom!&quot;;})def/; # doesn&#39;t match,
                                         # no &#39;Hi Mom!&#39;

Pay careful attention to the next example:

    $x =~ /abc(?{print &quot;Hi Mom!&quot;;})ddd/; # doesn&#39;t match,
                                         # no &#39;Hi Mom!&#39;
                                         # but why not?

At first glance, you&#39;d think that it shouldn&#39;t print, because obviously
the C&lt;ddd&gt; isn&#39;t going to match the target string. But look at this
example:

    $x =~ /abc(?{print &quot;Hi Mom!&quot;;})[dD]dd/; # doesn&#39;t match,
                                            # but _does_ print

Hmm. What happened here? If you&#39;ve been following along, you know that
the above pattern should be effectively (almost) the same as the last one;
enclosing the C&lt;&#39;d&#39;&gt; in a character class isn&#39;t going to change what it
matches. So why does the first not print while the second one does?

The answer lies in the optimizations the regexp engine makes. In the first
case, all the engine sees are plain old characters (aside from the
C&lt;?{}&gt; construct). It&#39;s smart enough to realize that the string C&lt;&#39;ddd&#39;&gt;
doesn&#39;t occur in our target string before actually running the pattern
through. But in the second case, we&#39;ve tricked it into thinking that our
pattern is more complicated. It takes a look, sees our
character class, and decides that it will have to actually run the
pattern to determine whether or not it matches, and in the process of
running it hits the print statement before it discovers that we don&#39;t
have a match.

To take a closer look at how the engine does optimizations, see the
section L&lt;/&quot;Pragmas and debugging&quot;&gt; below.

More fun with C&lt;?{}&gt;:

    $x =~ /(?{print &quot;Hi Mom!&quot;;})/;       # matches,
                                         # prints &#39;Hi Mom!&#39;
    $x =~ /(?{$c = 1;})(?{print &quot;$c&quot;;})/;  # matches,
                                           # prints &#39;1&#39;
    $x =~ /(?{$c = 1;})(?{print &quot;$^R&quot;;})/; # matches,
                                           # prints &#39;1&#39;

The bit of magic mentioned in the section title occurs when the regexp
backtracks in the process of searching for a match.  If the regexp
backtracks over a code expression and if the variables used within are
localized using C&lt;local&gt;, the changes in the variables produced by the
code expression are undone! Thus, if we wanted to count how many times
a character got matched inside a group, we could use, I&lt;e.g.&gt;,

    $x = &quot;aaaa&quot;;
    $count = 0;  # initialize &#39;a&#39; count
    $c = &quot;bob&quot;;  # test if $c gets clobbered
    $x =~ /(?{local $c = 0;})         # initialize count
           ( a                        # match &#39;a&#39;
             (?{local $c = $c + 1;})  # increment count
           )*                         # do this any number of times,
           aa                         # but match &#39;aa&#39; at the end
           (?{$count = $c;})          # copy local $c var into $count
          /x;
    print &quot;&#39;a&#39; count is $count, \$c variable is &#39;$c&#39;\n&quot;;

This prints

    &#39;a&#39; count is 2, $c variable is &#39;bob&#39;

If we replace the S&lt;C&lt; (?{local $c = $c + 1;})&gt;&gt; with
S&lt;C&lt; (?{$c = $c + 1;})&gt;&gt;, the variable changes are I&lt;not&gt; undone
during backtracking, and we get

    &#39;a&#39; count is 4, $c variable is &#39;bob&#39;

Note that only localized variable changes are undone.  Other side
effects of code expression execution are permanent.  Thus

    $x = &quot;aaaa&quot;;
    $x =~ /(a(?{print &quot;Yow\n&quot;;}))*aa/;

produces

   Yow
   Yow
   Yow
   Yow

The result C&lt;$^R&gt; is automatically localized, so that it will behave
properly in the presence of backtracking.

This example uses a code expression in a conditional to match a
definite article, either C&lt;&#39;the&#39;&gt; in English or C&lt;&#39;der|die|das&#39;&gt; in
German:

    $lang = &#39;DE&#39;;  # use German
    ...
    $text = &quot;das&quot;;
    print &quot;matched\n&quot;
        if $text =~ /(?(?{
                          $lang eq &#39;EN&#39;; # is the language English?
                         })
                       the |             # if so, then match &#39;the&#39;
                       (der|die|das)     # else, match &#39;der|die|das&#39;
                     )
                    /xi;

Note that the syntax here is C&lt;(?(?{...})I&lt;yes-regexp&gt;|I&lt;no-regexp&gt;)&gt;, not
C&lt;(?((?{...}))I&lt;yes-regexp&gt;|I&lt;no-regexp&gt;)&gt;.  In other words, in the case of a
code expression, we don&#39;t need the extra parentheses around the
conditional.

If you try to use code expressions where the code text is contained within
an interpolated variable, rather than appearing literally in the pattern,
Perl may surprise you:

    $bar = 5;
    $pat = &#39;(?{ 1 })&#39;;
    /foo(?{ $bar })bar/; # compiles ok, $bar not interpolated
    /foo(?{ 1 })$bar/;   # compiles ok, $bar interpolated
    /foo${pat}bar/;      # compile error!

    $pat = qr/(?{ $foo = 1 })/;  # precompile code regexp
    /foo${pat}bar/;      # compiles ok

If a regexp has a variable that interpolates a code expression, Perl
treats the regexp as an error. If the code expression is precompiled into
a variable, however, interpolating is ok. The question is, why is this an
error?

The reason is that variable interpolation and code expressions
together pose a security risk.  The combination is dangerous because
many programmers who write search engines often take user input and
plug it directly into a regexp:

    $regexp = &lt;&gt;;       # read user-supplied regexp
    $chomp $regexp;     # get rid of possible newline
    $text =~ /$regexp/; # search $text for the $regexp

If the C&lt;$regexp&gt; variable contains a code expression, the user could
then execute arbitrary Perl code.  For instance, some joker could
search for S&lt;C&lt;system(&#39;rm -rf *&#39;);&gt;&gt; to erase your files.  In this
sense, the combination of interpolation and code expressions I&lt;taints&gt;
your regexp.  So by default, using both interpolation and code
expressions in the same regexp is not allowed.  If you&#39;re not
concerned about malicious users, it is possible to bypass this
security check by invoking S&lt;C&lt;use re &#39;eval&#39;&gt;&gt;:

    use re &#39;eval&#39;;       # throw caution out the door
    $bar = 5;
    $pat = &#39;(?{ 1 })&#39;;
    /foo${pat}bar/;      # compiles ok

Another form of code expression is the I&lt;pattern code expression&gt;.
The pattern code expression is like a regular code expression, except
that the result of the code evaluation is treated as a regular
expression and matched immediately.  A simple example is

    $length = 5;
    $char = &#39;a&#39;;
    $x = &#39;aaaaabb&#39;;
    $x =~ /(??{$char x $length})/x; # matches, there are 5 of &#39;a&#39;


This final example contains both ordinary and pattern code
expressions.  It detects whether a binary string C&lt;1101010010001...&gt; has a
Fibonacci spacing 0,1,1,2,3,5,...  of the C&lt;&#39;1&#39;&gt;&#39;s:

    $x = &quot;1101010010001000001&quot;;
    $z0 = &#39;&#39;; $z1 = &#39;0&#39;;   # initial conditions
    print &quot;It is a Fibonacci sequence\n&quot;
        if $x =~ /^1         # match an initial &#39;1&#39;
                    (?:
                       ((??{ $z0 })) # match some &#39;0&#39;
                       1             # and then a &#39;1&#39;
		       (?{ $z0 = $z1; $z1 .= $^N; })
                    )+   # repeat as needed
                  $      # that is all there is
                 /x;
    printf &quot;Largest sequence matched was %d\n&quot;, length($z1)-length($z0);

Remember that C&lt;$^N&gt; is set to whatever was matched by the last
completed capture group. This prints

    It is a Fibonacci sequence
    Largest sequence matched was 5

Ha! Try that with your garden variety regexp package...

Note that the variables C&lt;$z0&gt; and C&lt;$z1&gt; are not substituted when the
regexp is compiled, as happens for ordinary variables outside a code
expression.  Rather, the whole code block is parsed as perl code at the
same time as perl is compiling the code containing the literal regexp
pattern.

This regexp without the C&lt;/x&gt; modifier is

    /^1(?:((??{ $z0 }))1(?{ $z0 = $z1; $z1 .= $^N; }))+$/

which shows that spaces are still possible in the code parts. Nevertheless,
when working with code and conditional expressions, the extended form of
regexps is almost necessary in creating and debugging regexps.


=head2 Backtracking control verbs

Perl 5.10 introduced a number of control verbs intended to provide
detailed control over the backtracking process, by directly influencing
the regexp engine and by providing monitoring techniques.  See
L&lt;perlre/&quot;Special Backtracking Control Verbs&quot;&gt; for a detailed
description.

Below is just one example, illustrating the control verb C&lt;(*FAIL)&gt;,
which may be abbreviated as C&lt;(*F)&gt;. If this is inserted in a regexp
it will cause it to fail, just as it would at some
mismatch between the pattern and the string. Processing
of the regexp continues as it would after any &quot;normal&quot;
failure, so that, for instance, the next position in the string or another
alternative will be tried. As failing to match doesn&#39;t preserve capture
groups or produce results, it may be necessary to use this in
combination with embedded code.

   %count = ();
   &quot;supercalifragilisticexpialidocious&quot; =~
       /([aeiou])(?{ $count{$1}++; })(*FAIL)/i;
   printf &quot;%3d &#39;%s&#39;\n&quot;, $count{$_}, $_ for (sort keys %count);

The pattern begins with a class matching a subset of letters.  Whenever
this matches, a statement like C&lt;$count{&#39;a&#39;}++;&gt; is executed, incrementing
the letter&#39;s counter. Then C&lt;(*FAIL)&gt; does what it says, and
the regexp engine proceeds according to the book: as long as the end of
the string hasn&#39;t been reached, the position is advanced before looking
for another vowel. Thus, match or no match makes no difference, and the
regexp engine proceeds until the entire string has been inspected.
(It&#39;s remarkable that an alternative solution using something like

   $count{lc($_)}++ for split(&#39;&#39;, &quot;supercalifragilisticexpialidocious&quot;);
   printf &quot;%3d &#39;%s&#39;\n&quot;, $count2{$_}, $_ for ( qw{ a e i o u } );

is considerably slower.)


=head2 Pragmas and debugging

Speaking of debugging, there are several pragmas available to control
and debug regexps in Perl.  We have already encountered one pragma in
the previous section, S&lt;C&lt;use re &#39;eval&#39;;&gt;&gt;, that allows variable
interpolation and code expressions to coexist in a regexp.  The other
pragmas are

    use re &#39;taint&#39;;
    $tainted = &lt;&gt;;
    @parts = ($tainted =~ /(\w+)\s+(\w+)/; # @parts is now tainted

The C&lt;taint&gt; pragma causes any substrings from a match with a tainted
variable to be tainted as well.  This is not normally the case, as
regexps are often used to extract the safe bits from a tainted
variable.  Use C&lt;taint&gt; when you are not extracting safe bits, but are
performing some other processing.  Both C&lt;taint&gt; and C&lt;eval&gt; pragmas
are lexically scoped, which means they are in effect only until
the end of the block enclosing the pragmas.

    use re &#39;/m&#39;;  # or any other flags
    $multiline_string =~ /^foo/; # /m is implied

The C&lt;re &#39;/flags&#39;&gt; pragma (introduced in Perl
5.14) turns on the given regular expression flags
until the end of the lexical scope.  See
L&lt;re/&quot;&#39;E&lt;sol&gt;flags&#39; mode&quot;&gt; for more
detail.

    use re &#39;debug&#39;;
    /^(.*)$/s;       # output debugging info

    use re &#39;debugcolor&#39;;
    /^(.*)$/s;       # output debugging info in living color

The global C&lt;debug&gt; and C&lt;debugcolor&gt; pragmas allow one to get
detailed debugging info about regexp compilation and
execution.  C&lt;debugcolor&gt; is the same as debug, except the debugging
information is displayed in color on terminals that can display
termcap color sequences.  Here is example output:

    % perl -e &#39;use re &quot;debug&quot;; &quot;abc&quot; =~ /a*b+c/;&#39;
    Compiling REx &#39;a*b+c&#39;
    size 9 first at 1
       1: STAR(4)
       2:   EXACT &lt;a&gt;(0)
       4: PLUS(7)
       5:   EXACT &lt;b&gt;(0)
       7: EXACT &lt;c&gt;(9)
       9: END(0)
    floating &#39;bc&#39; at 0..2147483647 (checking floating) minlen 2
    Guessing start of match, REx &#39;a*b+c&#39; against &#39;abc&#39;...
    Found floating substr &#39;bc&#39; at offset 1...
    Guessed: match at offset 0
    Matching REx &#39;a*b+c&#39; against &#39;abc&#39;
      Setting an EVAL scope, savestack=3
       0 &lt;&gt; &lt;abc&gt;           |  1:  STAR
                             EXACT &lt;a&gt; can match 1 times out of 32767...
      Setting an EVAL scope, savestack=3
       1 &lt;a&gt; &lt;bc&gt;           |  4:    PLUS
                             EXACT &lt;b&gt; can match 1 times out of 32767...
      Setting an EVAL scope, savestack=3
       2 &lt;ab&gt; &lt;c&gt;           |  7:      EXACT &lt;c&gt;
       3 &lt;abc&gt; &lt;&gt;           |  9:      END
    Match successful!
    Freeing REx: &#39;a*b+c&#39;

If you have gotten this far into the tutorial, you can probably guess
what the different parts of the debugging output tell you.  The first
part

    Compiling REx &#39;a*b+c&#39;
    size 9 first at 1
       1: STAR(4)
       2:   EXACT &lt;a&gt;(0)
       4: PLUS(7)
       5:   EXACT &lt;b&gt;(0)
       7: EXACT &lt;c&gt;(9)
       9: END(0)

describes the compilation stage.  C&lt;STAR(4)&gt; means that there is a
starred object, in this case C&lt;&#39;a&#39;&gt;, and if it matches, goto line 4,
I&lt;i.e.&gt;, C&lt;PLUS(7)&gt;.  The middle lines describe some heuristics and
optimizations performed before a match:

    floating &#39;bc&#39; at 0..2147483647 (checking floating) minlen 2
    Guessing start of match, REx &#39;a*b+c&#39; against &#39;abc&#39;...
    Found floating substr &#39;bc&#39; at offset 1...
    Guessed: match at offset 0

Then the match is executed and the remaining lines describe the
process:

    Matching REx &#39;a*b+c&#39; against &#39;abc&#39;
      Setting an EVAL scope, savestack=3
       0 &lt;&gt; &lt;abc&gt;           |  1:  STAR
                             EXACT &lt;a&gt; can match 1 times out of 32767...
      Setting an EVAL scope, savestack=3
       1 &lt;a&gt; &lt;bc&gt;           |  4:    PLUS
                             EXACT &lt;b&gt; can match 1 times out of 32767...
      Setting an EVAL scope, savestack=3
       2 &lt;ab&gt; &lt;c&gt;           |  7:      EXACT &lt;c&gt;
       3 &lt;abc&gt; &lt;&gt;           |  9:      END
    Match successful!
    Freeing REx: &#39;a*b+c&#39;

Each step is of the form S&lt;C&lt;&lt; n &lt;x&gt; &lt;y&gt; &gt;&gt;&gt;, with C&lt;&lt; &lt;x&gt; &gt;&gt; the
part of the string matched and C&lt;&lt; &lt;y&gt; &gt;&gt; the part not yet
matched.  The S&lt;C&lt;&lt; |  1:  STAR &gt;&gt;&gt; says that Perl is at line number 1
in the compilation list above.  See
L&lt;perldebguts/&quot;Debugging Regular Expressions&quot;&gt; for much more detail.

An alternative method of debugging regexps is to embed C&lt;print&gt;
statements within the regexp.  This provides a blow-by-blow account of
the backtracking in an alternation:

    &quot;that this&quot; =~ m@(?{print &quot;Start at position &quot;, pos, &quot;\n&quot;;})
                     t(?{print &quot;t1\n&quot;;})
                     h(?{print &quot;h1\n&quot;;})
                     i(?{print &quot;i1\n&quot;;})
                     s(?{print &quot;s1\n&quot;;})
                         |
                     t(?{print &quot;t2\n&quot;;})
                     h(?{print &quot;h2\n&quot;;})
                     a(?{print &quot;a2\n&quot;;})
                     t(?{print &quot;t2\n&quot;;})
                     (?{print &quot;Done at position &quot;, pos, &quot;\n&quot;;})
                    @x;

prints

    Start at position 0
    t1
    h1
    t2
    h2
    a2
    t2
    Done at position 4

=head1 SEE ALSO

This is just a tutorial.  For the full story on Perl regular
expressions, see the L&lt;perlre&gt; regular expressions reference page.

For more information on the matching C&lt;m//&gt; and substitution C&lt;s///&gt;
operators, see L&lt;perlop/&quot;Regexp Quote-Like Operators&quot;&gt;.  For
information on the C&lt;split&gt; operation, see L&lt;perlfunc/split&gt;.

For an excellent all-around resource on the care and feeding of
regular expressions, see the book I&lt;Mastering Regular Expressions&gt; by
Jeffrey Friedl (published by O&#39;Reilly, ISBN 1556592-257-3).

=head1 AUTHOR AND COPYRIGHT

Copyright (c) 2000 Mark Kvale.
All rights reserved.
Now maintained by Perl porters.

This document may be distributed under the same terms as Perl itself.

=head2 Acknowledgments

The inspiration for the stop codon DNA example came from the ZIP
code example in chapter 7 of I&lt;Mastering Regular Expressions&gt;.

The author would like to thank Jeff Pinyan, Andrew Johnson, Peter
Haworth, Ronald J Kimball, and Joe Smith for all their helpful
comments.

=cut

</pre>

                    </main>
                </div>

        </div>
        <footer>
        
        </footer>


    </body>
</html>

<!DOCTYPE html>
<html>
    <head>
        <meta charset="utf-8">
        <meta content="width=device-width, initial-scale=1" name="viewport">
        <link href="/theme/css/normalize.css" rel="stylesheet">
        <!--<link rel="stylesheet" href="/theme/css/skeleton.css" />-->
        <link href="/theme/css/statocles-default.css" rel="stylesheet">
        <link href="//maxcdn.bootstrapcdn.com/font-awesome/4.3.0/css/font-awesome.min.css" rel="stylesheet">
        <script crossorigin="anonymous" integrity="sha256-hwg4gsxgFZhOsEEamdOYGBf13FyQuiTwlAQgxVSNgt4=" src="http://code.jquery.com/jquery-3.2.1.min.js"></script>
        <title>perlfaq7 (source) - Minimum Viable Perl</title>
        <meta content="Statocles 0.086" name="generator">
        
        <script>
            $( document ).ready(function() {
                $('.sub-menu ul').hide();
                $('.sub-menu.perldoc ul').show();
                $('.sub-menu.perldoc div').toggleClass("fa-caret-up fa-caret-down");
                $(".sub-menu a").click(function () {
                    $(this).parent(".sub-menu").siblings().children('ul').hide();
                    $(this).parent(".sub-menu").children("ul").slideToggle("100");
                    $(this).find(".right").toggleClass("fa-caret-up fa-caret-down");
                });
            });
        </script>
    </head>
    <body>
        <header>
        </header>
        <div class="container">

                <div class="left column full-height">
                    <div class="sidebar">
                    
<a class="home" href="/">Minimum<br>Viable<br>Perl</a><br><br><br>

<div class="animated bounceInDown">
  <ul>
    <li class="basics sub-menu">
      <a href="/perldoc/perlfaq7/#basics">The basics<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/basics/hello">Hello world</a></li>
        <li><a href="/basics/variables">Variables</a></li>
        <li><a href="/basics/references">References</a></li>
        <li><a href="/basics/functions">Functions</a></li>
        <li><a href="/basics/flow">Conditionals</a></li>
        <li><a href="/basics/loops">Loops</a></li>
        <li><a href="/basics/exceptions">Exceptions</a></li>
        <li><a href="/basics/regexps">Regexps</a></li>
        <li><a href="/basics/files">Files</a></li>
      </ul>
    </li>
    <li class="oo sub-menu">
      <a href="/perldoc/perlfaq7/#oo">Object oriented programming<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/oo/story">The (weird) story of Perl OO</a></li>
        <li><a href="/oo/classes">Classes</a></li>
        <li><a href="/oo/attributes">Attributes</a></li>
        <li><a href="/oo/modifiers">Method modifiers</a></li>
        <li><a href="/oo/roles">Roles</a></li>
        <li><a href="/oo/inheritance">Inheritance</a></li>
      </ul>
    </li>
    <li class="debugging sub-menu">
      <a href="/perldoc/perlfaq7/#basics">Debugging<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/debugging/data-printer">Examining data structures</a></li>
        <li><a href="/debugging/stack-trace">Forcing stack traces</a></li>
      </ul>
    </li>
    <li class="dependencies sub-menu">
      <a href="/perldoc/perlfaq7/#dependencies">Perl libraries<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/dependencies/perl5lib">Loading libraries from custom locations</a></li>
        <li><a href="/dependencies/cpanm">Installing libraries with cpanm</a></li>
        <li><a href="/dependencies/metacpan">Choosing libraries from MetaCPAN</a></li>
        <li><a href="/dependencies/carton">Managing project dependencies with Carton</a></li>
        <li><a href="/dependencies/pmtools">What version is installed and where?</a></li>
      </ul>
    </li>
    <li class="testing sub-menu">
      <a href="/perldoc/perlfaq7/#testing">Testing<div class="fa fa-caret-down right"></div></a>
      <ul>
        <li><a href="/testing/intro">Introduction to testing</a></li>
        <li><a href="/testing/warnings">Testing warnings and exceptions</a></li>
        <li><a href="/testing/data">Testing data structures</a></li>
        <li><a href="/testing/mock">Testing with mock objects</a></li>
        <li><a href="/testing/libs">Build your own test libraries</a></li>
      </ul>
    </li>
    <li><a href="/perl">Perl design goals</a></li>
    <li><a href="/see-also">Book recommendations</a></li>
    <li><a href="/about">About this book</a></li>
  </ul>
</div>

                    
                    
                    </div>
                </div>

                <div class="right column">
                    <main>
                    <a class="button" href="/perldoc/perlfaq7/">
    Back to documentation
</a>
<pre>=head1 NAME

perlfaq7 - General Perl Language Issues

=head1 VERSION

version 5.021011

=head1 DESCRIPTION

This section deals with general Perl language issues that don&#39;t
clearly fit into any of the other sections.

=head2 Can I get a BNF/yacc/RE for the Perl language?

There is no BNF, but you can paw your way through the yacc grammar in
perly.y in the source distribution if you&#39;re particularly brave. The
grammar relies on very smart tokenizing code, so be prepared to
venture into toke.c as well.

In the words of Chaim Frenkel: &quot;Perl&#39;s grammar can not be reduced to BNF.
The work of parsing perl is distributed between yacc, the lexer, smoke
and mirrors.&quot;

=head2 What are all these $@%&amp;* punctuation signs, and how do I know when to use them?

They are type specifiers, as detailed in L&lt;perldata&gt;:

    $ for scalar values (number, string or reference)
    @ for arrays
    % for hashes (associative arrays)
    &amp; for subroutines (aka functions, procedures, methods)
    * for all types of that symbol name. In version 4 you used them like
      pointers, but in modern perls you can just use references.

There are a couple of other symbols that
you&#39;re likely to encounter that aren&#39;t
really type specifiers:

    &lt;&gt; are used for inputting a record from a filehandle.
    \  takes a reference to something.

Note that &lt;FILE&gt; is I&lt;neither&gt; the type specifier for files
nor the name of the handle. It is the C&lt;&lt; &lt;&gt; &gt;&gt; operator applied
to the handle FILE. It reads one line (well, record--see
L&lt;perlvar/$E&lt;sol&gt;&gt;) from the handle FILE in scalar context, or I&lt;all&gt; lines
in list context. When performing open, close, or any other operation
besides C&lt;&lt; &lt;&gt; &gt;&gt; on files, or even when talking about the handle, do
I&lt;not&gt; use the brackets. These are correct: C&lt;eof(FH)&gt;, C&lt;seek(FH, 0,
2)&gt; and &quot;copying from STDIN to FILE&quot;.

=head2 Do I always/never have to quote my strings or use semicolons and commas?

Normally, a bareword doesn&#39;t need to be quoted, but in most cases
probably should be (and must be under C&lt;use strict&gt;). But a hash key
consisting of a simple word and the left-hand
operand to the C&lt;&lt; =&gt; &gt;&gt; operator both
count as though they were quoted:

    This                    is like this
    ------------            ---------------
    $foo{line}              $foo{&#39;line&#39;}
    bar =&gt; stuff            &#39;bar&#39; =&gt; stuff

The final semicolon in a block is optional, as is the final comma in a
list. Good style (see L&lt;perlstyle&gt;) says to put them in except for
one-liners:

    if ($whoops) { exit 1 }
    my @nums = (1, 2, 3);

    if ($whoops) {
        exit 1;
    }

    my @lines = (
        &quot;There Beren came from mountains cold&quot;,
        &quot;And lost he wandered under leaves&quot;,
    );

=head2 How do I skip some return values?

One way is to treat the return values as a list and index into it:

    $dir = (getpwnam($user))[7];

Another way is to use undef as an element on the left-hand-side:

    ($dev, $ino, undef, undef, $uid, $gid) = stat($file);

You can also use a list slice to select only the elements that
you need:

    ($dev, $ino, $uid, $gid) = ( stat($file) )[0,1,4,5];

=head2 How do I temporarily block warnings?

If you are running Perl 5.6.0 or better, the C&lt;use warnings&gt; pragma
allows fine control of what warnings are produced.
See L&lt;perllexwarn&gt; for more details.

    {
        no warnings;          # temporarily turn off warnings
        $x = $y + $z;         # I know these might be undef
    }

Additionally, you can enable and disable categories of warnings.
You turn off the categories you want to ignore and you can still
get other categories of warnings. See L&lt;perllexwarn&gt; for the
complete details, including the category names and hierarchy.

    {
        no warnings &#39;uninitialized&#39;;
        $x = $y + $z;
    }

If you have an older version of Perl, the C&lt;$^W&gt; variable (documented
in L&lt;perlvar&gt;) controls runtime warnings for a block:

    {
        local $^W = 0;        # temporarily turn off warnings
        $x = $y + $z;         # I know these might be undef
    }

Note that like all the punctuation variables, you cannot currently
use my() on C&lt;$^W&gt;, only local().

=head2 What&#39;s an extension?

An extension is a way of calling compiled C code from Perl. Reading
L&lt;perlxstut&gt; is a good place to learn more about extensions.

=head2 Why do Perl operators have different precedence than C operators?

Actually, they don&#39;t. All C operators that Perl copies have the same
precedence in Perl as they do in C. The problem is with operators that C
doesn&#39;t have, especially functions that give a list context to everything
on their right, eg. print, chmod, exec, and so on. Such functions are
called &quot;list operators&quot; and appear as such in the precedence table in
L&lt;perlop&gt;.

A common mistake is to write:

    unlink $file || die &quot;snafu&quot;;

This gets interpreted as:

    unlink ($file || die &quot;snafu&quot;);

To avoid this problem, either put in extra parentheses or use the
super low precedence C&lt;or&gt; operator:

    (unlink $file) || die &quot;snafu&quot;;
    unlink $file or die &quot;snafu&quot;;

The &quot;English&quot; operators (C&lt;and&gt;, C&lt;or&gt;, C&lt;xor&gt;, and C&lt;not&gt;)
deliberately have precedence lower than that of list operators for
just such situations as the one above.

Another operator with surprising precedence is exponentiation. It
binds more tightly even than unary minus, making C&lt;-2**2&gt; produce a
negative four and not a positive one. It is also right-associating, meaning
that C&lt;2**3**2&gt; is two raised to the ninth power, not eight squared.

Although it has the same precedence as in C, Perl&#39;s C&lt;?:&gt; operator
produces an lvalue. This assigns $x to either $if_true or $if_false, depending
on the trueness of $maybe:

    ($maybe ? $if_true : $if_false) = $x;

=head2 How do I declare/create a structure?

In general, you don&#39;t &quot;declare&quot; a structure. Just use a (probably
anonymous) hash reference. See L&lt;perlref&gt; and L&lt;perldsc&gt; for details.
Here&#39;s an example:

    $person = {};                   # new anonymous hash
    $person-&gt;{AGE}  = 24;           # set field AGE to 24
    $person-&gt;{NAME} = &quot;Nat&quot;;        # set field NAME to &quot;Nat&quot;

If you&#39;re looking for something a bit more rigorous, try L&lt;perlootut&gt;.

=head2 How do I create a module?

L&lt;perlnewmod&gt; is a good place to start, ignore the bits
about uploading to CPAN if you don&#39;t want to make your
module publicly available.

L&lt;ExtUtils::ModuleMaker&gt; and L&lt;Module::Starter&gt; are also
good places to start. Many CPAN authors now use L&lt;Dist::Zilla&gt;
to automate as much as possible.

Detailed documentation about modules can be found at:
L&lt;perlmod&gt;, L&lt;perlmodlib&gt;, L&lt;perlmodstyle&gt;.

If you need to include C code or C library interfaces
use h2xs. h2xs will create the module distribution structure
and the initial interface files.
L&lt;perlxs&gt; and L&lt;perlxstut&gt; explain the details.

=head2 How do I adopt or take over a module already on CPAN?

Ask the current maintainer to make you a co-maintainer or
transfer the module to you.

If you can not reach the author for some reason contact
the PAUSE admins at modules@perl.org who may be able to help,
but each case is treated separately.

=over 4

=item *

Get a login for the Perl Authors Upload Server (PAUSE) if you don&#39;t
already have one: L&lt;http://pause.perl.org&gt;

=item *

Write to modules@perl.org explaining what you did to contact the
current maintainer. The PAUSE admins will also try to reach the
maintainer.

=item *

Post a public message in a heavily trafficked site announcing your
intention to take over the module.

=item *

Wait a bit. The PAUSE admins don&#39;t want to act too quickly in case
the current maintainer is on holiday. If there&#39;s no response to
private communication or the public post, a PAUSE admin can transfer
it to you.

=back

=head2 How do I create a class?
X&lt;class, creation&gt; X&lt;package&gt;

(contributed by brian d foy)

In Perl, a class is just a package, and methods are just subroutines.
Perl doesn&#39;t get more formal than that and lets you set up the package
just the way that you like it (that is, it doesn&#39;t set up anything for
you).

See also L&lt;perlootut&gt;, a tutorial that covers class creation, and L&lt;perlobj&gt;.

=head2 How can I tell if a variable is tainted?

You can use the tainted() function of the Scalar::Util module, available
from CPAN (or included with Perl since release 5.8.0).
See also L&lt;perlsec/&quot;Laundering and Detecting Tainted Data&quot;&gt;.

=head2 What&#39;s a closure?

Closures are documented in L&lt;perlref&gt;.

I&lt;Closure&gt; is a computer science term with a precise but
hard-to-explain meaning. Usually, closures are implemented in Perl as
anonymous subroutines with lasting references to lexical variables
outside their own scopes. These lexicals magically refer to the
variables that were around when the subroutine was defined (deep
binding).

Closures are most often used in programming languages where you can
have the return value of a function be itself a function, as you can
in Perl. Note that some languages provide anonymous functions but are
not capable of providing proper closures: the Python language, for
example. For more information on closures, check out any textbook on
functional programming. Scheme is a language that not only supports
but encourages closures.

Here&#39;s a classic non-closure function-generating function:

    sub add_function_generator {
        return sub { shift() + shift() };
    }

    my $add_sub = add_function_generator();
    my $sum = $add_sub-&gt;(4,5);                # $sum is 9 now.

The anonymous subroutine returned by add_function_generator() isn&#39;t
technically a closure because it refers to no lexicals outside its own
scope. Using a closure gives you a I&lt;function template&gt; with some
customization slots left out to be filled later.

Contrast this with the following make_adder() function, in which the
returned anonymous function contains a reference to a lexical variable
outside the scope of that function itself. Such a reference requires
that Perl return a proper closure, thus locking in for all time the
value that the lexical had when the function was created.

    sub make_adder {
        my $addpiece = shift;
        return sub { shift() + $addpiece };
    }

    my $f1 = make_adder(20);
    my $f2 = make_adder(555);

Now C&lt;&lt; $f1-&gt;($n) &gt;&gt; is always 20 plus whatever $n you pass in, whereas
C&lt;&lt; $f2-&gt;($n) &gt;&gt; is always 555 plus whatever $n you pass in. The $addpiece
in the closure sticks around.

Closures are often used for less esoteric purposes. For example, when
you want to pass in a bit of code into a function:

    my $line;
    timeout( 30, sub { $line = &lt;STDIN&gt; } );

If the code to execute had been passed in as a string,
C&lt;&lt; &#39;$line = &lt;STDIN&gt;&#39; &gt;&gt;, there would have been no way for the
hypothetical timeout() function to access the lexical variable
$line back in its caller&#39;s scope.

Another use for a closure is to make a variable I&lt;private&gt; to a
named subroutine, e.g. a counter that gets initialized at creation
time of the sub and can only be modified from within the sub.
This is sometimes used with a BEGIN block in package files to make
sure a variable doesn&#39;t get meddled with during the lifetime of the
package:

    BEGIN {
        my $id = 0;
        sub next_id { ++$id }
    }

This is discussed in more detail in L&lt;perlsub&gt;; see the entry on
I&lt;Persistent Private Variables&gt;.

=head2 What is variable suicide and how can I prevent it?

This problem was fixed in perl 5.004_05, so preventing it means upgrading
your version of perl. ;)

Variable suicide is when you (temporarily or permanently) lose the value
of a variable. It is caused by scoping through my() and local()
interacting with either closures or aliased foreach() iterator variables
and subroutine arguments. It used to be easy to inadvertently lose a
variable&#39;s value this way, but now it&#39;s much harder. Take this code:

    my $f = &#39;foo&#39;;
    sub T {
        while ($i++ &lt; 3) { my $f = $f; $f .= &quot;bar&quot;; print $f, &quot;\n&quot; }
    }

    T;
    print &quot;Finally $f\n&quot;;

If you are experiencing variable suicide, that C&lt;my $f&gt; in the subroutine
doesn&#39;t pick up a fresh copy of the C&lt;$f&gt; whose value is C&lt;&#39;foo&#39;&gt;. The
output shows that inside the subroutine the value of C&lt;$f&gt; leaks through
when it shouldn&#39;t, as in this output:

    foobar
    foobarbar
    foobarbarbar
    Finally foo

The $f that has &quot;bar&quot; added to it three times should be a new C&lt;$f&gt;
C&lt;my $f&gt; should create a new lexical variable each time through the loop.
The expected output is:

    foobar
    foobar
    foobar
    Finally foo

=head2 How can I pass/return a {Function, FileHandle, Array, Hash, Method, Regex}?

You need to pass references to these objects. See L&lt;perlsub/&quot;Pass by
Reference&quot;&gt; for this particular question, and L&lt;perlref&gt; for
information on references.

=over 4

=item Passing Variables and Functions

Regular variables and functions are quite easy to pass: just pass in a
reference to an existing or anonymous variable or function:

    func( \$some_scalar );

    func( \@some_array  );
    func( [ 1 .. 10 ]   );

    func( \%some_hash   );
    func( { this =&gt; 10, that =&gt; 20 }   );

    func( \&amp;some_func   );
    func( sub { $_[0] ** $_[1] }   );

=item Passing Filehandles

As of Perl 5.6, you can represent filehandles with scalar variables
which you treat as any other scalar.

    open my $fh, $filename or die &quot;Cannot open $filename! $!&quot;;
    func( $fh );

    sub func {
        my $passed_fh = shift;

        my $line = &lt;$passed_fh&gt;;
    }

Before Perl 5.6, you had to use the C&lt;*FH&gt; or C&lt;\*FH&gt; notations.
These are &quot;typeglobs&quot;--see L&lt;perldata/&quot;Typeglobs and Filehandles&quot;&gt;
and especially L&lt;perlsub/&quot;Pass by Reference&quot;&gt; for more information.

=item Passing Regexes

Here&#39;s an example of how to pass in a string and a regular expression
for it to match against. You construct the pattern with the C&lt;qr//&gt;
operator:

    sub compare {
        my ($val1, $regex) = @_;
        my $retval = $val1 =~ /$regex/;
        return $retval;
    }
    $match = compare(&quot;old McDonald&quot;, qr/d.*D/i);

=item Passing Methods

To pass an object method into a subroutine, you can do this:

    call_a_lot(10, $some_obj, &quot;methname&quot;)
    sub call_a_lot {
        my ($count, $widget, $trick) = @_;
        for (my $i = 0; $i &lt; $count; $i++) {
            $widget-&gt;$trick();
        }
    }

Or, you can use a closure to bundle up the object, its
method call, and arguments:

    my $whatnot = sub { $some_obj-&gt;obfuscate(@args) };
    func($whatnot);
    sub func {
        my $code = shift;
        &amp;$code();
    }

You could also investigate the can() method in the UNIVERSAL class
(part of the standard perl distribution).

=back

=head2 How do I create a static variable?

(contributed by brian d foy)

In Perl 5.10, declare the variable with C&lt;state&gt;. The C&lt;state&gt;
declaration creates the lexical variable that persists between calls
to the subroutine:

    sub counter { state $count = 1; $count++ }

You can fake a static variable by using a lexical variable which goes
out of scope. In this example, you define the subroutine C&lt;counter&gt;, and
it uses the lexical variable C&lt;$count&gt;. Since you wrap this in a BEGIN
block, C&lt;$count&gt; is defined at compile-time, but also goes out of
scope at the end of the BEGIN block. The BEGIN block also ensures that
the subroutine and the value it uses is defined at compile-time so the
subroutine is ready to use just like any other subroutine, and you can
put this code in the same place as other subroutines in the program
text (i.e. at the end of the code, typically). The subroutine
C&lt;counter&gt; still has a reference to the data, and is the only way you
can access the value (and each time you do, you increment the value).
The data in chunk of memory defined by C&lt;$count&gt; is private to
C&lt;counter&gt;.

    BEGIN {
        my $count = 1;
        sub counter { $count++ }
    }

    my $start = counter();

    .... # code that calls counter();

    my $end = counter();

In the previous example, you created a function-private variable
because only one function remembered its reference. You could define
multiple functions while the variable is in scope, and each function
can share the &quot;private&quot; variable. It&#39;s not really &quot;static&quot; because you
can access it outside the function while the lexical variable is in
scope, and even create references to it. In this example,
C&lt;increment_count&gt; and C&lt;return_count&gt; share the variable. One
function adds to the value and the other simply returns the value.
They can both access C&lt;$count&gt;, and since it has gone out of scope,
there is no other way to access it.

    BEGIN {
        my $count = 1;
        sub increment_count { $count++ }
        sub return_count    { $count }
    }

To declare a file-private variable, you still use a lexical variable.
A file is also a scope, so a lexical variable defined in the file
cannot be seen from any other file.

See L&lt;perlsub/&quot;Persistent Private Variables&quot;&gt; for more information.
The discussion of closures in L&lt;perlref&gt; may help you even though we
did not use anonymous subroutines in this answer. See
L&lt;perlsub/&quot;Persistent Private Variables&quot;&gt; for details.

=head2 What&#39;s the difference between dynamic and lexical (static) scoping? Between local() and my()?

C&lt;local($x)&gt; saves away the old value of the global variable C&lt;$x&gt;
and assigns a new value for the duration of the subroutine I&lt;which is
visible in other functions called from that subroutine&gt;. This is done
at run-time, so is called dynamic scoping. local() always affects global
variables, also called package variables or dynamic variables.

C&lt;my($x)&gt; creates a new variable that is only visible in the current
subroutine. This is done at compile-time, so it is called lexical or
static scoping. my() always affects private variables, also called
lexical variables or (improperly) static(ly scoped) variables.

For instance:

    sub visible {
        print &quot;var has value $var\n&quot;;
    }

    sub dynamic {
        local $var = &#39;local&#39;;    # new temporary value for the still-global
        visible();              #   variable called $var
    }

    sub lexical {
        my $var = &#39;private&#39;;    # new private variable, $var
        visible();              # (invisible outside of sub scope)
    }

    $var = &#39;global&#39;;

    visible();              # prints global
    dynamic();              # prints local
    lexical();              # prints global

Notice how at no point does the value &quot;private&quot; get printed. That&#39;s
because $var only has that value within the block of the lexical()
function, and it is hidden from the called subroutine.

In summary, local() doesn&#39;t make what you think of as private, local
variables. It gives a global variable a temporary value. my() is
what you&#39;re looking for if you want private variables.

See L&lt;perlsub/&quot;Private Variables via my()&quot;&gt; and
L&lt;perlsub/&quot;Temporary Values via local()&quot;&gt; for excruciating details.

=head2 How can I access a dynamic variable while a similarly named lexical is in scope?

If you know your package, you can just mention it explicitly, as in
$Some_Pack::var. Note that the notation $::var is B&lt;not&gt; the dynamic $var
in the current package, but rather the one in the &quot;main&quot; package, as
though you had written $main::var.

    use vars &#39;$var&#39;;
    local $var = &quot;global&quot;;
    my    $var = &quot;lexical&quot;;

    print &quot;lexical is $var\n&quot;;
    print &quot;global  is $main::var\n&quot;;

Alternatively you can use the compiler directive our() to bring a
dynamic variable into the current lexical scope.

    require 5.006; # our() did not exist before 5.6
    use vars &#39;$var&#39;;

    local $var = &quot;global&quot;;
    my $var    = &quot;lexical&quot;;

    print &quot;lexical is $var\n&quot;;

    {
        our $var;
        print &quot;global  is $var\n&quot;;
    }

=head2 What&#39;s the difference between deep and shallow binding?

In deep binding, lexical variables mentioned in anonymous subroutines
are the same ones that were in scope when the subroutine was created.
In shallow binding, they are whichever variables with the same names
happen to be in scope when the subroutine is called. Perl always uses
deep binding of lexical variables (i.e., those created with my()).
However, dynamic variables (aka global, local, or package variables)
are effectively shallowly bound. Consider this just one more reason
not to use them. See the answer to L&lt;&quot;What&#39;s a closure?&quot;&gt;.

=head2 Why doesn&#39;t &quot;my($foo) = E&lt;lt&gt;$fhE&lt;gt&gt;;&quot; work right?

C&lt;my()&gt; and C&lt;local()&gt; give list context to the right hand side
of C&lt;=&gt;. The &lt;$fh&gt; read operation, like so many of Perl&#39;s
functions and operators, can tell which context it was called in and
behaves appropriately. In general, the scalar() function can help.
This function does nothing to the data itself (contrary to popular myth)
but rather tells its argument to behave in whatever its scalar fashion is.
If that function doesn&#39;t have a defined scalar behavior, this of course
doesn&#39;t help you (such as with sort()).

To enforce scalar context in this particular case, however, you need
merely omit the parentheses:

    local($foo) = &lt;$fh&gt;;        # WRONG
    local($foo) = scalar(&lt;$fh&gt;);   # ok
    local $foo  = &lt;$fh&gt;;        # right

You should probably be using lexical variables anyway, although the
issue is the same here:

    my($foo) = &lt;$fh&gt;;    # WRONG
    my $foo  = &lt;$fh&gt;;    # right

=head2 How do I redefine a builtin function, operator, or method?

Why do you want to do that? :-)

If you want to override a predefined function, such as open(),
then you&#39;ll have to import the new definition from a different
module. See L&lt;perlsub/&quot;Overriding Built-in Functions&quot;&gt;.

If you want to overload a Perl operator, such as C&lt;+&gt; or C&lt;**&gt;,
then you&#39;ll want to use the C&lt;use overload&gt; pragma, documented
in L&lt;overload&gt;.

If you&#39;re talking about obscuring method calls in parent classes,
see L&lt;perlootut/&quot;Overriding methods and method resolution&quot;&gt;.

=head2 What&#39;s the difference between calling a function as &amp;foo and foo()?

(contributed by brian d foy)

Calling a subroutine as C&lt;&amp;foo&gt; with no trailing parentheses ignores
the prototype of C&lt;foo&gt; and passes it the current value of the argument
list, C&lt;@_&gt;. Here&#39;s an example; the C&lt;bar&gt; subroutine calls C&lt;&amp;foo&gt;,
which prints its arguments list:

    sub foo { print &quot;Args in foo are: @_\n&quot;; }

    sub bar { &amp;foo; }

    bar( &quot;a&quot;, &quot;b&quot;, &quot;c&quot; );

When you call C&lt;bar&gt; with arguments, you see that C&lt;foo&gt; got the same C&lt;@_&gt;:

    Args in foo are: a b c

Calling the subroutine with trailing parentheses, with or without arguments,
does not use the current C&lt;@_&gt;. Changing the example to put parentheses after
the call to C&lt;foo&gt; changes the program:

    sub foo { print &quot;Args in foo are: @_\n&quot;; }

    sub bar { &amp;foo(); }

    bar( &quot;a&quot;, &quot;b&quot;, &quot;c&quot; );

Now the output shows that C&lt;foo&gt; doesn&#39;t get the C&lt;@_&gt; from its caller.

    Args in foo are:

However, using C&lt;&amp;&gt; in the call still overrides the prototype of C&lt;foo&gt; if
present:

    sub foo ($$$) { print &quot;Args infoo are: @_\n&quot;; }

    sub bar_1 { &amp;foo; }
    sub bar_2 { &amp;foo(); }
    sub bar_3 { foo( $_[0], $_[1], $_[2] ); }
    # sub bar_4 { foo(); }
    # bar_4 doesn&#39;t compile: &quot;Not enough arguments for main::foo at ...&quot;

    bar_1( &quot;a&quot;, &quot;b&quot;, &quot;c&quot; );
    # Args in foo are: a b c

    bar_2( &quot;a&quot;, &quot;b&quot;, &quot;c&quot; );
    # Args in foo are:

    bar_3( &quot;a&quot;, &quot;b&quot;, &quot;c&quot; );
    # Args in foo are: a b c

The main use of the C&lt;@_&gt; pass-through feature is to write subroutines
whose main job it is to call other subroutines for you. For further
details, see L&lt;perlsub&gt;.

=head2 How do I create a switch or case statement?

In Perl 5.10, use the C&lt;given-when&gt; construct described in L&lt;perlsyn&gt;:

    use 5.010;

    given ( $string ) {
        when( &#39;Fred&#39; )        { say &quot;I found Fred!&quot; }
        when( &#39;Barney&#39; )      { say &quot;I found Barney!&quot; }
        when( /Bamm-?Bamm/ )  { say &quot;I found Bamm-Bamm!&quot; }
        default               { say &quot;I don&#39;t recognize the name!&quot; }
    };

If one wants to use pure Perl and to be compatible with Perl versions
prior to 5.10, the general answer is to use C&lt;if-elsif-else&gt;:

    for ($variable_to_test) {
        if    (/pat1/)  { }     # do something
        elsif (/pat2/)  { }     # do something else
        elsif (/pat3/)  { }     # do something else
        else            { }     # default
    }

Here&#39;s a simple example of a switch based on pattern matching,
lined up in a way to make it look more like a switch statement.
We&#39;ll do a multiway conditional based on the type of reference stored
in $whatchamacallit:

    SWITCH: for (ref $whatchamacallit) {

        /^$/           &amp;&amp; die &quot;not a reference&quot;;

        /SCALAR/       &amp;&amp; do {
                        print_scalar($$ref);
                        last SWITCH;
                      };

        /ARRAY/        &amp;&amp; do {
                        print_array(@$ref);
                        last SWITCH;
                      };

        /HASH/        &amp;&amp; do {
                        print_hash(%$ref);
                        last SWITCH;
                      };

        /CODE/        &amp;&amp; do {
                        warn &quot;can&#39;t print function ref&quot;;
                        last SWITCH;
                      };

        # DEFAULT

        warn &quot;User defined type skipped&quot;;

    }

See L&lt;perlsyn&gt; for other examples in this style.

Sometimes you should change the positions of the constant and the variable.
For example, let&#39;s say you wanted to test which of many answers you were
given, but in a case-insensitive way that also allows abbreviations.
You can use the following technique if the strings all start with
different characters or if you want to arrange the matches so that
one takes precedence over another, as C&lt;&quot;SEND&quot;&gt; has precedence over
C&lt;&quot;STOP&quot;&gt; here:

    chomp($answer = &lt;&gt;);
    if    (&quot;SEND&quot;  =~ /^\Q$answer/i) { print &quot;Action is send\n&quot;  }
    elsif (&quot;STOP&quot;  =~ /^\Q$answer/i) { print &quot;Action is stop\n&quot;  }
    elsif (&quot;ABORT&quot; =~ /^\Q$answer/i) { print &quot;Action is abort\n&quot; }
    elsif (&quot;LIST&quot;  =~ /^\Q$answer/i) { print &quot;Action is list\n&quot;  }
    elsif (&quot;EDIT&quot;  =~ /^\Q$answer/i) { print &quot;Action is edit\n&quot;  }

A totally different approach is to create a hash of function references.

    my %commands = (
        &quot;happy&quot; =&gt; \&amp;joy,
        &quot;sad&quot;,  =&gt; \&amp;sullen,
        &quot;done&quot;  =&gt; sub { die &quot;See ya!&quot; },
        &quot;mad&quot;   =&gt; \&amp;angry,
    );

    print &quot;How are you? &quot;;
    chomp($string = &lt;STDIN&gt;);
    if ($commands{$string}) {
        $commands{$string}-&gt;();
    } else {
        print &quot;No such command: $string\n&quot;;
    }

Starting from Perl 5.8, a source filter module, C&lt;Switch&gt;, can also be
used to get switch and case. Its use is now discouraged, because it&#39;s
not fully compatible with the native switch of Perl 5.10, and because,
as it&#39;s implemented as a source filter, it doesn&#39;t always work as intended
when complex syntax is involved.

=head2 How can I catch accesses to undefined variables, functions, or methods?

The AUTOLOAD method, discussed in L&lt;perlsub/&quot;Autoloading&quot;&gt; lets you capture
calls to undefined functions and methods.

When it comes to undefined variables that would trigger a warning
under C&lt;use warnings&gt;, you can promote the warning to an error.

    use warnings FATAL =&gt; qw(uninitialized);

=head2 Why can&#39;t a method included in this same file be found?

Some possible reasons: your inheritance is getting confused, you&#39;ve
misspelled the method name, or the object is of the wrong type. Check
out L&lt;perlootut&gt; for details about any of the above cases. You may
also use C&lt;print ref($object)&gt; to find out the class C&lt;$object&gt; was
blessed into.

Another possible reason for problems is that you&#39;ve used the
indirect object syntax (eg, C&lt;find Guru &quot;Samy&quot;&gt;) on a class name
before Perl has seen that such a package exists. It&#39;s wisest to make
sure your packages are all defined before you start using them, which
will be taken care of if you use the C&lt;use&gt; statement instead of
C&lt;require&gt;. If not, make sure to use arrow notation (eg.,
C&lt;&lt; Guru-&gt;find(&quot;Samy&quot;) &gt;&gt;) instead. Object notation is explained in
L&lt;perlobj&gt;.

Make sure to read about creating modules in L&lt;perlmod&gt; and
the perils of indirect objects in L&lt;perlobj/&quot;Method Invocation&quot;&gt;.

=head2 How can I find out my current or calling package?

(contributed by brian d foy)

To find the package you are currently in, use the special literal
C&lt;__PACKAGE__&gt;, as documented in L&lt;perldata&gt;. You can only use the
special literals as separate tokens, so you can&#39;t interpolate them
into strings like you can with variables:

    my $current_package = __PACKAGE__;
    print &quot;I am in package $current_package\n&quot;;

If you want to find the package calling your code, perhaps to give better
diagnostics as L&lt;Carp&gt; does, use the C&lt;caller&gt; built-in:

    sub foo {
        my @args = ...;
        my( $package, $filename, $line ) = caller;

        print &quot;I was called from package $package\n&quot;;
        );

By default, your program starts in package C&lt;main&gt;, so you will
always be in some package.

This is different from finding out the package an object is blessed
into, which might not be the current package. For that, use C&lt;blessed&gt;
from L&lt;Scalar::Util&gt;, part of the Standard Library since Perl 5.8:

    use Scalar::Util qw(blessed);
    my $object_package = blessed( $object );

Most of the time, you shouldn&#39;t care what package an object is blessed
into, however, as long as it claims to inherit from that class:

    my $is_right_class = eval { $object-&gt;isa( $package ) }; # true or false

And, with Perl 5.10 and later, you don&#39;t have to check for an
inheritance to see if the object can handle a role. For that, you can
use C&lt;DOES&gt;, which comes from C&lt;UNIVERSAL&gt;:

    my $class_does_it = eval { $object-&gt;DOES( $role ) }; # true or false

You can safely replace C&lt;isa&gt; with C&lt;DOES&gt; (although the converse is not true).

=head2 How can I comment out a large block of Perl code?

(contributed by brian d foy)

The quick-and-dirty way to comment out more than one line of Perl is
to surround those lines with Pod directives. You have to put these
directives at the beginning of the line and somewhere where Perl
expects a new statement (so not in the middle of statements like the C&lt;#&gt;
comments). You end the comment with C&lt;=cut&gt;, ending the Pod section:

    =pod

    my $object = NotGonnaHappen-&gt;new();

    ignored_sub();

    $wont_be_assigned = 37;

    =cut

The quick-and-dirty method only works well when you don&#39;t plan to
leave the commented code in the source. If a Pod parser comes along,
your multiline comment is going to show up in the Pod translation.
A better way hides it from Pod parsers as well.

The C&lt;=begin&gt; directive can mark a section for a particular purpose.
If the Pod parser doesn&#39;t want to handle it, it just ignores it. Label
the comments with C&lt;comment&gt;. End the comment using C&lt;=end&gt; with the
same label. You still need the C&lt;=cut&gt; to go back to Perl code from
the Pod comment:

    =begin comment

    my $object = NotGonnaHappen-&gt;new();

    ignored_sub();

    $wont_be_assigned = 37;

    =end comment

    =cut

For more information on Pod, check out L&lt;perlpod&gt; and L&lt;perlpodspec&gt;.

=head2 How do I clear a package?

Use this code, provided by Mark-Jason Dominus:

    sub scrub_package {
        no strict &#39;refs&#39;;
        my $pack = shift;
        die &quot;Shouldn&#39;t delete main package&quot;
            if $pack eq &quot;&quot; || $pack eq &quot;main&quot;;
        my $stash = *{$pack . &#39;::&#39;}{HASH};
        my $name;
        foreach $name (keys %$stash) {
            my $fullname = $pack . &#39;::&#39; . $name;
            # Get rid of everything with that name.
            undef $$fullname;
            undef @$fullname;
            undef %$fullname;
            undef &amp;$fullname;
            undef *$fullname;
        }
    }

Or, if you&#39;re using a recent release of Perl, you can
just use the Symbol::delete_package() function instead.

=head2 How can I use a variable as a variable name?

Beginners often think they want to have a variable contain the name
of a variable.

    $fred    = 23;
    $varname = &quot;fred&quot;;
    ++$$varname;         # $fred now 24

This works I&lt;sometimes&gt;, but it is a very bad idea for two reasons.

The first reason is that this technique I&lt;only works on global
variables&gt;. That means that if $fred is a lexical variable created
with my() in the above example, the code wouldn&#39;t work at all: you&#39;d
accidentally access the global and skip right over the private lexical
altogether. Global variables are bad because they can easily collide
accidentally and in general make for non-scalable and confusing code.

Symbolic references are forbidden under the C&lt;use strict&gt; pragma.
They are not true references and consequently are not reference-counted
or garbage-collected.

The other reason why using a variable to hold the name of another
variable is a bad idea is that the question often stems from a lack of
understanding of Perl data structures, particularly hashes. By using
symbolic references, you are just using the package&#39;s symbol-table hash
(like C&lt;%main::&gt;) instead of a user-defined hash. The solution is to
use your own hash or a real reference instead.

    $USER_VARS{&quot;fred&quot;} = 23;
    my $varname = &quot;fred&quot;;
    $USER_VARS{$varname}++;  # not $$varname++

There we&#39;re using the %USER_VARS hash instead of symbolic references.
Sometimes this comes up in reading strings from the user with variable
references and wanting to expand them to the values of your perl
program&#39;s variables. This is also a bad idea because it conflates the
program-addressable namespace and the user-addressable one. Instead of
reading a string and expanding it to the actual contents of your program&#39;s
own variables:

    $str = &#39;this has a $fred and $barney in it&#39;;
    $str =~ s/(\$\w+)/$1/eeg;          # need double eval

it would be better to keep a hash around like %USER_VARS and have
variable references actually refer to entries in that hash:

    $str =~ s/\$(\w+)/$USER_VARS{$1}/g;   # no /e here at all

That&#39;s faster, cleaner, and safer than the previous approach. Of course,
you don&#39;t need to use a dollar sign. You could use your own scheme to
make it less confusing, like bracketed percent symbols, etc.

    $str = &#39;this has a %fred% and %barney% in it&#39;;
    $str =~ s/%(\w+)%/$USER_VARS{$1}/g;   # no /e here at all

Another reason that folks sometimes think they want a variable to
contain the name of a variable is that they don&#39;t know how to build
proper data structures using hashes. For example, let&#39;s say they
wanted two hashes in their program: %fred and %barney, and that they
wanted to use another scalar variable to refer to those by name.

    $name = &quot;fred&quot;;
    $$name{WIFE} = &quot;wilma&quot;;     # set %fred

    $name = &quot;barney&quot;;
    $$name{WIFE} = &quot;betty&quot;;    # set %barney

This is still a symbolic reference, and is still saddled with the
problems enumerated above. It would be far better to write:

    $folks{&quot;fred&quot;}{WIFE}   = &quot;wilma&quot;;
    $folks{&quot;barney&quot;}{WIFE} = &quot;betty&quot;;

And just use a multilevel hash to start with.

The only times that you absolutely I&lt;must&gt; use symbolic references are
when you really must refer to the symbol table. This may be because it&#39;s
something that one can&#39;t take a real reference to, such as a format name.
Doing so may also be important for method calls, since these always go
through the symbol table for resolution.

In those cases, you would turn off C&lt;strict &#39;refs&#39;&gt; temporarily so you
can play around with the symbol table. For example:

    @colors = qw(red blue green yellow orange purple violet);
    for my $name (@colors) {
        no strict &#39;refs&#39;;  # renege for the block
        *$name = sub { &quot;&lt;FONT COLOR=&#39;$name&#39;&gt;@_&lt;/FONT&gt;&quot; };
    }

All those functions (red(), blue(), green(), etc.) appear to be separate,
but the real code in the closure actually was compiled only once.

So, sometimes you might want to use symbolic references to manipulate
the symbol table directly. This doesn&#39;t matter for formats, handles, and
subroutines, because they are always global--you can&#39;t use my() on them.
For scalars, arrays, and hashes, though--and usually for subroutines--
you probably only want to use hard references.

=head2 What does &quot;bad interpreter&quot; mean?

(contributed by brian d foy)

The &quot;bad interpreter&quot; message comes from the shell, not perl. The
actual message may vary depending on your platform, shell, and locale
settings.

If you see &quot;bad interpreter - no such file or directory&quot;, the first
line in your perl script (the &quot;shebang&quot; line) does not contain the
right path to perl (or any other program capable of running scripts).
Sometimes this happens when you move the script from one machine to
another and each machine has a different path to perl--/usr/bin/perl
versus /usr/local/bin/perl for instance. It may also indicate
that the source machine has CRLF line terminators and the
destination machine has LF only: the shell tries to find
/usr/bin/perl&lt;CR&gt;, but can&#39;t.

If you see &quot;bad interpreter: Permission denied&quot;, you need to make your
script executable.

In either case, you should still be able to run the scripts with perl
explicitly:

    % perl script.pl

If you get a message like &quot;perl: command not found&quot;, perl is not in
your PATH, which might also mean that the location of perl is not
where you expect it so you need to adjust your shebang line.

=head2 Do I need to recompile XS modules when there is a change in the C library?

(contributed by Alex Beamish)

If the new version of the C library is ABI-compatible (that&#39;s Application
Binary Interface compatible) with the version you&#39;re upgrading from, and if the
shared library version didn&#39;t change, no re-compilation should be necessary.

=head1 AUTHOR AND COPYRIGHT

Copyright (c) 1997-2013 Tom Christiansen, Nathan Torkington, and
other authors as noted. All rights reserved.

This documentation is free; you can redistribute it and/or modify it
under the same terms as Perl itself.

Irrespective of its distribution, all code examples in this file
are hereby placed into the public domain. You are permitted and
encouraged to use this code in your own programs for fun
or for profit as you see fit. A simple comment in the code giving
credit would be courteous but is not required.
</pre>

                    </main>
                </div>

        </div>
        <footer>
        
        </footer>


    </body>
</html>
